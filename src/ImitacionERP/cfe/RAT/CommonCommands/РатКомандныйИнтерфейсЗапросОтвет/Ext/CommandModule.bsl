//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Запрос = ПолучитьЗапросМенеджераИзПараметровВыполненияКоманды(ПараметрыВыполненияКоманды);
	
	Если Запрос = Неопределено Тогда
		
		// Если не определили запрос - то и возвращать нам некуда и нечего.
		// Считаем, что менеджер получит как минимум ошибку таймаута. 
		// Это приведет к вызову исключения и скриншоту клиента тестирования
		// Выведем ошибку в окно сообщений на клиенте тестирования
		
		РатОбщийКлиентСервер.СообщитьПользователю("Ошибка взаимодействия менеджера и клиента тестирования: некорректная структуру запроса");
		
		Возврат;
		
	КонецЕсли;
	
	Попытка
		
		Команда = Запрос.Команда; // см. РатВзаимодействиеСКлиентомТестирования.НоваяКоманда
		
		// необходимо держать параметры вызываемого метода в локальной переменной, 
		// при этом имя переменной передается 3-м параметром методу см. СигнатураМетода
		// необходимо для корректного вызова (методом Вычислить) соответствующего обработчика
		
		ПараметрыМетода = Команда.Параметры;
		
		Результат = РатАлгоритмы.ВызватьФункцию(Команда.Метод, ПараметрыМетода);
		
		Ответ = РатВзаимодействиеСКлиентомТестированияКлиентСервер.НовыйОтветУспешно(Результат);
		
	Исключение
		
		// последняя ошибка, которая "попалась" в данной попытке, нас не сильно интересует (если она не единственная)
		// определим причину данной ошибки и работать будем уже с ней
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПричинаОшибки = ?(ИнформацияОбОшибке.Причина = Неопределено, ИнформацияОбОшибке, ИнформацияОбОшибке.Причина);
		
		Ответ = РатВзаимодействиеСКлиентомТестированияКлиентСервер.НовыйОтветОшибка(ПричинаОшибки);
		
	КонецПопытки;
	
	ЗафиксироватьОтвет(Запрос, Ответ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьЗапросМенеджераИзПараметровВыполненияКоманды(ПараметрыВыполненияКоманды)
	
	Результат = Неопределено;
	
	Если ТипЗнч(ПараметрыВыполненияКоманды.Параметры) <> Тип("Структура") Тогда
		
		// на случай интерактивного вызова команды на клиенте тестирования
		// сообщим что это служебная команда и ничего не будем делать
		
		ПоказатьПредупреждение(, "Попытка запуска служебной команды автоматизированного тестирования");
		
	Иначе
		
		Результат = ПараметрыВыполненияКоманды.Параметры;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ЗафиксироватьОтвет(Запрос, Ответ)
	
	Попытка
		
		// т.к. дальше управление передаем на сервер
		// выделим из структуры запроса, только необходимое для фиксации в БД
		// немного сократим сериализуемые данные
		
		ЗаголовкиЗапроса = Новый Структура;
		ЗаголовкиЗапроса.Вставить("КлючОбъекта", Запрос.КлючОбъекта);
		ЗаголовкиЗапроса.Вставить("КлючНастроек", Запрос.КлючНастроек);
		ЗаголовкиЗапроса.Вставить("КлючЗапроса", Запрос.КлючЗапроса);
		
		ЗафиксироватьОтветНаСервере(ЗаголовкиЗапроса, Ответ);
		
	Исключение
		
		// так как не смогли зафиксировать ответ - считаем, что менеджер получит как минимум
		// ошибку таймаута. Что приведет к вызову исключения и скриншоту клиента тестирования
		// Выведем ошибку в окно сообщений на клиенте тестирования
		
		РатОбщийКлиентСервер.СообщитьПользователю(ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗафиксироватьОтветНаСервере(Знач Запрос, Знач Ответ)
	
	КлючОбъекта = Запрос.КлючОбъекта;
	КлючНастроек = Запрос.КлючНастроек;
	
	УстановитьПривилегированныйРежим(Истина); // BSLLS:SetPrivilegedMode-off
	
	ОтборНастроек = Новый Структура("КлючОбъекта, КлючНастроек", КлючОбъекта, КлючНастроек);
	
	ВыборкаНастроек = ХранилищеВнешнихДанныхНавигационныхСсылок.Выбрать(ОтборНастроек);
	
	Если ВыборкаНастроек.Следующий() Тогда
			
		Результат = Новый Структура;
		Результат.Вставить("КлючОбъекта", КлючОбъекта);
		Результат.Вставить("КлючНастроек", КлючНастроек);
		Результат.Вставить("КлючЗапроса", Запрос.КлючЗапроса);
		Результат.Вставить("ТипХранилища", "Ответ");
		Результат.Вставить("Тело", Ответ);
		
		ХранилищеВнешнихДанныхНавигационныхСсылок.Сохранить(КлючОбъекта, КлючНастроек, Результат, , ВыборкаНастроек.Пользователь);
		
	Иначе
		
		ВызватьИсключение
			"Не удалось зафиксировать ответ на запрос менеджера тестирования:
			|нарушена целостность хранилища внешних данных навигационных ссылок";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОписаниеПеременных

&НаКлиенте
// Описание таблицы для генерации сценариев
Перем ОписаниеТаблицы;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список = Элементы.ТипОбъекта.СписокВыбора;
	
	Для Каждого Тип Из РатМетаданные.ТипыМетаданных() Цикл
		
		Если Тип.Имя = "Перечисление" Тогда
			Продолжить;
		КонецЕсли;
		
		Коллекция = Метаданные[Тип.ИмяКоллекции];
		Картинка = БиблиотекаКартинок[Тип.Имя];
		
		Для Каждого ОбъектМетаданных Из Коллекция Цикл
			
			Список.Добавить(ОбъектМетаданных.ПолноеИмя(), ОбъектМетаданных.Представление(), НЕ Тип.ОбъектныеДанные, Картинка);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Список.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбработатьИзменениеТипа();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	
	ОбработатьИзменениеТипа();
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаОбъектПриИзменении(Элемент)
	
	УникальныйИдентификаторСсылки = СсылкаНаОбъект.УникальныйИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура РегистраторОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Регистратор = Регистратор(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистраторНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ОписаниеТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе, ОписаниеТаблицы", Истина, Истина, ОписаниеТаблицы);
	ОткрытьФорму("Обработка.РатГенераторСценариев.Форма.ФормаВыбораРегистра",
				 ПараметрыОткрытия,
				 Элемент,
				 УникальныйИдентификатор,
				 ,
				 ,
				 ,
				 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыбранныеЗаписиРегистра

&НаКлиенте
Процедура ВыбранныеЗаписиРегистраПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	Отказ = Истина;
	
	Если ОписаниеТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("РежимВыбора, ЗакрыватьПриВыборе, ОписаниеТаблицы", Истина, Ложь, ОписаниеТаблицы);
	ОткрытьФорму("Обработка.РатГенераторСценариев.Форма.ФормаВыбораРегистра",
				 ПараметрыОткрытия,
				 Элементы.ВыбранныеЗаписиРегистра,
				 УникальныйИдентификатор,
				 ,
				 ,
				 ,
				 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеЗаписиРегистраОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранныеЗаписиРегистра.НайтиПоЗначению(ВыбранноеЗначение) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Представление = ПредставлениеКлючаЗаписи(ВыбранноеЗначение);
	ВыбранныеЗаписиРегистра.Добавить(ВыбранноеЗначение, Представление);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьСкриптСоздания(Команда)
	
	СформироватьСкриптСозданияСервер(ОписаниеТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьИзменениеТипа()
	
	ВыбранныеЗаписиРегистра.Очистить();
	Регистратор = Неопределено;
	СсылкаНаОбъект = Неопределено;
	
	ОписаниеТаблицы = Неопределено;
	
	Если ПустаяСтрока(ТипОбъекта) Тогда
		Элементы.ГруппаТипыГенераторов.ТекущаяСтраница = Элементы.ГруппаНачальная;
		Возврат;
	КонецЕсли;
	
	ОписаниеТаблицы = ОписаниеТаблицы(ТипОбъекта);
	
	Если ОписаниеТаблицы.Ссылочный Тогда // Объект
		ИмяТипа = СтрШаблон("%1Ссылка.%2", ОписаниеТаблицы.ИмяТипа, ОписаниеТаблицы.Имя);
		ОграничениеТипа = Новый ОписаниеТипов(ИмяТипа);
		СсылкаНаОбъект = ОграничениеТипа.ПривестиЗначение(СсылкаНаОбъект);
		Элементы.СсылкаНаОбъект.ОграничениеТипа = ОграничениеТипа;
		Страница = Элементы.ГруппаСсылочныйОбъект;
	ИначеЕсли ОписаниеТаблицы.ЭтоРегистраДвижений Тогда
		Страница = Элементы.ГруппаДвижения;
	Иначе
		Страница = Элементы.ГруппаРегистры;
	КонецЕсли;
	
	Элементы.ГруппаТипыГенераторов.ТекущаяСтраница = Страница;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеТаблицы(Знач ТипОбъекта)
	
	Данные = РатМетаданные.РазложитьИмяТаблицы(ТипОбъекта);
	
	Описание = Новый Структура();
	Описание.Вставить("Имя", Данные.ИмяВида);
	Описание.Вставить("ИмяТипа", Данные.ИмяТипа);
	Описание.Вставить("Ссылочный", Данные.Тип.ОбъектныеДанные);
	Описание.Вставить("ПолноеИмя", СтрШаблон("%1.%2", Данные.ИмяТипа, Данные.ИмяВида));
	Описание.Вставить("ЭтоРегистраДвижений", Ложь);
	
	Если НЕ Данные.Тип.ОбъектныеДанные Тогда
		
		Если Данные.ИмяТипа = "РегистрСведений" Тогда
			
			МетаданныеОбъекта = Метаданные[Данные.Тип.ИмяКоллекции][Данные.ИмяВида];
			Описание.ЭтоРегистраДвижений = МетаданныеОбъекта.РежимЗаписи =
				Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору;
			
		Иначе
			
			Описание.ЭтоРегистраДвижений = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

&НаСервере
Процедура СформироватьСкриптСозданияСервер(Знач ОписаниеТаблицы)
	
	ТекстСкрипта = Неопределено;
	
	Если ОписаниеТаблицы.Ссылочный Тогда
		ТекстСкрипта = СкриптСоздания(СсылкаНаОбъект, ИсключатьНомерДату, ИмяИсточникаДанных);
	ИначеЕсли ОписаниеТаблицы.ЭтоРегистраДвижений Тогда
		ТекстСкрипта = СкриптСозданияДвижения(Регистратор, ОписаниеТаблицы, ИмяИсточникаДанных);
	Иначе
		ТекстСкрипта = СкриптСозданияЗаписейРегистра(ВыбранныеЗаписиРегистра.ВыгрузитьЗначения(),
													 ОписаниеТаблицы,
													 ИмяИсточникаДанных);
	КонецЕсли;
	
	Если ТекстСкрипта <> Неопределено Тогда
		Скрипт = ТекстСкрипта;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СкриптСоздания(СсылкаНаОбъект, ИсключатьНомерДату, ИмяИсточникаДанных)
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		СообщитьПользователю("Выберите объект", "СсылкаНаОбъект");
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеСсылки = ДанныеСсылки(СсылкаНаОбъект, ИсключатьНомерДату);
	
	Переменные = Новый Массив;
	ТабличныеЧасти = Новый Массив;
	Реквизиты = Новый Массив;
	
	ОбработатьРеквизиты(ДанныеСсылки.Реквизиты, Переменные, Реквизиты);
	ОбработатьТабличныеЧасти(ДанныеСсылки.ТабличныеЧасти, Переменные, ТабличныеЧасти, Реквизиты);
	
	Скрипт = Новый Массив();
	РатКоллекции.ОбъединитьМассивы(Скрипт, Переменные, , , Истина);
	РатКоллекции.ОбъединитьМассивы(Скрипт, ТабличныеЧасти, , , Истина);
	
	ШагСценария = СтрШаблон("	И Я создаю запись ""%1"" внешней системы ""%2""", ДанныеСсылки.Метаданные.ПолноеИмя(), ИмяИсточникаДанных);
	Скрипт.Добавить(ШагСценария);
	РатКоллекции.ОбъединитьМассивы(Скрипт, Реквизиты, , , Истина);
	
	ДобавитьШагиПроведенияДокумента(Скрипт, ДанныеСсылки, ИмяИсточникаДанных);
	
	Возврат СтрСоединить(Скрипт, Символы.ПС);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОбработатьРеквизиты(РеквизитыОбъекта, Переменные, Реквизиты)
	
	Для Каждого Элемент Из РеквизитыОбъекта Цикл
		
		ЭтоСоставнойТип = Элемент.Составной;
		ТипЗначения = ТипЗнч(Элемент.Значение);
		
		Если ЭтоСоставнойТип И НужноСоздатьПеременную(Элемент.Значение) Тогда
			ИмяПеременной = Элемент.Имя;
			Переменные.Добавить(СценарийПеременнаяСоставногоТипа(Элемент.Значение, ИмяПеременной));
			Значение = СтрШаблон("$%1$", ИмяПеременной);
		Иначе
			Значение = ПреобразоватьЗначение(Элемент.Значение, ЭтоСоставнойТип);
		КонецЕсли;
		
		Если РатТипыДанных.ЭтоСсылочныйТип(ТипЗначения) И НЕ РатМетаданные.ЭтоТипПеречисления(ТипЗначения) Тогда
			Реквизиты.Добавить(СтрШаблон("		# %1", Элемент.Значение));
		КонецЕсли;
		СтрокаРеквизита = СтрШаблон("		| '%1' | %2 |", Элемент.Имя, Значение);
		Реквизиты.Добавить(СтрокаРеквизита);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьТабличныеЧасти(ТабличныеЧастиОбъекта, Переменные, ТабличныеЧасти, Реквизиты)
	
	Для Каждого ТабличнаяЧасть Из ТабличныеЧастиОбъекта Цикл
		
		ТабличныеЧасти.Добавить(СтрШаблон("	И Я создаю таблицу данных ""%1""", ТабличнаяЧасть.Ключ));
		
		Данные = ТабличнаяЧасть.Значение;
		Колонки = РатКоллекции.ВыгрузитьЗначения(Данные.Колонки, "Имя");
		
		ТабличныеЧасти.Добавить(СтрШаблон("		| '%1' |", СтрСоединить(Колонки, "' | '")));
		
		Для Каждого Строка Из Данные Цикл
			
			СтрокаТаблицы = Новый Массив;
			
			Для Каждого Колонка Из Данные.Колонки Цикл
				
				Значение = Строка[Колонка.Имя];
				ЭтоСоставнойТип = РатТипыДанных.ЭтоСоставнойТип(Колонка.ТипЗначения);
				
				Если ЭтоСоставнойТип И НужноСоздатьПеременную(Значение) Тогда
					ИмяПеременной = СтрШаблон("%1%2_%3", ТабличнаяЧасть.Ключ, Данные.Индекс(Строка) + 1, Колонка.Имя);
					Переменные.Добавить(СценарийПеременнаяСоставногоТипа(Значение, ИмяПеременной));
					Значение = СтрШаблон("$%1$", ИмяПеременной);
				Иначе
					Значение = ПреобразоватьЗначение(Значение, ЭтоСоставнойТип);
				КонецЕсли;
				
				СтрокаТаблицы.Добавить(Значение);
				
			КонецЦикла;
			
			СтрокаСценария = СтрШаблон("		| %1 |", СтрСоединить(СтрокаТаблицы, " | "));
			ТабличныеЧасти.Добавить(СтрокаСценария);
			
		КонецЦикла;
		
		Реквизиты.Добавить(СтрШаблон("		| '%1' | $%1$ |", ТабличнаяЧасть.Ключ));
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьШагиПроведенияДокумента(Скрипт, ДанныеСсылки, ИмяИсточникаДанных)
	
	Если Метаданные.Документы.Содержит(ДанныеСсылки.Метаданные) И ДанныеСсылки.Ссылка.Проведен Тогда
		ШагСценария =  СтрШаблон("	И Я сохраняю в переменную ""УникальныйИдентификатор_%1"" реквизит результата запроса ""id""",
								 ДанныеСсылки.Метаданные.Имя);
		Скрипт.Добавить(ШагСценария);
		ШагСценария =  СтрШаблон("	И Я провожу документ ""%1.$УникальныйИдентификатор_%1$"" внешней системы ""%2""",
								 ДанныеСсылки.Метаданные.Имя,
								 ИмяИсточникаДанных);
		Скрипт.Добавить(ШагСценария);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СкриптСозданияДвижения(Регистратор, ОписаниеТаблицы, ИмяИсточникаДанных)
	
	Если НЕ ЗначениеЗаполнено(Регистратор) Тогда
		СообщитьПользователю("Выберите регистратор", "Регистратор");
		Возврат Неопределено;
	КонецЕсли;
	
	Скрипт = Новый Массив();
	
	Запрос = Новый Запрос;
	ШаблонЗапроса = "ВЫБРАТЬ * ИЗ %1 ГДЕ %1.Регистратор = &Регистратор"; // BSLLS:QueryParseError-off
	Запрос.Текст = СтрШаблон(ШаблонЗапроса, ОписаниеТаблицы.ПолноеИмя);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ИмяВидаДокумента = Регистратор.Метаданные().Имя;
	Шаг = СтрШаблон("И Я изменяю движения документа ""%1.%2"" по регистру ""%3"" внешней системы ""%4""",
					ИмяВидаДокумента,
					Регистратор.УникальныйИдентификатор(),
					ОписаниеТаблицы.Имя,
					ИмяИсточникаДанных);
	Скрипт.Добавить(Шаг);
	
	Колонки = РатКоллекции.ВыгрузитьЗначения(РезультатЗапроса.Колонки, "Имя");
	Колонки.Удалить(Колонки.Найти("Регистратор"));
	Колонки.Удалить(Колонки.Найти("НомерСтроки"));
	
	РазделительКолонок = " | ";
	Строка = "		| " + СтрСоединить(Колонки, РазделительКолонок) + РазделительКолонок;
	Скрипт.Добавить(Строка);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки = Новый Массив();
		
		Для Каждого Колонка Из Колонки Цикл
			
			ЭтоСоставнойТип = РезультатЗапроса.Колонки[Колонка].ТипЗначения.Типы().Количество() > 2;
			Значение = ПреобразоватьЗначение(Выборка[Колонка], ЭтоСоставнойТип);
			ДанныеСтроки.Добавить(Значение);
			
		КонецЦикла;
		
		Строка = "		| " + СтрСоединить(ДанныеСтроки, РазделительКолонок) + РазделительКолонок;
		Скрипт.Добавить(Строка);
		
	КонецЦикла;
	
	Возврат СтрСоединить(Скрипт, Символы.ПС);
	
КонецФункции

&НаСервереБезКонтекста
Функция СкриптСозданияЗаписейРегистра(ЗаписиРегистра, ОписаниеТаблицы, ИмяИсточникаДанных)
	
	Если НЕ ЗначениеЗаполнено(ЗаписиРегистра) Тогда
		СообщитьПользователю("Выберите записи регистра", "ВыбранныеЗаписиРегистра");
		Возврат Неопределено;
	КонецЕсли;
	
	Скрипт = Новый Массив();
	КлючевыеПоля = РатМетаданные.КлючевыеПоляТаблицы(ОписаниеТаблицы.ПолноеИмя);
	Запрос = Новый Запрос;
	Условия = Новый Массив();
	
	ТекстШага = СтрШаблон("И Я создаю запись ""%1"" внешней системы ""%2""", ОписаниеТаблицы.ПолноеИмя, ИмяИсточникаДанных);
	
	Для Каждого Поле Из КлючевыеПоля Цикл
		Условия.Добавить(СтрШаблон("Запись.%1 = &%1", Поле));
		Запрос.УстановитьПараметр(Поле, Неопределено);
	КонецЦикла;
	
	ШаблонЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1 * ИЗ %1 КАК Запись ГДЕ "; // BSLLS:QueryParseError-off
	Запрос.Текст = СтрШаблон(ШаблонЗапроса, ОписаниеТаблицы.ПолноеИмя) + СтрСоединить(Условия, " И ");
	
	Для Каждого Ключ Из ЗаписиРегистра Цикл
		
		Скрипт.Добавить(ТекстШага);
		ЗаполнитьЗначенияСвойств(Запрос.Параметры, Ключ);
		//@skip-check query-in-loop
		РезультатЗапроса = Запрос.Выполнить().Выгрузить(); // BSLLS:CreateQueryInCycle-off
		
		Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
			
			Значение = РезультатЗапроса[0][Колонка.Имя];
			ТипЗначения = ТипЗнч(Значение);
			
			Если РатТипыДанных.ЭтоСсылочныйТип(ТипЗначения) И НЕ РатМетаданные.ЭтоТипПеречисления(ТипЗначения) Тогда
				Скрипт.Добавить(СтрШаблон("		# %1", Значение));
			КонецЕсли;
			
			ЭтоСоставнойТип = Колонка.ТипЗначения.Типы().Количество() > 2;
			Значение = ПреобразоватьЗначение(РезультатЗапроса[0][Колонка.Имя], ЭтоСоставнойТип);
			Запись = СтрШаблон("		| %1 | %2 |", Колонка.Имя, Значение);
			Скрипт.Добавить(Запись);
			
		КонецЦикла;
		
		Скрипт.Добавить("");
		
	КонецЦикла;
	
	Возврат СтрСоединить(Скрипт, Символы.ПС);
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеСсылки(Ссылка, ИсключатьНомерДату)
	
	ДанныеСсылки = Новый Структура;
	ДанныеСсылки.Вставить("Реквизиты", Новый ТаблицаЗначений());
	ДанныеСсылки.Вставить("ТабличныеЧасти", Новый Структура());
	ДанныеСсылки.Вставить("Метаданные", Ссылка.Метаданные());
	ДанныеСсылки.Вставить("Ссылка", Ссылка);
	
	ДанныеСсылки.Реквизиты.Колонки.Добавить("Имя");
	ДанныеСсылки.Реквизиты.Колонки.Добавить("Значение");
	ДанныеСсылки.Реквизиты.Колонки.Добавить("Составной");
	
	Запрос = Новый Запрос;
	ПолноеИмяОбъекта = ДанныеСсылки.Метаданные.ПолноеИмя();
	ШаблонЗапроса = "ВЫБРАТЬ * ИЗ %1 КАК Выборка ГДЕ Ссылка = &Ссылка"; // BSLLS:QueryParseError-off
	Запрос.Текст = СтрШаблон(ШаблонЗапроса, ПолноеИмяОбъекта);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	РеквизитыИсключения = Новый Массив;
	РеквизитыИсключения.Добавить("Ссылка");
	РеквизитыИсключения.Добавить("Проведен");
	РеквизитыИсключения.Добавить("ПометкаУдаления");
	РеквизитыИсключения.Добавить("ВерсияДанных");
	РеквизитыИсключения.Добавить("Предопределенный");
	РеквизитыИсключения.Добавить("ИмяПредопределенныхДанных");
	
	Если ИсключатьНомерДату Тогда
		РеквизитыИсключения.Добавить("Номер");
		РеквизитыИсключения.Добавить("Дата");
	КонецЕсли;
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		
		Если РеквизитыИсключения.Найти(Колонка.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Значение = Выборка[Колонка.Имя];
		
		Если ТипЗнч(Значение) = Тип("РезультатЗапроса") Тогда
			ДанныеТЧ = ЗаполнитьДанныеТаблицы(Значение);
			
			Если ДанныеТЧ.Количество() Тогда
				ДанныеСсылки.ТабличныеЧасти.Вставить(Колонка.Имя, ДанныеТЧ);
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(Значение) Тогда
			Реквизит = ДанныеСсылки.Реквизиты.Добавить();
			Реквизит.Имя = Колонка.Имя;
			Реквизит.Значение = Значение;
			Реквизит.Составной = РатТипыДанных.ЭтоСоставнойТип(Колонка.ТипЗначения);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеСсылки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьДанныеТаблицы(РезультатЗапроса)
	
	Данные = Новый ТаблицаЗначений();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Данные;
	КонецЕсли;
	
	РеквизитыИсключения = Новый Массив;
	РеквизитыИсключения.Добавить("Ссылка");
	РеквизитыИсключения.Добавить("НомерСтроки");
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		
		Если РеквизитыИсключения.Найти(Колонка.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Данные.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
		
	КонецЦикла;
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки = Данные.Добавить();
		
		Для Каждого Колонка Из Данные.Колонки Цикл
			
			ДанныеСтроки[Колонка.Имя] = Выборка[Колонка.Имя];
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Данные;
	
КонецФункции

&НаСервереБезКонтекста
Функция НужноСоздатьПеременную(Значение)
	
	Возврат НЕ РатТипыДанных.ЭтоСсылочныйТип(ТипЗнч(Значение));
	
КонецФункции

&НаСервереБезКонтекста
Функция ПреобразоватьЗначение(Значение, ЭтоСоставнойРеквизит)
	
	Тип = ТипЗнч(Значение);
	
	Результат = Неопределено;
	ОбернутьВКавычки = Истина;
	
	Если Тип = Тип("Число") Тогда
		
		Результат = Формат(Значение, "ЧРД=.; ЧН=0; ЧГ=;");
		ОбернутьВКавычки = Ложь;
		
	ИначеЕсли РатJSON.ЭтоПримитивныйТипJSON(Тип) Тогда
		
		Результат = Значение;
		
	ИначеЕсли РатМетаданные.ЭтоТипПеречисления(Тип) Тогда
		
		Результат = РатПреобразования.ИмяЭлементаПеречисления(Значение);
		
	ИначеЕсли РатТипыДанных.ЭтоСсылочныйТип(Тип) Тогда
		
		Результат = Строка(Значение.УникальныйИдентификатор());
	
	ИначеЕсли Тип = Тип("Дата") Тогда
		
		Результат = XMLСтрока(Значение);
		
	ИначеЕсли Тип = Тип("УникальныйИдентификатор") Тогда
		
		Результат = Строка(Значение);
		
	ИначеЕсли РатТипыДанных.ЭтоСистемноеПеречисление(Тип) Тогда
		
		Результат = РатПреобразования.КлючСистемногоПеречисления(Значение);
		
	Иначе
		
		Попытка
			Результат = XMLСтрока(Значение);
		Исключение
			ВызватьИсключение СтрШаблон("Не поддерживается возврат значений типа '%1'.", Тип);
		КонецПопытки;
		
	КонецЕсли;
	
	Если ЭтоСоставнойРеквизит Тогда
		Результат = СтрШаблон("'%1.%2'", РатТипыДанных.ПредставлениеТипа(Тип), Результат);
	ИначеЕсли ОбернутьВКавычки И НЕ ПустаяСтрока(Результат) Тогда
		Результат = СтрШаблон("'%1'", Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СценарийПеременнаяСоставногоТипа(Значение, ИмяПеременной)
	
	ПредставлениеТипа = РатТипыДанных.ПредставлениеТипа(ТипЗнч(Значение));
	ПреобразованноеЗначение = ПреобразоватьЗначение(Значение, Ложь);
	
	Возврат СтрШаблон("	И Я создаю структуру ""%1""
					  |		| 'type' | '%2' |
					  |		| 'id'   | %3 |", ИмяПеременной, ПредставлениеТипа, ПреобразованноеЗначение);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеКлючаЗаписи(Знач КлючЗаписи)
	
	МетаданныеЗаписи = Метаданные.НайтиПоТипу(ТипЗнч(КлючЗаписи));
	КлючевыеПоляТаблицы = РатМетаданные.КлючевыеПоляТаблицы(МетаданныеЗаписи.ПолноеИмя());
	
	СтрокиПредставления = Новый Массив();
	Для Каждого Поле Из КлючевыеПоляТаблицы Цикл
		СтрокиПредставления.Добавить(СтрШаблон("%1 = %2", Поле, КлючЗаписи[Поле]));
	КонецЦикла;
	
	Возврат СтрСоединить(СтрокиПредставления, "; ");
	
КонецФункции

&НаСервереБезКонтекста
Функция Регистратор(Знач КлючЗаписи)
	
	Возврат КлючЗаписи.Регистратор;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СообщитьПользователю(ТекстСообщенияПользователю = "", Поле = "")
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СокрЛП(ТекстСообщенияПользователю);
	Сообщение.Поле = Поле;
	
	Сообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти

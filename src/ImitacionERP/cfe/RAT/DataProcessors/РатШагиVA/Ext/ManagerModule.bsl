//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Если НЕ (ТонкийКлиент ИЛИ ВебКлиент) Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ЭкспортныеШаги() Экспорт
	
	Макет = ПолучитьМакет("ЭкспортныеШаги");
	
	СостояниеРазбора = СостояниеРазбора();
	
	Для Инд = 1 По Макет.КоличествоСтрок() Цикл
		
		Строка = Макет.ПолучитьСтроку(Инд);
		
		Если ЭтоГраницаБлокаКода(Строка) Тогда
			
			СостояниеРазбора.ЭтоБлокИсходногоКода = НЕ СостояниеРазбора.ЭтоБлокИсходногоКода;
			
		КонецЕсли;
		
		Если НЕ СостояниеРазбора.ЭтоБлокИсходногоКода И ЭтоЗаголовок(Строка) Тогда
			
			ОбработатьЗаголовок(Строка, СостояниеРазбора);
			
		КонецЕсли;
		
		ДобавитьКОписанию(СостояниеРазбора.ТекущееОписаниеШага, Строка);
		
	КонецЦикла;
	
	ПодготовитьШаги(СостояниеРазбора.ОписаниеШагов);
	
	Возврат СостояниеРазбора.ОписаниеШагов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СостояниеРазбора()
	
	Состояние = Новый Структура();
	
	Состояние.Вставить("ИерархияЗаголовков", Новый Массив);
	Состояние.Вставить("ОписаниеШагов", Новый Массив());
	Состояние.Вставить("ТекущееОписаниеШага", Неопределено);
	Состояние.Вставить("ЭтоБлокИсходногоКода", Ложь);
	
	Возврат Состояние;
	
КонецФункции

Функция ОписаниеШага(ИмяШага, Заголовки, ТипШага, УровеньЗаголовка)
	
	ОписаниеШага = Новый Структура;
	ОписаниеШага.Вставить("Имя", СокрЛП(ИмяШага));
	ОписаниеШага.Вставить("Метод", "");
	ОписаниеШага.Вставить("Группа", СтрСоединить(Заголовки, ". "));
	ОписаниеШага.Вставить("ТипШага", ТипШага);
	ОписаниеШага.Вставить("Строки", Новый Массив());
	ОписаниеШага.Вставить("Описание", "");
	ОписаниеШага.Вставить("УровеньЗаголовка", УровеньЗаголовка);
	
	Возврат ОписаниеШага;
	
КонецФункции

Функция ЭтоЗаголовок(Строка)
	
	Возврат СтрНачинаетсяС(Строка, "#");
	
КонецФункции

Функция ЭтоГраницаБлокаКода(Строка)
	
	Возврат СтрНачинаетсяС(СокрЛ(Строка), "```");
	
КонецФункции

Функция УровеньЗаголовка(Строка)
	
	Возврат СтрЧислоВхождений(Строка, "#");
	
КонецФункции

Функция ТекстЗаголовка(Строка, Уровень)
	
	Возврат СокрЛП(Сред(Строка, Уровень + 1));
	
КонецФункции

Процедура ПодготовитьШаги(ОписаниеШагов)
	
	Для Каждого ОписаниеШага Из ОписаниеШагов Цикл
		
		Если ПустаяСтрока(ОписаниеШага.Метод) Тогда
			ВызватьИсключение СтрШаблон("Не указан метод для шага '%1'", ОписаниеШага.Имя);
		КонецЕсли;
		
		ОписаниеШага.Описание = СтрСоединить(ОписаниеШага.Строки, Символы.ПС);
		ОписаниеШага.Удалить("Строки");
		ОписаниеШага.Удалить("УровеньЗаголовка");
		
	КонецЦикла;

КонецПроцедуры

Процедура ОбработатьЗаголовок(Строка, СостояниеРазбора)
	
	УровеньЗаголовка = УровеньЗаголовка(Строка);
	ТекстЗаголовка = ТекстЗаголовка(Строка, УровеньЗаголовка);
	
	Префиксы = Новый Соответствие;
	Префиксы.Вставить("Шаг:", Неопределено);
	Префиксы.Вставить("Условие:", "Условие");
	Префиксы.Вставить("Цикл:", "Цикл");
	
	ЕстьПрефикс = Ложь;
	ТипБлока = Неопределено;
	ЗначениеПрефикса = Неопределено;
	
	Для Каждого Префикс Из Префиксы Цикл
		Если РатСтроки.СтрокаСодержитПрефикс(ТекстЗаголовка, Префикс.Ключ, , Ложь) Тогда
			ЕстьПрефикс = Истина;
			ТипБлока = Префикс.Значение;
			ЗначениеПрефикса = Префикс.Ключ;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьПрефикс Тогда
		
		ИмяШага = СокрЛП(РатСтроки.СократитьПрефикс(ТекстЗаголовка, ЗначениеПрефикса, , Ложь));
		
		СостояниеРазбора.ТекущееОписаниеШага = ОписаниеШага(ИмяШага, СостояниеРазбора.ИерархияЗаголовков, ТипБлока, УровеньЗаголовка);
		
		СостояниеРазбора.ОписаниеШагов.Добавить(СостояниеРазбора.ТекущееОписаниеШага);
		
	ИначеЕсли СостояниеРазбора.ТекущееОписаниеШага = Неопределено
		ИЛИ СостояниеРазбора.ТекущееОписаниеШага.УровеньЗаголовка >= УровеньЗаголовка Тогда
		
		СостояниеРазбора.ТекущееОписаниеШага = Неопределено;
		
		Пока УровеньЗаголовка <= СостояниеРазбора.ИерархияЗаголовков.Количество() Цикл
			
			СостояниеРазбора.ИерархияЗаголовков.Удалить(УровеньЗаголовка - 1);
			
		КонецЦикла;
		
		СостояниеРазбора.ИерархияЗаголовков.Добавить(ТекстЗаголовка);
		
	КонецЕсли; // BSLLS:IfElseIfEndsWithElse-off
	
КонецПроцедуры

Процедура ДобавитьКОписанию(ОписаниеШага, Строка)
	
	ПрефиксМетод = "Метод:";
	
	Если ОписаниеШага <> Неопределено Тогда
		
		ОписаниеШага.Строки.Добавить(Строка);
		
		Если РатСтроки.СтрокаСодержитПрефикс(Строка, ПрефиксМетод, , Ложь) Тогда
			
			ОписаниеШага.Метод = СокрЛП(РатСтроки.СократитьПрефикс(Строка, ПрефиксМетод, , Ложь));
			
			Если СтрНачинаетсяС(ОписаниеШага.Метод, "`") И СтрЗаканчиваетсяНа(ОписаниеШага.Метод, "`") Тогда
				
				Сдвиг = 2;
				ОписаниеШага.Метод = Сред(ОписаниеШага.Метод, Сдвиг, СтрДлина(ОписаниеШага.Метод) - Сдвиг);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

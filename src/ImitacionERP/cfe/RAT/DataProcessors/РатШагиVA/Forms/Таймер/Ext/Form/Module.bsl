//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОписаниеПеременных

// Обработчик оповещения для выполнения при срабатывании таймера
&НаКлиенте
Перем Обработчик;

// Признак активности таймера
&НаКлиенте
Перем Включен;

// Параметры настройки интервалов ожидания таймера
&НаКлиенте
Перем ПараметрыОжидания;

// Признак включения отладочных сообщений
&НаКлиенте
Перем ОтладкаВключена;

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Старт(ОписаниеОповещения, Знач ВходныеПараметрыОжидания = Неопределено) Экспорт
	
	Включен = Истина;
	
	ПараметрыОжидания = ПараметрыОжидания();
	
	Если ВходныеПараметрыОжидания <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыОжидания, ВходныеПараметрыОжидания);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОжидания.НачальныйИнтервал) Тогда
		ПараметрыОжидания.ТекущийИнтервал = ПараметрыОжидания.НачальныйИнтервал;
	КонецЕсли;
	
	Обработчик = ОписаниеОповещения;
	ПодключитьОбработчикОжидания("Тик", ПараметрыОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Стоп() Экспорт
	
	Включен = Ложь;
	ОтключитьОбработчикОжидания("Тик");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтладку(Включена) Экспорт
	
	ОтладкаВключена = Включена;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыОжидания(Параметры) Экспорт
	
	ПараметрыОжидания = Параметры;
	
КонецПроцедуры

&НаКлиенте
Процедура Тик()
	
	Если ОтладкаВключена Тогда
		РатОбщийКлиентСервер.СообщитьПользователю("Тик");
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Обработчик, ЭтотОбъект);
	
	Если Включен Тогда
		Если ОтладкаВключена Тогда
			РатОбщийКлиентСервер.СообщитьПользователю(СтрШаблон("Пауза %1 сек.", ПараметрыОжидания.ТекущийИнтервал));
		КонецЕсли;
		ПодключитьОбработчикОжидания("Тик", ПараметрыОжидания.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыОжидания()
	
	ПараметрыОжидания = Новый Структура;
	ПараметрыОжидания.Вставить("МаксимальныйИнтервал", 15);
	ПараметрыОжидания.Вставить("НачальныйИнтервал", 1);
	ПараметрыОжидания.Вставить("ТекущийИнтервал", 1);
	ПараметрыОжидания.Вставить("КоэффициентУвеличенияИнтервала", 1.4);
	
	Возврат ПараметрыОжидания;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьПараметрыОжидания() Экспорт
	
	ПараметрыОжидания.ТекущийИнтервал = ПараметрыОжидания.ТекущийИнтервал * ПараметрыОжидания.КоэффициентУвеличенияИнтервала;
	
	Если ПараметрыОжидания.ТекущийИнтервал > ПараметрыОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОжидания.ТекущийИнтервал = ПараметрыОжидания.МаксимальныйИнтервал;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОписаниеПеременных

&НаКлиенте
// контекст фреймворка Vanessa-Automation
Перем Ванесса Экспорт; // BSLLS:ExportVariables-off

&НаКлиенте
// Структура, в которой хранится состояние сценария между выполнением шагов.
// Очищается перед выполнением каждого сценария.
Перем Контекст Экспорт; // BSLLS:ExportVariables-off

&НаКлиенте
// Структура, в которой можно хранить служебные данные между запусками сценариев.
// Существует, пока открыта форма Vanessa-Automation.
Перем КонтекстСохраняемый Экспорт; // BSLLS:ExportVariables-off

&НаКлиенте
// Структура, в которая хранит кэш ответов. Существует, пока открыта форма Vanessa-Automation.
Перем КэшЗапросов;

&НаКлиенте
// Переменная хранения таймера, нужно чтобы форма не уничтожилась и таймер жил
Перем Таймер;

#КонецОбласти

//@skip-check module-structure-top-region
#Область ИнтерфейсVA
// BSLLS:MissingParameterDescription-off
// BSLLS:MissingReturnedValueDescription-off

&НаКлиенте
// Функция экспортирует список шагов, которые реализованы в данной внешней обработке.
Функция ПолучитьСписокТестов(КонтекстФреймворкаBDD) Экспорт
	
	Ванесса = КонтекстФреймворкаBDD;
	
	Шаги = Новый Массив;
	
	Для Каждого ЭкспортныйШаг Из  ЭкспортныеШаги() Цикл
		
		Устаревший = СтрНайти(ЭкспортныйШаг.Описание, "@Deprecated");
		ЗарегистрироватьШаг(Шаги,
							ЭкспортныйШаг.Метод,
							ЭкспортныйШаг.Имя,
							ЭкспортныйШаг.Описание,
							ЭкспортныйШаг.Группа,
							ЭкспортныйШаг.ТипШага,
							Устаревший);
		
	КонецЦикла;
	
	Возврат Шаги;
	
КонецФункции
// BSLLS:MissingParameterDescription-on
// BSLLS:MissingReturnedValueDescription-on

#КонецОбласти

///////////////////////////////////////////////////
// Реализация шагов
///////////////////////////////////////////////////

//@skip-check module-structure-top-region
#Область Шаги

&НаСервереБезКонтекста
// Возвращает список экспортных шагов обработки.
//
// Возвращаемое значение:
//   Массив из Структура - массив описаний экспортных шагов
//
Функция ЭкспортныеШаги()
	
	Возврат Обработки.РатШагиVA.ЭкспортныеШаги();
	
КонецФункции

&НаКлиенте
Процедура ЗарегистрироватьШаг(МассивТестов,
							  Сниппет,
							  ПримерШага,
							  ОписаниеШага,
							  Знач Группа,
							  ТипБлока = Неопределено,
							  Устаревший = Истина)
	
	ИмяПроцедуры = Лев(Сниппет, СтрНайти(Сниппет, "(") - 1);
	
	Если Устаревший Тогда
		Группа = Строка(Группа) + ". Устаревшее";
	КонецЕсли;
	
	Ванесса.ДобавитьШагВМассивТестов(МассивТестов, Сниппет, ИмяПроцедуры, ПримерШага,
		ОписаниеШага, Группа, ТипБлока);
	
КонецПроцедуры

#Область НастройкаЗапросов

&НаКлиенте
Процедура ЯУстанавливаюНастройкиПодключенияКВнешнейСистеме(ИмяНастройки, Настройки) Экспорт
	
	НастройкиПодключения = РатШагиВанессыКлиент.СтруктураДанных(Настройки, Ложь);
	РатВызовСервисаКлиент.УстановитьНастройкиПодключения(Контекст, ИмяНастройки, НастройкиПодключения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯУстанавливаюЗаголовкиЗапросаКВнешнейСистеме(ИмяНастройки, Заголовки) Экспорт
	
	РатВызовСервисаКлиент.УстановитьЗаголовкиНастройкиПодключения(Контекст, ИмяНастройки, Заголовки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯУстанавливаюРазрешенныеКодыОтветовОтВнешнейСистемы(ИмяНастройки, КодыОтветов) Экспорт
	
	РатВызовСервисаКлиент.УстановитьРазрешенныеКодыОтветов(Контекст, ИмяНастройки, КодыОтветов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯУстанавливаюПараметрыЗадержкиПовторногоЗапроса(Настройки) Экспорт
	
	УстанавливаемыеПараметры = РатШагиВанессыКлиент.СтруктураДанных(Настройки, Ложь);
	ПараметрыЗадержки = ПараметрыЗадержкиПовторногоЗапроса();
	
	РатКоллекции.ДополнитьСтруктуру(ПараметрыЗадержки, УстанавливаемыеПараметры);
	Контекст.Вставить(КлючПараметрыЗадержкиПовторногоЗапроса(), ПараметрыЗадержки);
	
КонецПроцедуры

#КонецОбласти

// BSLLS:MissingParameterDescription-off
// BSLLS:UsingServiceTag-off

#Область УниверсальныеЗапросы

&НаКлиенте
Процедура ЯВыполняюЗапросКРесурсуВнешнейСистемы(Метод, Ресурс, ИмяНастройки) Экспорт
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, Ресурс, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯВыполняюЗапросКРесурсуВнешнейСистемыСПараметрами(Метод, Ресурс, ИмяНастройки, ТаблицаПараметров) Экспорт
	
	ПараметрыЗапроса = РатШагиВанессыКлиент.СтруктураДанных(ТаблицаПараметров, Ложь);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, Ресурс, Метод, ПараметрыЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСРезультатомЗапроса

&НаКлиенте
Процедура ЯСохраняюВПеременнуюРеквизитРезультатаЗапроса(ИмяПеременной, ПутьКРеквизиту) Экспорт
	
	Значение = ПолучитьРеквизитРезультата(ПутьКРеквизиту);
	
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитРезультатаЗапросаРавен(ПутьКРеквизиту, ЭталонноеЗначение) Экспорт
	
	Значение = ПолучитьРеквизитРезультата(ПутьКРеквизиту);
	
	Равны = СравнитьЗначения(Значение, ЭталонноеЗначение);
	
	Если НЕ Равны Тогда
		
		Сообщение = СтрШаблон("Значение реквизита <%1> оказалось равно <%2 (%4)>, а ожидали <%3 (%5)>",
							  ПутьКРеквизиту,
							  Значение,
							  ЭталонноеЗначение,
							  ТипЗнч(Значение),
							  ТипЗнч(ЭталонноеЗначение));
		
		ВызватьИсключение Сообщение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитРезультатаЗапросаНеРавен(ПутьКРеквизиту, ЭталонноеЗначение) Экспорт
	
	Значение = ПолучитьРеквизитРезультата(ПутьКРеквизиту);
	
	Равны = СравнитьЗначения(Значение, ЭталонноеЗначение);
	
	Если Равны Тогда
		
		Сообщение = СтрШаблон("Ожидали, что значение реквизита <%1> не равно <%2 (%3)>>",
							  ПутьКРеквизиту,
							  ЭталонноеЗначение,
							  ТипЗнч(ЭталонноеЗначение));
		
		ВызватьИсключение Сообщение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитРезультатаЗапросаСоответствуетШаблону(ПутьКРеквизиту, Шаблон) Экспорт
	
	Значение = Строка(ПолучитьРеквизитРезультата(ПутьКРеквизиту));
	
	Равны = Ванесса.СтрокаСоответствуетШаблону(Значение, Шаблон);
	
	Если НЕ Равны Тогда
		
		Сообщение = СтрШаблон("Ожидали, что значение реквизита <%1> соответствует шаблону <%2>, но это не так.
							  |Полученное значение: <%3>",
							  ПутьКРеквизиту,
							  Шаблон,
							  Значение);
		
		ВызватьИсключение Сообщение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЯСохраняюВПеременнуюКоличествоЭлементовРезультатаЗапроса(ИмяПеременной) Экспорт
	
	Тело = РезультатЗапроса().Тело;
	
	Если ТипЗнч(Тело) <> Тип("Массив") Тогда
		ВызватьИсключение "Результат запроса должен быть коллекцией";
	КонецЕсли;
	
	Значение = Тело.Количество();
	
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоЭлементовРезультатаЗапросаРавно(ЭталонноеЗначение) Экспорт
	
	Тело = РезультатЗапроса().Тело;
	
	Если ТипЗнч(Тело) <> Тип("Массив") Тогда
		ВызватьИсключение "Результат запроса должен быть коллекцией";
	КонецЕсли;
	
	Значение = Тело.Количество();
	
	Равны = СравнитьЗначения(Значение, ЭталонноеЗначение);
	
	Если НЕ Равны Тогда
		
		ТипЗначения = ТипЗнч(Значение);
		ТипЭталонногоЗначения = ТипЗнч(ЭталонноеЗначение);
		Сообщение = СтрШаблон("Количество элементов результата запроса оказалось равно <%1 (%3)>, а ожидали <%2 (%4)>",
							  Значение,
							  ЭталонноеЗначение,
							  ТипЗначения,
							  ТипЭталонногоЗначения);
		
		ВызватьИсключение Сообщение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДляКаждогоЭлементаРезультатаЗапроса(ИмяПеременнойЭлемента) Экспорт
	
	ИмяПеременнойИтератора = "Итератор_" + ИмяПеременнойЭлемента;
	
	Если НЕ ОбработатьПрерываниеЦикла(ИмяПеременнойИтератора) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПервыйВызов = НЕ Контекст.Свойство(ИмяПеременнойИтератора);
	
	Если ЭтоПервыйВызов Тогда
		
		Тело = РезультатЗапроса().Тело;
		
		Если ТипЗнч(Тело) <> Тип("Массив") Тогда
			ВызватьИсключение "Результат запроса должен быть коллекцией";
		КонецЕсли;
		
		ЗначениеИтератора = Новый Структура("Позиция, Данные", -1, Тело);
		Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременнойИтератора, ЗначениеИтератора);
		
	Иначе
		
		ЗначениеИтератора = Контекст[ИмяПеременнойИтератора];
		
	КонецЕсли;
	
	ЗначениеИтератора.Позиция = ЗначениеИтератора.Позиция + 1;
	
	Если ЗначениеИтератора.Позиция < ЗначениеИтератора.Данные.Количество() Тогда
		
		Контекст.Вставить(ИмяПеременнойЭлемента, ЗначениеИтератора.Данные[ЗначениеИтератора.Позиция]);
		Ванесса.УстановитьРезультатУсловия(Истина);
		
	Иначе
		
		Контекст.Удалить(ИмяПеременнойИтератора);
		Ванесса.УстановитьРезультатУсловия(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОбработатьПрерываниеЦикла(ИмяПеременнойИтератора)
	
	Завершение = Контекст.Свойство("ЗавершитьВыполнениеЦикла") И Контекст.ЗавершитьВыполнениеЦикла = Истина;
	
	Если Завершение Тогда
		
		Контекст.ЗавершитьВыполнениеЦикла = Ложь;
		Ванесса.УстановитьРезультатУсловия(Ложь);
		Если Контекст.Свойство(ИмяПеременнойИтератора) Тогда
			Контекст.Удалить(ИмяПеременнойИтератора);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НЕ Завершение;
	
КонецФункции

&НаКлиенте
Процедура ЯСохраняюВПеременнуюРеквизитРезультатаЗапросаСТипом(ИмяПеременной, ПутьКРеквизиту, ИмяТипа) Экспорт
	
	Значение = ПолучитьРеквизитРезультата(ПутьКРеквизиту);
	
	ПреобразованноеЗначение = РатШагиВанессыСервер.ПреобразоватьЗначение(Значение, ИмяТипа);
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, ПреобразованноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеДанных

&НаКлиенте
Процедура ЯСоздаюСтруктуру(ИмяПеременной, ТаблицаДанных) Экспорт
	
	Значение = РатШагиВанессыКлиент.СтруктураДанных(ТаблицаДанных, Ложь);
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯСоздаюМассив(ИмяПеременной, Данные) Экспорт
	
	Значение = РатШагиВанессыКлиент.ПростойМассивЗначений(Данные);
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯСоздаюТаблицуДанных(ИмяПеременной, ТаблицаДанных) Экспорт
	
	Данные = РатШагиВанессыКлиент.МассивСтруктур(ТаблицаДанных);
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Данные);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯСоздаюТипизированнуюСтруктуру(ИмяПеременной, ТаблицаДанных) Экспорт
	
	Данные = РатШагиВанессыКлиент.СтруктураДанных(ТаблицаДанных, Истина);
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Данные);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯСоздаюПеременнуюСТипомИЗначением(ИмяПеременной, ИмяТипа, Значение) Экспорт
	
	Данные = РатШагиВанессыКлиент.ТипизированноеЗначение(ИмяТипа, Значение);
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Данные);
	
КонецПроцедуры

#КонецОбласти

#Область ЗапросыКБазам1С

#Область ПолучениеДанных

&НаКлиенте
Процедура ЯИщуЗаписиВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	ЗаписиПоОтбору(ИмяТаблицы, Условия, ИмяНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюДанныеВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	ЗаписиПоОтбору(ИмяТаблицы, Условия, ИмяНастройки, "*");
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюЗаписьВнешнейСистемы(ИдентификаторЗаписи, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().GET;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеДанных

&НаКлиенте
Процедура ЯСоздаюЗаписьВнешнейСистемы(ИмяТаблицы, ИмяНастройки, Данные) Экспорт
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИмяТаблицы, Контекст);
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = РатШагиВанессыКлиент.ДанныеОбъектаИБ(Данные);
	
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯСоздаюЗаписиВнешнейСистемы(ИмяТаблицы, ИмяНастройки, Данные) Экспорт
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИмяТаблицы, Контекст);
	
	КоллекцияЗаписей = РатШагиВанессыКлиент.МассивСтруктур(Данные);
	
	ПараметрыПакета = Новый Структура;
	ПараметрыПакета.Вставить("Записи", КоллекцияЗаписей);
	ПараметрыПакета.Вставить("Индекс", -1);
	ПараметрыПакета.Вставить("ОписаниеЗапроса", ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод));
	ПараметрыПакета.Вставить("ПараметрыПовторногоЗапроса");
	
	СоздатьСледующуюЗапись(Неопределено, ПараметрыПакета);
	
КонецПроцедуры

#КонецОбласти

#Область ИзменениеДанных

&НаКлиенте
Процедура ЯИзменяюЗаписьВнешнейСистемы(ИдентификаторЗаписи, ИмяНастройки, Данные) Экспорт
	
	Метод = МетодыHTTP().PUT;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст);
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = РатШагиВанессыКлиент.ДанныеОбъектаИБ(Данные);
	
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯИзменяюЗаписиВнешнейСистемыСоответствующиеОтборуУстановив(ИмяТаблицы, ИмяНастройки, Отбор, НовыеЗначения) Экспорт
	
	НовыеЗначенияРеквизитов = РатШагиВанессыКлиент.СтруктураДанных(НовыеЗначения, Ложь);
	
	Ответ = ЗаписиПоОтбору(ИмяТаблицы, Отбор, ИмяНастройки);
	Данные = Ответ.Тело;
	
	Если НЕ ЗначениеЗаполнено(Данные) Тогда
		Если ОтладкаВключена() Тогда
			РатОбщийКлиентСервер.СообщитьПользователю("Нет данных соответствующих отбору для изменения");
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Метод = МетодыHTTP().PUT;
	АдресТаблицы = РатВызовСервисаКлиент.АдресРесурса(ИмяТаблицы, Контекст);
	
	МетаданныеТаблицы = МетаданныеТаблицы(ИмяТаблицы, ИмяНастройки);
	НормализоватьТаблицуРегистра(Данные, МетаданныеТаблицы);
	КлючевыеРеквизитыСтрокой = СтрСоединить(МетаданныеТаблицы.КлючевыеРеквизиты, ", ");
	
	ПараметрыПакета = Новый Структура;
	ПараметрыПакета.Вставить("Записи", Данные);
	ПараметрыПакета.Вставить("Метаданные", МетаданныеТаблицы);
	ПараметрыПакета.Вставить("АдресТаблицы", АдресТаблицы);
	ПараметрыПакета.Вставить("КлючевыеРеквизиты", КлючевыеРеквизитыСтрокой);
	ПараметрыПакета.Вставить("НовыеЗначения", НовыеЗначенияРеквизитов);
	ПараметрыПакета.Вставить("Индекс", -1);
	ПараметрыПакета.Вставить("ОписаниеЗапроса", ОписаниеЗапроса(ИмяНастройки, Неопределено, Метод));
	ПараметрыПакета.Вставить("ПараметрыПовторногоЗапроса");
	
	ИзменитьСледующуюЗапись(Неопределено, ПараметрыПакета);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПровожуДокументВнешнейСистемы(ИдентификаторДокумента, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().PUT;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса("Документ." + ИдентификаторДокумента, Контекст);
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = НастройкиЗаписиДокумента("Posting");
	
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯОтменяюПроведениеДокументаВнешнейСистемы(ИдентификаторДокумента, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().PUT;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса("Документ." + ИдентификаторДокумента, Контекст);
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = НастройкиЗаписиДокумента("UndoPosting");
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область УдалениеДанных

&НаКлиенте
Процедура ЯУдаляюЗаписьВнешнейСистемы(ИдентификаторЗаписи, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().DELETE;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯУдаляюЗаписиВнешнейСистемыСоответствующиеОтбору(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	Отбор = РатШагиВанессыКлиент.СтруктураПоиска(Условия);
	МетаданныеТаблицы = МетаданныеТаблицы(ИмяТаблицы, ИмяНастройки);
	
	КонтекстВыполнения = КонтекстВанессы();
	РатВызовСервисаКлиент.УдалитьЗаписиСоответствующиеОтбору(КонтекстВыполнения, МетаданныеТаблицы, ИмяНастройки, Отбор);
	
КонецПроцедуры

#КонецОбласти

#Область Условия

&НаКлиенте
Процедура ЕслиНеСуществуетЗаписьВнешнейСистемы(ИдентификаторЗаписи, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().GET;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст);
	
	Ответ = ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, , Ложь);
	
	КодыОтветов = КодыОтветов();
	
	Если Ответ.КодСостояния = КодыОтветов.Успешно Тогда
		
		Ванесса.УстановитьРезультатУсловия(Ложь);
		
	ИначеЕсли Ответ.КодСостояния = КодыОтветов.НеНайдено Тогда
		
		Ванесса.УстановитьРезультатУсловия(Истина);
		
	Иначе
		
		НастройкиПодключения = РатВызовСервисаКлиент.НастройкиПодключения(Контекст, ИмяНастройки);
		РатВызовСервисаКлиент.ПроверитьОтвет(Ответ, НастройкиПодключения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕслиНеСуществуетЗаписейВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	Ответ = ЗаписиПоОтбору(ИмяТаблицы, Условия, ИмяНастройки, , 1);
	
	Если ЗначениеЗаполнено(Ответ.Тело) Тогда
		
		Ванесса.УстановитьРезультатУсловия(Ложь);
		
	Иначе
		
		Ванесса.УстановитьРезультатУсловия(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуществуетЗаписьВнешнейСистемы(ИдентификаторЗаписи, ИмяНастройки) Экспорт
	
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст);
	ПроверитьСуществование(АдресРесурса, ИмяНастройки, СтрШаблон("Не найдена запись `%1`", ИдентификаторЗаписи), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НеСуществуетЗаписьВнешнейСистемы(ИдентификаторЗаписи, ИмяНастройки) Экспорт
	
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст);
	ПроверитьСуществование(АдресРесурса, ИмяНастройки, СтрШаблон("Запись `%1` существует", ИдентификаторЗаписи), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СуществуетЗаписьВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	ПроверитьСуществованиеПоУсловиям(ИмяТаблицы, Условия, ИмяНастройки, "Не найдена запись по переданным условиям", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НеСуществуетЗаписьВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	ПроверитьСуществованиеПоУсловиям(ИмяТаблицы, Условия, ИмяНастройки, "Существует запись соответствующая условиям", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ДвиженияДокументов

&НаКлиенте
Процедура ЯПолучаюДвиженияДокументаВнешнейСистемы(ИдентификаторДокумента, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().GET;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса("Документ." + ИдентификаторДокумента, Контекст);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюДвиженияДокументаПоРегиструВнешнейСистемы(ИдентификаторДокумента, ИмяРегистра, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().GET;
	Путь = СтрШаблон("Документ.%1.Движения.%2",
							ИдентификаторДокумента,
							ИмяРегистра);
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(Путь, Контекст);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯИзменяюДвиженияДокументаПоРегиструВнешнейСистемы(ИдентификаторДокумента, ИмяРегистра, ИмяНастройки, Данные) Экспорт
	
	Метод = МетодыHTTP().PUT;
	Путь = СтрШаблон("Документ.%1.Движения.%2",
							ИдентификаторДокумента,
							ИмяРегистра);
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(Путь, Контекст);
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = РатШагиВанессыКлиент.ДанныеНабораЗаписей(Данные);
	
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗадания

&НаКлиенте
Процедура ЯЗапускаюРегламентноеЗаданиеВнешнейСистемы(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	ЗапуститьРегламентноеЗадание(ИдентификаторЗадания, ИмяНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯЗапускаюРегламентноеЗаданиеВнешнейСистемыИОжидаюЗавершения(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	ОписаниеФоновогоЗадания = ЗапуститьРегламентноеЗадание(ИдентификаторЗадания, ИмяНастройки);
	ОжиданиеЗавершенияФоновогоЗадания(ИмяНастройки, ОписаниеФоновогоЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯОстанавливаюРегламентноеЗаданиеВнешнейСистемы(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса("РегламентныеЗадания." + ИдентификаторЗадания, Контекст, "Остановить");
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯОжидаюЗавершенияРегламентногоЗаданияВнешнейСистемы(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	ОписаниеФоновогоЗадания = АктивноеФоновоеЗадание(ИдентификаторЗадания, ИмяНастройки);
	ОжиданиеЗавершенияФоновогоЗадания(ИмяНастройки, ОписаниеФоновогоЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПерезапускаюРегламентноеЗаданиеВнешнейСистемы(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	ПерезапуститьРегламентноеЗадание(ИдентификаторЗадания, ИмяНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПерезапускаюРегламентноеЗаданиеВнешнейСистемыИОжидаюЗавершения(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	ОписаниеФоновогоЗадания = ПерезапуститьРегламентноеЗадание(ИдентификаторЗадания, ИмяНастройки);
	ОжиданиеЗавершенияФоновогоЗадания(ИмяНастройки, ОписаниеФоновогоЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯУстанавливаюМаксимальноеВремяОжиданияЗавершенияРегламентногоЗадания(ВремяОжидания) Экспорт
	
	МаксимальноеКоличествоЧастейВремени = 2;
	
	Если ПустаяСтрока(ВремяОжидания) Тогда
		ВызватьИсключение "Не указано время ожидания";
	КонецЕсли;
	
	Части = СтрРазделить(ВремяОжидания, " ", Ложь);
	
	Если Части.Количество() > МаксимальноеКоличествоЧастейВремени Тогда
		ВызватьИсключение "Неверная строка временного интервала " + ВремяОжидания;
	КонецЕсли;
	
	РезультатВремя = ИзвлечьВремяОжидания(Части);
	
	Ванесса.СохранитьЗначениеПеременнойВКонтекст("ВремяОжидания", РезультатВремя.Значение * РезультатВремя.Множитель);
	
КонецПроцедуры

&НаКлиенте
Функция ИзвлечьВремяОжидания(Части)
	
	Значение = 0;
	Множитель = 1;
	
	Подстрока1 = Части[0];
	Подстрока2 = ?(Части.Количество() > 1, Части[1], Неопределено);
	Подстрока1ЭтоЧисло = РатСтроки.СтрокаСодержитТолькоЦифры(Подстрока1, , Ложь);
	НеверныйФормат = Ложь;
	
	СекундВМинуте = 60;
	СекундВЧасе = 3600;
	
	Если Подстрока2 = Неопределено Тогда
		
		Если Подстрока1ЭтоЧисло Тогда
			Значение = Число(Части[0]);
		ИначеЕсли СтрСравнить(Подстрока1, "секунда") = 0 Тогда
			Значение = 1;
		ИначеЕсли СтрСравнить(Подстрока1, "минута") = 0 Тогда
			Значение = СекундВМинуте;
		ИначеЕсли СтрСравнить(Подстрока1, "час") = 0 Тогда
			Значение = СекундВЧасе;
		Иначе
			НеверныйФормат = Истина;
		КонецЕсли;
		
	ИначеЕсли Подстрока1ЭтоЧисло Тогда
		
		Значение = Число(Части[0]);
		
		Множители = Новый Структура;
		Множители.Вставить("секунда", 1);
		Множители.Вставить("секунды", 1);
		Множители.Вставить("секунд", 1);
		Множители.Вставить("минута", СекундВМинуте);
		Множители.Вставить("минуты", СекундВМинуте);
		Множители.Вставить("минут", СекундВМинуте);
		Множители.Вставить("час", СекундВЧасе);
		Множители.Вставить("часа", СекундВЧасе);
		Множители.Вставить("часов", СекундВЧасе);
		
		Если Множители.Свойство(Части[1]) Тогда
			Множитель = Множители[Части[1]];
		Иначе
			ВызватьИсключение "Неизвестная мера времени, доступно: секунда, минута, час";
		КонецЕсли;
		
	Иначе
		НеверныйФормат = Истина;
	КонецЕсли;
	
	Если НеверныйФормат Тогда
		ВызватьИсключение "Неверная строка временного интервала " + СтрСоединить(Части, " ");
	КонецЕсли;
	
	Результат = Новый Структура("Значение, Множитель", Значение, Множитель);
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЯВключаюРегламентноеЗаданиеВнешнейСистемы(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = СтрШаблон("РегламентныеЗадания/%1/Включить", ИдентификаторЗадания);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯВыключаюРегламентноеЗаданиеВнешнейСистемы(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = СтрШаблон("РегламентныеЗадания/%1/Выключить", ИдентификаторЗадания);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯВыключаюВсеРегламентныеЗаданияВнешнейСистемы(ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = "РегламентныеЗадания/Выключить";
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюПараметрыРегламентногоЗаданияВнешнейСистемы(ИдентификаторЗадания, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().GET;
	АдресРесурса = СтрШаблон("РегламентныеЗадания/%1", ИдентификаторЗадания);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

#КонецОбласти

#Область ЗапросыКВиртуальнымТаблицам

&НаКлиенте
Процедура ЯПолучаюОстаткиВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	ДанныеВиртуальнойТаблицы(ИмяТаблицы, "Остатки", Условия, ИмяНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюАгрегированныеОстаткиВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	ВыбираемыеПоля = РесурсыТаблицы(ИмяТаблицы, ИмяНастройки, "Остаток");
	
	ДополнительныеПараметры = ДополнительныеПараметрыВиртуальнойТаблицы(, , ВыбираемыеПоля);
	ДанныеВиртуальнойТаблицы(ИмяТаблицы, "Остатки", Условия, ИмяНастройки, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюОстаткиВнешнейСистемыНаПоУсловиям(ИмяТаблицы, ИмяНастройки, ДатаОстатков, Условия) Экспорт
	
	ДополнительныеПараметры = ДополнительныеПараметрыВиртуальнойТаблицы(ДатаОстатков);
	ДанныеВиртуальнойТаблицы(ИмяТаблицы, "Остатки", Условия, ИмяНастройки, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюОборотыВнешнейСистемыСПериодичностьюПоУсловиям(ИмяТаблицы, ИмяНастройки, Периодичность, Условия) Экспорт
	
	ДополнительныеПараметры = ДополнительныеПараметрыВиртуальнойТаблицы(, Периодичность);
	ДанныеВиртуальнойТаблицы(ИмяТаблицы, "Обороты", Условия, ИмяНастройки, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюАгрегированныеОборотыВнешнейСистемыСПериодичностьюПоУсловиям(ИмяТаблицы,
																			   ИмяНастройки,
																			   Периодичность,
																			   Условия) Экспорт
	
	ВыбираемыеПоля = РесурсыТаблицы(ИмяТаблицы, ИмяНастройки, "Оборот");
	
	Если ЗначениеЗаполнено(Периодичность) И СтрСравнить(Периодичность, "Период") <> 0 Тогда
		ВыбираемыеПоля.Добавить("Период");
	КонецЕсли;
	
	ДополнительныеПараметры = ДополнительныеПараметрыВиртуальнойТаблицы(, Периодичность, ВыбираемыеПоля);
	ДанныеВиртуальнойТаблицы(ИмяТаблицы, "Обороты", Условия, ИмяНастройки, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюОстаткиИОборотыВнешнейСистемыСПериодичностьюПоУсловиям(ИмяТаблицы,
																		 ИмяНастройки,
																		 Периодичность,
																		 Условия) Экспорт
	
	ДополнительныеПараметры = ДополнительныеПараметрыВиртуальнойТаблицы(, Периодичность);
	ДанныеВиртуальнойТаблицы(ИмяТаблицы, "ОстаткиИОбороты", Условия, ИмяНастройки, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюСрезПоследнихВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	ДанныеВиртуальнойТаблицы(ИмяТаблицы, "СрезПоследних", Условия, ИмяНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯПолучаюСрезПервыхВнешнейСистемыПоУсловиям(ИмяТаблицы, ИмяНастройки, Условия) Экспорт
	
	ДанныеВиртуальнойТаблицы(ИмяТаблицы, "СрезПервых", Условия, ИмяНастройки);
	
КонецПроцедуры

#КонецОбласти

#Область ПользователиИнформационнойБазы

&НаКлиенте
Процедура ЯПолучаюДанныеПользователяВнешнейСистемы(ИмяПользователя, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().GET;
	АдресРесурса = "Пользователи/" + ИмяПользователя;
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯСоздаюПользователяВнешнейСистемы(ИмяНастройки, Данные) Экспорт
	
	Метод = МетодыHTTP().PUT;
	АдресРесурса = "Пользователи";
	
	Тело = ТелоДляИзмененияПользователяИБ(Данные);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, Тело);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯИзменяюПользователяВнешнейСистемы(ИмяПользователя, ИмяНастройки, Данные) Экспорт
	
	Метод = МетодыHTTP().PUT;
	АдресРесурса = "Пользователи/" + ИмяПользователя;
	
	Тело = ТелоДляИзмененияПользователяИБ(Данные);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, Тело);
	
КонецПроцедуры

&НаКлиенте
Процедура СуществуетПользовательВнешнейСистемы(ИмяПользователя, ИмяНастройки) Экспорт
	
	АдресРесурса = "Пользователи/" + ИмяПользователя;
	ПроверитьСуществование(АдресРесурса, ИмяНастройки, СтрШаблон("Не найден пользователь `%1`", ИмяПользователя), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НеСуществуетПользовательВнешнейСистемы(ИмяПользователя, ИмяНастройки) Экспорт
	
	АдресРесурса = "Пользователи/" + ИмяПользователя;
	ПроверитьСуществование(АдресРесурса, ИмяНастройки, СтрШаблон("Существует пользователь `%1`", ИмяПользователя), Истина);
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияИзменений

&НаКлиенте
Процедура ЯРегистрируюИзменениеНаУзлеВнешнейСистемы(ИдентификаторОбъекта, ИдентификаторУзла, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().PUT;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторУзла, Контекст, "Изменения");
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = РатКоллекции.ЗначениеВМассиве(ИдентификаторОбъекта);
	
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯУдаляюРегистрациюИзмененийНаУзлеВнешнейСистемы(ИдентификаторОбъекта, ИдентификаторУзла, ИмяНастройки) Экспорт
	
	Метод = МетодыHTTP().DELETE;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторУзла, Контекст, "Изменения");
	
	ПараметрыЗапроса = РатКоллекции.ЗначениеВМассиве(ИдентификаторОбъекта);
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектЗарегистрированНаУзлеВнешнейСистемы(ИдентификаторОбъекта, ИдентификаторУзла, ИмяНастройки) Экспорт
	
	Зарегистрирован = ОбъектЗарегистрирован(ИдентификаторОбъекта, ИдентификаторУзла, ИмяНастройки);
	
	Если НЕ Зарегистрирован Тогда
		ВызватьИсключение "Объект не зарегистрирован на узле";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНеЗарегистрированНаУзлеВнешнейСистемы(ИдентификаторОбъекта, ИдентификаторУзла, ИмяНастройки) Экспорт
	
	Зарегистрирован = ОбъектЗарегистрирован(ИдентификаторОбъекта, ИдентификаторУзла, ИмяНастройки);
	
	Если Зарегистрирован Тогда
		ВызватьИсключение "Объект не зарегистрирован на узле";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ТабличныеЧасти

&НаКлиенте
Процедура ЯПолучаюСтрокиТабличнойЧастиОбъектаВнешнейСистемы(Знач ИмяТабличнойЧасти, ИдентификаторЗаписи, ИмяНастройки) Экспорт
	
	МетаданныеТаблицы = МетаданныеЗаписи(ИдентификаторЗаписи, ИмяНастройки);
	ИмяТабличнойЧасти = РатШагиВанессыКлиент.ИмяТабличнойЧасти(МетаданныеТаблицы, ИмяТабличнойЧасти);
	
	Метод = МетодыHTTP().GET;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст, ИмяТабличнойЧасти);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯДобавляюСтрокиТабличнойЧастиОбъектаВнешнейСистемы(Знач ИмяТабличнойЧасти,
															 ИдентификаторЗаписи,
															 ИмяНастройки,
															 ДанныеТабличнойЧасти) Экспорт
	
	МетаданныеТаблицы = МетаданныеЗаписи(ИдентификаторЗаписи, ИмяНастройки);
	ИмяТабличнойЧасти = РатШагиВанессыКлиент.ИмяТабличнойЧасти(МетаданныеТаблицы, ИмяТабличнойЧасти);
	МетаданныеТабличнойЧасти = МетаданныеТаблицы.ТабличныеЧасти[ИмяТабличнойЧасти];
	
	Метод = МетодыHTTP().PUT;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст, ИмяТабличнойЧасти);
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = РатШагиВанессыКлиент.ДанныеТабличнойЧасти(МетаданныеТабличнойЧасти, ДанныеТабличнойЧасти);
	
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯУдаляюСтрокиТабличнойЧастиОбъектаВнешнейСистемыПоОтбору(Знач ИмяТабличнойЧасти,
																   ИдентификаторЗаписи,
																   ИмяНастройки,
																   ДанныеОтбора) Экспорт
	
	МетаданныеТаблицы = МетаданныеЗаписи(ИдентификаторЗаписи, ИмяНастройки);
	ИмяТабличнойЧасти = РатШагиВанессыКлиент.ИмяТабличнойЧасти(МетаданныеТаблицы, ИмяТабличнойЧасти);
	МетаданныеТабличнойЧасти = МетаданныеТаблицы.ТабличныеЧасти[ИмяТабличнойЧасти];
	
	Тело = РатШагиВанессыКлиент.СтруктураРеквизитов(ДанныеОтбора, МетаданныеТабличнойЧасти, "filter");
	
	Метод = МетодыHTTP().DELETE;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст, ИмяТабличнойЧасти);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, Тело);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯОчищаюТабличнуюЧастьОбъектаВнешнейСистемы(Знач ИмяТабличнойЧасти, ИдентификаторЗаписи, ИмяНастройки) Экспорт
	
	МетаданныеТаблицы = МетаданныеЗаписи(ИдентификаторЗаписи, ИмяНастройки);
	ИмяТабличнойЧасти = РатШагиВанессыКлиент.ИмяТабличнойЧасти(МетаданныеТаблицы, ИмяТабличнойЧасти);
	
	Тело = Новый Структура("filter", Неопределено);
	
	Метод = МетодыHTTP().DELETE;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст, ИмяТабличнойЧасти);
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, Тело);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯИзменяюСтрокиТабличнойЧастиОбъектаВнешнейСистемыСКлючевымиПолями(Знач ИмяТабличнойЧасти,
																			Знач ИдентификаторЗаписи,
																			Знач ИмяНастройки,
																			Знач КлючевыеПоля,
																			Знач ДанныеТабличнойЧасти) Экспорт
	
	МетаданныеТаблицы = МетаданныеЗаписи(ИдентификаторЗаписи, ИмяНастройки);
	ИмяТабличнойЧасти = РатШагиВанессыКлиент.ИмяТабличнойЧасти(МетаданныеТаблицы, ИмяТабличнойЧасти);
	МетаданныеТабличнойЧасти = МетаданныеТаблицы.ТабличныеЧасти[ИмяТабличнойЧасти];
	
	КлючевыеПоля = СтрРазделить(КлючевыеПоля, ", ");
	КлючевыеПоля = РатШагиВанессыКлиент.СкорректироватьКлючевыеПоля(МетаданныеТабличнойЧасти, КлючевыеПоля);

	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст, ИмяТабличнойЧасти);
	Метод = МетодыHTTP().PATCH;
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = РатШагиВанессыКлиент.ДанныеТабличнойЧасти(МетаданныеТабличнойЧасти, ДанныеТабличнойЧасти);
	ОписаниеЗапроса.Тело.Вставить("filterBy", КлючевыеПоля);
	
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯИзменяюИлиДобавляюСтрокиТабличнойЧастиОбъектаВнешнейСистемыСКлючевымиПолями(Знач ИмяТабличнойЧасти,
																					   Знач ИдентификаторЗаписи,
																					   Знач ИмяНастройки,
																					   Знач КлючевыеПоля,
																					   Знач ДанныеТабличнойЧасти) Экспорт
	
	МетаданныеТаблицы = МетаданныеЗаписи(ИдентификаторЗаписи, ИмяНастройки);
	ИмяТабличнойЧасти = РатШагиВанессыКлиент.ИмяТабличнойЧасти(МетаданныеТаблицы, ИмяТабличнойЧасти);
	МетаданныеТабличнойЧасти = МетаданныеТаблицы.ТабличныеЧасти[ИмяТабличнойЧасти];
	
	КлючевыеПоля = СтрРазделить(КлючевыеПоля, ", ");
	КлючевыеПоля = РатШагиВанессыКлиент.СкорректироватьКлючевыеПоля(МетаданныеТабличнойЧасти, КлючевыеПоля);

	Метод = МетодыHTTP().PATCH;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторЗаписи, Контекст, ИмяТабличнойЧасти);
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод);
	ОписаниеЗапроса.Тело = РатШагиВанессыКлиент.ДанныеТабличнойЧасти(МетаданныеТабличнойЧасти, ДанныеТабличнойЧасти);
	ОписаниеЗапроса.Тело.Вставить("filterBy", КлючевыеПоля);
	ОписаниеЗапроса.Тело.Вставить("settings", Новый Структура("append", Истина));
	
	ИзменитьДанныеНаСервере(ОписаниеЗапроса);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЯВыполняюАлгоритмВоВнешнейСистеме(ИмяНастройки, ТекстАлгоритма) Экспорт
	
	Тело = ОписаниеПроизвольногоАлгоритма(ТекстАлгоритма);
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = "ВыполнитьАлгоритм";
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, Тело);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯВыполняюАлгоритмСПараметрамиВоВнешнейСистеме(Параметры, ИмяНастройки, ТекстАлгоритма) Экспорт
	
	СтруктураПараметров = РатШагиВанессыКлиент.СтруктураДанных(Параметры, Истина);
	Тело = ОписаниеПроизвольногоАлгоритма(ТекстАлгоритма, СтруктураПараметров);
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = "ВыполнитьАлгоритм";
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, Тело);
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеПроизвольногоАлгоритма(Алгоритм, Параметры = Неопределено, БезопасныйРежим = Ложь)
	
	Возврат Новый Структура("Алгоритм, Параметры, БезопасныйРежим", Алгоритм, Параметры, БезопасныйРежим);
	
КонецФункции

// BSLLS:MissingParameterDescription-on
// BSLLS:UsingServiceTag-on
#КонецОбласти

#Область АнализТабличныхДокументов

&НаКлиенте
Процедура ТабличныйДокументИзПеременнойРавенМакетуПоШаблону(ИмяПеременной, ИмяЭталонногоМакета) Экспорт
	
	КонтекстШагов = КонтекстВанессы();
	
	ИмяФайлаМакета = Неопределено;
	ТабДокБыло = РатШагиВанессыКлиент.ЭталонныйМакет(КонтекстШагов, ИмяЭталонногоМакета);
	ТабДокСтало = РатШагиВанессыКлиент.ПроверяемыйМакет(КонтекстШагов, ИмяПеременной);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("СравнениеПоШаблону", Истина);
	
	МакетыСовпали = Ложь;
	ТекстИсключения = "";
	Попытка
		Ванесса.ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям(ТабДокБыло, ТабДокСтало,,,, ДопПараметры);
		МакетыСовпали = Истина;
	Исключение
		ТекстИсключения = ОписаниеОшибки();
	КонецПопытки;
	
	Если НЕ МакетыСовпали Тогда
		ТабДокБыло1 = Ванесса.УбратьОтступыИзМакета(ТабДокБыло);
		ТабДокСтало1 = Ванесса.УбратьОтступыИзМакета(ТабДокСтало);
		
		Попытка
			Ванесса.ПроверитьРавенствоТабличныхДокументовТолькоПоЗначениям(ТабДокБыло1, ТабДокСтало1,,,, ДопПараметры);
			МакетыСовпали = Истина;
		Исключение
			ТекстИсключения = ОписаниеОшибки();
			
			Ванесса.УстановитьЗначенияТаблицДляСравнения(ТабДокБыло, ТабДокСтало);
			Ванесса.ПрикрепитьМакетКСценарию(ТабДокСтало, Ванесса.Локализовать("ТекущееЗначениеМакета"));
			Ванесса.ПрикрепитьМакетКСценарию(ТабДокБыло, Ванесса.Локализовать("ЭталонноеЗначениеМакета"));
			Если Ванесса.РежимСовместимостиБольшеИлиРавен837 Тогда
				Ванесса.ПрикрепитьМакетКСценарию(Ванесса.ПолучитьРазличияВМакетах(ТабДокБыло, ТабДокСтало),
												 Ванесса.Локализовать("Различия"));
			КонецЕсли;
			
			Ванесса.ПрикрепитьКСценариюДополнительныеФайлыСравненияЗначенияСЭталоном(ТабДокБыло, ТабДокСтало);
		КонецПопытки;
	КонецЕсли;
	
	Если НЕ МакетыСовпали Тогда
		
		ПараметрыАктуализации = Новый Структура;
		ПараметрыАктуализации.Вставить("ТипСравненияЗначения", "СравнениеТабличногоДокументаСМакетом");
		Если ИмяФайлаМакета <> Неопределено Тогда
			ПараметрыАктуализации.Вставить("ИмяФайлаМакета", ИмяФайлаМакета);
		КонецЕсли;
		Ванесса.УстановитьЗначенияДляАктуализацииПараметровШага(ПараметрыАктуализации);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВТабличномДокументеИзПеременнойИМакетеОдинаковыеИзображения(ИмяПеременной, ИмяЭталонногоМакета) Экспорт
	
	КонтекстШагов = КонтекстВанессы();
	
	РатШагиВанессыКлиент.СравнитьИзображенияТабличныхДокументов(КонтекстШагов, ИмяПеременной, ИмяЭталонногоМакета);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЯчейкиЭлементаСоответствуютПредикату(Знач ИмяОбласти, Знач ИмяЭлемента, ОписаниеПредиката) Экспорт
	
	ИмяОбласти = ПредикатОбластиТабличногоДокумента(ИмяОбласти);
	ИмяЭлемента = ЗаменитьСпециальнуюСтроку(ИмяЭлемента, "Текущий элемент");
		
	РатАнализТабличногоДокумента.ПроверитьСвойстваОбласти(Ванесса, ОписаниеПредиката, ИмяОбласти, ИмяЭлемента);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯЗапоминаюЗначенияСвойствЯчейкиЭлементаВПеременную(ИменаСвойств,
															 Знач ИмяОбласти,
															 Знач ИмяЭлемента,
															 ИмяПеременной) Экспорт
	
	ПроверитьЗаполненность(ИменаСвойств, "Не указаны имена свойств");
	ПроверитьЗаполненность(ИмяПеременной, "Не указано имя переменной");
	
	ИмяОбласти = ПредикатОбластиТабличногоДокумента(ИмяОбласти);
	ИмяЭлемента = ЗаменитьСпециальнуюСтроку(ИмяЭлемента, "Текущий элемент");
	
	ЗначенияСвойств = РатАнализТабличногоДокумента.ЗначенияСвойствОбласти(Ванесса, ИменаСвойств, ИмяОбласти, ИмяЭлемента);
	Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, ЗначенияСвойств);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияСвойствПеременнойСоответствуетПредикату(ИмяПеременной, ОписаниеПредиката) Экспорт
	
	ПроверитьЗаполненность(ОписаниеПредиката, "Не задано описание предиката свойств");
	ПроверитьЗаполненность(ИмяПеременной, "Не указано имя переменной");
		
	Предикат = РатАнализТабличногоДокумента.СформироватьПредикатПоОписанию(ОписаниеПредиката);
	Значение = Ванесса.ПолучитьЗначениеПеременнойИзКонтекста(ИмяПеременной);
	
	Результат = РатКоллекции.СравнитьСвойстваИсточникаСПредикатом(Значение, Предикат);
		
	Если Не Результат.ПолноеСоответствие Тогда

		Сообщения = Новый Массив;
		Сообщения.Добавить("Значения свойств не соответствуют предикату:");
		Сообщения.Добавить(Результат.ОписаниеРасхождений);

		ВызватьИсключение СтрСоединить(Сообщения, Символы.ПС);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЭмуляцияДействийПользователя

&НаКлиенте
Процедура ЯЭмулируюВызовКонтекстногоМеню() Экспорт
	
	СочетаниеКлавиш = РатЭмуляцияДействийПользователя.ИменованныеСочетанияКлавиш().ВызовКонтекстногоМеню;
	РатЭмуляцияДействийПользователя.ЭмулироватьНажатиеСочетанияКлавиш(Ванесса, СочетаниеКлавиш);
	
КонецПроцедуры

&НаКлиенте
Процедура ЯЭмулируюНажатиеВвод() Экспорт
	
	СочетаниеКлавиш = РатЭмуляцияДействийПользователя.ИменованныеСочетанияКлавиш().Ввод;
	РатЭмуляцияДействийПользователя.ЭмулироватьНажатиеСочетанияКлавиш(Ванесса, СочетаниеКлавиш);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ШаблоныЗапросов

&НаКлиенте
Функция ВремяОжидания()
	
	Если Контекст.Свойство("ВремяОжидания") Тогда
		Возврат Контекст.ВремяОжидания;
	Иначе
		Возврат 300;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ОбрабатыватьОшибкиФоновыхЗадания()
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ЗапросыКВнешнейСистеме

&НаКлиенте
Функция ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод, ПроверятьОтвет = Истина)
	
	НастройкиПодключения = РатВызовСервисаКлиент.НастройкиПодключения(Контекст, ИмяНастройки);
	
	ОписаниеЗапроса = РатВызовСервисаКлиент.ОписаниеЗапроса(НастройкиПодключения, Метод, АдресРесурса);
	ОписаниеЗапроса.ПроверятьОтвет = ПроверятьОтвет;
	ОписаниеЗапроса.Отладка = ОтладкаВключена();
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

&НаКлиенте
Функция ВыполнитьХТТПЗапрос(ИмяНастройки,
							АдресРесурса,
							Метод,
							Параметры = Неопределено,
							ПроверятьОтвет = Истина,
							ПоместитьВКонтекст = Истина)
	
	ОписаниеЗапроса = ОписаниеЗапроса(ИмяНастройки, АдресРесурса, Метод, ПроверятьОтвет);
	
	Если СтрСравнить(Метод, МетодыHTTP().GET) = 0 Тогда
		ОписаниеЗапроса.ПараметрыЗапроса = Параметры;
	Иначе
		ОписаниеЗапроса.Тело = Параметры;
	КонецЕсли;
	
	Ответ = РатВызовСервисаКлиент.ВыполнитьHttpЗапрос(ОписаниеЗапроса);
	
	Если ПоместитьВКонтекст Тогда
		СохранитьВКонтекст(Ответ);
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

&НаКлиенте
Процедура СохранитьВКонтекст(Ответ)
	
	Ванесса.СохранитьЗначениеПеременнойВКонтекст("РезультатЗапроса", Ответ);
	
КонецПроцедуры

// Метаданные таблицы.
// 
// Параметры:
//  ИмяТаблицы - Строка - Имя таблицы
//  ИмяНастройки - Строка - Имя настройки
// 
// Возвращаемое значение:
//  см. РатМетаданные.МетаданныеТаблицы
&НаКлиенте
Функция МетаданныеТаблицы(ИмяТаблицы, ИмяНастройки)
	
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИмяТаблицы, Контекст, "Метаданные");
	
	Данные = ДанныеИзКэша(АдресРесурса, ИмяНастройки);
	
	Если Данные <> Неопределено Тогда
		Возврат Данные;
	КонецЕсли;
	
	Метод = МетодыHTTP().GET;
	
	Результат = ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, , , Ложь);
	
	Данные = Результат.Тело;
	
	ЗапомнитьДанныеКэше(АдресРесурса, ИмяНастройки, Данные);
	
	Возврат Данные;
	
КонецФункции

&НаКлиенте
Функция МетаданныеЗаписи(Знач ИдентификаторЗаписи, ИмяНастройки)
	
	Части = СтрРазделить(ИдентификаторЗаписи, "/.");
	
	ИмяТаблицы = СтрШаблон("%1.%2", Части[0], Части[1]);
	
	Возврат МетаданныеТаблицы(ИмяТаблицы, ИмяНастройки);
	
КонецФункции

&НаКлиенте
Функция ЗаписиПоОтбору(ИмяТаблицы, Условия, ИмяНастройки, ПолучаемыеПоля = Неопределено, ОграничениеКоличества = Неопределено)
	
	КонтекстВанессы = КонтекстВанессы();
	Ответ = РатВызовСервисаКлиент.Поиск(КонтекстВанессы,
										ИмяТаблицы,
										Условия,
										ИмяНастройки,
										ПолучаемыеПоля,
										ОграничениеКоличества);
	
	СохранитьВКонтекст(Ответ);
	
	Возврат Ответ;
	
КонецФункции

&НаКлиенте
Процедура ДанныеВиртуальнойТаблицы(ИмяТаблицы,
								   ИмяВиртуальнойТаблицы,
								   Условия,
								   ИмяНастройки,
								   ДополнительныеПараметры = Неопределено)
	
	Метод = МетодыHTTP().POST;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИмяТаблицы, Контекст, ИмяВиртуальнойТаблицы);
	Тело = РатШагиВанессыКлиент.СтруктураПоиска(Условия, "Условия");
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры) Тогда
		РатКоллекции.ДополнитьСтруктуру(Тело, ДополнительныеПараметры);
	КонецЕсли;
	
	ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, Тело);
	
КонецПроцедуры

//@skip-check method-too-many-params
&НаКлиенте
// BSLLS:MissingParameterDescription-off
Функция ДополнительныеПараметрыВиртуальнойТаблицы(Период = Неопределено, // BSLLS:MissingReturnedValueDescription-off
												  Периодичность = Неопределено,
												  ПолучаемыеПоля = Неопределено,
												  ОграничениеКоличества = Неопределено)
// BSLLS:MissingParameterDescription-on
	ДополнительныеПараметры = Новый Структура();
	
	Если Период <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("Период", Период);
	КонецЕсли;
	
	Если Периодичность <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("Периодичность", Периодичность);
	КонецЕсли;
	
	Если ПолучаемыеПоля <> Неопределено Тогда
		Если ТипЗнч(ПолучаемыеПоля) = Тип("Строка") Тогда
			Поля = СтрРазделить(ПолучаемыеПоля, ", ", Ложь);
		Иначе
			Поля = ПолучаемыеПоля;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("Поля", Поля);
	КонецЕсли;
	
	Если ОграничениеКоличества <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("Количество", ОграничениеКоличества);
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

&НаКлиенте
Процедура ОжиданиеЗавершенияФоновогоЗадания(ИмяНастройки, ОписаниеФоновогоЗадания)
	
	Если НЕ ЗначениеЗаполнено(ОписаниеФоновогоЗадания) ИЛИ ОписаниеФоновогоЗадания.Состояние <> "Активно" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчика = Новый Структура("ИмяНастройки, Идентификатор",
										   ИмяНастройки,
										   ОписаниеФоновогоЗадания.УникальныйИдентификатор);
	ПараметрыОбработчика.Вставить("Состояние", ОписаниеФоновогоЗадания.Состояние);
	ПараметрыОбработчика.Вставить("ВремяОжидания", ВремяОжидания());
	ПараметрыОбработчика.Вставить("Окончание", ТекущаяУниверсальнаяДатаВМиллисекундах() + ПараметрыОбработчика.ВремяОжидания);
	
	Обработчик = Новый ОписаниеОповещения("ОбработчикОжиданиеЗавершенияФоновогоЗадания", ЭтотОбъект, ПараметрыОбработчика);
	
	Таймер = ЗапуститьНовыйТаймер(Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданиеЗавершенияФоновогоЗадания(ТаймерОбработчика, ПараметрыОбработчика) Экспорт
	
	ОписаниеОшибки = Неопределено;
	
	Попытка
		//@skip-check use-non-recommended-method
		Если ТекущаяУниверсальнаяДатаВМиллисекундах() <= ПараметрыОбработчика.Окончание Тогда
			
			ТаймерОбработчика.ОбновитьПараметрыОжидания();
			
			Метод = МетодыHTTP().GET;
			АдресРесурса = "ФоновыеЗадания/" + ПараметрыОбработчика.Идентификатор;
			
			Ответ = ВыполнитьХТТПЗапрос(ПараметрыОбработчика.ИмяНастройки, АдресРесурса, Метод);
			ПараметрыОбработчика.Состояние = Ответ.Тело.Состояние;
			
			Если ПараметрыОбработчика.Состояние = "Активно" Тогда
				Возврат;
			ИначеЕсли ЗначениеЗаполнено(Ответ.Тело.ИнформацияОбОшибке) И ОбрабатыватьОшибкиФоновыхЗадания() Тогда
				ОписаниеОшибки = Ответ.Тело.ИнформацияОбОшибке;
			КонецЕсли;
			
		КонецЕсли;
	Исключение
		ОписаниеОшибки = РатСтроки.ПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	ТаймерОбработчика.Стоп();
	Таймер = Неопределено;
	
	Если ОписаниеОшибки = Неопределено И ПараметрыОбработчика.Состояние = "Активно" Тогда
		ОписаниеОшибки = СтрШаблон("Задание не завершилось за %1 секунд", ПараметрыОбработчика.ВремяОжидания);
	КонецЕсли;
	
	Ванесса.ПродолжитьВыполнениеШагов(ЗначениеЗаполнено(ОписаниеОшибки), ОписаниеОшибки);
	
КонецПроцедуры

&НаКлиенте
Функция ОбъектЗарегистрирован(ИдентификаторОбъекта, ИдентификаторУзла, ИмяНастройки)
	
	Метод = МетодыHTTP().GET;
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса(ИдентификаторУзла, Контекст, "Изменения/" + ИдентификаторОбъекта);
	
	Ответ = ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод);
	
	Возврат Ответ.Тело.Статус = "Зарегистрирован" ИЛИ Ответ.Тело.Статус = "Отправлен";
	
КонецФункции

&НаКлиенте
Процедура ПроверитьСуществование(АдресРесурса, ИмяНастройки, Сообщение, Реверс)
	
	Метод = МетодыHTTP().GET;
	
	Ответ = ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, , Ложь);
	
	КодыОтветов = КодыОтветов();
	
	Если Реверс Тогда
		КодСостоянияОшибки = КодыОтветов.Успешно;
		ОжидаемыйКодСостояния = КодыОтветов.НеНайдено;
	Иначе
		КодСостоянияОшибки = КодыОтветов.НеНайдено;
		ОжидаемыйКодСостояния = КодыОтветов.Успешно;
	КонецЕсли;
	
	Успешно = Ответ.КодСостояния = ОжидаемыйКодСостояния;
	
	Если НЕ Успешно И Ответ.КодСостояния <> КодСостоянияОшибки Тогда
		НастройкиПодключения = РатВызовСервисаКлиент.НастройкиПодключения(Контекст, ИмяНастройки);
		РатВызовСервисаКлиент.ПроверитьОтвет(Ответ, НастройкиПодключения);
	КонецЕсли;
	
	Если НЕ Успешно Тогда
		ВызватьИсключение Сообщение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСуществованиеПоУсловиям(ИмяТаблицы, Условия, ИмяНастройки, Сообщение, Реверс)
	
	Ответ = ЗаписиПоОтбору(ИмяТаблицы, Условия, ИмяНастройки, , 1);
	
	Успешно = ЗначениеЗаполнено(Ответ.Тело);
	
	Если Реверс Тогда
		Успешно = НЕ Успешно;
	КонецЕсли;
	
	Если НЕ Успешно Тогда
		ВызватьИсключение Сообщение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция МетодыHTTP()
	
	Возврат РатОбщийКлиентСервер.МетодыHTTP();
	
КонецФункции

&НаКлиенте
Функция КодыОтветов()
	
	Возврат РатОбщийКлиентСервер.КодыОтветов();
	
КонецФункции

#КонецОбласти

#Область РаботаСПеременными

// Результат запроса.
// 
// Возвращаемое значение:
//  Структура - Структура ответа:
//  * Заголовки - Соответствие из Строка
//  * КодСостояния - Число
//  * Тело - Структура
//         - Массив из Произвольный
//         - Строка
&НаКлиенте
Функция РезультатЗапроса()
	
	Возврат Контекст.РезультатЗапроса;
	
КонецФункции

&НаКлиенте
Функция ПолучитьРеквизитРезультата(Знач ПутьКРеквизиту)
	
	РатШагиВанессыКлиент.ПодставитьПеременныеВСтроку(Контекст, ПутьКРеквизиту);
	
	Шаги = СтрРазделить(ПутьКРеквизиту, ".");
	
	Результат = РезультатЗапроса().Тело;
	
	Для Каждого Шаг Из Шаги Цикл
		
		Если СтрНайти(Шаг, "[") И СтрЗаканчиваетсяНа(Шаг, "]") Тогда
			
			ПозицияСкобки = СтрНайти(Шаг, "[");
			Реквизит = Лев(Шаг, ПозицияСкобки - 1);
			ПредставлениеИндекса = Сред(Шаг, ПозицияСкобки + 1, СтрДлина(Шаг) - ПозицияСкобки - 1);
			
		Иначе
			
			Реквизит = Шаг;
			ПредставлениеИндекса = Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Реквизит) Тогда
			Результат = Результат[Реквизит];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПредставлениеИндекса) Тогда
			
			ПредставлениеИндекса = СтрЗаменить(ПредставлениеИндекса, " ", "");
			Индекс = Число(ПредставлениеИндекса);
			
			Если Индекс < 0 Тогда
				Индекс = Результат.Количество() + Индекс;
			КонецЕсли;
			
			Результат = Результат[Индекс];
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция СравнитьЗначения(Значение, ЭталонноеЗначение)
	
	Равны = Ложь;
	
	Если ТипЗнч(Значение) = ТипЗнч(ЭталонноеЗначение) Тогда
		Равны = Значение = ЭталонноеЗначение;
	Иначе
		Равны = СравнитьЗначенияСПриведениемТипа(Значение, ЭталонноеЗначение);
	КонецЕсли;
	
	Возврат Равны;
	
КонецФункции

&НаСервереБезКонтекста
Функция СравнитьЗначенияСПриведениемТипа(Знач Значение, Знач ЭталонноеЗначение)
	
	ОписаниеТипов = Новый ОписаниеТипов(РатКоллекции.ЗначениеВМассиве(ТипЗнч(Значение)));
	Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
	ПриведенноеЭталонноеЗначение = РатПреобразования.ДесериализоватьЗначение(ЭталонноеЗначение,
																			 ОписаниеТипов,
																			 Статус,
																			 "Эталонное значение");
	
	Если НЕ Статус.Успешно Тогда
		ВызватьИсключение Статус.ОписаниеОшибки[0].Текст;
	КонецЕсли;
	
	Возврат Значение = ПриведенноеЭталонноеЗначение;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Функция ОтладкаВключена()
	
	Возврат Ванесса.Объект.DebugLog;
	
КонецФункции

&НаКлиенте
Функция РесурсыТаблицы(ИмяТаблицы, ИмяНастройки, СуффиксИмени)
	
	МетаданныеТаблицы = МетаданныеТаблицы(ИмяТаблицы, ИмяНастройки);
	РесурсыТаблицы = Новый Массив();
	
	Для Каждого Реквизит Из МетаданныеТаблицы.Реквизиты Цикл
		Если Реквизит.Вид = "Ресурс" Тогда
			РесурсыТаблицы.Добавить(Реквизит.Имя + СуффиксИмени);
		КонецЕсли;
	КонецЦикла;
	
	Возврат РесурсыТаблицы;
	
КонецФункции

#Область КэшЗапросов

&НаКлиенте
Функция ДанныеИзКэша(Идентификатор, ИмяНастройки)
	
	КэшНастройки = КэшЗапросовНастройки(ИмяНастройки);
	
	Возврат КэшНастройки.Получить(Идентификатор);
	
КонецФункции

&НаКлиенте
Процедура ЗапомнитьДанныеКэше(Идентификатор, ИмяНастройки, Данные)
	
	КэшНастройки = КэшЗапросовНастройки(ИмяНастройки);
	КэшНастройки.Вставить(Идентификатор, Данные);
	
КонецПроцедуры

&НаКлиенте
Функция КэшЗапросовНастройки(ИмяНастройки)
	
	Если КэшЗапросов = Неопределено Тогда
		КэшЗапросов = Новый Соответствие();
	КонецЕсли;
	
	КэшНастройки = КэшЗапросов[ИмяНастройки];
	
	Если КэшНастройки = Неопределено Тогда
		КэшНастройки = Новый Соответствие();
		КэшЗапросов.Вставить(ИмяНастройки, КэшНастройки);
	КонецЕсли;
	
	Возврат КэшНастройки;
	
КонецФункции

#КонецОбласти

#Область Таймер

&НаКлиенте
Функция НовыйТаймер()
	
	//@skip-check use-non-recommended-method
	НовыйТаймер = ПолучитьФорму("Обработка.РатШагиVA.Форма.Таймер"); // BSLLS:GetFormMethod-off
	НовыйТаймер.УстановитьОтладку(ОтладкаВключена());
	
	Возврат НовыйТаймер;
	
КонецФункции

&НаКлиенте
Функция ЗапуститьНовыйТаймер(Обработчик, ПараметрыОжидания = Неопределено)
	
	Ванесса.ЗапретитьВыполнениеШагов();
	Таймер = НовыйТаймер();
	Таймер.Старт(Обработчик, ПараметрыОжидания);
	
	Возврат Таймер;
	
КонецФункции

#КонецОбласти

#Область РегламентныеЗадания

&НаКлиенте
Функция АктивноеФоновоеЗадание(ИдентификаторЗадания, ИмяНастройки)
	
	Метод = МетодыHTTP().GET;
	Ответ = ЗапросРегламентныеЗадания(ИдентификаторЗадания, "ФоновоеЗадание", Метод, ИмяНастройки);
	
	Возврат ?(ЭтоОтветСОшибкой(Ответ), Неопределено, Ответ.Тело);
	
КонецФункции

&НаКлиенте
Функция ЗапуститьРегламентноеЗадание(ИдентификаторЗадания, ИмяНастройки)
	
	Метод = МетодыHTTP().POST;
	Ответ = ЗапросРегламентныеЗадания(ИдентификаторЗадания, "Запустить", Метод, ИмяНастройки);
	
	Возврат ?(ЭтоОтветСОшибкой(Ответ), Неопределено, Ответ.Тело);
	
КонецФункции

&НаКлиенте
Функция ПерезапуститьРегламентноеЗадание(ИдентификаторЗадания, ИмяНастройки)
	
	Метод = МетодыHTTP().POST;
	Ответ = ЗапросРегламентныеЗадания(ИдентификаторЗадания, "Перезапустить", Метод, ИмяНастройки);
	Возврат ЗапущенноеФоновоеЗадания(Ответ, ИдентификаторЗадания, ИмяНастройки);
	
КонецФункции

&НаКлиенте
Функция ЗапущенноеФоновоеЗадания(Ответ, ИдентификаторЗадания, ИмяНастройки)
	
	Если НЕ ЭтоОтветСОшибкой(Ответ) Тогда
		Возврат Ответ.Тело.Запущено;
	КонецЕсли;
	
	ОписаниеФоновогоЗадания = АктивноеФоновоеЗадание(ИдентификаторЗадания, ИмяНастройки);
	
	Если НЕ ЗначениеЗаполнено(ОписаниеФоновогоЗадания) Тогда
		Возврат ЗапуститьРегламентноеЗадание(ИдентификаторЗадания, ИмяНастройки);
	Иначе
		Возврат ОписаниеФоновогоЗадания;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ЗапросРегламентныеЗадания(ИдентификаторЗадания, Команда, Метод, ИмяНастройки)
	
	АдресРесурса = РатВызовСервисаКлиент.АдресРесурса("РегламентныеЗадания." + ИдентификаторЗадания, Контекст, Команда);
	Ответ = ВыполнитьХТТПЗапрос(ИмяНастройки, АдресРесурса, Метод, , ОбрабатыватьОшибкиФоновыхЗадания());
	
	КодыОтветов = КодыОтветов();
	
	Если Ответ.КодСостояния = КодыОтветов.ОшибкаСервера Тогда
		ТипОшибки = РатКоллекции.ЗначениеСтруктуры(Ответ.Тело, "id");
		Если ТипОшибки = "internal_configuration" Тогда
			Возврат Ответ;
		КонецЕсли;
	КонецЕсли;
	
	РатВызовСервисаКлиент.ПроверитьОтвет(Ответ, РатВызовСервисаКлиент.НастройкиПодключения(Контекст, ИмяНастройки));
	
	Возврат Ответ;
	
КонецФункции

&НаКлиенте
Функция ЭтоОтветСОшибкой(Ответ)
	
	Возврат Ответ.КодСостояния = КодыОтветов().ОшибкаСервера;
	
КонецФункции

#КонецОбласти

#Область ПовторныеЗапросы

&НаКлиенте
Процедура ИзменитьДанныеНаСервере(ОписаниеЗапроса,
								  ОбработчикЗавершения = Неопределено,
								  ПараметрыПовторногоЗапроса = Неопределено)
	
	Если ПараметрыПовторногоЗапроса = Неопределено Тогда
		ПараметрыПовторногоЗапроса = ПараметрыПовторногоЗапроса(ОписаниеЗапроса, ОбработчикЗавершения);
	КонецЕсли;
	
	ПараметрыПовторногоЗапроса.ТекущаяПопытка = 0;
	ОбработчикПовторныйЗапрос(Неопределено, ПараметрыПовторногоЗапроса);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыПовторногоЗапроса(ОписаниеЗапроса, ОбработчикЗавершения)
	
	ПараметрыЗадержки = ПараметрыЗадержкиПовторногоЗапроса();
	
	ПараметрыПовторногоЗапроса = Новый Структура;
	ПараметрыПовторногоЗапроса.Вставить("ОписаниеЗапроса", ОписаниеЗапроса);
	ПараметрыПовторногоЗапроса.Вставить("ОбработчикЗавершения", ОбработчикЗавершения);
	ПараметрыПовторногоЗапроса.Вставить("ПроверятьОтвет", ОписаниеЗапроса.ПроверятьОтвет);
	ПараметрыПовторногоЗапроса.Вставить("МаксимальноеКоличествоПопыток", ПараметрыЗадержки.МаксимальноеКоличествоПопыток);
	ПараметрыПовторногоЗапроса.Вставить("ТекущаяПопытка", 0);
	ПараметрыПовторногоЗапроса.Вставить("Таймер", Неопределено);
	ПараметрыПовторногоЗапроса.Вставить("ТекстыОшибок", Новый Массив);
	ПараметрыПовторногоЗапроса.Вставить("ОстановленоВыполнениеШагов", Ложь);
	ОписаниеЗапроса.ПроверятьОтвет = Ложь;
	
	ПараметрыПовторногоЗапроса.ТекстыОшибок.Добавить("Конфликт блокировок при выполнении транзакции");
	ПараметрыПовторногоЗапроса.ТекстыОшибок.Добавить("Lock conflict during the transaction");
	
	Возврат ПараметрыПовторногоЗапроса;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикПовторныйЗапрос(ТаймерОбработчика, ПараметрыПовторногоЗапроса) Экспорт
	
	ПараметрыПовторногоЗапроса.ТекущаяПопытка = ПараметрыПовторногоЗапроса.ТекущаяПопытка + 1;
	
	Если ПараметрыПовторногоЗапроса.ТекущаяПопытка > 1 Тогда
		Сообщение = СтрШаблон("Предпринята повторная попытка выполнить запрос %1/%2",
							  ПараметрыПовторногоЗапроса.ТекущаяПопытка,
							  ПараметрыПовторногоЗапроса.МаксимальноеКоличествоПопыток);
		РатОбщийКлиентСервер.СообщитьПользователю(Сообщение);
	КонецЕсли;
	
	Ответ = РатВызовСервисаКлиент.ВыполнитьHttpЗапрос(ПараметрыПовторногоЗапроса.ОписаниеЗапроса);
	
	ВыполнитьПовторныйЗапрос = НеобходимПовторныйЗапрос(Ответ, ПараметрыПовторногоЗапроса);
	
	Если НЕ ВыполнитьПовторныйЗапрос Тогда
		ОбработатьЗавершениеПовторныхЗапросов(Ответ, ПараметрыПовторногоЗапроса);
		Возврат;
	КонецЕсли;
	
	ДостигнутМаксимумПопыток = ДостигнутМаксимумПопыток(ПараметрыПовторногоЗапроса);
	ЭтоПервыйВызов = ПараметрыПовторногоЗапроса.ТекущаяПопытка = 1;
	
	Если ДостигнутМаксимумПопыток Тогда
		Проблема = РатВызовСервисаКлиент.ПроверитьОтвет(Ответ,
														ПараметрыПовторногоЗапроса.ОписаниеЗапроса.НастройкиПодключения,
														Ложь);
		ОписаниеОшибки = "Достигнут максимум попыток выполнить запрос!
						 |" + Проблема;
		ЗавершитьШагСПовторнымиЗапросами(ПараметрыПовторногоЗапроса, ОписаниеОшибки);
	ИначеЕсли ЭтоПервыйВызов Тогда
		Обработчик = Новый ОписаниеОповещения("ОбработчикПовторныйЗапрос", ЭтотОбъект, ПараметрыПовторногоЗапроса);
		ПараметрыПовторногоЗапроса.Таймер = ЗапуститьНовыйТаймер(Обработчик, ПараметрыЗадержкиПовторногоЗапроса());
		ПараметрыПовторногоЗапроса.ОстановленоВыполнениеШагов = Истина;
	Иначе
		ТаймерОбработчика.ОбновитьПараметрыОжидания();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗавершениеПовторныхЗапросов(Ответ, ПараметрыПовторногоЗапроса)
	
	Если ПараметрыПовторногоЗапроса.ПроверятьОтвет Тогда
		ОписаниеОшибки = РатВызовСервисаКлиент.ПроверитьОтвет(Ответ,
															  ПараметрыПовторногоЗапроса.ОписаниеЗапроса.НастройкиПодключения,
															  Ложь);
	КонецЕсли;
	
	СохранитьВКонтекст(Ответ);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Или ПараметрыПовторногоЗапроса.ОбработчикЗавершения = Неопределено Тогда
		ЗавершитьШагСПовторнымиЗапросами(ПараметрыПовторногоЗапроса, ОписаниеОшибки);
	Иначе
		ОчиститьПараметрыПовторногоЗапроса(ПараметрыПовторногоЗапроса);
		ВыполнитьОбработкуОповещения(ПараметрыПовторногоЗапроса.ОбработчикЗавершения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьШагСПовторнымиЗапросами(ПараметрыПовторногоЗапроса, ОписаниеОшибки = Неопределено)
	
	ОчиститьПараметрыПовторногоЗапроса(ПараметрыПовторногоЗапроса);
	
	Если ПараметрыПовторногоЗапроса <> Неопределено И ПараметрыПовторногоЗапроса.ОстановленоВыполнениеШагов Тогда
		
		Ванесса.ПродолжитьВыполнениеШагов(ЗначениеЗаполнено(ОписаниеОшибки), ОписаниеОшибки);
		
	ИначеЕсли ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		
		ВызватьИсключение ОписаниеОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПараметрыПовторногоЗапроса(ПараметрыПовторногоЗапроса)
	
	Если ПараметрыПовторногоЗапроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПовторногоЗапроса.Таймер <> Неопределено Тогда
		ПараметрыПовторногоЗапроса.Таймер.Стоп();
	КонецЕсли;
	
	ПараметрыПовторногоЗапроса.ТекущаяПопытка = 0;
	ПараметрыПовторногоЗапроса.Таймер = Неопределено;
	Таймер = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Функция НеобходимПовторныйЗапрос(Ответ, ПараметрыПовторногоЗапроса)
	
	Если Ответ.КодСостояния <> РатОбщийКлиентСервер.КодыОтветов().ОшибкаСервера Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстОшибки = Неопределено;
	
	Тело = РатВызовСервисаКлиент.ТекстТела(Ответ);
	
	Для Каждого ПроверяемыйТекстОшибки Из ПараметрыПовторногоЗапроса.ТекстыОшибок Цикл
		
		Если СтрНайти(Тело, ПроверяемыйТекстОшибки) Тогда
			ТекстОшибки = ПроверяемыйТекстОшибки;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТекстОшибки <> Неопределено;
	
КонецФункции

&НаКлиенте
Функция ДостигнутМаксимумПопыток(ПараметрыПовторногоЗапроса)
	
	ДостигнутМаксимум = ПараметрыПовторногоЗапроса.ТекущаяПопытка >= ПараметрыПовторногоЗапроса.МаксимальноеКоличествоПопыток;
	Если ДостигнутМаксимум Тогда
		Сообщение = СтрШаблон("Достигнут максимум (%1) попыток выполнить запрос",
							  ПараметрыПовторногоЗапроса.МаксимальноеКоличествоПопыток);
		РатОбщийКлиентСервер.СообщитьПользователю(Сообщение);
	КонецЕсли;
	
	Возврат ДостигнутМаксимум;
	
КонецФункции

&НаКлиенте
Функция ПараметрыЗадержкиПовторногоЗапроса()
	
	ПараметрыОжидания = Неопределено;
	
	Если Контекст.Свойство(КлючПараметрыЗадержкиПовторногоЗапроса(), ПараметрыОжидания) Тогда
		Возврат ПараметрыОжидания;
	КонецЕсли;
	
	ПараметрыОжидания = Новый Структура;
	ПараметрыОжидания.Вставить("МаксимальноеКоличествоПопыток", 3);
	ПараметрыОжидания.Вставить("НачальныйИнтервал", 3);
	
	Возврат ПараметрыОжидания;
	
КонецФункции

&НаКлиенте
Функция КлючПараметрыЗадержкиПовторногоЗапроса()
	Возврат "ПараметрыЗадержкиПовторногоЗапроса";
КонецФункции

//@skip-check bsl-variable-name-invalid
//@skip-check doc-comment-parameter-section
&НаКлиенте
Процедура СоздатьСледующуюЗапись(_, ПараметрыПакета) Экспорт // BSLLS:MissingParameterDescription-off
	
	ПараметрыПакета.Индекс = ПараметрыПакета.Индекс + 1;
	Если ПараметрыПакета.Индекс >= ПараметрыПакета.Записи.Количество() Тогда
		ЗавершитьШагСПовторнымиЗапросами(ПараметрыПакета.ПараметрыПовторногоЗапроса);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("СоздатьСледующуюЗапись", ЭтотОбъект, ПараметрыПакета);
	Запись = ПараметрыПакета.Записи[ПараметрыПакета.Индекс];
	
	ПараметрыПакета.ОписаниеЗапроса.Тело = РатШагиВанессыКлиент.ДанныеОбъектаИБ(Запись);
	ИзменитьДанныеНаСервере(ПараметрыПакета.ОписаниеЗапроса, Обработчик, ПараметрыПакета.ПараметрыПовторногоЗапроса);
	
КонецПроцедуры

//@skip-check bsl-variable-name-invalid
//@skip-check doc-comment-parameter-section
&НаКлиенте
Процедура ИзменитьСледующуюЗапись(_, ПараметрыПакета) Экспорт // BSLLS:MissingParameterDescription-off
	
	ПараметрыПакета.Индекс = ПараметрыПакета.Индекс + 1;
	
	Если ПараметрыПакета.Индекс >= ПараметрыПакета.Записи.Количество() Тогда
		ЗавершитьШагСПовторнымиЗапросами(ПараметрыПакета.ПараметрыПовторногоЗапроса);
		Возврат;
	КонецЕсли;
	
	Запись = ПараметрыПакета.Записи[ПараметрыПакета.Индекс];
	
	Если ПараметрыПакета.Метаданные.ЭтоРегистр Тогда
		Значения = Новый Структура(ПараметрыПакета.КлючевыеРеквизиты);
		ЗаполнитьЗначенияСвойств(Значения, Запись);
		РатКоллекции.ДополнитьСтруктуру(Значения, ПараметрыПакета.НовыеЗначения);
	Иначе
		Значения = ПараметрыПакета.НовыеЗначения;
	КонецЕсли;
	
	ИдентификаторЗаписи = РатВызовСервисаКлиент.ИдентификаторЗаписи(Запись, ПараметрыПакета.Метаданные);
	
	ПараметрыПакета.ОписаниеЗапроса.АдресРесурса = СтрШаблон("%1/%2", ПараметрыПакета.АдресТаблицы, ИдентификаторЗаписи);
	ПараметрыПакета.ОписаниеЗапроса.Тело = РатШагиВанессыКлиент.ДанныеОбъектаИБ(Значения);
	
	Обработчик = Новый ОписаниеОповещения("ИзменитьСледующуюЗапись", ЭтотОбъект, ПараметрыПакета);
	
	ИзменитьДанныеНаСервере(ПараметрыПакета.ОписаниеЗапроса, Обработчик, ПараметрыПакета.ПараметрыПовторногоЗапроса);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Функция ТелоДляИзмененияПользователяИБ(Данные)
	
	Тело = РатШагиВанессыКлиент.СтруктураДанных(Данные, Ложь);
	
	Если РатТипыДанных.ЭтоСтруктура(Тело) И Тело.Свойство("Роли") И ТипЗнч(Тело.Роли) = Тип("Строка") Тогда
		Тело.Роли = СтрРазделить(Тело.Роли, ";,", Ложь);
		Для Инд = 0 По Тело.Роли.ВГраница() Цикл
			Тело.Роли[Инд] = СокрЛП(Тело.Роли[Инд]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Тело;
	
КонецФункции

&НаКлиенте
Функция ЗаменитьСпециальнуюСтроку(ИсходнаяСтрока, СпециальнаяСтрока, НовоеЗначение = Неопределено)
	
	Результат = ИсходнаяСтрока;
	
	Если СтрСравнить(СокрЛП(ИсходнаяСтрока), СпециальнаяСтрока) = 0 Тогда
		
		Результат = НовоеЗначение;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Предикат области табличного документа.
// 
// Параметры:
//  ИмяОбласти - Строка - описание определяемой области табличного документа
// 
// Возвращаемое значение:
//  Неопределено - Текущая область
//  Строка - Имя области
//  Структура - предикат поиска области:
//  * Текст - Строка - область ищется содержанию в тексте области заданной строки
&НаКлиенте
Функция ПредикатОбластиТабличногоДокумента(Знач ИмяОбласти)
	
	НормализованноеИмяОбласти = ВРег(СокрЛП(ИмяОбласти));
	ТокенТекущаяЯчейка = "ТЕКУЩАЯ ЯЧЕЙКА";
	ТокенПоискТекста = "СОДЕРЖИТ ТЕКСТ:";
	
	Если Не ЗначениеЗаполнено(НормализованноеИмяОбласти) Или НормализованноеИмяОбласти = ТокенТекущаяЯчейка Тогда
		
		Результат = Неопределено;
		
	ИначеЕсли СтрНачинаетсяС(НормализованноеИмяОбласти, ТокенПоискТекста) Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Текст", Сред(СокрЛ(ИмяОбласти), СтрДлина(ТокенПоискТекста) + 1));
		
	Иначе
		
		Результат = СокрЛП(ИмяОбласти);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполненность(Значение, СообщениеОбОшибке)
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		
		ВызватьИсключение СообщениеОбОшибке;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция КонтекстВанессы()
	
	Возврат РатШагиВанессыКлиент.КонтекстШагов(Ванесса, Контекст, КонтекстСохраняемый);
	
КонецФункции

&НаКлиенте
Функция НастройкиЗаписиДокумента(РежимЗаписи)
	
	НастройкиЗаписи = Новый Структура("writeMode", РежимЗаписи);
	Возврат Новый Структура("writeSettings", НастройкиЗаписи);
	
КонецФункции

&НаКлиенте
Процедура НормализоватьТаблицуРегистра(Данные, Метаданные)
	
	Если НЕ Метаданные.ЭтоРегистр Или Метаданные.КлючевыеРеквизиты.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	Для Инд = 0 По Данные.ВГраница() Цикл
		Данные[Инд] = Новый Структура(Метаданные.КлючевыеРеквизиты[0], Данные[Инд]);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

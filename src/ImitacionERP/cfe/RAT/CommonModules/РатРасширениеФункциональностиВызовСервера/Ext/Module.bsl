//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Определяет имя модуля адаптации конфигурации для указанного модуля.
//
// Параметры:
//   ИмяМодуля - Строка - имя модуля, для которого определяется имя адаптации
//   НаКлиенте - Булево - признак клиентского модуля
//   НаСервере - Булево - признак серверного модуля
//
// Возвращаемое значение:
//   Строка, Неопределено - имя модуля адаптации или Неопределено, если модуль не найден
//
Функция ИмяМодуляАдаптацииКонфигурации(Знач ИмяМодуля, Знач НаКлиенте, Знач НаСервере) Экспорт
	
	ИмяОбщегоМодуля = ИмяМодуля + "_" + РатРасширениеФункциональности.ИмяКонфигурации();
	
	ИменаМодулейАдаптации = ИменаМодулейАдаптации("Конфигурация", НаКлиенте, НаСервере, ИмяОбщегоМодуля);
	
	Для Каждого ИмяМодуляАдаптации Из ИменаМодулейАдаптации Цикл
		Если СтрСравнить(ИмяМодуляАдаптации, ИмяОбщегоМодуля) = 0 Тогда
			Возврат ИмяМодуляАдаптации;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает имена модулей адаптации решения для указанного модуля.
//
// Параметры:
//   ИмяМодуля - Строка - имя модуля, для которого определяются имена адаптаций
//   НаКлиенте - Булево - признак клиентского модуля
//   НаСервере - Булево - признак серверного модуля
//
// Возвращаемое значение:
//   Массив из Строка - имена модулей адаптации
//
Функция ИменаМодулейАдаптацииРешения(Знач ИмяМодуля, Знач НаКлиенте, Знач НаСервере) Экспорт
	
	Возврат ИменаМодулейАдаптации("Решение", НаКлиенте, НаСервере, ИмяМодуля);
	
КонецФункции

// Возвращает имя текущей конфигурации.
//
// Возвращаемое значение:
//   Строка - имя конфигурации
//
Функция ИмяКонфигурации() Экспорт
	
	Возврат Метаданные.Имя;
	
КонецФункции

// Возвращает имена модулей клиентских команд.
//
// Возвращаемое значение:
//   Массив из Строка - имена модулей клиентских команд
//
Функция ИменаМодулейКлиентскихКоманд() Экспорт
	
	Возврат ИменаМодулейПодсистемы(Метаданные.Подсистемы.РатРасширениеФункциональности.Подсистемы.КлиентскиеКоманды, Истина, Ложь);
	
КонецФункции

// Возвращает имена модулей сервиса.
//
// Возвращаемое значение:
//   Массив из Строка - имена модулей сервиса
//
Функция ИменаМодулейСервиса() Экспорт
	
	Возврат ИменаМодулейПодсистемы(Метаданные.Подсистемы.РатРасширениеФункциональности.Подсистемы.Сервис, Ложь, Истина);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает имена модулей адаптации для указанной подсистемы.
//
// Параметры:
//   ИмяПодсистемы - Строка - имя подсистемы
//   НаКлиенте - Булево - признак клиентского модуля
//   НаСервере - Булево - признак серверного модуля
//   ПрефиксИмениМодуля - Строка - префикс имени модуля для фильтрации
//
// Возвращаемое значение:
//   Массив из Строка - имена модулей адаптации
//
Функция ИменаМодулейАдаптации(ИмяПодсистемы, НаКлиенте, НаСервере, ПрефиксИмениМодуля)
	
	Подсистема = Метаданные.Подсистемы.РатАдаптацияФункциональности.Подсистемы.Найти(ИмяПодсистемы);
	
	Возврат ИменаМодулейПодсистемы(Подсистема, НаКлиенте, НаСервере, ПрефиксИмениМодуля);
	
КонецФункции

// Возвращает имена модулей указанной подсистемы.
//
// Параметры:
//   Подсистема - МетаданныеПодсистема - подсистема метаданных
//   НаКлиенте - Булево - признак клиентского модуля
//   НаСервере - Булево - признак серверного модуля
//   ПрефиксИмениМодуля - Строка, Неопределено - префикс имени модуля для фильтрации
//
// Возвращаемое значение:
//   Массив из Строка - имена модулей подсистемы
//
Функция ИменаМодулейПодсистемы(Подсистема, НаКлиенте, НаСервере, ПрефиксИмениМодуля = Неопределено)
	
	Модули = Новый Массив();
	
	Для Каждого Объект Из Подсистема.Состав Цикл
		
		Если НЕ Метаданные.ОбщиеМодули.Содержит(Объект) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПрефиксИмениМодуля = Неопределено ИЛИ СтрНачинаетсяС(Объект.Имя, ПрефиксИмениМодуля) Тогда
			Модули.Добавить(Объект.Имя);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Модули;
	
КонецФункции

#КонецОбласти

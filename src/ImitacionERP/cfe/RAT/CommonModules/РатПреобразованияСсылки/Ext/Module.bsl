//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Сериализует ссылочное значение в структуру для JSON.
//
// Параметры:
//   Значение - ЛюбаяСсылка - Ссылка на объект информационной базы
//   Тип - Тип - Тип сериализуемого значения
//
// Возвращаемое значение:
//   Структура - Сериализованная ссылка:
//     * type - Строка - представление типа (например, "CatalogRef.Контрагенты")
//     * id - Строка - идентификатор объекта (UUID, имя предопределенного или имя элемента перечисления)
//     * presentation - Строка - строковое представление объекта
//
Функция СериализоватьСсылку(Значение, Тип) Экспорт
	
	Результат = Новый Структура("type, id, presentation");
	Результат.type = РатТипыДанных.ПредставлениеТипа(Тип);
	Результат.presentation = Строка(Значение);
	
	ЧастиИмени = СтрРазделить(Результат.type, ".");
	
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Результат.id = "";
	ИначеЕсли ЧастиИмени[0] = "EnumRef" Тогда
		Результат.id = ИмяЭлементаПеречисления(Значение);
	ИначеЕсли ЧастиИмени[0] = "BusinessProcessRoutePointRef" Тогда
		Результат.id = XMLСтрока(Значение);
	Иначе
		Результат.id = Строка(Значение.УникальныйИдентификатор());
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сериализует реквизит составного типа в структуру для JSON.
//
// Параметры:
//   Значение - Произвольный - Значение реквизита составного типа
//
// Возвращаемое значение:
//   Структура, Неопределено - Сериализованное значение:
//     * type - Строка - представление типа
//     * id - Произвольный - сериализованное значение
//
Функция СериализоватьРеквизитСоставногоТипа(Значение) Экспорт
	
	Если Значение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура("type, id");
	Результат.type = РатТипыДанных.ПредставлениеТипа(ТипЗнч(Значение));
	Результат.id = РатПреобразования.СериализоватьЗначение(Значение);
	
	Возврат Результат;
	
КонецФункции

// Десериализует ссылку из переданного идентификатора.
//
// Параметры:
//   ПараметрыДесериализации - Структура - Параметры для десериализации:
//     * ОжидаемыйТип - Тип - ожидаемый тип ссылки
//     * Значение - Строка - идентификатор объекта (UUID, имя предопределенного или имя элемента перечисления)
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   ЛюбаяСсылка, Неопределено - Найденная ссылка или Неопределено при ошибке
//
Функция ДесериализоватьСсылку(ПараметрыДесериализации, СтатусОбработкиЗапроса) Экспорт
	
	ОжидаемыйТип = ПараметрыДесериализации.ОжидаемыйТип;
	Идентификатор = ПараметрыДесериализации.Значение;
	ПараметрыДесериализации.Вставить("Метаданные", Метаданные.НайтиПоТипу(ОжидаемыйТип));
	ПараметрыДесериализации.Вставить("ТипМетаданных", РатМетаданные.ТипМетаданных(ПараметрыДесериализации.Метаданные.ПолноеИмя()));
	
	ИмяТаблицы = СтрШаблон("%1.%2", ПараметрыДесериализации.ТипМетаданных.Имя, ПараметрыДесериализации.Метаданные.Имя);
	ПараметрыДесериализации.Вставить("ИмяТаблицы", ИмяТаблицы);
	ПараметрыДесериализации.Вставить("Менеджер", РатМетаданные.МенеджерТаблицы(ИмяТаблицы, СтатусОбработкиЗапроса));
	
	Если Идентификатор = Неопределено ИЛИ ПустаяСтрока(Идентификатор) Тогда
		
		Результат = ПараметрыДесериализации.Менеджер.ПустаяСсылка();
		
	ИначеЕсли ПараметрыДесериализации.ТипЗначения <> Тип("Строка") Тогда
		
		ВызватьИсключение "Не верный тип данных " + ПараметрыДесериализации.ТипЗначения;
		
	ИначеЕсли РатМетаданные.ЭтоТипПеречисления(ОжидаемыйТип) Тогда
		
		Результат = ДесериализоватьПеречисление(ПараметрыДесериализации, СтатусОбработкиЗапроса);
		
	Иначе
		
		Результат = НайтиСсылкуПоИдентификатору(ПараметрыДесериализации, СтатусОбработкиЗапроса);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Десериализует реквизит составного типа.
//
// Параметры:
//   Значение - Произвольный - Значение для десериализации
//   ПараметрыДесериализации - Структура - Параметры для десериализации
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Произвольный, Неопределено - Десериализованное значение или Неопределено при ошибке
//
Функция ДесериализоватьРеквизитСоставногоТипа(Значение, ПараметрыДесериализации, СтатусОбработкиЗапроса) Экспорт
	
	Результат = РатПреобразованияВспомогательные.ДесериализоватьТипизированноеЗначение(Значение, СтатусОбработкиЗапроса, ПараметрыДесериализации);
	
	ТипНеСоответствуетОграничению = СтатусОбработкиЗапроса.Успешно
		И Результат <> Неопределено
		И ПараметрыДесериализации.ОграничениеТипа <> Неопределено
		И Не ПараметрыДесериализации.ОграничениеТипа.СодержитТип(ТипЗнч(Результат));
	
	Если ТипНеСоответствуетОграничению Тогда
		
		КлассыОшибок = РатОбщий.КлассыОшибок();
		ТекстОшибки = "Переданный тип не входит в состав типа реквизита";
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.Десериализация, ТекстОшибки);
		
		Результат = Неопределено;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Десериализует значение перечисления из переданного идентификатора.
//
// Параметры:
//   ПараметрыДесериализации - Структура - Параметры для десериализации:
//     * ОжидаемыйТип - Тип - ожидаемый тип перечисления
//     * Значение - Строка - имя элемента перечисления
//     * Менеджер - МенеджерПеречисления - менеджер перечисления
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   ПеречислениеСсылка, Неопределено - Значение перечисления или Неопределено при ошибке
//
Функция ДесериализоватьПеречисление(ПараметрыДесериализации, СтатусОбработкиЗапроса) Экспорт
	
	Идентификатор = ПараметрыДесериализации.Значение;
	
	Для Каждого Значение Из ПараметрыДесериализации.Менеджер Цикл
		
		Если СтрСравнить(Строка(Значение), Идентификатор) = 0 Тогда
			Возврат Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	ОжидаемыйТип = ПараметрыДесериализации.ОжидаемыйТип;
	Попытка
		
		Возврат XMLЗначение(ОжидаемыйТип, Идентификатор);
		
	Исключение
		ТекстОшибки = СтрШаблон("Не удалось определить значение перечисления '%1' (%2)", Идентификатор, ОжидаемыйТип);
		ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), ТекстОшибки);
		КлассОшибки = РатОбщий.КлассыОшибок().НеНайденаЗаписьИБ;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецПопытки;
	
	Возврат Неопределено;
	
КонецФункции

// Находит ссылку на объект информационной базы по переданному идентификатору.
// Поиск выполняется последовательно:
// + По уникальному идентификатору (если это UUID)
// + По имени предопределенного элемента (если объект поддерживает предопределенные)
// + По реквизиту объекта (если не найдено выше)
//
// Параметры:
//   ПараметрыДесериализации - Структура - Параметры для десериализации:
//     * Значение - Строка - идентификатор объекта
//     * ТипМетаданных - см. РатМетаданные.ТипМетаданных
//     * ИмяТаблицы - Строка - полное имя таблицы
//     * Менеджер - МенеджерОбъекта - менеджер объекта
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   ЛюбаяСсылка, Неопределено - Найденная ссылка или Неопределено при ошибке
//
Функция НайтиСсылкуПоИдентификатору(ПараметрыДесериализации, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Неопределено;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Результат;
	КонецЕсли;
	
	Идентификатор = ПараметрыДесериализации.Значение;
	ТипМетаданных = ПараметрыДесериализации.ТипМетаданных;
	
	Если ТипМетаданных.Поиск.Найти("Ссылка") <> Неопределено И РатСтроки.ЭтоУникальныйИдентификатор(Идентификатор) Тогда
		
		Результат = РатИнформационнаяБаза.СсылкаПоУникальномуИдентификатору(Идентификатор, ПараметрыДесериализации.ИмяТаблицы, СтатусОбработкиЗапроса);
		
		Если СтатусОбработкиЗапроса.Успешно И ЗначениеЗаполнено(Результат) Тогда
			Возврат Результат;
		КонецЕсли;
		
	КонецЕсли;
		
	Если ПараметрыДесериализации.ТипМетаданных.Предопределенные
		И РатОбщий.ЭтоВалидныйИдентификатор(Идентификатор)
		И РатМетаданные.ТаблицаСодержитПредопределенный(ПараметрыДесериализации.ИмяТаблицы, Идентификатор) Тогда
		
		Результат = ПараметрыДесериализации.Менеджер[Идентификатор];
		
	Иначе
		
		Результат = РатИнформационнаяБаза.СсылкаПоРеквизиту(Идентификатор, ПараметрыДесериализации.ИмяТаблицы, СтатусОбработкиЗапроса);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает имя элемента перечисления в формате XML.
//
// Параметры:
//   ЗначениеПеречисления - ПеречислениеСсылка - Значение перечисления
//
// Возвращаемое значение:
//   Строка - Имя элемента перечисления в формате XML
//
Функция ИмяЭлементаПеречисления(ЗначениеПеречисления) Экспорт
	
	Возврат XMLСтрока(ЗначениеПеречисления);

КонецФункции

#КонецОбласти

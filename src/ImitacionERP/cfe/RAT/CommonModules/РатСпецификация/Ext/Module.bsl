//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Устанавливает описание сервиса в OpenAPI спецификации.
// 
// Параметры:
//  Спецификация - см. РатOpenAPI.СпецификацияСервиса
//  Представление - Строка - Название сервиса
//  Описание - Строка - Подробное описание сервиса
Процедура ОписаниеСервиса(Спецификация, Представление, Описание) Экспорт
	
	Спецификация.info.title = Представление;
	Спецификация.info.description = Описание;
	
КонецПроцедуры

// Добавляет сервер в OpenAPI спецификацию.
// 
// Параметры:
//  Спецификация - см. РатOpenAPI.СпецификацияСервиса
//  Адрес - Строка - URL адрес сервера
//  Описание - Строка, Неопределено - Описание сервера
Процедура ДобавитьСервер(Спецификация, Адрес, Описание = Неопределено) Экспорт
	
	ОписаниеСервера = РатOpenAPI.НовыйСервер(Адрес, Описание);
	Спецификация.servers.Добавить(ОписаниеСервера);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Получает или создает блок описания ресурса (endpoint) в OpenAPI спецификации.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  АдресРесурса - Строка - Базовый адрес ресурса
//  Суффикс - Строка, Неопределено - Дополнительный путь ресурса
// 
// Возвращаемое значение:
//  см. РатOpenAPI.БлокПуть
Функция Ресурс(СпецификацияСервиса, Знач АдресРесурса, Суффикс = Неопределено) Экспорт
	
	ПолныйАдресРесурса = РатСтроки.ОбъединитьПутиURL(АдресРесурса, Суффикс);
	
	Блок = СпецификацияСервиса.paths[ПолныйАдресРесурса];
	
	Если Блок = Неопределено Тогда
		Блок = РатOpenAPI.БлокПуть();
		СпецификацияСервиса.paths.Вставить(ПолныйАдресРесурса, Блок);
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	Возврат Блок;
	
КонецФункции

// Формирует адрес ресурса для объекта метаданных.
// 
// Параметры:
//  ОписаниеТипа - Структура - Строка таблицы см. РатМетаданные.ТипыМетаданных
//  Объект - ОбъектМетаданных
//  Суффикс - Строка, Неопределено - Дополнительный путь ресурса
// 
// Возвращаемое значение:
//  Строка - Адрес ресурса в формате /{ИмяКоллекции}/{ИмяОбъекта}/{Суффикс}
Функция АдресОбъектаМетаданных(ОписаниеТипа, Объект, Суффикс = Неопределено) Экспорт
	
	Возврат РатСтроки.ОбъединитьПутиURL("/" + ОписаниеТипа.ИмяКоллекции, Объект.Имя, Суффикс);
	
КонецФункции

// Получает или создает блок описания ресурса для объекта метаданных.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  ОписаниеТипа - Структура - Строка таблицы см. РатМетаданные.ТипыМетаданных
//  Объект - ОбъектМетаданных
//  Суффикс - Строка, Неопределено - Дополнительный путь ресурса
// 
// Возвращаемое значение:
//  см. РатOpenAPI.БлокПуть
Функция РесурсОбъектаМетаданных(СпецификацияСервиса, ОписаниеТипа, Объект, Суффикс = Неопределено) Экспорт
	
	//@skip-check constructor-function-return-section
	Возврат Ресурс(СпецификацияСервиса, АдресОбъектаМетаданных(ОписаниеТипа, Объект, Суффикс), Объект.Представление());
	
КонецФункции

// Формирует название группы для объекта метаданных.
// 
// Параметры:
//  ОписаниеТипа - Структура - Строка таблицы см. РатМетаданные.ТипыМетаданных
//  Объект - ОбъектМетаданных
// 
// Возвращаемое значение:
//  Строка - Название группы в формате "{ИмяКоллекции}: {ПредставлениеОбъекта}"
Функция ГруппуОбъектаМетаданных(ОписаниеТипа, Объект) Экспорт
	
	Возврат СтрШаблон("%1: %2", ОписаниеТипа.ИмяКоллекции, Объект.Представление());
	
КонецФункции

// Добавляет группу операций в OpenAPI спецификацию.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  Имя - Строка - Идентификатор группы
//  Описание - Строка, Неопределено - Описание группы
Процедура ДобавитьГруппу(СпецификацияСервиса, Имя, Знач Описание = Неопределено) Экспорт
	
	Если Описание = Неопределено Тогда
		Описание = Имя;
	КонецЕсли;
	
	СпецификацияСервиса.tags.Добавить(РатOpenAPI.БлокТег(Имя, Описание));
	
КонецПроцедуры

// Регистрирует параметр в OpenAPI спецификации.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  ИмяСсылки - Строка - Идентификатор параметра
//  Имя - Строка - Имя параметра
//  Схема - см. РатOpenAPI.НоваяСхемаДанных
//  Описание - Строка - Описание параметра
//  Положение - см. РатOpenAPI.ПоложениеПараметра
Процедура ЗарегистрироватьПараметр(СпецификацияСервиса, ИмяСсылки, Имя, Схема, Описание, Знач Положение = Неопределено) Экспорт
	
	Если Положение = Неопределено Тогда
		Положение = РатOpenAPI.ПоложениеПараметра().Путь;
	КонецЕсли;
	
	Параметр = РатOpenAPI.НовыйПараметр(Имя, Положение, Описание);
	Параметр.Вставить("schema", Схема);
	СпецификацияСервиса.components.parameters.Вставить(ИмяСсылки, Параметр);
	
КонецПроцедуры

// Регистрирует тело ответа в OpenAPI спецификации.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  Имя - Строка - Имя тела ответа, используется для формирования ссылок
//  ТелоОтвета - см. РатOpenAPI.БлокТелоОтвета
Процедура ЗарегистрироватьТелоОтвета(СпецификацияСервиса, Имя, ТелоОтвета) Экспорт
	
	СпецификацияСервиса.components.responses.Вставить(Имя, ТелоОтвета);
	
КонецПроцедуры

#Область Схемы

// Регистрирует схему в OpenAPI спецификации.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  ИмяСхемы - Строка - Идентификатор схемы
//  Схема - см. РатOpenAPI.НоваяСхемаДанных
Процедура ЗарегистрироватьСхему(СпецификацияСервиса, ИмяСхемы, Схема) Экспорт
	
	СпецификацияСервиса.components.schemas.Вставить(ИмяСхемы, Схема);
	
КонецПроцедуры

// Создает таблицу для описания реквизитов объекта.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с колонками:
//  * Имя - Строка - Имя реквизита
//  * Тип - ОписаниеТипов, Тип - Тип данных реквизита
//  * Схема - см. РатOpenAPI.НоваяСхемаДанных
//  * Представление - Строка - Представление реквизита
//  * Обязательный - Булево - Признак обязательности реквизита
//  * ТолькоЧтение - Булево - Признак доступности только для чтения
//  * ТолькоЗапись - Булево - Признак доступности только для записи
//  * ВозможныеЗначения - Массив из Строка - Список допустимых значений
Функция ОписаниеРеквизитов() Экспорт
	
	ОписаниеРеквизитов = Новый ТаблицаЗначений;
	ОписаниеРеквизитов.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	ОписаниеРеквизитов.Колонки.Добавить("Тип", Новый ОписаниеТипов("ОписаниеТипов, Тип"));
	ОписаниеРеквизитов.Колонки.Добавить("Схема", Новый ОписаниеТипов("Структура, Соответствие"));
	ОписаниеРеквизитов.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	
	ТипБулево = Новый ОписаниеТипов("Булево");
	ОписаниеРеквизитов.Колонки.Добавить("Обязательный", ТипБулево);
	ОписаниеРеквизитов.Колонки.Добавить("ТолькоЧтение", ТипБулево);
	ОписаниеРеквизитов.Колонки.Добавить("ТолькоЗапись", ТипБулево);
	ОписаниеРеквизитов.Колонки.Добавить("ВозможныеЗначения", Новый ОписаниеТипов("Массив"));
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

// Добавляет описание реквизита в таблицу описания реквизитов.
// 
// Параметры:
//  ОписаниеРеквизитов - см. ОписаниеРеквизитов
//  Имя - Строка - Имя реквизита
//  Представление - Строка - Представление реквизита
//  Тип - Строка, Структура, ОписаниеТипов, Тип - Тип реквизита
//      - см. РатOpenAPI.БлокСсылка
//  ТолькоЧтение - Булево - Признак доступности только для чтения
//  Обязательный - Булево - Признак обязательности реквизита
//  ТолькоЗапись - Булево - Признак доступности только для записи
// 
// Возвращаемое значение:
//  СтрокаТаблицыЗначений - Строка таблицы с описанием реквизита
Функция ДобавитьРеквизит(ОписаниеРеквизитов, Имя, Представление, Тип, ТолькоЧтение = Ложь, Обязательный = Истина, ТолькоЗапись = Ложь) Экспорт
	
	ОписаниеРеквизита = ОписаниеРеквизитов.Добавить();
	ОписаниеРеквизита.Имя = Имя;
	ОписаниеРеквизита.Представление = Представление;
	ОписаниеРеквизита.ТолькоЧтение = ТолькоЧтение;
	ОписаниеРеквизита.ТолькоЗапись = ТолькоЗапись;
	ОписаниеРеквизита.Обязательный = Обязательный;
	
	ТипЗначения = ТипЗнч(Тип);
	Если ТипЗначения = Тип("Строка") Тогда
		ОписаниеРеквизита.Схема = РатOpenAPI.НормализованнаяСхема(Тип);
	ИначеЕсли ТипЗначения = Тип("Структура") Или ТипЗначения = Тип("Соответствие") Тогда
		ОписаниеРеквизита.Схема = Тип;
	Иначе
		ОписаниеРеквизита.Тип = Тип;
	КонецЕсли;
	
	Возврат ОписаниеРеквизита;
	
КонецФункции

// Создает схему объекта по описанию его реквизитов и регистрирует ее в OpenAPI спецификации.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  ОписаниеРеквизитов - см. ОписаниеРеквизитов
//  ИмяСхемы - Строка - Имя схемы
//  Описание - Строка - Описание схемы
//  СуществующиеСхемы - Соответствие из см. РатOpenAPI.НоваяСхемаДанных - Информацию о зарегистрированных схемам (Тип -> Схема) 
// 
// Возвращаемое значение:
//  см. РатOpenAPI.НоваяСхемаОбъекта
Функция ЗарегистрироватьОписаниеОбъекта(СпецификацияСервиса, ОписаниеРеквизитов, ИмяСхемы, Описание, СуществующиеСхемы = Неопределено) Экспорт
	
	Схема = СхемаПоОписаниюОбъекта(ОписаниеРеквизитов, Описание, , СуществующиеСхемы);
	
	ЗарегистрироватьСхему(СпецификацияСервиса, ИмяСхемы, Схема);
	
	Возврат Схема;
	
КонецФункции

// Создает схему объекта по описанию его реквизитов.
// 
// Параметры:
//  ОписаниеРеквизитов - см. ОписаниеРеквизитов
//  Описание - Строка - Описание схемы
//  ВыходнаяСхема - Булево - Признак создания выходной схемы
//  СуществующиеСхемы - Соответствие из см. РатOpenAPI.НоваяСхемаДанных - Информацию о зарегистрированных схемам (Тип -> Схема) 
// 
// Возвращаемое значение:
//  см. РатOpenAPI.НоваяСхемаОбъекта
Функция СхемаПоОписаниюОбъекта(ОписаниеРеквизитов, Описание, ВыходнаяСхема = Ложь, СуществующиеСхемы = Неопределено) Экспорт
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта(Описание);
	
	Для Каждого Реквизит Из ОписаниеРеквизитов Цикл
		
		Если ЗначениеЗаполнено(Реквизит.Схема) Тогда
			ОписаниеПоля = Реквизит.Схема;
		ИначеЕсли СуществующиеСхемы <> Неопределено И СуществующиеСхемы[Реквизит.Тип] <> Неопределено Тогда
			ОписаниеПоля = СуществующиеСхемы[Реквизит.Тип];
		Иначе
			ОписаниеПоля = СхемаРеквизита(Реквизит.Тип, Реквизит.Представление, ВыходнаяСхема);
		КонецЕсли;
		
		Если Реквизит.ТолькоЧтение Тогда
			ОписаниеПоля.Вставить("readOnly", Истина);
		КонецЕсли;
		
		Если Реквизит.ТолькоЗапись Тогда
			ОписаниеПоля.Вставить("writeOnly", Истина);
		КонецЕсли;
		
		Если НЕ Реквизит.Обязательный Тогда
			ОписаниеПоля.Вставить("nullable", Истина);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Реквизит.ВозможныеЗначения) Тогда
			РатOpenAPI.УстановитьДоступныеЗначения(ОписаниеПоля, Неопределено, Реквизит.ВозможныеЗначения);
		КонецЕсли;
		
		РатOpenAPI.ДобавитьРеквизит(Схема, Реквизит.Имя, ОписаниеПоля);
		
	КонецЦикла;
	
	Возврат Схема;

КонецФункции

// Формирует имя схемы для объекта метаданных.
// 
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
//  Суффикс - Строка, Неопределено - Дополнительный суффикс имени схемы
// 
// Возвращаемое значение:
//  Строка - Имя схемы в формате "{ПолноеИмяОбъекта}[.{Суффикс}]"
Функция ИмяСхемыОписанияОбъектаМетаданных(ОбъектМетаданных, Суффикс = Неопределено) Экспорт
	
	ИмяСхемы = ОбъектМетаданных.ПолноеИмя();
	ИмяСхемы = РатСтроки.ДобавитьСтроку(ИмяСхемы, Суффикс, ".");
	
	Возврат ИмяСхемы;
	
КонецФункции

// Формирует имя выходной схемы для объекта метаданных.
// 
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
//  Суффикс - Строка, Неопределено - Дополнительный суффикс имени схемы
// 
// Возвращаемое значение:
//  Строка - Имя выходной схемы в формате "{ПолноеИмяОбъекта}[.{Суффикс}].out"
Функция ИмяВыходнойСхемыОбъектаМетаданных(ОбъектМетаданных, Суффикс = Неопределено) Экспорт
	
	Возврат ИмяСхемыОписанияОбъектаМетаданных(ОбъектМетаданных, Суффикс) + ".out";
	
КонецФункции

// Формирует имя входной схемы для объекта метаданных.
// 
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
//  Суффикс - Строка, Неопределено - Дополнительный суффикс имени схемы
// 
// Возвращаемое значение:
//  Строка - Имя входной схемы в формате "{ПолноеИмяОбъекта}[.{Суффикс}].in"
Функция ИмяВходнойСхемыОбъектаМетаданных(ОбъектМетаданных, Суффикс = Неопределено) Экспорт
	
	Возврат ИмяСхемыОписанияОбъектаМетаданных(ОбъектМетаданных, Суффикс) + ".in";
	
КонецФункции

// Создает ссылку на схему объекта метаданных.
// 
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
//  Суффикс - Строка, Неопределено - Дополнительный суффикс имени схемы
// 
// Возвращаемое значение:
//  см. РатOpenAPI.СсылкаНаСхему
Функция СсылкуНаСхему(ОбъектМетаданных, Суффикс = Неопределено) Экспорт
	
	Возврат РатOpenAPI.СсылкаНаСхему(ИмяСхемыОписанияОбъектаМетаданных(ОбъектМетаданных, Суффикс));
	
КонецФункции

// Создает ссылку на выходную схему объекта метаданных.
// 
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
//  Суффикс - Строка, Неопределено - Дополнительный суффикс имени схемы
// 
// Возвращаемое значение:
//  см. РатOpenAPI.СсылкаНаСхему
Функция СсылкуНаВыходнуюСхему(ОбъектМетаданных, Суффикс = Неопределено) Экспорт
	
	Возврат РатOpenAPI.СсылкаНаСхему(ИмяВыходнойСхемыОбъектаМетаданных(ОбъектМетаданных, Суффикс));
	
КонецФункции

// Создает ссылку на входную схему объекта метаданных.
// 
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
//  Суффикс - Строка, Неопределено - Дополнительный суффикс имени схемы
// 
// Возвращаемое значение:
//  см. РатOpenAPI.СсылкаНаСхему
Функция СсылкуНаВходнуюСхему(ОбъектМетаданных, Суффикс = Неопределено) Экспорт
	
	Возврат РатOpenAPI.СсылкаНаСхему(ИмяВходнойСхемыОбъектаМетаданных(ОбъектМетаданных, Суффикс));
	
КонецФункции

// Создает схему для реквизита по его типу.
// 
// Параметры:
//  ТипРеквизита - ОписаниеТипов, Тип - Тип реквизита
//  Описание - Строка - Описание реквизита
//  ВыходнаяСхема - Булево - Признак создания выходной схемы
// 
// Возвращаемое значение:
//  см. РатOpenAPI.НоваяСхемаДанных
Функция СхемаРеквизита(ТипРеквизита, Описание, ВыходнаяСхема = Ложь) Экспорт
	
	Если ТипЗнч(ТипРеквизита) = Тип("ОписаниеТипов") Тогда
		Типы = ТипРеквизита.Типы();
	Иначе
		Типы = РатКоллекции.ЗначениеВМассиве(ТипРеквизита);
	КонецЕсли;
	
	СсылочныеТипы = Новый Массив;
	НеСсылочныеТипы = Новый Массив;
	
	Для Каждого Тип Из Типы Цикл
		
		Если РатТипыДанных.ЭтоСсылочныйТип(Тип) Тогда
			СсылочныеТипы.Добавить(Тип);
		Иначе
			НеСсылочныеТипы.Добавить(Тип);
		КонецЕсли;
		
	КонецЦикла;
	
	СоставнаяСхема = НеСсылочныеТипы.Количество() + (СсылочныеТипы.Количество() > 0) > 1;
	
	Если СоставнаяСхема Тогда
		
		ОписаниеПоля = ОписаниеСоставногоТипа(СсылочныеТипы, НеСсылочныеТипы, Описание, ВыходнаяСхема);
			
	ИначеЕсли СсылочныеТипы.Количество() Тогда
		
		ОписаниеПоля = ОписаниеСсылочногоТипа(СсылочныеТипы, Описание, ВыходнаяСхема);
		
	Иначе
		
		ОписаниеПоля = ОписаниеПримитивногоТипа(НеСсылочныеТипы[0], Описание);
		
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	Возврат ОписаниеПоля;

КонецФункции

Функция СхемаСистемногоПеречисления(Перечисление, Описание) Экспорт
	
	Ключи = РатПреобразованияСистемныеПеречисления.КлючиСистемногоПеречисления(Перечисление);
	Возврат РатOpenAPI.НоваяСхемаПеречисления(Ключи, Описание);
	
КонецФункции

#КонецОбласти

#Область Операции

// Создает новую операцию сервиса в OpenAPI спецификации.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  ИмяОперации - Строка - Идентификатор операции
//  Описание - Строка - Описание операции
//  Группа - Строка - Идентификатор группы операций
//  Путь - Строка - Путь ресурса
//  Метод - Строка - HTTP метод
// 
// Возвращаемое значение:
//  см. РатOpenAPI.НоваяОперация
Функция НоваяОперацияСервиса(СпецификацияСервиса, ИмяОперации, Описание, Группа, Путь, Метод) Экспорт
	
	ОписаниеОперации = РатOpenAPI.НоваяОперация(ИмяОперации, Описание, Группа);
	
	ЗарегистрироватьОперацию(СпецификацияСервиса, Путь, Метод, ОписаниеОперации);
	
	Возврат ОписаниеОперации;
	
КонецФункции

// Создает новую операцию ресурса в OpenAPI спецификации.
// 
// Параметры:
//  Ресурс - см. РатOpenAPI.БлокПуть
//  Метод - Строка - HTTP метод
//  ИмяОперации - Строка - Идентификатор операции
//  Описание - Строка - Описание операции
//  Группа - Строка - Идентификатор группы операций
// 
// Возвращаемое значение:
//  см. РатOpenAPI.НоваяОперация
Функция НоваяОперацияРесурса(Ресурс, Метод, ИмяОперации, Описание = Неопределено, Группа = Неопределено) Экспорт
	
	ОписаниеОперации = РатOpenAPI.НоваяОперация(ИмяОперации, Описание, Группа);
	
	ЗарегистрироватьОперацию(Неопределено, Ресурс, Метод, ОписаниеОперации);
	
	Возврат ОписаниеОперации;
	
КонецФункции

// Регистрирует операцию в OpenAPI спецификации.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//  Путь - Строка - Путь ресурса
//  Метод - Строка - HTTP метод
//  ОписаниеОперации - см. РатOpenAPI.НоваяОперация
Процедура ЗарегистрироватьОперацию(СпецификацияСервиса, Путь, Метод, ОписаниеОперации) Экспорт
	
	Если ТипЗнч(Путь) = Тип("Строка") Тогда
		ОписаниеПути = Ресурс(СпецификацияСервиса, Путь);
	Иначе
		ОписаниеПути = Путь;
	КонецЕсли;
	
	ЗарегистрироватьОперациюРесурса(ОписаниеПути, Метод, ОписаниеОперации);
	
КонецПроцедуры

// Регистрирует операцию в блоке описания ресурса.
// 
// Параметры:
//  Ресурс - см. РатOpenAPI.БлокПуть
//  Метод - Строка - HTTP метод
//  ОписаниеОперации - см. РатOpenAPI.НоваяОперация
Процедура ЗарегистрироватьОперациюРесурса(Ресурс, Метод, ОписаниеОперации) Экспорт
	
	Ресурс.Вставить(НРег(Метод), ОписаниеОперации);
	
КонецПроцедуры

// Добавляет стандартные ответы к операции.
// 
// Параметры:
//  Операция - см. РатOpenAPI.НоваяОперация
//  СхемаУспешногоОтвета - Строка - Имя схемы, зарегистрированной через см. ЗарегистрироватьСхему
//                       - см. РатOpenAPI.НоваяСхемаДанных
Процедура ДобавитьСтандартныеОтветы(Операция, СхемаУспешногоОтвета = Неопределено) Экспорт
	
	ТелоОшибки = РатOpenAPI.СсылкаНаТелоОтвета("error");
	ТелоОшибкиПлатформы = РатOpenAPI.СсылкаНаТелоОтвета("platformError");
	
	КодыОтветов = РатОбщийКлиентСервер.КодыОтветов();
	
	ДобавитьОтвет(Операция, "default", ТелоОшибкиПлатформы);
	ДобавитьОтвет(Операция, КодыОтветов.ОшибкаСервера, ТелоОшибки);
	ДобавитьОтвет(Операция, КодыОтветов.НеНайдено, ТелоОшибки);
	
	Если СхемаУспешногоОтвета <> Неопределено Тогда
		Если ТипЗнч(СхемаУспешногоОтвета) = Тип("Строка") Тогда
			Схема = РатOpenAPI.СсылкаНаСхему(СхемаУспешногоОтвета);
		Иначе
			Схема = СхемаУспешногоОтвета;
		КонецЕсли;
		ТелоОтвета = РатOpenAPI.БлокТелоОтвета(Схема);
		ДобавитьОтвет(Операция, КодыОтветов.Успешно, ТелоОтвета);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет ответ к операции.
// 
// Параметры:
//  Операция - см. РатOpenAPI.НоваяОперация
//  КодОтвета - Строка - HTTP код ответа
//  ТелоОтвета - см. РатOpenAPI.БлокТелоОтвета
Процедура ДобавитьОтвет(Операция, КодОтвета, ТелоОтвета) Экспорт
	
	Операция.responses.Вставить(Строка(КодОтвета), ТелоОтвета);
	
КонецПроцедуры

// Добавляет параметр к операции.
// 
// Параметры:
//  Операция - см. РатOpenAPI.НоваяОперация
//  Имя - Строка - Имя параметра
//  Схема - см. РатOpenAPI.НоваяСхемаДанных
//  Описание - Строка - Описание параметра
//  Положение - см. РатOpenAPI.ПоложениеПараметра
Процедура ДобавитьПараметрОперации(Операция, Имя, Схема, Описание, Знач Положение = Неопределено) Экспорт
	
	Если Положение = Неопределено Тогда
		Положение = РатOpenAPI.ПоложениеПараметра().Путь;
	КонецЕсли;
		
	Параметр = РатOpenAPI.НовыйПараметр(Имя, Положение, Описание);
	Параметр.Вставить("schema", Схема);
	Операция.parameters.Добавить(Параметр);
	
КонецПроцедуры

// Добавляет известный параметр (зарегистрированный через см.ЗарегистрироватьПараметр) к операции.
// 
// Параметры:
//  Операция - см. РатOpenAPI.НоваяОперация
//  ИмяПараметра - Строка - Имя параметра
Процедура ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметра) Экспорт
	
	Операция.parameters.Добавить(РатOpenAPI.СсылкаНаПараметр(ИмяПараметра));
	
КонецПроцедуры

// Устанавливает описание тела запроса для операции.
// 
// Параметры:
//  Операция - см. РатOpenAPI.НоваяОперация
//  СхемаЗапроса - Строка - Имя ссылки на схему
//               - см. РатOpenAPI.СсылкаНаСхему
//               - см. РатOpenAPI.НоваяСхемаДанных
Процедура УстановитьОписаниеТелаОперации(Операция, СхемаЗапроса) Экспорт
	
	Если ТипЗнч(СхемаЗапроса) = Тип("Строка") Тогда
		Схема = РатOpenAPI.СсылкаНаСхему(СхемаЗапроса);
	Иначе
		Схема = СхемаЗапроса;
	КонецЕсли;
	
	Операция.requestBody = РатOpenAPI.БлокТелоЗапроса(Схема);
	
КонецПроцедуры

#КонецОбласти

// Возвращает имена базовых схем OpenAPI спецификации.
// 
// Возвращаемое значение:
//  Структура - Структура с именами базовых схем:
// * ОтветУспешно - Строка - Имя схемы успешного ответа
// * ВыходнаяСсылка - Строка - Имя схемы выходной ссылки
// * ВходнаяСсылкаСтруктура - Строка - Имя схемы входной ссылки в виде структуры
// * ВходнаяСсылкаСоставное - Строка - Имя схемы входной составной ссылки
// * ВходнаяСсылкаПростая - Строка - Имя схемы простой входной ссылки
// * ВходнаяКоллекцияСсылок - Строка - Имя схемы коллекции входных составных ссылок
// * ВыходнаяКоллекцияСсылок - Строка - Имя схемы коллекции выходных ссылок
// * УникальныйИдентификатор - Строка - Имя схемы уникального идентификатора
Функция БазовыеСхемы() Экспорт
	
	Схемы = Новый Структура;
	Схемы.Вставить("ОтветУспешно", "success");
	Схемы.Вставить("ВыходнаяСсылка", "outputRef");
	Схемы.Вставить("ВходнаяСсылкаСтруктура", "inputRef");
	Схемы.Вставить("ВходнаяСсылкаСоставное", "complexRef");
	Схемы.Вставить("ВходнаяСсылкаПростая", "ref");
	Схемы.Вставить("ВходнаяКоллекцияСсылок", "arrayOfComplexRef");
	Схемы.Вставить("ВыходнаяКоллекцияСсылок", "arrayOfRef");
	Схемы.Вставить("УникальныйИдентификатор", "uid");
	
	Возврат Схемы;
	
КонецФункции

// Добавляет базовые компоненты в OpenAPI спецификацию.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
Процедура ДобавитьБазовыеКомпоненты(СпецификацияСервиса) Экспорт
	
#Область Схемы
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	БазовыеСхемы = БазовыеСхемы();
	
	// ОтветУспешно
	СхемаSuccess = РатOpenAPI.НоваяСхемаОбъекта("Результат успешной обработки объекта");
	РатOpenAPI.ДобавитьРеквизит(СхемаSuccess, "message", ТипыДанных.Строка, "Сообщение");
	ЗарегистрироватьСхему(СпецификацияСервиса, БазовыеСхемы.ОтветУспешно, СхемаSuccess);
	
	// УникальныйИдентификатор
	СхемаUID = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка, "Уникальный идентификатор объекта (GUID)");
	ЗарегистрироватьСхему(СпецификацияСервиса, БазовыеСхемы.УникальныйИдентификатор, СхемаUID);
	
	// Выходная ссылка структура
	СхемаСтруктураСсылки = РатOpenAPI.НоваяСхемаОбъекта("Структура ссылки на объект");
	РатOpenAPI.ДобавитьРеквизит(СхемаСтруктураСсылки,
								"type",
								ТипыДанных.Строка,
								"Имя типа, например CatalogRef.Пользователи, DocumentRef.ПКО");
	РатOpenAPI.ДобавитьРеквизит(СхемаСтруктураСсылки, "id", РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.УникальныйИдентификатор));
	РатOpenAPI.ДобавитьРеквизит(СхемаСтруктураСсылки, "presentation", ТипыДанных.Строка, "Представление объекта");
	ЗарегистрироватьСхему(СпецификацияСервиса, БазовыеСхемы.ВыходнаяСсылка,  СхемаСтруктураСсылки);
	
	// Входная ссылка структура
	СхемаСтруктураСсылки = РатOpenAPI.НоваяСхемаОбъекта("Структура ссылки на объект");
	РатOpenAPI.ДобавитьРеквизит(СхемаСтруктураСсылки,
								"type",
								ТипыДанных.Строка,
								"Имя типа, например CatalogRef.Пользователи, DocumentRef.ПКО");
	РатOpenAPI.ДобавитьРеквизит(СхемаСтруктураСсылки, "id", РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.УникальныйИдентификатор));
	ЗарегистрироватьСхему(СпецификацияСервиса, БазовыеСхемы.ВходнаяСсылкаСтруктура,  СхемаСтруктураСсылки);
	
	// СсылкаСтрока
	СхемаСтрокаСсылки = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка, "Строка ссылка в формате `{ВидОбъекта}.{ТипОбъекта}.{Идентификатор}`.
	|Например: `Документы.РКО.1234567890`, `Справочники.Пользователи.a772d037-9519-4c2b-8c9b-ad9de187fe90`");
	
	// Схемы указания ссылок
	ВходнаяСсылкаСтруктура = РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ВходнаяСсылкаСтруктура);
	
	СхемаСсылкиИдентификатор = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка,
		"Идентификатор объекта.
		|  В качестве идентификатора может использоваться GUID, имя предопределенного,
		|  номер документа, наименование или код справочника");
	
	// complexRef
	ВариантыУказанияСсылки = РатКоллекции.ЗначениеВМассиве(ВходнаяСсылкаСтруктура, СхемаСтрокаСсылки);
	ВходнаяСсылкаСоставное = РатOpenAPI.НоваяСхемаСоставногоТипа(ВариантыУказанияСсылки, "Ссылка на объект составного типа");
	ЗарегистрироватьСхему(СпецификацияСервиса, БазовыеСхемы.ВходнаяСсылкаСоставное, ВходнаяСсылкаСоставное);
	
	// ref
	ВариантыУказанияСсылки = РатКоллекции.ЗначениеВМассиве(ВходнаяСсылкаСтруктура, СхемаСтрокаСсылки, СхемаСсылкиИдентификатор);
	ВходнаяСсылкаПростая = РатOpenAPI.НоваяСхемаСоставногоТипа(ВариантыУказанияСсылки, "Ссылка на объект");
	ЗарегистрироватьСхему(СпецификацияСервиса, БазовыеСхемы.ВходнаяСсылкаПростая, ВходнаяСсылкаПростая);
	
	// arrayOfRef
	ВыходнаяКоллекцияСсылок = РатOpenAPI.НоваяСхемаКоллекции(РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ВыходнаяСсылка), "Коллекция ссылок");
	ЗарегистрироватьСхему(СпецификацияСервиса, БазовыеСхемы.ВыходнаяКоллекцияСсылок, ВыходнаяКоллекцияСсылок);
	
	ВходнаяКоллекцияСсылок = РатOpenAPI.НоваяСхемаКоллекции(РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ВходнаяСсылкаСоставное), "Коллекция ссылок");
	ЗарегистрироватьСхему(СпецификацияСервиса, БазовыеСхемы.ВходнаяКоллекцияСсылок, ВходнаяКоллекцияСсылок);
	
#КонецОбласти
	
#Область Параметры
	
	ЗарегистрироватьПараметр(СпецификацияСервиса, "path_uid", "uid", РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.УникальныйИдентификатор),
		"Уникальный идентификатор объекта (UID)");
	
	СхемаСтрока = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка);
	
	Параметр = ПараметрИдентификаторСправочника();
	ЗарегистрироватьПараметр(СпецификацияСервиса, Параметр.Ключ, Параметр.ИмяПараметра, СхемаСтрока,
		"Идентификатор объекта.
		|В качестве идентификатор можно использовать (при наличии) `УникальныйИдентификатор`, `ИмяПредопределенного`, `Код` или `Наименование`");
	
	Параметр = ПараметрИдентификаторДокумента();
	ЗарегистрироватьПараметр(СпецификацияСервиса, Параметр.Ключ, Параметр.ИмяПараметра, СхемаСтрока,
		"Идентификатор документа.
		|В качестве идентификатор можно использовать `УникальныйИдентификатор` или `Номер`");
	
	Параметр = ПараметрИдентификаторУзла();
	ЗарегистрироватьПараметр(СпецификацияСервиса, Параметр.Ключ, Параметр.ИмяПараметра, СхемаСтрока,
		"Идентификатор узла.
		|В качестве идентификатор можно использовать `УникальныйИдентификатор`, `Код`, `Наименование`");
	
	ЗарегистрироватьПараметр(СпецификацияСервиса, "name", "name", СхемаСтрока,
		"Имя объекта заданное в конфигураторе (Имя предопределенного элемента или перечисления)");
	
	Параметр = ПараметрИдентификаторУзла();
	ЗарегистрироватьПараметр(СпецификацияСервиса, Параметр.Ключ, Параметр.ИмяПараметра, СхемаСтрока,
		"Идентификатор узла.
		|В качестве идентификатор можно использовать `УникальныйИдентификатор`, `Код`, `Наименование`");
	
	Параметр = ПараметрИдентификаторРегистра();
	ЗарегистрироватьПараметр(СпецификацияСервиса, Параметр.Ключ, Параметр.ИмяПараметра, СхемаСтрока,
		"Идентификатор записи регистра сведений.
		|В качестве идентификатор используется перечисление ключевых свойств разделенных точкой с запятой (`;`). Примеры:
		|* `Склад=Основной;Номенклатура=0123456789`
		|* `Документ=Документ.ПКО.1234567890;Период=2021-01-01T22:00:00`");
	
#КонецОбласти
	
#Область Ответы
	ТелоОшибки = РатOpenAPI.БлокТелоОтвета(СхемаОшибки());
	ТелоОшибкиПлатформы = Новый Структура("description", "Ошибка платформы");
	ЗарегистрироватьТелоОтвета(СпецификацияСервиса, "error", ТелоОшибки);
	ЗарегистрироватьТелоОтвета(СпецификацияСервиса, "platformError", ТелоОшибкиПлатформы);
#КонецОбласти

КонецПроцедуры

// Возвращает параметр описание известного параметра-идентификатора для объекта метаданных.
// Для разных видов метаданных в качестве идентификатора объекта могут выступать разные реквизиты (Код, Наименование, Номер)
// 
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
// 
// Возвращаемое значение:
//  Структура - Структура с параметрами:
//  * ИмяПараметра - Строка - Имя параметра
//  * Ключ - Строка - Ключ параметра
Функция ПараметрИдентификаторОбъекта(ОбъектМетаданных) Экспорт
	
	ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	
	Если РатМетаданные.ЭтоТаблицаПеречисления(ПолноеИмя) Тогда
		ПараметрИдентификатора = Новый Структура("ИмяПараметра, Ключ", "name", "name");
	ИначеЕсли РатМетаданные.ЭтоТаблицаДокумента(ПолноеИмя) Тогда
		ПараметрИдентификатора = ПараметрИдентификаторДокумента();
	ИначеЕсли РатМетаданные.ЭтоТаблицаПланаОбмена(ПолноеИмя) Тогда
		ПараметрИдентификатора = ПараметрИдентификаторУзла();
	ИначеЕсли РатМетаданные.ЭтоТаблицаРегистра(ПолноеИмя) Тогда
		ПараметрИдентификатора = ПараметрИдентификаторРегистра();
	Иначе
		ПараметрИдентификатора = ПараметрИдентификаторСправочника();
	КонецЕсли;
	
	Возврат ПараметрИдентификатора;
	
КонецФункции

// Устанавливает настройку базовой аутентификации в OpenAPI спецификации.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
Процедура УстановитьНастройкуБазовойАутентификации(СпецификацияСервиса) Экспорт
	
	СпецификацияСервиса.components.securitySchemes.Вставить("basicAuth", РатOpenAPI.НастройкаБазовойАутентификации());
	СпецификацияСервиса.security.Добавить(Новый Структура("basicAuth", Новый Массив));
	
КонецПроцедуры

// Регистрирует дополнительные операции из модулей сервиса.
// 
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
Процедура ЗарегистрироватьДополнительныеОперации(СпецификацияСервиса) Экспорт
	
	Для Каждого Модуль Из РатРасширениеФункциональности.МодулиСервиса() Цикл
		
		Если РатРасширениеФункциональности.МетодОбъектаСуществует(Модуль, "ЗарегистрироватьОписание") Тогда
			Модуль.ЗарегистрироватьОписание(СпецификацияСервиса);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СхемаОшибки()
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	Идентификаторы = Новый Массив;
	Для Каждого КлассОшибки Из РатОбщий.КлассыОшибок() Цикл
		Если Идентификаторы.Найти(КлассОшибки.Значение.Идентификатор) = Неопределено Тогда
			Идентификаторы.Добавить(КлассОшибки.Значение.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	СхемаId = РатOpenAPI.НоваяСхемаПеречисления(Идентификаторы, "Идентификатор ошибки");
	
	СхемаStackItem = РатOpenAPI.НоваяСхемаОбъекта("Описание перехваченной ошибки");
	РатOpenAPI.ДобавитьРеквизит(СхемаStackItem, "id", СхемаId);
	РатOpenAPI.ДобавитьРеквизит(СхемаStackItem, "description", ТипыДанных.Строка, "Расшифровка идентификатора");
	РатOpenAPI.ДобавитьРеквизит(СхемаStackItem, "message", ТипыДанных.Строка, "Сообщение об ошибке");
	
	СхемаРеквизитаStack = РатOpenAPI.НоваяСхемаКоллекции(СхемаStackItem, "Дерево перехваченных ошибок");
	
	СхемаError = РатOpenAPI.НоваяСхемаОбъекта("Данные ошибки");
	РатOpenAPI.ДобавитьРеквизит(СхемаError, "id", СхемаId);
	РатOpenAPI.ДобавитьРеквизит(СхемаError, "description", ТипыДанных.Строка, "Расшифровка идентификатора");
	РатOpenAPI.ДобавитьРеквизит(СхемаError, "message", ТипыДанных.Строка, "Сообщение об ошибке");
	РатOpenAPI.ДобавитьРеквизит(СхемаError, "stack", СхемаРеквизитаStack);
	
	Возврат СхемаError;
	
КонецФункции

// Возвращает параметр идентификатора для справочника.
// 
// Возвращаемое значение:
//  Структура - Структура с параметрами:
//  * ИмяПараметра - Строка - Имя параметра
//  * Ключ - Строка - Ключ параметра
Функция ПараметрИдентификаторСправочника()
	
	Возврат Новый Структура("ИмяПараметра, Ключ", "identifier", "catalog_id");
	
КонецФункции

// Возвращает параметр идентификатора для документа.
// 
// Возвращаемое значение:
//  Структура - Структура с параметрами:
//  * ИмяПараметра - Строка - Имя параметра
//  * Ключ - Строка - Ключ параметра
Функция ПараметрИдентификаторДокумента()
	
	Возврат Новый Структура("ИмяПараметра, Ключ", "identifier", "document_id");
	
КонецФункции

// Возвращает параметр идентификатора для узла.
// 
// Возвращаемое значение:
//  Структура - Структура с параметрами:
//  * ИмяПараметра - Строка - Имя параметра
//  * Ключ - Строка - Ключ параметра
Функция ПараметрИдентификаторУзла()
	
	Возврат Новый Структура("ИмяПараметра, Ключ", "identifier", "plan_id");
	
КонецФункции

// Возвращает параметр идентификатора для регистра.
// 
// Возвращаемое значение:
//  Структура - Структура с параметрами:
//  * ИмяПараметра - Строка - Имя параметра
//  * Ключ - Строка - Ключ параметра
Функция ПараметрИдентификаторРегистра()
	
	Возврат Новый Структура("ИмяПараметра, Ключ", "identifier", "reg_id");
	
КонецФункции

Функция ОписаниеСоставногоТипа(СсылочныеТипы, НеСсылочныеТипы, Описание = Неопределено, ВыходнаяСхема = Ложь)
	
	Схемы = Новый Массив;
	Если СсылочныеТипы.Количество() Тогда
		Схемы.Добавить(ОписаниеСсылочногоТипа(СсылочныеТипы, , ВыходнаяСхема));
	КонецЕсли;
	
	Для Каждого Тип Из НеСсылочныеТипы Цикл
		Схемы.Добавить(ОписаниеПримитивногоТипа(Тип));
	КонецЦикла;
	
	Возврат РатOpenAPI.НоваяСхемаСоставногоТипа(Схемы, Описание);
	
КонецФункции

Функция ОписаниеСсылочногоТипа(СсылочныеТипы, Описание = Неопределено, ВыходнаяСхема = Ложь)
	
	БазовыеСхемы = БазовыеСхемы();
	
	Если ВыходнаяСхема Тогда
		Схема = РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ВыходнаяСсылка);
	ИначеЕсли СсылочныеТипы.Количество() > 1 Тогда
		Схема = РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ВходнаяСсылкаСоставное);
	Иначе
		Схема = РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ВходнаяСсылкаПростая);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Описание) Тогда
		ПолноеОписание = СтрШаблон("%1. Ссылка на `%2`", Описание, СтрСоединить(СсылочныеТипы, "; "));
		Схема = РатOpenAPI.НоваяСхемаОбъединения(ПолноеОписание, Схема);
	КонецЕсли;
	
	Возврат Схема;
	
КонецФункции

Функция ОписаниеПримитивногоТипа(Тип, Описание = Неопределено)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	Если Тип = Тип("Строка") Тогда
		ОписаниеПоля = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка, Описание);
	ИначеЕсли Тип = Тип("Булево") Тогда
		ОписаниеПоля = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Булево, Описание);
	ИначеЕсли Тип = Тип("Число") Тогда
		ОписаниеПоля = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.ДробноеЧисло, Описание);
		ОписаниеПоля.Вставить("format", "float");
	ИначеЕсли Тип = Тип("Дата") Тогда
		ОписаниеПоля = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Дата, Описание);
		ОписаниеПоля.Вставить("format", "date-time");
	ИначеЕсли Тип = Тип("УникальныйИдентификатор") Тогда
		ОписаниеПоля = РатOpenAPI.СсылкаНаСхему(БазовыеСхемы().УникальныйИдентификатор);
	ИначеЕсли Тип = Тип("Массив") Тогда
		ОписаниеПоля = РатOpenAPI.НоваяСхемаКоллекции(ТипыДанных.Объект, Описание);
	Иначе
		ОписаниеПоля = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка, Строка(Тип) + ". " + Описание);
	КонецЕсли;
	
	Возврат ОписаниеПоля;
	
КонецФункции

#КонецОбласти

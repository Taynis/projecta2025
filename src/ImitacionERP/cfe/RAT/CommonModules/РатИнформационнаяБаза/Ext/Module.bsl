//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Возвращает коллекцию записей таблицы по переданным параметрам.
//
// Параметры:
//   ПараметрыВыборки - см. РатИнформационнаяБаза.ПараметрыВыборки
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Массив из Произвольный - Если среди выбранных полей одна колонка.
//   ТаблицаЗначений        - Если выбрано несколько колонок.
//
// Пример:
//   Параметры = РатИнформационнаяБаза.ПараметрыВыборки();
//   Параметры.ИмяТаблицы = "Справочник.Номенклатура";
//   Параметры.ВыбираемыеПоля = СтрРазделить("Ссылка,Наименование", ",");
//   Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   Данные = РатИнформационнаяБаза.ДанныеСписка(Параметры, Статус);
//
Функция ДанныеСписка(ПараметрыВыборки, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатИнформационнаяБазаПереопределяемый.ДанныеСписка(ПараметрыВыборки, СтатусОбработкиЗапроса, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ПараметрыВыборки.ВыбираемыеПоля.Количество() = 0 Тогда
		ПараметрыВыборки.ВыбираемыеПоля = РатМетаданные.КлючевыеПоляТаблицы(ПараметрыВыборки.ИмяТаблицы);
	КонецЕсли;
	
	Запрос = СформироватьЗапрос(ПараметрыВыборки.ИмяТаблицы,
								ПараметрыВыборки.ВыбираемыеПоля,
								ПараметрыВыборки.Отбор,
								ПараметрыВыборки.Сортировка,
								ПараметрыВыборки.КоличествоЭлементов);
	
	Попытка
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Колонки.Количество() = 1 Тогда
			Данные = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку(0);
		Иначе
			Данные = РезультатЗапроса.Выгрузить();
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), "Не удалось выполнить запрос к данным");
		КлассОшибки = РатОбщий.КлассыОшибок().АнализПараметровЗапроса;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецПопытки;
	
	Возврат Данные;
	
КонецФункции

// Возвращает данные объекта информационной базы.
//
// Параметры:
//   Идентификатор          - ЛюбаяСсылка - Ссылка на объект ИБ
//                          - КлючЗаписи - Идентификатор записи регистра
//   ИмяТаблицы             - Строка - Полное имя таблицы хранения.
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   - Структура    - Данные объекта.
//   - Неопределено - Если не удалось найти объект по идентификатору.
//
// Пример:
//   Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   Данные = РатИнформационнаяБаза.ДанныеЭлемента(СсылкаНаОбъект, "Справочник.Номенклатура", Статус);
//
Функция ДанныеЭлемента(Идентификатор, ИмяТаблицы, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатИнформационнаяБазаПереопределяемый.ДанныеЭлемента(Идентификатор, ИмяТаблицы, СтатусОбработкиЗапроса, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Условия = Новый Массив();
	
	Если РатМетаданные.ТаблицаСодержитОбъектныеДанные(ИмяТаблицы) Тогда
		Условия.Добавить(Предикат("Ссылка", Идентификатор));
	Иначе
		КлючевыеПоля = РатМетаданные.КлючевыеПоляТаблицы(ИмяТаблицы);
		
		Для Каждого Поле Из КлючевыеПоля Цикл
			Условия.Добавить(Предикат(Поле, Идентификатор[Поле]));
		КонецЦикла;
	КонецЕсли;
	
	Запрос = СформироватьЗапрос(ИмяТаблицы, "*", Условия, , 1);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		КлассОшибки = РатОбщий.КлассыОшибок().НеНайденаЗаписьИБ;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, "Запись с указанным идентификатором не существует");
		Возврат Неопределено;
		
	КонецЕсли;
	
	Данные = Новый Структура(СтрСоединить(РатКоллекции.ВыгрузитьЗначения(РезультатЗапроса.Колонки, "Имя"), ","));
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(Данные, Выборка);
	
	ТипРезультатЗапроса = Тип("РезультатЗапроса");
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		
		Если Колонка.ТипЗначения.СодержитТип(ТипРезультатЗапроса) Тогда
			
			Данные[Колонка.Имя] = Выборка[Колонка.Имя].Выгрузить();
			
		ИначеЕсли РатТипыДанных.ЭтоСоставнойТип(Колонка.ТипЗначения) И НЕ РатТипыДанных.ЭтоСсылочныйТип(ТипЗнч(Выборка[Колонка.Имя])) Тогда
			
			Данные[Колонка.Имя] = РатПреобразования.СериализоватьРеквизитСоставногоТипа(Выборка[Колонка.Имя]);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Данные;
	
КонецФункции

// Создает объект информационной базы.
//
// Параметры:
//   ДанныеЗаписи           - см. РатИнформационнаяБаза.ДанныеЗаписи
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
// 
// Возвращаемое значение:
//   - ЛюбаяСсылка - Если объект ссылочного типа.
//   - КлючЗаписи  - Если это запись регистра.
//
// Пример:
//   Данные = РатИнформационнаяБаза.ДанныеЗаписи("Справочник.Валюты");
//   Данные.Реквизиты.Наименование = "Новая валюта";
//   Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   НоваяСсылка = РатИнформационнаяБаза.СоздатьЭлемент(Данные, Статус);
//
Функция СоздатьЭлемент(ДанныеЗаписи, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатИнформационнаяБазаПереопределяемый.СоздатьЭлемент(ДанныеЗаписи, СтатусОбработкиЗапроса, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИмяТаблицы = ДанныеЗаписи.Метаданные.ИмяТаблицы;
	
	ОписаниеТаблицы = РатМетаданные.РазложитьИмяТаблицы(ИмяТаблицы);
	
	Если НЕ ДоступноРедактирование(ОписаниеТаблицы, СтатусОбработкиЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запись = СоздатьОбъект(ОписаниеТаблицы, ДанныеЗаписи, СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьДанные(Запись, ДанныеЗаписи, СтатусОбработкиЗапроса, Истина);
	
	СохранитьОбъект(Запись, ДанныеЗаписи, СтатусОбработкиЗапроса);
	
	Возврат ИдентификаторЗаписи(Запись, ИмяТаблицы, СтатусОбработкиЗапроса);
	
КонецФункции

// Изменяет существующий объект информационной базы.
//
// Параметры:
//   Идентификатор - ЛюбаяСсылка - Ссылка на объект ИБ
//                 - КлючЗаписи - Идентификатор записи регистра
//   ДанныеЗаписи - см. РатИнформационнаяБаза.ДанныеЗаписи
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   - Неопределено - При возникновении ошибок.
//   - см. РатОбщий.РезультатДанныеЗаписаны
//
// Пример:
//   Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   Данные = РатИнформационнаяБаза.ДанныеЗаписи("Справочник.Валюты");
//   Данные.Реквизиты.Наименование = "Новое имя";
//   Результат = РатИнформационнаяБаза.ОбновитьЭлемент(СсылкаНаОбъект, Данные, Статус);
//
Функция ОбновитьЭлемент(Идентификатор, ДанныеЗаписи, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатИнформационнаяБазаПереопределяемый.ОбновитьЭлемент(Идентификатор, ДанныеЗаписи, СтатусОбработкиЗапроса, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИмяТаблицы = ДанныеЗаписи.Метаданные.ИмяТаблицы;
	ОписаниеТаблицы = РатМетаданные.РазложитьИмяТаблицы(ИмяТаблицы);
	
	Если НЕ ДоступноРедактирование(ОписаниеТаблицы, СтатусОбработкиЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запись = ПолучитьОбъектДляРедактирования(Идентификатор, ОписаниеТаблицы, СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьДанные(Запись, ДанныеЗаписи, СтатусОбработкиЗапроса, Ложь);
	
	СохранитьОбъект(Запись, ДанныеЗаписи, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Возврат РатОбщий.РезультатДанныеЗаписаны("Объект успешно обновлен");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Удаляет объект информационной базы.
//
// Параметры:
//   Идентификатор          - ЛюбаяСсылка - Ссылка на объект ИБ
//                          - КлючЗаписи - Идентификатор записи регистра
//   ИмяТаблицы             - Строка - Имя таблицы объекта.
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   - Неопределено - При возникновении ошибок.
//   - см. РатОбщий.РезультатДанныеЗаписаны
//
// Пример:
//   Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   РатИнформационнаяБаза.УдалитьЭлемент(СсылкаНаОбъект, "Справочник.Номенклатура", Статус);
//
Функция УдалитьЭлемент(Идентификатор, ИмяТаблицы, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатИнформационнаяБазаПереопределяемый.УдалитьЭлемент(Идентификатор, ИмяТаблицы, СтатусОбработкиЗапроса, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОписаниеТаблицы = РатМетаданные.РазложитьИмяТаблицы(ИмяТаблицы);
	
	Если НЕ ДоступноРедактирование(ОписаниеТаблицы, СтатусОбработкиЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запись = ПолучитьОбъектДляРедактирования(Идентификатор, ОписаниеТаблицы, СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если РатМетаданные.ТаблицаСодержитОбъектныеДанные(ИмяТаблицы) Тогда
		Запись.УстановитьПометкуУдаления(Истина);
	Иначе
		Запись.Удалить();
	КонецЕсли;
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Возврат РатОбщий.РезультатДанныеЗаписаны("Объект успешно удален");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#Область Движения

// Возвращает коллекцию движений документа по всем регистраторам.
//
// Параметры:
//   Документ - ДокументСсылка - Ссылка на документ, движения которого нужно получить
//   ИмяТаблицы - Строка - Полное имя таблицы документа
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   - Неопределено - При возникновении ошибок.
//   - Структура - Данные движений документа:
//     * Ключ - Строка - Имя регистра
//     * Значение - ТаблицаЗначений - Строки записей регистра
//
// Пример:
//   Движения = РатИнформационнаяБаза.ДвиженияДокументаПоРегистрам(Ссылка, "Документ.РеализацияТоваров", Статус);
Функция ДвиженияДокументаПоРегистрам(Документ, ИмяТаблицы, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатИнформационнаяБазаПереопределяемый.ДвиженияДокументаПоРегистрам(Документ, ИмяТаблицы, СтатусОбработкиЗапроса, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат = Неопределено;
	
	РегистрыДвижений = РатМетаданные.ТаблицыДвижений(ИмяТаблицы);
	
	Результат = Новый Структура;
	
	Для Каждого Таблица Из РегистрыДвижений Цикл
		
		//@skip-check query-in-loop
		ДвиженияПоРегистру = ДвиженияПоРегистру(Документ, Таблица.Значение, СтатусОбработкиЗапроса);
		Результат.Вставить(Таблица.Ключ, ДвиженияПоРегистру);
		
	КонецЦикла;
	
	//@skip-check constructor-function-return-section
	Возврат Результат;
	
КонецФункции

// Возвращает строки движений документа по конкретному регистру.
//
// Параметры:
//   Документ - ДокументСсылка - Ссылка на документ, движения которого нужно получить
//   ПолноеИмяРегистра - Строка - Полное имя таблицы регистра
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   - Неопределено - При возникновении ошибок.
//   - ТаблицаЗначений - Строки движений документа по регистру
//
// Пример:
//   Движения = РатИнформационнаяБаза.ДвиженияПоРегистру(Ссылка, "РегистрНакопления.Продажи", Статус);
Функция ДвиженияПоРегистру(Документ, ПолноеИмяРегистра, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатИнформационнаяБазаПереопределяемый.ДвиженияПоРегистру(Документ, ПолноеИмяРегистра, СтатусОбработкиЗапроса, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрШаблон("ВЫБРАТЬ * ИЗ %1 ГДЕ Регистратор = &Регистратор", ПолноеИмяРегистра); // BSLLS:QueryParseError-off
	Запрос.УстановитьПараметр("Регистратор", Документ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый ТаблицаЗначений;
	Иначе
		Возврат РезультатЗапроса.Выгрузить();
	КонецЕсли;
	
КонецФункции

// Изменяет записи движений документа по регистру.
//
// Параметры:
//   Документ - ДокументСсылка - Ссылка на документ, движения которого нужно изменить
//   Данные - Массив из Структура - Новый набор движений документа
//   ПолноеИмяРегистра - Строка - Полное имя таблицы регистра
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Пример:
//   РатИнформационнаяБаза.УстановитьДвижения(Ссылка, МассивДвижений, "РегистрНакопления.Продажи", Статус);
Процедура УстановитьДвижения(Документ, Данные, ПолноеИмяРегистра, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат;
	КонецЕсли;
	
	ОтказОсновнойПроцедуры = Ложь;
	РатИнформационнаяБазаПереопределяемый.УстановитьДвижения(Документ, Данные, ПолноеИмяРегистра, СтатусОбработкиЗапроса, ОтказОсновнойПроцедуры);
	
	Если ОтказОсновнойПроцедуры Тогда
		Возврат;
	КонецЕсли;
	
	Менеджер = РатМетаданные.МенеджерТаблицы(ПолноеИмяРегистра, СтатусОбработкиЗапроса);
	
	НаборЗаписей = Менеджер.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Документ);
	
	ДатаДокумента = РатОбщий.ПолучитьЗначениеРеквизита(Документ, "Дата");
	
	Для Каждого Запись Из Данные.Данные Цикл
		
		Строка = НаборЗаписей.Добавить();
		Строка.Регистратор = Документ;
		Строка.Период = ДатаДокумента;
		ЗаполнитьЗначенияСвойств(Строка, Запись);
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Создает структуру с параметрами для создания и обновления записей в информационной базе.
//
// Параметры:
//  ИмяТаблицы - Строка - Полное имя таблицы в которой будут производится изменения.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Параметры записи объекта:
//    * Метаданные - Структура - Метаданные записи:
//        * ИмяТаблицы - Строка - Имя таблицы.
//        * ОбъектныеДанные - Булево - Признак того, что таблица содержит объектные данные.
//    * Реквизиты - Структура - Реквизиты объекта. Используется для объектных данных.
//    * ТабличныеЧасти - Структура - Табличные части объекта. Используется для объектных данных.
//    * Данные - Массив из Структура - Данные для записи. Используется для табличных данных.
//    * ДополнительныеСвойства - Структура - Дополнительные свойства объекта.
//    * ДополнительныеПараметры - Структура - Дополнительные параметры записи.
//    * Параметры - Структура - Параметры записи:
//        * РежимЗаписи - РежимЗаписиДокумента - Режим записи документа.
//        * РежимПроведения - РежимПроведенияДокумента - Режим проведения документа.
//        * ОбменДаннымиЗагрузка - Булево - Признак того, что запись выполняется в режиме обмена данными.
//        * Замещать - Булево - Признак того, что существующая запись будет замещена.
//
Функция ДанныеЗаписи(ИмяТаблицы) Экспорт
	
	ОбъектныеДанные = РатМетаданные.ТаблицаСодержитОбъектныеДанные(ИмяТаблицы);
	
	ДанныеЗаписи = Новый Структура;
	
	МетаданныеЗаписи = Новый Структура("ИмяТаблицы, ОбъектныеДанные", ИмяТаблицы, ОбъектныеДанные);
	ДанныеЗаписи.Вставить("Метаданные", МетаданныеЗаписи);
	
	Если ОбъектныеДанные Тогда
		ДанныеЗаписи.Вставить("Реквизиты", Новый Структура);
		ДанныеЗаписи.Вставить("ТабличныеЧасти", Новый Структура);
	Иначе
		ДанныеЗаписи.Вставить("Данные", Новый Массив);
	КонецЕсли;
	
	ДанныеЗаписи.Вставить("ДополнительныеСвойства", Новый Структура);
	ДанныеЗаписи.Вставить("ДополнительныеПараметры", Новый Структура);
	
	ПараметрыЗаписи = Новый Структура("РежимЗаписи, РежимПроведения");
	ПараметрыЗаписи.Вставить("ОбменДаннымиЗагрузка", Ложь);
	ПараметрыЗаписи.Вставить("Замещать",             Истина);
	
	ДанныеЗаписи.Вставить("Параметры", ПараметрыЗаписи);
	
	//@skip-check constructor-function-return-section
	Возврат Новый ФиксированнаяСтруктура(ДанныеЗаписи);
	
КонецФункции

// Создает структуру с параметрами выборки данных из информационной базы.
//
// Возвращаемое значение:
//  Структура - Параметры выборки данных:
// * ИмяТаблицы - Строка - Полное имя таблицы, из которой будут выбираться данные.
// * Отбор - Массив из см. РатИнформационнаяБаза.Предикат
// * Сортировка - Массив из Строка - Поля для сортировки данных.
// * ВыбираемыеПоля - Массив из Строка - Поля, которые будут выбраны.
// * КоличествоЭлементов - Число - Максимальное количество элементов, которое будет выбрано.
Функция ПараметрыВыборки() Экспорт
	
	ПараметрыВыборки = Новый Структура;
	ПараметрыВыборки.Вставить("ИмяТаблицы", "");
	ПараметрыВыборки.Вставить("Отбор", Новый Массив());
	ПараметрыВыборки.Вставить("Сортировка", Новый Массив());
	ПараметрыВыборки.Вставить("ВыбираемыеПоля", Новый Массив);
	ПараметрыВыборки.Вставить("КоличествоЭлементов", 10);
	
	Возврат ПараметрыВыборки;
	
КонецФункции

// Создает структуру с параметрами для отбора данных.
//
// Параметры:
//  Реквизит - Строка - Имя реквизита, по которому будет производиться отбор.
//  Значение - Произвольный - Значение для отбора.
//  Тип - Произвольный - Тип значения. Не используется.
//  Условие - Строка - Условие отбора. Может принимать значения
//    + Равно
//    + Больше
//    + БольшеИлиРавно
//    + Меньше
//    + МеньшеИлиРавно
//    + Подобно
//    + ВСписке
//    + Содержит
//    + НачинаетсяС
//    + ЗаканчиваетсяНа
//    + ВГруппе
//    + НеРавно
//    + НеБольше
//    + НеБольшеИлиРавно
//    + НеМеньше
//    + НеМеньшеИлиРавно
//    + НеПодобно
//    + НеВСписке
//    + НеСодержит
//    + НеНачинаетсяС
//    + НеЗаканчиваетсяНа
//    + НеВГруппе
//
// Возвращаемое значение:
//  Структура - Параметры отбора данных:
//  * Реквизит - Строка - Имя реквизита, по которому будет производиться отбор.
//  * Значение - Произвольный - Значение для отбора.
//  * Тип - Произвольный, Неопределено - Тип значения.
//  * Условие - Строка, Неопределено - Условие отбора.
Функция Предикат(Реквизит, Значение, Тип = Неопределено, Условие = Неопределено) Экспорт
	
	Предикат = Новый Структура();
	Предикат.Вставить("Реквизит", Реквизит);
	Предикат.Вставить("Значение", Значение);
	Предикат.Вставить("Тип", Тип);
	Предикат.Вставить("Условие", Условие);
	
	//@skip-check constructor-function-return-section
	Возврат Предикат;
	
КонецФункции

// Возвращает ссылку на объект по его уникальному идентификатору.
//
// Параметры:
//  Идентификатор - УникальныйИдентификатор, Строка - Уникальный идентификатор объекта.
//  ИмяТаблицы - Строка - Полное имя таблицы, в которой будет производиться поиск.
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  ЛюбаяСсылка - Ссылка на найденный объект.
//  Неопределено - Если объект не найден.
//
Функция СсылкаПоУникальномуИдентификатору(Идентификатор, ИмяТаблицы, СтатусОбработкиЗапроса) Экспорт
	
	Менеджер = РатМетаданные.МенеджерТаблицы(ИмяТаблицы, СтатусОбработкиЗапроса);
	
	Результат = Менеджер.ПолучитьСсылку(Новый УникальныйИдентификатор(Идентификатор));
	
	Если НЕ СуществуетОбъектПоСсылке(ИмяТаблицы, Результат) Тогда
		
		Результат = Неопределено;
		ТекстОшибки = СтрШаблон("Не удалось найти ссылку по уникальному идентификатору. %1 (%2)", ИмяТаблицы, Идентификатор);
		КлассОшибки =  РатОбщий.КлассыОшибок().НеНайденаЗаписьИБ;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает ссылку на объект по значению одного из его реквизитов.
//
// Параметры:
//  ЗначениеРеквизита - Произвольный - Значение реквизита для поиска.
//  ИмяТаблицы - Строка - Полное имя таблицы, в которой будет производиться поиск.
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  ЛюбаяСсылка - Ссылка на найденный объект.
//  Неопределено - Если объект не найден.
//
Функция СсылкаПоРеквизиту(ЗначениеРеквизита, ИмяТаблицы, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Неопределено;
	СтруктураТаблицы = РатМетаданные.СтруктураТаблицы(ИмяТаблицы);
	ЗапросыПоиска = Новый Массив();
	ТипМетаданных = РатМетаданные.ТипМетаданных(ИмяТаблицы);
	
	Для Каждого ПолеПоиска Из ТипМетаданных.Поиск Цикл
		
		Если ПолеПоиска = "Ссылка" ИЛИ НЕ СтруктураТаблицы.Реквизиты.Свойство(ПолеПоиска) Тогда
			Продолжить;
		КонецЕсли;
		
		ВыбираемыеПоля = СтрШаблон("%1 КАК Приоритет, Ссылка", ЗапросыПоиска.Количество());
		
		Если СтруктураТаблицы.Реквизиты.Свойство("Дата") Тогда
			
			Запрос = СформироватьЗапрос(ИмяТаблицы, ВыбираемыеПоля, ПолеПоиска + " = &Идентификатор", "Дата Убыв", 1);
			Запрос.Текст = СтрШаблон("ВЫБРАТЬ * ИЗ (%1) КАК Выборка", Запрос.Текст); // BSLLS:QueryParseError-off
		Иначе
			Запрос = СформироватьЗапрос(ИмяТаблицы, ВыбираемыеПоля, ПолеПоиска + " = &Идентификатор", , 1);
		КонецЕсли;
		
		ЗапросыПоиска.Добавить(Запрос.Текст);
		
	КонецЦикла;
	
	Если ЗапросыПоиска.Количество() Тогда
		
		ТекстЗапроса = СтрСоединить(ЗапросыПоиска, РатОбщийКлиентСервер.РазделительОператоровЗапроса(Истина));
		Запрос = Новый Запрос(ТекстЗапроса);
		
		РатСтроки.ДобавитьСтроку(Запрос.Текст, "УПОРЯДОЧИТЬ ПО Приоритет ВОЗР", Символы.ПС);
		
		Запрос.УстановитьПараметр("Идентификатор", ЗначениеРеквизита);
		
		Результат = РатОбщий.ЗначениеИзЗапроса(Запрос, "Ссылка");
		
		// Проверяем, что запись найдена
		Если Результат = Неопределено Тогда
			ТекстОшибки = СтрШаблон("Не удалось найти ссылку по реквизитам поиска. %1 (%2)", ИмяТаблицы, ЗначениеРеквизита);
			КлассОшибки = РатОбщий.КлассыОшибок().НеНайденаЗаписьИБ;
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		КонецЕсли;
		
	Иначе
		
		ТекстОшибки = СтрШаблон("Не найдены поля поиска для таблицы %1", ИмяТаблицы);
		КлассОшибки =  РатОбщий.КлассыОшибок().НеНайденаЗаписьИБ;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сохраняет объект в информационной базе.
//
// Параметры:
//  Объект - Произвольный - Объект для сохранения.
//  ДанныеЗаписи - см. РатИнформационнаяБаза.ДанныеЗаписи
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
Процедура СохранитьОбъект(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса) Экспорт
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат;
	КонецЕсли;
	
	ОтказОсновнойПроцедуры = Ложь;
	РатИнформационнаяБазаПереопределяемый.СохранитьОбъект(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса, ОтказОсновнойПроцедуры);
	
	Если ОтказОсновнойПроцедуры Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = ДанныеЗаписи.Метаданные.ИмяТаблицы;
	ПараметрыЗаписи = ДанныеЗаписи.Параметры;
	
	Если ПараметрыЗаписи.ОбменДаннымиЗагрузка Тогда
		Объект.ОбменДанными.Загрузка = Истина;
	КонецЕсли;
	
	ПолучитьСообщенияПользователю(Истина);
	
	ПередЗаписьюОбъекта(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		Если РатМетаданные.ЭтоТаблицаДокумента(ИмяТаблицы) Тогда
			
			РежимЗаписи = ПараметрыЗаписи.РежимЗаписи;
			Если РежимЗаписи = Неопределено И Объект.Проведен Тогда
				РежимЗаписи = РежимЗаписиДокумента.Проведение;
			КонецЕсли;
			
			Объект.Записать(РежимЗаписи, ПараметрыЗаписи.РежимПроведения);
			
		ИначеЕсли НЕ РатМетаданные.ТаблицаСодержитОбъектныеДанные(ИмяТаблицы) Тогда
			
			Замещать = ПараметрыЗаписи.Замещать;
			Объект.Записать(Замещать);
			
		Иначе
			
			Объект.Записать();
			
		КонецЕсли;
		
	Исключение
		
		Сообщения = РатКоллекции.ВыгрузитьЗначения(ПолучитьСообщенияПользователю(Истина), "Текст");
		Сообщения.Вставить(0, СтрШаблон("Не удалось записать %1", Объект));
		Сообщения.Добавить(РатСтроки.ПредставлениеОшибки(ИнформацияОбОшибке()));
		ТекстОшибки = СтрСоединить(Сообщения, Символы.ПС);
		
		КлассОшибки = РатОбщий.КлассыОшибок().ЛогикаКонфигурации;
		
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СозданиеРедактированиеДанных

// Создает объект по типу метаданных.
//
// Параметры:
//   Менеджер - Произвольный - Менеджер таблицы
//   ТипОбъекта - Строка - Тип объекта метаданных
//   ДанныеЗаписи - Структура - Данные для создания объекта
//
// Возвращаемое значение:
//   Произвольный - Созданный объект
//   Неопределено - Если тип объекта не поддерживается
//
Функция СоздатьОбъектПоТипу(Менеджер, ТипОбъекта, ДанныеЗаписи)
	
	Результат = Неопределено;
	
	Если СтрСравнить(ТипОбъекта, "Документ") = 0 Тогда
		Результат = Менеджер.СоздатьДокумент();
	ИначеЕсли СтрСравнить(ТипОбъекта, "Справочник") = 0 Или СтрСравнить(ТипОбъекта, "ПланВидовХарактеристик") = 0 Тогда
		Если РатКоллекции.ЗначениеСтруктуры(ДанныеЗаписи.Реквизиты, "ЭтоГруппа", Ложь) = Истина Тогда
			Результат = Менеджер.СоздатьГруппу();
		Иначе
			Результат = Менеджер.СоздатьЭлемент();
		КонецЕсли;
	ИначеЕсли СтрСравнить(ТипОбъекта, "РегистрСведений") = 0 Тогда
		Результат = Менеджер.СоздатьМенеджерЗаписи();
	ИначеЕсли СтрСравнить(ТипОбъекта, "ПланСчетов") = 0 Тогда
		Результат = Менеджер.СоздатьЭлемент();
	ИначеЕсли СтрСравнить(ТипОбъекта, "ПланВидовРасчета") = 0 Тогда
		Результат = Менеджер.СоздатьВидРасчета();
	ИначеЕсли СтрСравнить(ТипОбъекта, "ПланОбмена") = 0 Тогда
		Результат = Менеджер.СоздатьУзел();
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СоздатьОбъект(ОписаниеТаблицы, ДанныеЗаписи, СтатусОбработкиЗапроса)
	
	КлассОшибки = РатОбщий.КлассыОшибок().НедоступнаяФункциональность;
	
	Менеджер = РатМетаданные.МенеджерТаблицы(ОписаниеТаблицы, СтатусОбработкиЗапроса);
	ТипОбъекта = ОписаниеТаблицы.ИмяТипа;
	
	Если Менеджер = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = СоздатьОбъектПоТипу(Менеджер, ТипОбъекта, ДанныеЗаписи);
	
	Если Результат = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Для %1 не поддерживается создание записей ИБ", ТипОбъекта);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	Если Результат <> Неопределено И ОписаниеТаблицы.Тип.ОбъектныеДанные Тогда
		ПредставлениеСсылки = РатКоллекции.ЗначениеСтруктуры(ДанныеЗаписи.Реквизиты, "Ссылка");
		Если ПредставлениеСсылки <> Неопределено Тогда
			Ссылка = Менеджер.ПолучитьСсылку(ПредставлениеСсылки);
			Результат.УстановитьСсылкуНового(Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьДанные(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса, ЭтоНовый)
	
	ОтказОсновнойПроцедуры = Ложь;
	РатИнформационнаяБазаПереопределяемый.УстановитьДанные(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса, ОтказОсновнойПроцедуры);
	
	Если ОтказОсновнойПроцедуры ИЛИ НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеЗаписи = ДанныеЗаписи.Метаданные;
	
	Попытка
		
		Если МетаданныеЗаписи.ОбъектныеДанные Тогда
			УстановитьДанныеОбъекта(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса);
			РатКоллекции.ОбъединитьВСтруктуру(Объект.ДополнительныеСвойства, ДанныеЗаписи.ДополнительныеСвойства);
		Иначе
			УстановитьДанныеТабличногоОбъекта(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса, ЭтоНовый);
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), "Не удалось преобразовать данные объекта для записи в базу данных");
		КлассОшибки = РатОбщий.КлассыОшибок().Неизвестная;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура УстановитьДанныеТабличногоОбъекта(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса, ЭтоНовый)
	
	ПараметрыЗаписи = ДанныеЗаписи.Параметры;
	
	Данные = ДанныеЗаписи.Данные[0];
	
	Если ЭтоНовый Тогда
		
		КлючевыеПоля = РатМетаданные.КлючевыеПоляТаблицы(ДанныеЗаписи.Метаданные.ИмяТаблицы);
		Для Каждого КлючевоеПоле Из КлючевыеПоля Цикл
			
			Если НЕ Данные.Свойство(КлючевоеПоле) Тогда
				ТекстОшибки = "Не указан ключевой реквизит " + КлючевоеПоле;
				КлассОшибки = РатОбщий.КлассыОшибок().АнализПараметровЗапроса;
				РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЭтоНовый И ПараметрыЗаписи.Замещать Тогда
		Объект.Прочитать();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, Данные);
	
КонецПроцедуры

Процедура УстановитьДанныеОбъекта(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса)
	
	// Реквизиты, которые могут быть переданы, но не должны попадать при заполнении 
	РеквизитыИсключения = "ЭтоГруппа";
	
	КопияРеквизитов = Неопределено;
	
	Для Каждого Имя Из СтрРазделить(РеквизитыИсключения, ", ", Ложь) Цикл
		
		Если ДанныеЗаписи.Реквизиты.Свойство(Имя) Тогда
			
			Если КопияРеквизитов = Неопределено Тогда
				КопияРеквизитов = РатКоллекции.СкопироватьСтруктуру(ДанныеЗаписи.Реквизиты);
			КонецЕсли;
			
			КопияРеквизитов.Удалить(Имя);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если КопияРеквизитов = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект, ДанныеЗаписи.Реквизиты);
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, КопияРеквизитов);
	КонецЕсли;
	
	Для Каждого ДанныеТабличнойЧасти Из ДанныеЗаписи.ТабличныеЧасти Цикл
		
		ТабличнаяЧасть = Объект[ДанныеТабличнойЧасти.Ключ];
		ТабличнаяЧасть.Очистить();
		
		Для Каждого СтрокаДанных Из ДанныеТабличнойЧасти.Значение Цикл
			
			СтрокаТЧ = ТабличнаяЧасть.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаДанных);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Процедура ПередЗаписьюОбъекта(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса)
	
	ОтказОсновногоОбработчика = Ложь;
	РатИнформационнаяБазаПереопределяемый.ПередЗаписьюОбъекта(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса, ОтказОсновногоОбработчика);
	
	Если ОтказОсновногоОбработчика Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = ДанныеЗаписи.Метаданные.ИмяТаблицы;
	ТипМетаданных = РатМетаданные.ТипМетаданных(ИмяТаблицы);
	
	УстановкаКода = ТипМетаданных.Поиск.Найти("Код") <> Неопределено;
	
	АвтоНумерацияПрименима = ТипМетаданных.Имя = "Документ"
		ИЛИ ТипМетаданных.Имя = "БизнесПроцесс"
		ИЛИ ТипМетаданных.Имя = "Задача";
		
	Если АвтоНумерацияПрименима И НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		Объект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если АвтоНумерацияПрименима И НЕ ЗначениеЗаполнено(Объект.Номер) Тогда
		Объект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Если УстановкаКода И РатОбщийКлиентСервер.ПеременнаяСодержитСвойство(Объект, "Код") И НЕ ЗначениеЗаполнено(Объект.Код) Тогда
		Объект.УстановитьНовыйКод();
	КонецЕсли;

КонецПроцедуры

Функция ИдентификаторЗаписи(Запись, ИмяТаблицы, СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипЗаписи = ТипЗнч(Запись);
	Результат = Неопределено;
	
	Если РатМетаданные.ЭтоРегистрСведений(ТипЗаписи) Тогда
		
		Результат = КлючЗаписи(Запись, ИмяТаблицы, СтатусОбработкиЗапроса);
		
	Иначе
		
		Результат = Запись.Ссылка;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОбъектДляРедактирования(Ссылка, ОписаниеТаблицы, СтатусОбработкиЗапроса)
	
	Если РатМетаданные.ЭтоТаблицаОбъектныхДанных(ОписаниеТаблицы) Тогда
		Результат = Ссылка.ПолучитьОбъект();
	Иначе
		Результат = СоздатьМенеджерЗаписи(ОписаниеТаблицы, Ссылка, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		КлассОшибки = РатОбщий.КлассыОшибок().НеНайденаЗаписьИБ;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, "Не найдена запись по ссылке (ключу записи) в информационной базе");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДоступноРедактирование(ОписаниеТаблицы, СтатусОбработкиЗапроса)
	
	Доступно = РатМетаданные.ДоступноРедактирование(ОписаниеТаблицы);
	
	Если НЕ Доступно Тогда
		
		ТекстОшибки = СтрШаблон("Редактирование этого типа объекта (%1) не поддерживается", ОписаниеТаблицы.ИмяТипа);
		КлассОшибки = РатОбщий.КлассыОшибок().НедоступнаяФункциональность;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	ИначеЕсли РатМетаданные.ЭтоТаблицаРегистра(ОписаниеТаблицы) Тогда
		
		Если НЕ РатМетаданные.ЭтоТаблицаНезависимогоРегистрСведений(ОписаниеТаблицы) Тогда
			
			ТекстОшибки = СтрШаблон("Непосредственное редактирование записей регистра (%1.%2) подчиненного регистратору не поддерживается.
									|Воспользуйтесь методами для работы с движениями", ОписаниеТаблицы.ИмяТипа, ОписаниеТаблицы.ИмяВида);
			КлассОшибки = РатОбщий.КлассыОшибок().НедоступнаяФункциональность;
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
			Доступно = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Доступно;
	
КонецФункции

Функция КлючЗаписи(Запись, ИмяТаблицы, СтатусОбработкиЗапроса)
	
	КлючевыеПоля = РатМетаданные.КлючевыеПоляТаблицы(ИмяТаблицы);
	ИменаРеквизитовКлюча = СтрСоединить(КлючевыеПоля, ", ");
	
	ДанныеКлюча = Новый Структура(ИменаРеквизитовКлюча);
	ЗаполнитьЗначенияСвойств(ДанныеКлюча, Запись);
	
	Менеджер = РатМетаданные.МенеджерТаблицы(ИмяТаблицы, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Менеджер.СоздатьКлючЗаписи(ДанныеКлюча);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция СоздатьМенеджерЗаписи(ОписаниеТаблицы, КлючЗаписи, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	Менеджер = РатМетаданные.МенеджерТаблицы(ОписаниеТаблицы, СтатусОбработкиЗапроса);
	
	Если Менеджер = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		
		Результат = Менеджер.СоздатьМенеджерЗаписи();
		
		Если КлючЗаписи = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
		
		Для Каждого ИмяРеквизита Из РатМетаданные.ОтборРегистра(ОписаниеТаблицы) Цикл
			Результат[ИмяРеквизита] = КлючЗаписи[ИмяРеквизита];
		КонецЦикла;
		
	Исключение
		
		ТекстОшибки = СтрШаблон("Не удалось создать менеджер записи для редактирования регистра (%1.%2)",
								ОписаниеТаблицы.ИмяТипа,
								ОписаниеТаблицы.ИмяВида);
		ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), ТекстОшибки);
		КлассОшибки = РатОбщий.КлассыОшибок().Неизвестная;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция РазделительПолейЗапроса(Разделитель = ",")
	
	Возврат Разделитель + Символы.ПС + Символы.Таб;
	
КонецФункции

// Сформировать запрос.
// 
// Параметры:
//  ИмяТаблицы - Строка - Имя таблицы
//  ВыбираемыеПоля - Строка, Массив из Строка - Выбираемые поля
//  Условия - Массив из см. Предикат
//  Сортировка - Неопределено, Строка, Массив из Строка - Сортировка
//  Количество - Неопределено, Число - Количество
// 
// Возвращаемое значение:
//  Запрос - Сформировать запрос
//@skip-check method-too-many-params
Функция СформироватьЗапрос(ИмяТаблицы, ВыбираемыеПоля = "*", Условия = Неопределено, Сортировка = Неопределено, Количество = Неопределено)
	
	Запрос = Новый Запрос;
	БлокиТекстаЗапроса = Новый Массив;
	
	БлокиТекстаЗапроса.Добавить(СформироватьБлокВыбора(ВыбираемыеПоля, Количество));
	БлокиТекстаЗапроса.Добавить(СтрШаблон("ИЗ %1 КАК Выборка", ИмяТаблицы));
	БлокиТекстаЗапроса.Добавить(СформироватьБлокУсловий(Условия, Запрос));
	
	Если ЗначениеЗаполнено(Сортировка) Тогда
		БлокиТекстаЗапроса.Добавить("УПОРЯДОЧИТЬ ПО");
		Если ТипЗнч(Сортировка) = Тип("Массив") Тогда
			БлокиТекстаЗапроса.Добавить("	" + СтрСоединить(Сортировка, РазделительПолейЗапроса()));
		Иначе
			БлокиТекстаЗапроса.Добавить("	" + Сортировка);
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(БлокиТекстаЗапроса, Символы.ПС);
	
	Возврат Запрос;
КонецФункции

Функция СформироватьБлокВыбора(ВыбираемыеПоля, Количество)
	БлокиТекстаЗапроса = Новый Массив;
	
	Если Количество <> Неопределено Тогда
		БлокиТекстаЗапроса.Добавить("ВЫБРАТЬ ПЕРВЫЕ " + Формат(Количество, "ЧН=0; ЧГ=0;"));
	Иначе
		БлокиТекстаЗапроса.Добавить("ВЫБРАТЬ");
	КонецЕсли;
	
	Если ТипЗнч(ВыбираемыеПоля) = Тип("Массив") Тогда
		БлокиТекстаЗапроса.Добавить("	" + СтрСоединить(ВыбираемыеПоля, РазделительПолейЗапроса()));
	Иначе
		БлокиТекстаЗапроса.Добавить("	" + ВыбираемыеПоля);
	КонецЕсли;
	
	Возврат СтрСоединить(БлокиТекстаЗапроса, Символы.ПС);
	
КонецФункции

Функция СформироватьБлокУсловий(Условия, Запрос)
	
	Если НЕ ЗначениеЗаполнено(Условия) Тогда
		Возврат "";
	КонецЕсли;
	
	БлокиТекстаЗапроса = Новый Массив;
	БлокиТекстаЗапроса.Добавить("ГДЕ");
	
	Если ТипЗнч(Условия) = Тип("Строка") Тогда
		БлокиТекстаЗапроса.Добавить("	" + Условия);
	Иначе
		ДобавитьИ = Ложь;
		НомерПараметра = 1;
		Для Каждого Предикат Из Условия Цикл
			ИмяПараметра = СтрШаблон("Параметр%1", РатСтроки.ЧислоВСтроку(НомерПараметра));
			НомерПараметра = НомерПараметра + 1;
			
			Выражение = ВыражениеПредиката(Предикат, ИмяПараметра);
			
			Если ДобавитьИ Тогда
				БлокиТекстаЗапроса.Добавить(" И ");
			Иначе
				ДобавитьИ = Истина;
			КонецЕсли;
			
			БлокиТекстаЗапроса.Добавить(Выражение);
			Запрос.УстановитьПараметр(ИмяПараметра, Предикат.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СтрСоединить(БлокиТекстаЗапроса, Символы.ПС);
КонецФункции

Функция ПолучитьОператорСравнения(Условие)
	ВидыСравнения = РатОбщийКлиентСервер.ВидыСравнения();
	
	Если НЕ ЗначениеЗаполнено(Условие) Тогда
		Возврат "=";
	ИначеЕсли НЕ ВидыСравнения.Свойство(Условие) Тогда
		ВызватьИсключение "Неизвестный оператор сравнения";
	КонецЕсли;
	
	Условие = ВидыСравнения[Условие];
	
	Если Условие = ВидыСравнения.Равно Тогда
		Возврат "=";
	ИначеЕсли Условие = ВидыСравнения.Больше Тогда
		Возврат ">";
	ИначеЕсли Условие = ВидыСравнения.БольшеИлиРавно Тогда
		Возврат ">=";
	ИначеЕсли Условие = ВидыСравнения.Меньше Тогда
		Возврат "<";
	ИначеЕсли Условие = ВидыСравнения.МеньшеИлиРавно Тогда
		Возврат "<=";
	ИначеЕсли Условие = ВидыСравнения.Подобно Тогда
		Возврат "Подобно";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ВыражениеПредиката(Предикат, ИмяПараметра)
	Выражение = Неопределено;
	Отрицания = СтрНачинаетсяС(НРег(Предикат.Условие), "не");
	
	Если Отрицания Тогда
		Условие = Сред(Предикат.Условие, 3);
	Иначе
		Условие = Предикат.Условие;
	КонецЕсли;
	
	Оператор = ПолучитьОператорСравнения(Условие);
	
	Если Оператор <> Неопределено Тогда
		Выражение = СтрШаблон("Выборка.%1 %2 &%3", Предикат.Реквизит, Оператор, ИмяПараметра);
	ИначеЕсли Условие = РатОбщийКлиентСервер.ВидыСравнения().ВСписке Тогда
		Выражение = СтрШаблон("Выборка.%1 В (&%2)", Предикат.Реквизит, ИмяПараметра);
	ИначеЕсли Условие = РатОбщийКлиентСервер.ВидыСравнения().Содержит Тогда
		Выражение = СтрШаблон("Выборка.%1 ПОДОБНО ""%%"" + &%2 + ""%%""", Предикат.Реквизит, ИмяПараметра);
	ИначеЕсли Условие = РатОбщийКлиентСервер.ВидыСравнения().НачинаетсяС Тогда
		Выражение = СтрШаблон("Выборка.%1 ПОДОБНО &%2 + ""%%""", Предикат.Реквизит, ИмяПараметра);
	ИначеЕсли Условие = РатОбщийКлиентСервер.ВидыСравнения().ЗаканчиваетсяНа Тогда
		Выражение = СтрШаблон("Выборка.%1 ПОДОБНО ""%%"" + &%2", Предикат.Реквизит, ИмяПараметра);
	ИначеЕсли Условие = РатОбщийКлиентСервер.ВидыСравнения().ВГруппе Тогда
		Выражение = СтрШаблон("Выборка.%1 В ИЕРАРХИИ (&%2)", Предикат.Реквизит, ИмяПараметра);
	Иначе
		ВызватьИсключение СтрШаблон("Неизвестное условие сравнения: %1", Условие);
	КонецЕсли;
	
	Если Отрицания Тогда
		Выражение = "НЕ " + Выражение;
	КонецЕсли;
	
	Возврат Выражение;
КонецФункции

Функция СуществуетОбъектПоСсылке(ИмяТаблицы, Ссылка)
	
	Запрос = Новый Запрос;
	// BSLLS:QueryParseError-off
	Запрос.Текст = СтрШаблон(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1 КАК Поле1
		|ИЗ %1 КАК Таблица
		|ГДЕ Ссылка = &Ссылка", ИмяТаблицы);
	// BSLLS:QueryParseError-on
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

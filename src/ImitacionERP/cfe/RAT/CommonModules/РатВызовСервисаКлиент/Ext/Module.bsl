//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

#Область НастройкиПодключений

Функция НастройкиПодключений(Контекст) Экспорт
	
	Ключ = "РатНастройкиПодключений";
	НастройкиПодключений = Неопределено;
	
	Если НЕ Контекст.Свойство(Ключ, НастройкиПодключений) Тогда
		НастройкиПодключений = Новый Соответствие();
		Контекст.Вставить(Ключ, НастройкиПодключений);
		ИнициализироватьНастройкиПодключений(Контекст, НастройкиПодключений);
	КонецЕсли;
	
	Возврат НастройкиПодключений;
	
КонецФункции

Функция НастройкиПодключения(Контекст, ИмяНастройки) Экспорт
	
	НастройкиПодключений = НастройкиПодключений(Контекст);
	НастройкиПодключения = НастройкиПодключений[НРег(ИмяНастройки)];
	
	Если НастройкиПодключения = Неопределено Тогда
		
		НастройкиПодключения = НайтиНастройкиПодключения(ИмяНастройки);
		
		Если НастройкиПодключения <> Неопределено Тогда
			ДобавитьНастройкуПодключения(Контекст, ИмяНастройки, НастройкиПодключения);
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НастройкиПодключения) Тогда
		ВызватьИсключение СтрШаблон("Не удалось определить настройки подключения для '%1'", ИмяНастройки);
	КонецЕсли;
	
	Возврат НастройкиПодключения;
	
КонецФункции

// Устанавливает настройки подключения к сервису.
//
// Параметры:
//   Контекст - Произвольный - контекст выполнения
//   ИмяНастройки - Строка - имя настройки подключения
//   Настройки - Структура - параметры подключения:
//     * Сервер - Строка - адрес сервера
//     * Порт - Число - порт сервера (опционально)
//     * Схема - Строка - схема подключения (опционально)
//     * Ресурс - Строка - путь к ресурсу (опционально)
//     * Пользователь - Строка - имя пользователя (опционально)
//     * Пароль - Строка - пароль пользователя (опционально)
//
Процедура УстановитьНастройкиПодключения(Контекст, ИмяНастройки, Настройки) Экспорт
	
	Если НЕ Настройки.Свойство("Сервер") Тогда
		ВызватьИсключение "Не указан сервер подключения";
	КонецЕсли;
	
	НастройкиПодключения = РатШагиВанессыКлиентСервер.ОписаниеНастройкиПодключения();
	
	ПараметрыАдреса = РатКоннекторHTTP.РазобратьURL(Настройки.Сервер);
	НастройкиПодключения.Сервер = ПараметрыАдреса.Сервер;
	НастройкиПодключения.Порт = ПараметрыАдреса.Порт;
	НастройкиПодключения.Схема = ПараметрыАдреса.Схема;
	НастройкиПодключения.Ресурс = ПараметрыАдреса.Путь;
	НастройкиПодключения.Пользователь = ПараметрыАдреса.Аутентификация.Пользователь;
	НастройкиПодключения.Пароль = ПараметрыАдреса.Аутентификация.Пароль;
	
	ЗаполнитьЗначенияСвойств(НастройкиПодключения, Настройки, , "Сервер");
	
	ДобавитьНастройкуПодключения(Контекст, ИмяНастройки, НастройкиПодключения);
	
КонецПроцедуры

Процедура УстановитьЗаголовкиНастройкиПодключения(Контекст, ИмяНастройки, Заголовки) Экспорт
	
	ШаблонЗапроса = НастройкиПодключения(Контекст, ИмяНастройки);
	ШаблонЗапроса.Заголовки.Очистить();
	
	РатШагиВанессыКлиент.СтруктураДанных(Заголовки, Ложь, ШаблонЗапроса.Заголовки);
	
КонецПроцедуры

Процедура УстановитьРазрешенныеКодыОтветов(Контекст, ИмяНастройки, Знач КодыОтветов) Экспорт
	
	Если ТипЗнч(КодыОтветов) = Тип("Строка") Тогда
		КодыОтветов = РатКоллекции.ЗначениеВМассиве(Новый Структура("Кол1", КодыОтветов));
	КонецЕсли;
	
	ШаблонЗапроса = НастройкиПодключения(Контекст, ИмяНастройки);
	
	ШаблонЗапроса.РазрешенныеКодыСостояния.Очистить();
	
	Для Каждого Строка Из КодыОтветов Цикл
		
		ШаблонЗапроса.РазрешенныеКодыСостояния.Добавить(Число(Строка.Кол1));
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

Функция АдресРесурса(ИмяТаблицы, Контекст, Суффикс = Неопределено) Экспорт
	
	АдресРесурса = СтрЗаменить(ИмяТаблицы, ".", "/");
	АдресРесурса = РатСтроки.ДобавитьСтроку(АдресРесурса, Суффикс, "/");
	
	РатШагиВанессыКлиент.ПодставитьПеременныеВСтроку(Контекст, АдресРесурса);
	
	Возврат АдресРесурса;
	
КонецФункции

Процедура УдалитьЗаписиСоответствующиеОтбору(КонтекстВанессы, МетаданныеТаблицы, ИмяНастройки, Отбор) Экспорт
	
	АдресТаблицы = АдресРесурса(МетаданныеТаблицы.ИмяТаблицы, КонтекстВанессы.Контекст);
	
	ИмяРеквизитаПометкаУдаления = "ПометкаУдаления";
	
	Для Каждого Реквизит Из МетаданныеТаблицы.Реквизиты Цикл
		
		Если СтрСравнить(Реквизит.Имя, ИмяРеквизитаПометкаУдаления) = 0 Тогда
			Если РатТипыДанных.ЭтоМассив(Отбор.filter) Тогда
				Предикат = Новый Структура("key, value", ИмяРеквизитаПометкаУдаления, Ложь);
				Отбор.filter.Добавить(Предикат);
			Иначе
				Отбор.filter.Вставить(ИмяРеквизитаПометкаУдаления, Ложь);
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ОписаниеЗапроса = НовоеОписаниеЗапроса(КонтекстВанессы, ИмяНастройки, РатОбщийКлиентСервер.МетодыHTTP().DELETE, Неопределено);
	
	Пока Истина Цикл
		
		Лимит = 100;
		Ответ = Поиск(КонтекстВанессы, МетаданныеТаблицы.ИмяТаблицы, Отбор, ИмяНастройки, , Лимит);
		Данные = Ответ.Тело;
		
		Если Данные.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Для Каждого Запись Из Данные Цикл
			
			ИдентификаторЗаписи = ИдентификаторЗаписи(Запись, МетаданныеТаблицы);
			ОписаниеЗапроса.АдресРесурса = СтрШаблон("%1/%2", АдресТаблицы, ИдентификаторЗаписи);
			Ответ = ВыполнитьHttpЗапрос(ОписаниеЗапроса, КонтекстВанессы);
			СохранитьРезультатЗапросаВКонтекст(КонтекстВанессы, Ответ);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет поиск записей по заданным условиям.
//
// Параметры:
//   КонтекстВанессы - Произвольный - контекст выполнения
//   ИмяТаблицы - Строка - имя таблицы для поиска
//   Условия - Структура - условия поиска
//   ИмяНастройки - Строка - имя настройки подключения
//   ПолучаемыеПоля - Строка, Массив из Строка- список полей для получения (опционально)
//   ОграничениеКоличества - Число - максимальное количество записей (опционально)
//
// Возвращаемое значение:
//   Структура - результат выполнения запроса:
//     * Тело - Произвольный - данные ответа
//     * КодСостояния - Число - HTTP код ответа
//     * РасшифровкаКода - Строка - расшифровка кода ответа
//
Функция Поиск(КонтекстВанессы, ИмяТаблицы, Условия, ИмяНастройки, ПолучаемыеПоля = Неопределено, ОграничениеКоличества = Неопределено) Экспорт
	
	Контекст = КонтекстВанессы.Контекст;
	
	Метод = РатОбщийКлиентСервер.МетодыHTTP().POST;
	АдресРесурса = АдресРесурса(ИмяТаблицы, Контекст, "Поиск");
	Тело = РатШагиВанессыКлиент.СтруктураПоиска(Условия);
	
	Если ПолучаемыеПоля <> Неопределено Тогда
		Если ТипЗнч(ПолучаемыеПоля) = Тип("Строка") Тогда
			Поля = СтрРазделить(ПолучаемыеПоля, ", ", Ложь);
		Иначе
			Поля = ПолучаемыеПоля;
		КонецЕсли;
		
		Тело.Вставить("fields", Поля);
	КонецЕсли;
	
	Если ОграничениеКоличества <> Неопределено Тогда
		Тело.Вставить("limit", ОграничениеКоличества);
	КонецЕсли;
	
	ОписаниеЗапроса = НовоеОписаниеЗапроса(КонтекстВанессы, ИмяНастройки, Метод, АдресРесурса);
	ОписаниеЗапроса.Тело = Тело;
	
	Возврат ВыполнитьHttpЗапрос(ОписаниеЗапроса, КонтекстВанессы);
	
КонецФункции

// Создает описание HTTP запроса.
//
// Параметры:
//   НастройкиПодключения - Структура - настройки подключения
//   Метод - Строка - HTTP метод (GET, POST, PUT, DELETE)
//   АдресРесурса - Строка - относительный путь к ресурсу
//
// Возвращаемое значение:
//   Структура - описание запроса:
//     * НастройкиПодключения - Структура - настройки подключения
//     * Метод - Строка - HTTP метод
//     * АдресРесурса - Строка - путь к ресурсу
//     * ПараметрыЗапроса - Соответствие из Произвольный - параметры запроса
//     * Тело - Произвольный - тело запроса
//     * ПроверятьОтвет - Булево - проверять ли ответ
//     * Заголовки - Соответствие из Строка - HTTP заголовки
//     * Отладка - Булево - включить отладку
//
Функция ОписаниеЗапроса(НастройкиПодключения, Метод = "GET", АдресРесурса = Неопределено) Экспорт
	
	ОписаниеЗапроса = Новый Структура();
	ОписаниеЗапроса.Вставить("НастройкиПодключения", НастройкиПодключения);
	ОписаниеЗапроса.Вставить("Метод", Метод);
	ОписаниеЗапроса.Вставить("АдресРесурса", АдресРесурса);
	ОписаниеЗапроса.Вставить("ПараметрыЗапроса");
	ОписаниеЗапроса.Вставить("Тело");
	ОписаниеЗапроса.Вставить("ПроверятьОтвет", Истина);
	ОписаниеЗапроса.Вставить("Заголовки", Новый Соответствие());
	ОписаниеЗапроса.Вставить("Отладка", Ложь);
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

// Выполняет HTTP запрос с заданными параметрами.
//
// Параметры:
//   ОписаниеЗапроса - Структура - описание запроса, см. ОписаниеЗапроса
//   КонтекстВанессы - Произвольный - контекст выполнения (опционально)
//
// Возвращаемое значение:
//   Структура - результат выполнения запроса:
//     * Тело - Произвольный - данные ответа
//     * КодСостояния - Число - HTTP код ответа
//     * РасшифровкаКода - Строка - расшифровка кода ответа
//
Функция ВыполнитьHttpЗапрос(ОписаниеЗапроса, КонтекстВанессы = Неопределено) Экспорт
	
	Если КонтекстВанессы <> Неопределено Тогда
		ОчиститьКонтекстПередЗапросом(КонтекстВанессы);
	КонецЕсли;
	
	НастройкиПодключения = ОписаниеЗапроса.НастройкиПодключения;
	
	Если НЕ ЗначениеЗаполнено(НастройкиПодключения) Тогда
		ВызватьИсключение "Не установлены настройки подключения";
	КонецЕсли;
	
	ЗапросКЭтойБазе = НастройкиПодключения.ЭтаБаза;
	
	Если ОписаниеЗапроса.Отладка Тогда
		Если ЗапросКЭтойБазе Тогда
			АдресСервера = "self";
		Иначе
			АдресСервера = СтрШаблон("%1://%2", НастройкиПодключения.Схема, НастройкиПодключения.Сервер);
		КонецЕсли;
		ПолныйАдрес = РатСтроки.ОбъединитьПути("/", АдресСервера, НастройкиПодключения.Ресурс, ОписаниеЗапроса.АдресРесурса);
		РатШагиВанессыКлиентСервер.ВывестиЗапрос(ПолныйАдрес, ОписаниеЗапроса);
	КонецЕсли;
	
	Если ЗапросКЭтойБазе Или ОписаниеЗапроса.НастройкиПодключения.ВыполнятьНаСервере Тогда
		//@skip-check bsl-legacy-check-expression-type
		Ответ = РатШагиВанессыВызовСервера.ВыполнитьHttpЗапрос(ОписаниеЗапроса);
	Иначе
		Ответ = РатШагиВанессыКлиентСервер.ВыполнитьHttpЗапрос(ОписаниеЗапроса);
	КонецЕсли;
	
	Если ОписаниеЗапроса.Отладка Тогда
		РатШагиВанессыКлиентСервер.ВывестиОтвет(Ответ);
	КонецЕсли;
	
	Если ОписаниеЗапроса.ПроверятьОтвет Тогда
		ПроверитьОтвет(Ответ, НастройкиПодключения);
	КонецЕсли;
	
	Если НЕ ЗапросКЭтойБазе Или НастройкиПодключения.ИспользуетсяСериализация Тогда
		//@skip-check empty-except-statement
		Попытка
			Ответ.Тело = РатJSON.ОбъектИзСтроки(Ответ.Тело);
		Исключение
			// Исключение не обрабатывается
		КонецПопытки;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет ответ на соответствие разрешенным кодам состояния.
//
// Параметры:
//   Ответ - Структура - результат выполнения запроса
//   НастройкиПодключения - Структура - настройки подключения
//   ВыбрасыватьИсключение - Булево - выбрасывать исключение при ошибке
//
// Возвращаемое значение:
//   Строка - текст ошибки, если ответ не соответствует разрешенным кодам
//
Функция ПроверитьОтвет(Ответ, НастройкиПодключения, ВыбрасыватьИсключение = Истина) Экспорт
	
	РазрешенныеКодыСостояния = НастройкиПодключения.РазрешенныеКодыСостояния;
	ТекстОшибки = Неопределено;
	
	Если РазрешенныеКодыСостояния.Найти(Ответ.КодСостояния) = Неопределено Тогда
		
		СтрокаРазрешенныеКодыСостояния = СтрСоединить(РазрешенныеКодыСостояния, ", ");
		Тело = ТекстТела(Ответ);
		
		ТекстОшибки = СтрШаблон("Запрос вернул %1 (%2)
								|Разрешенные коды ответов: %3
								|%4", Ответ.КодСостояния, Ответ.РасшифровкаКода, СтрокаРазрешенныеКодыСостояния, Тело);
		Если ВыбрасыватьИсключение Тогда
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

// Возвращает текстовое представление тела ответа.
//
// Параметры:
//   Ответ - Структура - результат выполнения запроса
//
// Возвращаемое значение:
//   Строка - текстовое представление тела ответа
//
Функция ТекстТела(Ответ) Экспорт
	
	ТелоЭтоСтрока = ТипЗнч(Ответ.Тело) = Тип("Строка");
	Возврат ?(ТелоЭтоСтрока, Ответ.Тело, РатJSON.ОбъектВСтроку(Ответ.Тело));
	
КонецФункции

Функция НовоеОписаниеЗапроса(КонтекстВанессы, ИмяНастройки, Метод, АдресРесурса)
	
	ОписаниеЗапроса = ОписаниеЗапроса(НастройкиПодключения(КонтекстВанессы.Контекст, ИмяНастройки), Метод, АдресРесурса);
	ОписаниеЗапроса.Отладка = КонтекстВанессы.Ванесса.Объект.DebugLog;
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

Процедура ОчиститьКонтекстПередЗапросом(КонтекстВанессы)
	
	СохранитьЗначениеПеременнойВКонтекст(КонтекстВанессы, "РезультатЗапроса", Неопределено);
	
КонецПроцедуры

Процедура СохранитьРезультатЗапросаВКонтекст(КонтекстВанессы, Ответ)
	
	СохранитьЗначениеПеременнойВКонтекст(КонтекстВанессы, "РезультатЗапроса", Ответ);
	
КонецПроцедуры

Процедура СохранитьЗначениеПеременнойВКонтекст(КонтекстВанессы, ИмяПеременной, Значение)
	
	Ванесса = КонтекстВанессы.Ванесса;
	
	Если ТипЗнч(Ванесса) = Тип("Структура") Тогда
		КонтекстВанессы.Контекст.Вставить(ИмяПеременной, Значение);
	Иначе
		Ванесса.СохранитьЗначениеПеременнойВКонтекст(ИмяПеременной, Значение);
	КонецЕсли;
	
КонецПроцедуры

Функция ИдентификаторЗаписи(Запись, МетаданныеТаблицы) Экспорт
	
	Если МетаданныеТаблицы.ЭтоРегистр Тогда
		Возврат ИдентификаторЗаписиРегистра(Запись, МетаданныеТаблицы);
	ИначеЕсли Запись.Свойство("Ссылка") Тогда
		Возврат Запись.Ссылка.id;
	Иначе
		Возврат Запись.id;
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторЗаписиРегистра(Запись, МетаданныеТаблицы)
	
	ИдентификаторЗаписи = "";
	ИндексРеквизитов = РатКоллекции.ВСоответствие(МетаданныеТаблицы.Реквизиты, "Имя");
	
	Для Каждого Поле Из МетаданныеТаблицы.КлючевыеРеквизиты Цикл
		
		ОписаниеРеквизита = ИндексРеквизитов[Поле];
		ЗначениеРеквизита = ПолучитьЗначениеРеквизита(Запись, Поле, МетаданныеТаблицы);
		ЗначениеПоля = ПолучитьПредставлениеЗначения(ЗначениеРеквизита, ОписаниеРеквизита);
		
		ИдентификаторЗаписи = РатСтроки.ДобавитьСтроку(ИдентификаторЗаписи, СтрШаблон("%1=%2", Поле, ЗначениеПоля), ";");
		
	КонецЦикла;
	
	Возврат ИдентификаторЗаписи;
	
КонецФункции

Функция ПолучитьЗначениеРеквизита(Запись, Поле, МетаданныеТаблицы)
	
	Если МетаданныеТаблицы.КлючевыеРеквизиты.Количество() > 1 ИЛИ ТипЗнч(Запись) = Тип("Структура") Тогда
		Возврат Запись[Поле];
	Иначе
		Возврат Запись;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьПредставлениеЗначения(ЗначениеРеквизита, ОписаниеРеквизита)
	
	УказаноТипизированноеЗначения = РатПреобразованияКлиентСервер.ЭтоТипизированноеЗначение(ЗначениеРеквизита);
	ИспользоватьТипизированноеЗначение = ОписаниеРеквизита.СоставнойТип ИЛИ УказаноТипизированноеЗначения;
	
	Если ИспользоватьТипизированноеЗначение И ЗначениеРеквизита <> Неопределено Тогда
		Возврат ПолучитьПредставлениеТипизированногоЗначения(ЗначениеРеквизита, УказаноТипизированноеЗначения);
	Иначе
		Возврат РатШагиВанессыКлиент.ПредставлениеЗначения(ЗначениеРеквизита);
	КонецЕсли;
	
КонецФункции

Функция ПолучитьПредставлениеТипизированногоЗначения(ЗначениеРеквизита, УказаноТипизированноеЗначения)
	
	Если УказаноТипизированноеЗначения Тогда
		ТипизированноеЗначение = ЗначениеРеквизита;
	Иначе
		ТипизированноеЗначение = Новый Структура("type, id");
		ТипизированноеЗначение.type = РатТипыДанных.ПредставлениеТипа(ТипЗнч(ЗначениеРеквизита));
		ТипизированноеЗначение.id = ЗначениеРеквизита;
	КонецЕсли;
	
	Если ТипЗнч(ТипизированноеЗначение) = Тип("Структура") Тогда
		ПредставлениеЗначения = РатШагиВанессыКлиент.ПредставлениеЗначения(ТипизированноеЗначение.id);
		Возврат СтрШаблон("%1.%2", ТипизированноеЗначение.type, ПредставлениеЗначения);
	Иначе
		Возврат ТипизированноеЗначение;
	КонецЕсли;
	
КонецФункции

#Область НастройкиПодключения

Функция НайтиНастройкиПодключения(ИмяНастройки)
	
	Параметры = РатКоллекции.ЗначениеВМассиве(ИмяНастройки);
	Возврат РатРасширениеФункциональности.ВычислитьЗначениеМетода("РатШагиВанессыКлиент", "НайтиНастройкиПодключения", Параметры);
	
КонецФункции

Процедура ДобавитьНастройкуПодключения(Контекст, ИмяНастройки, НастройкиПодключения)
	
	НастройкиПодключений(Контекст).Вставить(НРег(ИмяНастройки), НастройкиПодключения);
	
КонецПроцедуры

Процедура ИнициализироватьНастройкиПодключений(Контекст, НастройкиПодключений)
	
	ЭтаБаза = РатШагиВанессыКлиентСервер.ОписаниеНастройкиПодключения();
	ЭтаБаза.ВыполнятьНаСервере = Истина;
	ЭтаБаза.ЭтаБаза = Истина;
	
	ДобавитьНастройкуПодключения(Контекст, "ЭтаБаза", ЭтаБаза);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

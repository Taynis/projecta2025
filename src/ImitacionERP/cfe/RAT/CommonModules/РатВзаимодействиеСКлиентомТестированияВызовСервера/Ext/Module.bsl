//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Создает новый запрос к клиенту тестирования.
// Запрос описывается как параметризованная навигационная ссылка на специально подготовленную общую команду.
//
// Параметры:
//   Команда - см. РатВзаимодействиеСКлиентомТестирования.НоваяКоманда
//
// Возвращаемое значение:
//   Структура - запрос к клиенту тестирования со следующими полями:
//     * КлючОбъекта - Строка - ключ объекта в хранилище
//     * КлючНастроек - Строка - ключ настроек в хранилище
//     * КлючЗапроса - УникальныйИдентификатор - идентификатор запроса
//     * ТипХранилища - Строка - тип хранилища ("Запрос")
//     * ТаймаутОжиданияОтвета - Число - время ожидания ответа
//     * НавигационнаяСсылка - Строка - ссылка на команду
//     * Команда - см. РатВзаимодействиеСКлиентомТестирования.НоваяКоманда
//
Функция НовыйЗапрос(Команда) Экспорт
	
	// запрос описывается как параметризованная навигационная ссылка
	// на специально подготовленную общую команду 
	
	// формируем навигационную ссылку на команду, указывая в качестве параметра
	// пустую структуру - необходимо исключительно для получения идентификатора
	// в хранилище навигационных ссылок (КлючНастроек)
	
	МетаКоманда = Метаданные.ОбщиеКоманды.РатКомандныйИнтерфейсЗапросОтвет;
	Представление = МетаКоманда.Синоним;
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(МетаКоманда, , Представление, Новый Структура);
	
	РазделитьКлючаНастроек = "?extdata=";
	ПозицияКлючаНастроек = СтрНайти(НавигационнаяСсылка, РазделитьКлючаНастроек);
	
	КлючОбъекта = МетаКоманда.ПолноеИмя();
	КлючНастроек = Сред(НавигационнаяСсылка, ПозицияКлючаНастроек + СтрДлина(РазделитьКлючаНастроек));
	
	// после получения необходимых ключей для работы с системным хранилищем
	// описываем структуру запроса и записываем в хранилище по ранее
	// полученным ключам (КлючОбъекта и КлючНастройки)
	
	Запрос = Новый Структура;
	
	// набор ключей для чтения/записи в системное хранилище
	Запрос.Вставить("КлючОбъекта", КлючОбъекта);
	Запрос.Вставить("КлючНастроек", КлючНастроек);

	// для контроля возможных коллизий при работе с API
	Запрос.Вставить("КлючЗапроса", Новый УникальныйИдентификатор());
	Запрос.Вставить("ТипХранилища", "Запрос");
	
	// для вызова клиента тестирования через API платформы
	Запрос.Вставить("ТаймаутОжиданияОтвета", Команда.ТаймаутОжиданияОтвета);
	Запрос.Вставить("НавигационнаяСсылка", НавигационнаяСсылка);
	
	// описание команды, которую требуется выполнить на клиенте тестирования
	// см. РатВзаимодействиеСКлиентомТестирования.НоваяКоманда
	Запрос.Вставить("Команда", Команда);
	
	УстановитьПривилегированныйРежим(Истина); // BSLLS:SetPrivilegedMode-off
	
	ХранилищеВнешнихДанныхНавигационныхСсылок.Сохранить(КлючОбъекта, КлючНастроек, Запрос);
	
	Возврат Запрос;
	
КонецФункции

// Получает ответ от клиента тестирования по запросу.
//
// Параметры:
//   Запрос - см. НовыйЗапрос
//
// Возвращаемое значение:
//   см. РатВзаимодействиеСКлиентомТестированияКлиентСервер.НовыйОтвет
//
Функция ПолучитьОтветОтКлиентаТестирования(Запрос) Экспорт
	
	ВыборкаДанных = ПолучитьДанныеИзХранилища(Запрос);
	
	Если ВыборкаДанных.Успешно Тогда
		
		ДанныеОтветаНаЗапрос = ВыборкаДанных.Данные;
		
		Если ДанныеОтветаНаЗапрос.КлючЗапроса = Запрос.КлючЗапроса Тогда
			
			Если ДанныеОтветаНаЗапрос.ТипХранилища = "Ответ" Тогда
				
				Ответ = ДанныеОтветаНаЗапрос.Тело;
				// FIXIT: удаление данных из хранилища
				
			ИначеЕсли ДанныеОтветаНаЗапрос.ТипХранилища = "Запрос" Тогда
				
				// запрос еще обрабатывается клиентом тестирования,
				// и ответ не сформирован
				
				Ответ = РатВзаимодействиеСКлиентомТестированияКлиентСервер.НовыйОтветВПроцессе();
				
			Иначе
				
				Ответ = РатВзаимодействиеСКлиентомТестированияКлиентСервер.НовыйОтветОшибкаСервиса(
					"Нарушена целостность передачи данных: неизвестный тип хранилища данных");
				
			КонецЕсли;
			
		Иначе
			
			Ответ = РатВзаимодействиеСКлиентомТестированияКлиентСервер.НовыйОтветОшибкаСервиса(
					"Нарушена целостность передачи данных: нарушена последовательность запрос - ответ");
			
		КонецЕсли;
		
	Иначе
		
		Ответ = РатВзаимодействиеСКлиентомТестированияКлиентСервер.НовыйОтветОшибкаСервиса(
			"Нарушена целостность передачи данных: нет данных хранилища навигационных ссылок");
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает данные из хранилища по отбору.
//
// Параметры:
//   Отбор - Структура - параметры отбора:
//     * КлючОбъекта - Строка - ключ объекта в хранилище
//     * КлючНастроек - Строка - ключ настроек в хранилище
//
// Возвращаемое значение:
//   Структура - результат выборки:
//     * Успешно - Булево - признак успешного получения данных
//     * Данные - Структура - данные из хранилища
//
Функция ПолучитьДанныеИзХранилища(Отбор)
	
	Результат = Новый Структура;
	Результат.Вставить("Успешно", Ложь);
	Результат.Вставить("Данные", Неопределено);
	
	ОтборДанных = Новый Структура("КлючОбъекта, КлючНастроек", Отбор.КлючОбъекта, Отбор.КлючНастроек);
	
	УстановитьПривилегированныйРежим(Истина); // BSLLS:SetPrivilegedMode-off
	
	ВыборкаНастроек = ХранилищеВнешнихДанныхНавигационныхСсылок.Выбрать(ОтборДанных);
		
	Если ВыборкаНастроек.Следующий() Тогда
		
		Результат.Успешно = Истина;
		Результат.Данные = ВыборкаНастроек.Настройки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

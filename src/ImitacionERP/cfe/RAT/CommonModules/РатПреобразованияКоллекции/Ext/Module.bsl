//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Сериализует массив в массив для JSON.
//
// Параметры:
//   Значение - Массив из Произвольный- массив для сериализации
//
// Возвращаемое значение:
//   Массив из Произвольный - сериализованный массив
//
// Пример:
//   Массив = Новый Массив;
//   Массив.Добавить("Элемент1");
//   Массив.Добавить(42);
//   Результат = РатПреобразованияКоллекции.СериализоватьМассив(Массив);
//
Функция СериализоватьМассив(Значение) Экспорт
	
	Результат = Новый Массив;
	Для Каждого Элемент Из Значение Цикл
		Результат.Добавить(РатПреобразования.СериализоватьЗначение(Элемент));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Сериализует структуру или соответствие в структуру для JSON.
//
// Параметры:
//   Значение - Структура, Соответствие из Произвольный - структура или соответствие для сериализации
//   Тип - Тип - тип структуры или соответствия
//
// Возвращаемое значение:
//   Структура, Соответствие из Произвольный - сериализованная структура или соответствие
//
// Пример:
//   Структура = Новый Структура("Поле1, Поле2", "Значение1", 42);
//   Результат = РатПреобразованияКоллекции.СериализоватьСтруктуру(Структура, ТипЗнч(Структура));
//
Функция СериализоватьСтруктуру(Значение, Тип) Экспорт
	
	Результат = Новый(Тип);
	Для Каждого Элемент Из Значение Цикл
		Результат.Вставить(Элемент.Ключ, РатПреобразования.СериализоватьЗначение(Элемент.Значение));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Сериализует таблицу значений в массив структур для JSON.
//
// Параметры:
//   Значение - ТаблицаЗначений - таблица значений для сериализации
//
// Возвращаемое значение:
//   Массив из Структура - массив структур, где каждая структура представляет строку таблицы
//
// Пример:
//   Таблица = Новый ТаблицаЗначений;
//   Таблица.Колонки.Добавить("Колонка1");
//   Таблица.Колонки.Добавить("Колонка2");
//   Строка = Таблица.Добавить();
//   Строка.Колонка1 = "Значение1";
//   Строка.Колонка2 = 42;
//   Результат = РатПреобразованияКоллекции.СериализоватьТаблицу(Таблица);
//
Функция СериализоватьТаблицу(Значение) Экспорт
	
	Ключи = СтрСоединить(РатКоллекции.ВыгрузитьЗначения(Значение.Колонки, "Имя"), ",");
	Результат = Новый Массив;
	
	ОписаниеКолонок = Новый Массив;
	
	Для Каждого Колонка Из Значение.Колонки Цикл
		ОписаниеКолонок.Добавить(Новый Структура("Имя, СоставнойТип", Колонка.Имя, РатТипыДанных.ЭтоСоставнойТип(Колонка.ТипЗначения)));
	КонецЦикла;
	
	Для Каждого Строка Из Значение Цикл
		
		ДанныеСтроки = Новый Структура(Ключи);
		
		Для Каждого Колонка Из ОписаниеКолонок Цикл
			
			ЗначениеЯчейки = Строка[Колонка.Имя];
			Если Колонка.СоставнойТип И НЕ РатТипыДанных.ЭтоСсылочныйТип(ТипЗнч(ЗначениеЯчейки)) Тогда
				ДанныеСтроки.Вставить(Колонка.Имя, РатПреобразованияСсылки.СериализоватьРеквизитСоставногоТипа(ЗначениеЯчейки));
			Иначе
				ДанныеСтроки.Вставить(Колонка.Имя, РатПреобразования.СериализоватьЗначение(ЗначениеЯчейки));
			КонецЕсли;
			
		КонецЦикла;
		
		Результат.Добавить(ДанныеСтроки);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Сериализует список значений в массив для JSON.
//
// Параметры:
//   Значение - СписокЗначений из Произвольный - список значений для сериализации
//
// Возвращаемое значение:
//   Массив из Произвольный - сериализованный список значений
//
// Пример:
//   СписокЗначений = Новый СписокЗначений;
//   СписокЗначений.Добавить("Элемент1");
//   СписокЗначений.Добавить(42);
//   Результат = РатПреобразованияКоллекции.СериализоватьСписокЗначений(СписокЗначений);
//
Функция СериализоватьСписокЗначений(Значение) Экспорт
	
	МассивЗначений = Значение.ВыгрузитьЗначения();
	Результат = СериализоватьМассив(МассивЗначений);
	
	Возврат Результат;
	
КонецФункции

// Десериализует массив из JSON данных.
//
// Параметры:
//   Значение - Массив из Произвольный - массив JSON данных
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Массив из Произвольный - десериализованный массив
//
Функция ДесериализоватьМассив(Значение, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Новый Массив();
	
	Для Каждого Элемент Из Значение Цикл
		Результат.Добавить(РатПреобразованияВспомогательные.ДесериализоватьПроизвольноеЗначение(Элемент, СтатусОбработкиЗапроса));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Десериализует структуру из JSON данных.
//
// Параметры:
//   Значение - Структура - структура JSON данных
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Структура - десериализованная структура
//
Функция ДесериализоватьСтруктуру(Значение, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Новый Структура();
	
	Для Каждого Элемент Из Значение Цикл
		Результат.Вставить(Элемент.Ключ, РатПреобразованияВспомогательные.ДесериализоватьПроизвольноеЗначение(Элемент.Значение, СтатусОбработкиЗапроса));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Преобразует массив данных в коллекцию согласно описанию.
//
// Параметры:
//   Данные - Массив из Произвольный - массив данных для преобразования
//   ОписаниеКоллекции - Структура - описание коллекции:
//     * Имя - Строка - имя коллекции
//     * ТипЭлемента - Тип - тип элемента коллекции
//     * РеквизитыЭлемента - Массив из см. РатПреобразования.ОписаниеРеквизита - описание реквизитов элемента
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//   Описание - Строка - описание операции для логирования
//
// Возвращаемое значение:
//   Массив из Произвольный - преобразованная коллекция
//
Функция ПреобразоватьМассивДанных(Данные, ОписаниеКоллекции, СтатусОбработкиЗапроса, Описание) Экспорт
	
	Коллекция = Новый Массив;
	
	Если Данные.Количество() = 0 Тогда
		Возврат Коллекция;
	КонецЕсли;
	
	ОписаниеРеквизита = РатПреобразования.ОписаниеРеквизита("Элемент " + ОписаниеКоллекции.Имя, ОписаниеКоллекции.ТипЭлемента);
	ОписаниеРеквизита.ВложенныеРеквизиты = ОписаниеКоллекции.РеквизитыЭлемента;
	
	ОписаниеОперации = СтрШаблон("%1 элемент %2", Описание, ОписаниеКоллекции.Имя);
	 
	Для Каждого Элемент Из Данные Цикл
		
		Значение = РатПреобразованияВспомогательные.ПреобразованноеЗначение(Элемент, ОписаниеРеквизита, СтатусОбработкиЗапроса, ОписаниеОперации);
		
		Коллекция.Добавить(Значение);
		
	КонецЦикла;
	
	Возврат Коллекция;
	
КонецФункции

// Проверяет, является ли значение коллекцией (массив, структура, соответствие, список значений, таблица значений).
//
// Параметры:
//   Значение - Произвольный - значение для проверки
//
// Возвращаемое значение:
//   Булево - Истина, если значение является коллекцией
//
// Пример:
//   Результат = РатПреобразованияКоллекции.ЭтоКоллекция(Новый Массив); // Истина
//   Результат = РатПреобразованияКоллекции.ЭтоКоллекция(Новый Структура); // Истина
//   Результат = РатПреобразованияКоллекции.ЭтоКоллекция("Строка"); // Ложь
//
Функция ЭтоКоллекция(Значение) Экспорт
	
	ТипЗначения = ТипЗнч(Значение);
	
	Возврат ТипЗначения = Тип("Массив")
		ИЛИ ТипЗначения = Тип("Структура")
		ИЛИ ТипЗначения = Тип("Соответствие")
		ИЛИ ТипЗначения = Тип("СписокЗначений")
		ИЛИ ТипЗначения = Тип("ТаблицаЗначений");
	
КонецФункции

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Проверяет, является ли значение типизированным.
//
// Параметры:
//   Значение - Произвольный - проверяемое значение
//
// Возвращаемое значение:
//   Булево - Истина, если значение является типизированным
//
// Пример:
//   Если РатПреобразованияКлиентСервер.ЭтоТипизированноеЗначение(Значение) Тогда
//       // Значение является типизированным
//   КонецЕсли;
//
Функция ЭтоТипизированноеЗначение(Значение) Экспорт
	
	Если РатТипыДанных.ЭтоСтруктура(Значение) Тогда
		
		Возврат Значение.Свойство("type");
		
	ИначеЕсли РатТипыДанных.ЭтоСтрока(Значение) Тогда
		
		Возврат СтрЧислоВхождений(Значение, ".") >= 1;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

// Преобразует строку в массив строк по указанному разделителю.
//
// Параметры:
//   Значение - Строка - исходная строка
//   Разделитель - Строка - разделитель для разбиения строки (по умолчанию ";")
//   УдалятьКонцевыеПробелы - Булево - признак удаления концевых пробелов (по умолчанию Ложь)
//
// Возвращаемое значение:
//   Массив из Строка - массив строк, полученный разбиением исходной строки
//
// Пример:
//   МассивСтрок = РатПреобразованияКлиентСервер.СтрокуВМассив("Строка1;Строка2;Строка3");
//   // Результат: ["Строка1", "Строка2", "Строка3"]
//
Функция СтрокуВМассив(Знач Значение, Разделитель = ";", УдалятьКонцевыеПробелы = Ложь) Экспорт
	
	КодУниверсальногоРазделителя = 5855;
	УниверсальныйРазделитель = Символ(КодУниверсальногоРазделителя);
	МодифицированнаяСтрока = СтрЗаменить(Значение, Разделитель, УниверсальныйРазделитель);
	
	МассивСтрок = ?(МодифицированнаяСтрока = "", Новый Массив, СтрРазделить(МодифицированнаяСтрока, УниверсальныйРазделитель));
	
	Если УдалятьКонцевыеПробелы Тогда
		
		Для Индекс = 0 По МассивСтрок.ВГраница() Цикл
			
			МассивСтрок[Индекс] = СокрЛП(МассивСтрок[Индекс]);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивСтрок;
	
КонецФункции

// Преобразует строку в число.
//
// Параметры:
//   Строка - Строка - строка для преобразования
//
// Возвращаемое значение:
//   Число - преобразованное число
//
// Пример:
//   Число = РатПреобразованияКлиентСервер.ЧислоИзСтроки("123.45");
//   // Результат: 123.45
//
Функция ЧислоИзСтроки(Знач Строка) Экспорт
	
	ОписаниеТипа = Новый ОписаниеТипов("Число, Булево");
	Возврат ОписаниеТипа.ПривестиЗначение(Строка);
	
КонецФункции

// Преобразует значение в булево.
//
// Параметры:
//   Значение - Произвольный - значение для преобразования
//   ПоУмолчанию - Булево, Неопределено - значение по умолчанию, если преобразование невозможно
//
// Возвращаемое значение:
//   Булево - преобразованное булево значение
//   Неопределено - если преобразование невозможно и не указано значение по умолчанию
//
// Пример:
//   Булево = РатПреобразованияКлиентСервер.ВБулево("true");
//   // Результат: Истина
//
Функция ВБулево(Знач Значение, ПоУмолчанию = Неопределено) Экспорт
	
	ТипЗначения = ТипЗнч(Значение);
	
	Если ТипЗначения = Тип("Число") Тогда
		Возврат Булево(Значение);
	КонецЕсли;
	
	ЭтоИстина = Ложь;
	ЭтоЛожь = Ложь;
	
	Если ТипЗначения = Тип("Строка") Тогда
		Значение = НРег(Значение);
		
		ЭтоИстина = Значение = "true"
			ИЛИ Значение = "yes"
			ИЛИ Значение = "истина"
			ИЛИ Значение = "да"
			ИЛИ Значение = "1";
		
		ЭтоЛожь = Значение = "false"
			ИЛИ Значение = "no"
			ИЛИ Значение = "ложь"
			ИЛИ Значение = "нет"
			ИЛИ Значение = "0";
	КонецЕсли;
	
	Если ЭтоИстина ИЛИ ЭтоЛожь Тогда
		Результат = ЭтоИстина;
	Иначе
		Результат = ПоУмолчанию;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Преобразует строку в дату и время.
//
// Параметры:
//   Строка - Строка - строка для преобразования
//
// Возвращаемое значение:
//   Дата - преобразованная дата и время
//
// Пример:
//   ДатаВремя = РатПреобразованияКлиентСервер.ДатаВремяИзСтроки("01.01.2024 12:00:00");
//   // Результат: 01.01.2024 12:00:00
//
Функция ДатаВремяИзСтроки(Знач Строка) Экспорт

	КвалификаторДаты = Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя);
	ОписаниеТипа = Новый ОписаниеТипов("Дата", Неопределено, Неопределено, КвалификаторДаты);
	Возврат ОписаниеТипа.ПривестиЗначение(Строка);

КонецФункции

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

/////////////////////////////////////////////////////////////////////////////////
// Заголовок
//  Описание
/////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вычисляет указанное выражение и возвращает его результат.
//
// Параметры:
//  Выражение - Строка - Выражение для вычисления
//  Параметры - Произвольный, Неопределено - Параметры для использования в выражении
//
// Возвращаемое значение:
//  Произвольный - Результат вычисления выражения
//
// Пример:
//   Результат = РатАлгоритмы.ВычислитьВыражение("2 + 2");
//   // Результат = 4
Функция ВычислитьВыражение(Выражение, Параметры = Неопределено) Экспорт
	
#Если ВебКлиент Тогда
	ВызватьИсключение "Метод не поддерживается в веб-клиенте";
#ИначеЕсли Сервер Или ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Тогда
	УстановитьБезопасныйРежим(Истина);
	Возврат Вычислить(Выражение); // BSLLS:ExecuteExternalCodeInCommonModule-off
#Иначе
	Возврат Вычислить(Выражение); // BSLLS:ExecuteExternalCodeInCommonModule-off
#КонецЕсли
	
КонецФункции

// Вызывает указанную функцию с переданными параметрами.
//
// Параметры:
//  ИмяМетода - Строка - Имя функции для вызова
//  Параметры - Неопределено - Вызов функции без параметров
//            - Массив из Произвольный- Массив параметров для передачи в функцию
//            - Произвольный - Параметр для передачи в функцию
//
// Возвращаемое значение:
//  Произвольный - Результат выполнения функции
//
// Примеры:
//   // Вызов без параметров
//   Результат = РатАлгоритмы.ВызватьФункцию("ТекущаяДата", Неопределено);
//   // Результат = текущая дата
//
//   // Вызов с одним параметром
//   Результат = РатАлгоритмы.ВызватьФункцию("Окр", 3.7);
//   // Результат = 4
//
//   // Вызов с массивом параметров
//   Параметры = Новый Массив;
//   Параметры.Добавить(3.7);
//   Результат = РатАлгоритмы.ВызватьФункцию("Окр", Параметры);
//   // Результат = 4
//
Функция ВызватьФункцию(ИмяМетода, Параметры) Экспорт
	
	Выражение = СигнатураМетода(ИмяМетода, Параметры);
	Возврат ВычислитьВыражение(Выражение, Параметры);
	
КонецФункции

// Выполняет указанное выражение.
// Отличается от ВычислитьВыражение тем, что поддерживает операторы присваивания и другие операторы выполнения.
//
// Параметры:
//  Выражение - Строка - Выражение для выполнения
//  Параметры - Произвольный, Неопределено - Параметры для использования в выражении
//
// Исключения:
//  ВызватьИсключение - Если метод вызывается в веб-клиенте
//
// Пример:
//   РатАлгоритмы.ВыполнитьАлгоритм("Перем = 2 + 2; Сообщить(Перем)");
//   // Выведет: 4
//
Процедура ВыполнитьАлгоритм(Выражение, Параметры = Неопределено) Экспорт
	
#Если ВебКлиент Тогда
	ВызватьИсключение "Метод не поддерживается в веб-клиенте";
#ИначеЕсли Сервер Или ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Тогда
	УстановитьБезопасныйРежим(Истина);
	Выполнить(Выражение); // BSLLS:ExecuteExternalCodeInCommonModule-off
#Иначе
	Выполнить(Выражение); // BSLLS:ExecuteExternalCodeInCommonModule-off
#КонецЕсли
	
КонецПроцедуры

// Выполняет указанную процедуру с переданными параметрами.
// Отличается от ВызватьФункцию тем, что не возвращает результат выполнения.
//
// Параметры:
//  ИмяМетода - Строка - Имя процедуры для выполнения
//  Параметры - Неопределено - Вызов процедуры без параметров
//            - Массив из Произвольный - Массив параметров для передачи в процедуру
//            - Произвольный - Параметр для передачи в процедуру
//
// Исключения:
//  ВызватьИсключение - Если метод вызывается в веб-клиенте
//
// Примеры:
//   // Выполнение без параметров
//   РатАлгоритмы.ВыполнитьМетод("Сообщить");
//
//   // Выполнение с одним параметром
//   РатАлгоритмы.ВыполнитьМетод("Сообщить", "Привет, мир!");
//
//   // Выполнение с массивом параметров
//   Параметры = Новый Массив;
//   Параметры.Добавить("Привет");
//   Параметры.Добавить("мир");
//   РатАлгоритмы.ВыполнитьМетод("Сообщить", Параметры);
//
Процедура ВыполнитьМетод(ИмяМетода, Параметры) Экспорт
	
	Выражение = СигнатураМетода(ИмяМетода, Параметры);
	ВыполнитьАлгоритм(Выражение, Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция СигнатураМетода(Метод, ПараметрыМетода)

	СигнатураПараметров = Новый Массив;
	
	Если ПараметрыМетода = Неопределено Тогда
		// Вызов без параметров
		Возврат СтрШаблон("%1()", Метод);
	ИначеЕсли ТипЗнч(ПараметрыМетода) = Тип("Массив") Тогда
		// Массив параметров
		Для ИндексПараметра = 0 По ПараметрыМетода.ВГраница() Цикл
			СигнатураПараметров.Добавить(СтрШаблон("Параметры[%1]", Формат(ИндексПараметра, "ЧН=; ЧГ=;")));
		КонецЦикла;
	Иначе
		// Один параметр
		СигнатураПараметров.Добавить("Параметры");
	КонецЕсли;
	
	Возврат СтрШаблон("%1(%2)", Метод, СтрСоединить(СигнатураПараметров, ", "));

КонецФункции

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Возвращает коллекцию используемых типов содержимого.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Типы содержимого:
//    * Текст - Строка - Тип для простого текста
//    * HTML - Строка - Тип для HTML страницы
//    * JSON - Строка - Тип для данных в формате json
//
// Пример:
//   Типы = РатОбщийКлиентСервер.ТипыСодержимого();
//   // Типы.Текст = "text/plain"
//   // Типы.HTML = "text/html"
//   // Типы.JSON = "application/json"
Функция ТипыСодержимого() Экспорт
	
	Типы = Новый Структура;
	Типы.Вставить("Текст", "text/plain");
	Типы.Вставить("HTML", "text/html");
	Типы.Вставить("JSON", "application/json");
	
	Возврат Новый ФиксированнаяСтруктура(Типы);
	
КонецФункции

// Возвращает перечень используемых HTTP методов.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Методы HTTP:
//    * GET - Строка - Метод получения данных
//    * POST - Строка - Метод создания данных
//    * DELETE - Строка - Метод удаления данных
//    * PUT - Строка - Метод полного обновления данных
//    * PATCH - Строка - Метод частичного обновления данных
//
// Пример:
//   Методы = РатОбщийКлиентСервер.МетодыHTTP();
//   // Методы.GET = "GET"
//   // Методы.POST = "POST"
//   // Методы.DELETE = "DELETE"
//   // Методы.PUT = "PUT"
//   // Методы.PATCH = "PATCH"
Функция МетодыHTTP() Экспорт
	
	Методы = Новый Структура;
	Методы.Вставить("GET",     "GET");
	Методы.Вставить("POST",    "POST");
	Методы.Вставить("DELETE",  "DELETE");
	Методы.Вставить("PUT",     "PUT");
	Методы.Вставить("PATCH",   "PATCH");
	
	Возврат Новый ФиксированнаяСтруктура(Методы);

КонецФункции

// Возвращает коды ответов HTTP запросов.
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Коды ответов:
//    * Успешно - Число - 200 OK
//    * Создано - Число - 201 Created
//    * Принято - Число - 202 Accepted
//    * НетСодержимого - Число - 204 No Content
//    * ПлохойЗапрос - Число - 400 Bad Request
//    * НеНайдено - Число - 404 Not Found
//    * МетодНеПоддерживается - Число - 405 Method Not Allowed
//    * ОшибкаСервера - Число - 500 Internal Server Error
//
// Пример:
//   Коды = РатОбщийКлиентСервер.КодыОтветов();
//   // Коды.Успешно = 200
//   // Коды.Создано = 201
//   // Коды.ОшибкаСервера = 500
Функция КодыОтветов() Экспорт
	
	КодыОтветов = Новый Структура;
	КодыОтветов.Вставить("Успешно", 200);
	КодыОтветов.Вставить("Создано", 201);
	КодыОтветов.Вставить("Принято", 202);
	КодыОтветов.Вставить("НетСодержимого", 204);
	КодыОтветов.Вставить("ПлохойЗапрос", 400);
	КодыОтветов.Вставить("НеНайдено", 404);
	КодыОтветов.Вставить("МетодНеПоддерживается", 405);
	КодыОтветов.Вставить("ОшибкаСервера", 500);
	
	Возврат Новый ФиксированнаяСтруктура(КодыОтветов);
	
КонецФункции

// Возвращает коллекцию видов сравнения для использования в запросах.
//
// Возвращаемое значение:
//  Структура - Виды сравнения:
//    * Равно - Строка - Точное совпадение
//    * Больше - Строка - Значение больше указанного
//    * БольшеИлиРавно - Строка - Значение больше или равно указанному
//    * Меньше - Строка - Значение меньше указанного
//    * МеньшеИлиРавно - Строка - Значение меньше или равно указанному
//    * ВСписке - Строка - Значение содержится в списке
//    * Подобно - Строка - Значение соответствует шаблону
//    * Содержит - Строка - Значение содержит указанную подстроку
//    * НачинаетсяС - Строка - Значение начинается с указанной подстроки
//    * ЗаканчиваетсяНа - Строка - Значение заканчивается указанной подстрокой
//    * ВГруппе - Строка - Значение входит в группу
//
// Пример:
//   Виды = РатОбщийКлиентСервер.ВидыСравнения();
//   // Виды.Равно = "Равно"
//   // Виды.Подобно = "Подобно"
//   // Виды.ВСписке = "ВСписке"
Функция ВидыСравнения() Экспорт
	
	Виды = Новый Структура;
	Виды.Вставить("Равно", "Равно");
	Виды.Вставить("Больше", "Больше");
	Виды.Вставить("БольшеИлиРавно", "БольшеИлиРавно");
	Виды.Вставить("Меньше", "Меньше");
	Виды.Вставить("МеньшеИлиРавно", "МеньшеИлиРавно");
	Виды.Вставить("ВСписке", "ВСписке");
	Виды.Вставить("Подобно", "Подобно");
	Виды.Вставить("Содержит", "Содержит");
	Виды.Вставить("НачинаетсяС", "НачинаетсяС");
	Виды.Вставить("ЗаканчиваетсяНа", "ЗаканчиваетсяНа");
	Виды.Вставить("ВГруппе", "ВГруппе");
	
	Возврат Виды;
	
КонецФункции

// Формирует текст ошибки с возможным префиксом.
//
// Параметры:
//  Ошибка - ИнформацияОбОшибке - Объект с информацией об ошибке
//  Префикс - Строка, Неопределено - Префикс для текста ошибки
//
// Возвращаемое значение:
//  Строка - Форматированный текст ошибки
//
// Пример:
//   Попытка
//       // Код, который может вызвать ошибку
//   Исключение
//       ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), "Ошибка выполнения");
//       // Результат: "Ошибка выполнения: Описание ошибки"
Функция ТекстОшибки(Ошибка, Префикс = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Префикс) Тогда
		Возврат СтрШаблон("%1:%2%3", Префикс, Символы.ПС, РатСтроки.ПредставлениеОшибки(Ошибка));
	Иначе
		Возврат РатСтроки.ПредставлениеОшибки(Ошибка);
	КонецЕсли;
	
КонецФункции

// Формирует и выводит сообщение пользователю с префиксом даты и времени.
//
// Параметры:
//  СообщениеПользователю - Строка - Текст сообщения для вывода
//
// Пример:
//   РатОбщийКлиентСервер.СообщитьПользователю("Операция выполнена успешно");
//   // Результат: "01.01.2024 12:00:00 [RAT]: Операция выполнена успешно"
Процедура СообщитьПользователю(Знач СообщениеПользователю = "") Экспорт
	
	//@skip-check use-non-recommended-method
	СообщениеПользователю = СтрШаблон("%1 [RAT]: %2", ТекущаяДата(), СообщениеПользователю); // BSLLS:DeprecatedCurrentDate-off
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СокрЛП(СообщениеПользователю);
	Сообщение.Сообщить();
	
КонецПроцедуры // СообщитьПользователю

// Проверяет наличие свойства у значения любого типа данных.
//
// Параметры:
//  Переменная - Произвольный - Переменная любого типа, для которой проверяется наличие свойства
//  ИмяСвойства - Строка - Имя проверяемого свойства
//
// Возвращаемое значение:
//  Булево - Истина, если свойство существует, Ложь в противном случае
//
// Пример:
//   Структура = Новый Структура("Ключ", "Значение");
//   Результат = РатОбщийКлиентСервер.ПеременнаяСодержитСвойство(Структура, "Ключ");
//   // Результат = Истина
Функция ПеременнаяСодержитСвойство(Переменная, ИмяСвойства) Экспорт
	
	Если Переменная = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Инициализируем структуру для теста с ключом (значение переменной "ИмяСвойства") и значением произвольного GUID'а
	GUIDПроверка = Новый УникальныйИдентификатор;
	СтруктураПроверка = Новый Структура;
	СтруктураПроверка.Вставить(ИмяСвойства, GUIDПроверка);
	// Заполняем созданную структуру из переданного значения переменной
	ЗаполнитьЗначенияСвойств(СтруктураПроверка, Переменная);
	// Если значение для свойства структуры осталось GUIDПроверка, то искомое свойство не найдено, и наоборот.
	Возврат СтруктураПроверка[ИмяСвойства] <> GUIDПроверка;
	
КонецФункции // ПеременнаяСодержитСвойство

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает фрагмент текста запроса, отделяющего один запрос от другого.
//
// Возвращаемое значение:
//  Строка - Разделитель запросов в виде комментария
//
// Пример:
//   Разделитель = РатОбщийКлиентСервер.РазделительПакетаЗапросов();
//   // Результат: "; ////////////////////////////////////////////////////////////////////////////////"
Функция РазделительПакетаЗапросов() Экспорт
	
	Возврат "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
КонецФункции

// Возвращает фрагмент текста запроса для объединения операторов.
//
// Параметры:
//  Все - Булево - Признак добавления оператора "ВСЕ" в объединение
//
// Возвращаемое значение:
//  Строка - Текст оператора объединения запросов
//
// Пример:
//   Разделитель = РатОбщийКлиентСервер.РазделительОператоровЗапроса(Истина);
//   // Результат: "ВСЕ"
Функция РазделительОператоровЗапроса(Все = Истина) Экспорт
	
	Если Все Тогда
		Суффикс = " ВСЕ";
	Иначе
		Суффикс = "";
	КонецЕсли;
	
	Возврат СтрШаблон("
		|
		|ОБЪЕДИНИТЬ%1
		|
		|", Суффикс);
	
КонецФункции

Функция ПараметрыЗапускаПриложения(ПараметрыЗапуска) Экспорт
	
	ПрочитанныеПараметры = Новый Соответствие();
	НаборПараметровЗапуска = РатСтроки.СтрокуВМассив(ПараметрыЗапуска, ";", Истина);
	
	КоличествоКогдаЕстьЗначение = 2;
	
	Для Каждого ПараметрИзНабора Из НаборПараметровЗапуска Цикл
		
		Если ПустаяСтрока(ПараметрИзНабора) Тогда
			Продолжить;
		КонецЕсли;
		
		СоставПараметра = РатСтроки.СтрокуВМассив(ПараметрИзНабора, "=", Истина);
		
		Ключ = СоставПараметра[0];
		Значение = Неопределено;
		
		Если СоставПараметра.Количество() = КоличествоКогдаЕстьЗначение Тогда
			
			Значение = СоставПараметра[1];
			
		ИначеЕсли  СоставПараметра.Количество() > КоличествоКогдаЕстьЗначение Тогда
			
			Значение = Сред(ПараметрИзНабора, СтрНайти(ПараметрИзНабора, "=") + 1);
			
		КонецЕсли;
		
		ПрочитанныеПараметры.Вставить(Ключ, Значение);
		
	КонецЦикла;
	
	ПараметрыЗапускаПриложения = Новый Структура;
	
	Для Каждого Элемент Из ПрочитанныеПараметры Цикл
		//@skip-check empty-except-statement
		Попытка
			ПараметрыЗапускаПриложения.Вставить(Элемент.Ключ, Элемент.Значение);
		Исключение
			// Пропускаем невалидные ключи
			Продолжить;
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат ПараметрыЗапускаПриложения;
	
КонецФункции

#КонецОбласти

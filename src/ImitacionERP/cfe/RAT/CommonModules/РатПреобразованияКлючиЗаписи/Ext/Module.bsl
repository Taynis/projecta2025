//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Разбирает строковый идентификатор составного ключа в структуру.
// Формат идентификатора: "поле1=значение1;поле2=значение2"
//
// Параметры:
//   Идентификатор - Строка - строковое представление составного ключа
//
// Возвращаемое значение:
//   Структура - значения полей ключа
//
// Пример:
//   Идентификатор = "Регистратор=00000000-0000-0000-0000-000000000000;Период=2024-01-01T00:00:00";
//   Значения = РатПреобразованияКлючиЗаписи.ЗначенияИдентификатора(Идентификатор);
//   // Результат: Структура с полями "Регистратор" и "Период"
//
Функция ЗначенияИдентификатора(Идентификатор) Экспорт
	
	Результат = Новый Структура;
	
	Если ПустаяСтрока(Идентификатор) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Значения = СтрРазделить(Идентификатор, ";");
	
	Для Каждого Значение Из Значения Цикл
		
		Если ПустаяСтрока(Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиЗначения = СтрРазделить(Значение, "=");
		
		Если ЧастиЗначения.Количество() >= 2 Тогда
			Результат.Вставить(СокрЛП(ЧастиЗначения[0]), СокрЛП(ЧастиЗначения[1]));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Сериализует ключ записи регистра в структуру для JSON.
//
// Параметры:
//   КлючЗаписи - РегистрСведенийКлючЗаписи, РегистрНакопленияКлючЗаписи - ключ записи регистра
//   Тип - Тип - тип ключа записи
//
// Возвращаемое значение:
//   Структура - сериализованные значения ключевых полей регистра
//
// Пример:
//   КлючЗаписи = РегистрыСведений.МойРегистр.СоздатьКлючЗаписи(Новый Структура("Измерение1", Значение1));
//   Результат = РатПреобразованияКлючиЗаписи.СериализоватьКлючЗаписи(КлючЗаписи, ТипЗнч(КлючЗаписи));
//   // Результат: Структура с сериализованными значениями ключевых полей
//
Функция СериализоватьКлючЗаписи(КлючЗаписи, Тип) Экспорт
	
	// TODO Подозреваю нужна оптимизация
	Мета = Метаданные.НайтиПоТипу(Тип);
	
	КлючевыеПоля = РатМетаданные.КлючевыеПоляТаблицы(Мета.ПолноеИмя());
	Результат = Новый Структура();
	
	Для Каждого Поле Из КлючевыеПоля Цикл
		
		Если РатОбщийКлиентСервер.ПеременнаяСодержитСвойство(КлючЗаписи, Поле) Тогда
			Результат.Вставить(Поле, РатПреобразования.СериализоватьЗначение(КлючЗаписи[Поле]));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Десериализует ключ записи регистра из строкового идентификатора.
//
// Параметры:
//   Идентификатор - Строка - строковое представление составного ключа в формате "поле1=значение1;поле2=значение2"
//   ИмяТаблицы - Строка - полное имя таблицы регистра (например, "РегистрСведений.МойРегистр")
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   КлючЗаписи, Неопределено - ключ записи регистра или Неопределено при ошибке
//
// Пример:
//   Идентификатор = "Измерение1=Значение1;Измерение2=Значение2";
//   СтатусОбработкиЗапроса = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   КлючЗаписи = РатПреобразованияКлючиЗаписи.ДесериализоватьКлючЗаписи(
//       Идентификатор, "РегистрСведений.МойРегистр", СтатусОбработкиЗапроса);
//
Функция ДесериализоватьКлючЗаписи(Идентификатор, ИмяТаблицы, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Неопределено;
	
	Попытка
		ЗначенияИдентификатора = ЗначенияИдентификатора(Идентификатор);
	Исключение
		ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), "Не удалось разобрать идентификатор записи регистра");
		КлассОшибки = РатОбщий.КлассыОшибок().РазборПараметровЗапроса;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
		Возврат Результат;
	КонецПопытки;
	
	КлючевыеПоля = РатМетаданные.КлючевыеПоляТаблицы(ИмяТаблицы);
	РеквизитыТаблицы = РатПовтИсп.СтруктураТаблицы(ИмяТаблицы);
	
	ЗначенияКлюча = Новый Структура;
	
	Для Каждого Ключ Из КлючевыеПоля Цикл
		
		Если НЕ ЗначенияИдентификатора.Свойство(Ключ) Тогда
			ТекстОшибки = "Не указан обязательный реквизит ключа " + Ключ;
			КлассОшибки = РатОбщий.КлассыОшибок().Десериализация;
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		Иначе
			Описание = СтрШаблон("значение реквизита '%1' составного ключа", Ключ);
			Значение = РатПреобразования.ДесериализоватьЗначение(ЗначенияИдентификатора[Ключ],
																 РеквизитыТаблицы.Реквизиты[Ключ],
																 СтатусОбработкиЗапроса,
																 Описание);
			
			ЗначенияКлюча.Вставить(Ключ, Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Менеджер = РатМетаданные.МенеджерТаблицы(ИмяТаблицы, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Результат = Менеджер.СоздатьКлючЗаписи(ЗначенияКлюча);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

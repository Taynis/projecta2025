//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Преобразует значение в строку JSON.
//
// Параметры:
//   Объект - Произвольный - Объект, данные которого нужно преобразовать в JSON.
//   Параметры - Структура, Неопределено - Настройки преобразования:
//     + БезПреобразования - Булево - Не выполнять предварительную сериализацию значения.
//
// Возвращаемое значение:
//   Строка - Значение в JSON.
//
// Пример:
//   СтрокаJSON = РатJSON.ОбъектВСтроку(Новый Структура("Ключ", "Значение"));
//
Функция ОбъектВСтроку(Объект, Параметры = Неопределено) Экспорт
	
#Если ВебКлиент Тогда
	ВызватьИсключение "Метод не поддерживается в веб-клиенте";
#Иначе
	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	
	Если Параметры = Неопределено ИЛИ НЕ РатКоллекции.ЗначениеСтруктуры(Параметры, "БезПреобразования", Ложь) Тогда
		ПодготовленныеДанные = РатПреобразованияВызовСервера.СериализоватьЗначение(Объект);
	Иначе
		ПодготовленныеДанные = Объект;
	КонецЕсли;
	
	ЗаписатьJSON(Запись, ПодготовленныеДанные);
	
	Возврат Запись.Закрыть();
#КонецЕсли
	
КонецФункции

// Восстанавливает значение из строки JSON.
//
// Параметры:
//   Строка - Строка - Строка JSON.
//
// Возвращаемое значение:
//   Произвольный - Объект, восстановленный из строки JSON.
//
// Пример:
//   Объект = РатJSON.ОбъектИзСтроки("{"Ключ": "Значение"}");
//
Функция ОбъектИзСтроки(Строка) Экспорт
	
#Если ВебКлиент Тогда
	ВызватьИсключение "Метод не поддерживается в веб-клиенте";
#Иначе
	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(Строка);
	Объект = ПрочитатьJSON(Чтение);
	
	Чтение.Закрыть();
	
	//@skip-check constructor-function-return-section
	Возврат Объект;
#КонецЕсли
	
КонецФункции

// Проверяет, является ли переданный тип примитивным типом JSON.
//
// Параметры:
//   Тип - Тип - Тип для проверки.
//
// Возвращаемое значение:
//   Булево - Истина, если тип является примитивным типом JSON (Строка, Число, Булево, Неопределено).
//
// Пример:
//   Если РатJSON.ЭтоПримитивныйТипJSON(Тип("Строка")) Тогда ...
//
Функция ЭтоПримитивныйТипJSON(Тип) Экспорт
	
	ПримитивныеТипы = Новый ОписаниеТипов("Строка, Число, Булево, Неопределено");
	
	Возврат ПримитивныеТипы.СодержитТип(Тип);
	
КонецФункции

#КонецОбласти

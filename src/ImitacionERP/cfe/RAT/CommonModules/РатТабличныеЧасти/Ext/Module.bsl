//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание операций для работы с табличными частями документов.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	ЗарегистрироватьБазовыеСхемы(СпецификацияСервиса);
	
	ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса);
	
КонецПроцедуры

// Регистрирует шаблоны HTTP-обработчиков для работы с табличными частями.
//
// Параметры:
//   Шаблоны - см. РатШаблоныАдресов.Шаблоны
//
Процедура ЗарегистрироватьШаблоны(Шаблоны) Экспорт
	
	Обработчики = ОбработчикиЗапросов();
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	
	ТипыМетаданных = РатМетаданные.ТипыМетаданных();
	ИменаТиповМетаданных = ТипыМетаданных.ВыгрузитьКолонку("Имя");
	ИменаКоллекцийТиповМетаданных = ТипыМетаданных.ВыгрузитьКолонку("ИмяКоллекции");
	
	ПоддерживаемыеТипыМетаданных = РатКоллекции.ОбъединитьМассивы(ИменаТиповМетаданных, ИменаКоллекцийТиповМетаданных);
	
	ОграничениеПоТипу = Новый Структура("ТипМетаданных", ПоддерживаемыеТипыМетаданных);
	
	// Табличные части
	ШаблонТабличнойЧасти = "/{ТипМетаданных}/{ВидМетаданных}/{Идентификатор}/{ИмяТабличнойЧасти}";
	
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны, Методы.GET,
									 ШаблонТабличнойЧасти,
									 Обработчики.ДанныеТабличнойЧасти,
									 ,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны, Методы.PUT,
									 ШаблонТабличнойЧасти,
									 Обработчики.ДобавитьЗаписиТабличнойЧасти,
									 ,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны, Методы.DELETE,
									 ШаблонТабличнойЧасти,
									 Обработчики.УдалитьЗаписиТабличнойЧасти,
									 ,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны, Методы.PATCH,
									 ШаблонТабличнойЧасти,
									 Обработчики.ИзменитьЗаписиТабличнойЧасти,
									 ,
									 ОграничениеПоТипу);
	
КонецПроцедуры

Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
	
	Обработчики = ОбработчикиЗапросов();
	
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.ДанныеТабличнойЧасти Тогда
		
		Результат = ДанныеТабличнойЧасти(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ДобавитьЗаписиТабличнойЧасти Тогда
		
		Результат = ДобавитьЗаписиТабличнойЧасти(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.УдалитьЗаписиТабличнойЧасти Тогда
		
		Результат = УдалитьЗаписиТабличнойЧасти(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ИзменитьЗаписиТабличнойЧасти Тогда
		
		Результат = ИзменитьЗаписиТабличнойЧасти(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Спецификация

Процедура ЗарегистрироватьБазовыеСхемы(СпецификацияСервиса)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	// БазоваяСхемаТелаИзмененияСтроки
	СхемаНастроек = РатOpenAPI.НоваяСхемаОбъекта("Настройки изменения");
	РатOpenAPI.ДобавитьРеквизит(СхемаНастроек,
								"append",
								ТипыДанных.Булево,
								"Добавлять строки, если нет подходящих существующих");
	РатOpenAPI.ДобавитьРеквизит(СхемаНастроек,
								"throwException",
								ТипыДанных.Булево,
								"Выбрасывать исключение, если нет удаляемых строк");
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта();
	СхемаФильтрации = РатOpenAPI.НоваяСхемаКоллекции(РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка, "Имя ключевого реквизита"),
													 "Ключевые реквизиты табличной части, по которым происходит поиск изменяемых строк");
	РатOpenAPI.ДобавитьРеквизит(Схема, "filterBy", СхемаФильтрации);
	РатOpenAPI.ДобавитьРеквизит(Схема, "settings", СхемаНастроек);
	
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, "changeTabularSectionBody", Схема);
	
	// БазоваяСхемаТелаУдаленияСтрокТабличнойЧасти
	СхемаНастроек = РатOpenAPI.НоваяСхемаОбъекта("Настройки удаления");
	РатOpenAPI.ДобавитьРеквизит(СхемаНастроек,
								"throwException",
								ТипыДанных.Булево,
								"Выбрасывать исключение, если нет удаляемых строк");
	Схема = РатOpenAPI.НоваяСхемаОбъекта();
	СхемаФильтрации = РатOpenAPI.НоваяСхемаСтруктуры("Фильтр по реквизитам табличной части. Смотрите описание соответствующей табличной части");
	РатOpenAPI.ДобавитьРеквизит(Схема, "filter", СхемаФильтрации);
	РатOpenAPI.ДобавитьРеквизит(Схема, "settings", СхемаНастроек);
	
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, "removeFromTabularSectionBody", Схема);
	
КонецПроцедуры

Процедура ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса)
	
	ОписаниеМетаданных = РатПовтИсп.ТипыМетаданных();
	
	Для Каждого Описание Из ОписаниеМетаданных Цикл
		
		Если НЕ Описание.ТабличныеЧасти Тогда
			Продолжить;
		КонецЕсли;
		
		Коллекция = Метаданные[Описание.ИмяКоллекции];
		
		Если НЕ Коллекция.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Объект Из Коллекция Цикл
			
			Группа = РатСпецификация.ГруппуОбъектаМетаданных(Описание, Объект);
			БазовыйАдрес = РатСпецификация.АдресОбъектаМетаданных(Описание, Объект);
			
			ПараметрИдентификатора = РатСпецификация.ПараметрИдентификаторОбъекта(Объект);
			
			ДобавитьОперацииРаботыСТабличнымиЧастями(СпецификацияСервиса,
													 БазовыйАдрес,
													 ПараметрИдентификатора,
													 Объект,
													 Описание,
													 Группа);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОперацииРаботыСТабличнымиЧастями(СпецификацияСервиса,
												   БазовыйАдрес,
												   ПараметрИдентификатора,
												   Объект,
												   ОписаниеТипа,
												   Группа)
	
	Если НЕ ОписаниеТипа.ТабличныеЧасти Или Объект.ТабличныеЧасти.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Подготовка схем для операций работы с табличными частями
	ИменаТабличныхЧастей = РатКоллекции.ВыгрузитьЗначения(Объект.ТабличныеЧасти, "Имя");
	СхемаПараметраИмяТабличнойЧасти = РатOpenAPI.НоваяСхемаПеречисления(ИменаТабличныхЧастей, "Доступные имена табличных частей");
	
	СхемыКоллекций = СхемаСвязанныхКоллекций(Объект.ТабличныеЧасти);
	СхемаТабличныхЧастей = СхемыКоллекций.СхемаКоллекций;
	СхемаДанных = СхемыКоллекций.СхемаИзмененияКоллекций;
	
	Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса,
									БазовыйАдрес,
									СтрШаблон("{%1}/{ИмяТабличнойЧасти}", ПараметрИдентификатора.ИмяПараметра));
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Ресурс, ПараметрИдентификатора.Ключ);
	РатСпецификация.ДобавитьПараметрОперации(Ресурс,
											 "ИмяТабличнойЧасти",
											 СхемаПараметраИмяТабличнойЧасти,
											 "Имя табличной части объекта");
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	
	ОперацияПолучениеДанных(Методы, Ресурс, Группа, СхемаТабличныхЧастей);
	
	Если НЕ ОписаниеТипа.Редактирование Тогда
		Возврат;
	КонецЕсли;
	
	ОперацияДобавлениеДанных(Методы, Ресурс, Группа, СхемаДанных);
	
	ОперацияУдалениеДанных(Методы, Ресурс, Группа);
	
	ОперацияИзмененияДанных(Методы, Ресурс, Группа, СхемаДанных);
	
КонецПроцедуры

Процедура ОперацияПолучениеДанных(Методы, Ресурс, Группа, СхемаТабличныхЧастей)
	
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.GET, "Табличные части: получение данных", , Группа);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаТабличныхЧастей);
	
КонецПроцедуры

Процедура ОперацияДобавлениеДанных(Методы, Ресурс, Группа, СхемаДанных)
	
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.PUT, "Табличные части: добавление записей", , Группа);
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта();
	РатOpenAPI.ДобавитьРеквизит(Схема, "data", СхемаДанных);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, Схема);
	
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, РатСпецификация.БазовыеСхемы().ОтветУспешно);
	
КонецПроцедуры

Процедура ОперацияУдалениеДанных(Методы, Ресурс, Группа)
	
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.DELETE, "Табличные части: удаление записей", , Группа);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаТелаУдаленияСтрокТабличнойЧасти());
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, РатСпецификация.БазовыеСхемы().ОтветУспешно);
	
КонецПроцедуры

Процедура ОперацияИзмененияДанных(Методы, Ресурс, Группа, СхемаДанных)
	
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс,
		Методы.PATCH,
		"Табличные части: изменение записей",
		"Концептуальное описание параметров
		|* filterBy - Имена ключевых полей, по которым происходит поиск изменяемых строк.
		|* data - Данные изменяемых строк - значения ключевых полей и новые значения. Необязательно указывать значения всех реквизитов ТЧ, а также набор реквизитов для разных строк может отличатся
		|* settings - Настройки алгоритма изменения
		|
		|Предположим нам нужно изменить количество для определенной номенклатуры, для этого формируем следующий запрос:
		|
		|`filterBy`: [""Номенклатура""]
		|`data`: [{""Номенклатура"": ""Ручка"", ""Количество"": 999}]
		|
		|Если мы не уверены, что данные есть в документе и хотим их добавить при отсутствии, то можем дополнительно указать
		|
		|`settings`: {""append"": true}",
		Группа);
	
	Схема = СхемаТелаИзмененияСтроки(СхемаДанных);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, Схема);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, РатСпецификация.БазовыеСхемы().ОтветУспешно);
	
КонецПроцедуры

Функция СхемаСвязанныхКоллекций(Коллекции)
	
	СхемыДанных = Новый Массив;
	Если Коллекции.Количество() = 1 Тогда
		СхемаКоллекций = РатСпецификация.СсылкуНаВыходнуюСхему(Коллекции[0]);
		СхемыДанных.Добавить(РатСпецификация.СсылкуНаВходнуюСхему(Коллекции[0]));
		СхемыДанных.Добавить(РатСпецификация.СсылкуНаВходнуюСхему(Коллекции[0], "Строка"));
	Иначе
		СхемыТабличныхЧастей = Новый Массив;
		Для Каждого Коллекция Из Коллекции Цикл
			СхемыТабличныхЧастей.Добавить(РатСпецификация.СсылкуНаВыходнуюСхему(Коллекция));
			
			СхемыДанных.Добавить(РатСпецификация.СсылкуНаВходнуюСхему(Коллекция));
			СхемыДанных.Добавить(РатСпецификация.СсылкуНаВходнуюСхему(Коллекция, "Строка"));
		КонецЦикла;
		СхемаКоллекций = РатOpenAPI.НоваяСхемаСоставногоТипа(СхемыТабличныхЧастей, "Данные табличной части.");
	КонецЕсли;
	
	СхемаДанных = РатOpenAPI.НоваяСхемаСоставногоТипа(СхемыДанных, "Добавляемые данные, одна или несколько строк");
	
	Результат = Новый Структура("СхемаКоллекций, СхемаИзмененияКоллекций", СхемаКоллекций, СхемаДанных);
	
	Возврат Результат;
	
КонецФункции

Функция СхемаТелаИзмененияСтроки(СхемаДанных)
	
	СхемаИзменений = РатOpenAPI.НоваяСхемаОбъекта();
	РатOpenAPI.ДобавитьРеквизит(СхемаИзменений, "data", СхемаДанных);
	
	Возврат РатOpenAPI.НоваяСхемаОбъединения(Неопределено, РатOpenAPI.СсылкаНаСхему("changeTabularSectionBody"), СхемаИзменений);
	
КонецФункции

Функция СхемаТелаУдаленияСтрокТабличнойЧасти()
	Возврат РатOpenAPI.СсылкаНаСхему("removeFromTabularSectionBody");
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Функция ОбработчикиЗапросов()
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("ДанныеТабличнойЧасти",         "ДанныеТабличнойЧасти");
	Обработчики.Вставить("ДобавитьЗаписиТабличнойЧасти", "ДобавитьЗаписиТабличнойЧасти");
	Обработчики.Вставить("УдалитьЗаписиТабличнойЧасти",  "УдалитьЗаписиТабличнойЧасти");
	Обработчики.Вставить("ИзменитьЗаписиТабличнойЧасти", "ИзменитьЗаписиТабличнойЧасти");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

Функция ДанныеТабличнойЧасти(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	Параметры = ПараметрыРаботыСТабличнойЧастью(ПараметрыЗапроса, Неопределено, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = СтрШаблон("ВЫБРАТЬ * ИЗ %1.%2 ГДЕ Ссылка = &Ссылка", // BSLLS:QueryParseError-off
								 Параметры.ИмяТаблицы,
								 Параметры.ИмяТабличнойЧасти);
		Запрос.УстановитьПараметр("Ссылка", Параметры.ИдентификаторЗаписи);
		
		Результат = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьЗаписиТабличнойЧасти(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	ОжидаемыйТипТела = Новый ОписаниеТипов("Структура");
	Параметры = ПараметрыРаботыСТабличнойЧастью(ПараметрыЗапроса, ОжидаемыйТипТела, СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЕстьРеквизит(ПараметрыЗапроса.Тело, "data", Истина, СтатусОбработкиЗапроса) Тогда
		ПроверитьТипРеквизита(ПараметрыЗапроса.Тело.data,
							  Новый ОписаниеТипов("Структура, Массив"),
							  "добавляемые данные (data)",
							  СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Если ТипЗнч(ПараметрыЗапроса.Тело.data) = Тип("Массив") Тогда
			Данные = ПараметрыЗапроса.Тело.data;
		Иначе
			Данные = РатКоллекции.ЗначениеВМассиве(ПараметрыЗапроса.Тело.data);
		КонецЕсли;
		
		Данные = ПодготовленныеДанныеТабличнойЧасти(Данные,
													Параметры.ИмяТаблицы,
													Параметры.ИмяТабличнойЧасти,
													СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Объект = Параметры.ИдентификаторЗаписи.ПолучитьОбъект();
	
	ТабличнаяЧасть = Объект[Параметры.ИмяТабличнойЧасти];
	Для Каждого Запись Из Данные Цикл
		ЗаполнитьЗначенияСвойств(ТабличнаяЧасть.Добавить(), Запись);
	КонецЦикла;
	
	Возврат СохранитьОбъект(Объект, Параметры, СтатусОбработкиЗапроса);
	
КонецФункции

Функция УдалитьЗаписиТабличнойЧасти(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	ОжидаемыйТипТела = Новый ОписаниеТипов("Структура");
	Параметры = ПараметрыРаботыСТабличнойЧастью(ПараметрыЗапроса, ОжидаемыйТипТела, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно И НЕ ПараметрыЗапроса.Тело.Свойство("filter") Тогда
		Сообщение = "Не указаны параметры фильтрации удаляемых данных, поле filter";
		КлассыОшибок = РатОбщий.КлассыОшибок();
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.АнализПараметровЗапроса, Сообщение);
	КонецЕсли;
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Описание = СтрШаблон("фильтр удаления записей табличной части %1", Параметры.ИмяТабличнойЧасти);
		ДанныеФильтрации = ПараметрыЗапроса.Тело.filter;
		Если ДанныеФильтрации = Неопределено Тогда
			Фильтр = Новый Структура();
		Иначе
			Фильтр = РатПреобразования.РеквизитыИзТела(ДанныеФильтрации,
													   Параметры.РеквизитыТабличнойЧасти,
													   СтатусОбработкиЗапроса,
													   Описание,
													   Истина);
		КонецЕсли;
	КонецЕсли;
	НастройкиИзменения = НастройкиИзмененияТабличнойЧасти(ПараметрыЗапроса.Тело);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Объект = Параметры.ИдентификаторЗаписи.ПолучитьОбъект();
	ТабличнаяЧасть = Объект[Параметры.ИмяТабличнойЧасти];
	
	ОтфильтрованныеСтроки = ОтфильтрованныеСтроки(ТабличнаяЧасть, Фильтр);
	
	ЕстьИзменения = ОтфильтрованныеСтроки.Количество();
	
	Если Фильтр.Количество() И НЕ ЕстьИзменения И НастройкиИзменения.ВыбрасыватьОшибкуЕслиНеНайдены Тогда
		КлассыОшибок = РатОбщий.КлассыОшибок();
		Сообщение = "Отсутствуют записи удовлетворяющие условиям удаления";
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.НеНайденаЗаписьИБ, Сообщение);
	КонецЕсли;
	
	Если ОтфильтрованныеСтроки = ТабличнаяЧасть Тогда
		ТабличнаяЧасть.Очистить();
	Иначе
		Для Каждого Строка Из ОтфильтрованныеСтроки Цикл
			ТабличнаяЧасть.Удалить(Строка);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЕстьИзменения Тогда
		Возврат СохранитьОбъект(Объект, Параметры, СтатусОбработкиЗапроса);
	Иначе
		Возврат РатОбщий.РезультатДанныеЗаписаны("Нет изменений");
	КонецЕсли;
	
КонецФункции

Функция ИзменитьЗаписиТабличнойЧасти(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	ОжидаемыйТипТела = Новый ОписаниеТипов("Структура");
	Параметры = ПараметрыРаботыСТабличнойЧастью(ПараметрыЗапроса, ОжидаемыйТипТела, СтатусОбработкиЗапроса);
	
	Если ЕстьРеквизит(ПараметрыЗапроса.Тело, "filterBy", Истина, СтатусОбработкиЗапроса) Тогда
		ПроверитьТипРеквизита(ПараметрыЗапроса.Тело.filterBy,
							  Новый ОписаниеТипов("Строка, Массив"),
							  "параметры фильтрации (filterBy)",
							  СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если ЕстьРеквизит(ПараметрыЗапроса.Тело, "data", Истина, СтатусОбработкиЗапроса) Тогда
		ПроверитьТипРеквизита(ПараметрыЗапроса.Тело.data,
							  Новый ОписаниеТипов("Структура, Массив"),
							  "данные изменения (data)",
							  СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыЗапроса.Тело.filterBy) = Тип("Строка") Тогда
		РеквизитыФильтрации = ПараметрыЗапроса.Тело.filterBy;
	Иначе
		РеквизитыФильтрации = СтрСоединить(ПараметрыЗапроса.Тело.filterBy, ", ");
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыЗапроса.Тело.data) = Тип("Структура") Тогда
		СырыеПараметрыИзменения = РатКоллекции.ЗначениеВМассиве(ПараметрыЗапроса.Тело.data);
	Иначе
		СырыеПараметрыИзменения = ПараметрыЗапроса.Тело.data;
	КонецЕсли;
	
	ДанныеИзменения = ПодготовленныеДанныеТабличнойЧасти(СырыеПараметрыИзменения,
														 Параметры.ИмяТаблицы,
														 Параметры.ИмяТабличнойЧасти,
														 СтатусОбработкиЗапроса);
	НастройкиИзменения = НастройкиИзмененияТабличнойЧасти(ПараметрыЗапроса.Тело);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Объект = Параметры.ИдентификаторЗаписи.ПолучитьОбъект();
	ТабличнаяЧасть = Объект[Параметры.ИмяТабличнойЧасти];
	ЕстьИзменения = Ложь;
	
	Для Каждого Патч Из ДанныеИзменения Цикл
		
		Если ПрименитьПатч(ТабличнаяЧасть, РеквизитыФильтрации, Патч, НастройкиИзменения, СтатусОбработкиЗапроса) Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьИзменения Тогда
		Возврат СохранитьОбъект(Объект, Параметры, СтатусОбработкиЗапроса);
	Иначе
		Возврат РатОбщий.РезультатДанныеЗаписаны("Нет изменений");
	КонецЕсли;
	
КонецФункции

#КонецОбласти

Функция ПараметрыРаботыСТабличнойЧастью(ПараметрыЗапроса, ОжидаемыйТипТела, СтатусОбработкиЗапроса)
	
	Параметры = Новый Структура("ТипМетаданных, ВидМетаданных, Идентификатор");
	КлассыОшибок = РатОбщий.КлассыОшибок();
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса,
										"ТипМетаданных, ВидМетаданных, Идентификатор, ИмяТабличнойЧасти",
										СтатусОбработкиЗапроса);
	
	ОжидаемЗаполненноеТело = ОжидаемыйТипТела <> Неопределено;
	ЕстьТело = ЗначениеЗаполнено(ПараметрыЗапроса.Тело);
	
	Если ОжидаемЗаполненноеТело И НЕ ЕстьТело Тогда
		ТекстОшибки = "Ожидали заполненное тело запроса";
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.АнализПараметровЗапроса, ТекстОшибки);
	ИначеЕсли НЕ ОжидаемЗаполненноеТело И ЕстьТело Тогда
		ТекстОшибки = "Ожидали незаполненное тело запроса";
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.АнализПараметровЗапроса, ТекстОшибки);
	ИначеЕсли ОжидаемЗаполненноеТело И ЕстьТело Тогда
		ПроверитьТипРеквизита(ПараметрыЗапроса.Тело, ОжидаемыйТипТела, "тело запроса", СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Параметры, ПараметрыЗапроса);
	ИдентификаторЗаписи = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	Параметры.Вставить("ИдентификаторЗаписи", ИдентификаторЗаписи);
	Параметры.Вставить("ИмяТабличнойЧасти", ПараметрыЗапроса.ИмяТабличнойЧасти);
	Параметры.Вставить("ИмяТаблицы", ПараметрыЗапроса.ИмяТаблицы);
	
	РеквизитыЗаписи = РатПовтИсп.СтруктураТаблицы(Параметры.ИмяТаблицы);
	
	Если НЕ РеквизитыЗаписи.ТабличныеЧасти.Свойство(Параметры.ИмяТабличнойЧасти) Тогда
		Сообщение = СтрШаблон("%1 не содержит табличную часть %2", Параметры.ИмяТаблицы, Параметры.ИмяТабличнойЧасти);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.АнализПараметровЗапроса, Сообщение);
	Иначе
		РеквизитыТабличнойЧасти = РеквизитыЗаписи.ТабличныеЧасти[Параметры.ИмяТабличнойЧасти];
		Параметры.Вставить("РеквизитыТабличнойЧасти", РеквизитыТабличнойЧасти);
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

Функция ПодготовленныеДанныеТабличнойЧасти(Данные, ИмяТаблицыОбъекта, ИмяТабличнойЧасти, СтатусОбработкиЗапроса)
	
	РеквизитыЗаписи = РатПовтИсп.СтруктураТаблицы(ИмяТаблицыОбъекта);
	РеквизитыТабличнойЧасти = РеквизитыЗаписи.ТабличныеЧасти[ИмяТабличнойЧасти];
	
	ТабличнаяЧасть = Новый Массив();
	
	Если ТипЗнч(Данные) = Тип("Массив") Тогда
		
		Для Инд = 0 По Данные.ВГраница() Цикл
			
			Описание = СтрШаблон("табличной части '%1[%2]'", ИмяТабличнойЧасти, Инд);
			ЗаписьТабличнойЧасти = РатПреобразования.РеквизитыИзТела(Данные[Инд],
																	 РеквизитыТабличнойЧасти,
																	 СтатусОбработкиЗапроса,
																	 Описание,
																	 Истина);
			ТабличнаяЧасть.Добавить(ЗаписьТабличнойЧасти);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Возврат ТабличнаяЧасть;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции
Функция ЕстьРеквизит(Объект, Свойство, ПроверитьЗаполненность, СтатусОбработкиЗапроса)
	
	ЕстьРеквизит = Объект.Свойство(Свойство);
	КлассыОшибок = РатОбщий.КлассыОшибок();
	
	Если СтатусОбработкиЗапроса.Успешно И НЕ ЕстьРеквизит Тогда
		
		Сообщение = "Не указан обязательный реквизит " + Свойство;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.АнализПараметровЗапроса, Сообщение);
		
	ИначеЕсли ПроверитьЗаполненность И СтатусОбработкиЗапроса.Успешно И НЕ ЗначениеЗаполнено(Объект[Свойство]) Тогда
		
		Сообщение = "Не заполнен обязательный реквизит " + Свойство;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.АнализПараметровЗапроса, Сообщение);
		
	КонецЕсли;
	
	Возврат ЕстьРеквизит;
	
КонецФункции

Процедура ПроверитьТипРеквизита(Реквизит, ОжидаемыйТип, ОписаниеРеквизита, СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат;
	КонецЕсли;
	
	ТипРеквизита = ТипЗнч(Реквизит);
	КлассыОшибок = РатОбщий.КлассыОшибок();
	
	Если НЕ ОжидаемыйТип.СодержитТип(ТипРеквизита) Тогда
		ТекстОшибки = СтрШаблон("Ожидали, что %1 имеет тип %2, а получили %3", ОписаниеРеквизита, ОжидаемыйТип, ТипРеквизита);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.АнализПараметровЗапроса, ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Функция СохранитьОбъект(Объект, Параметры, СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеЗаписи = РатИнформационнаяБаза.ДанныеЗаписи(Параметры.ИмяТаблицы);
	РатИнформационнаяБаза.СохранитьОбъект(Объект, ДанныеЗаписи, СтатусОбработкиЗапроса);
	
	Результат = Неопределено;
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Результат = РатОбщий.РезультатДанныеЗаписаны("Объект успешно обновлен");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПрименитьПатч(ТабличнаяЧасть, РеквизитыФильтрации, Патч, НастройкиИзменения, СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьИзменения = Ложь;
	Отбор = Новый Структура(РеквизитыФильтрации);
	ЗаполнитьЗначенияСвойств(Отбор, Патч);
	ИзменяемыеСтроки = ОтфильтрованныеСтроки(ТабличнаяЧасть, Отбор);
	
	Для Каждого Строка Из ИзменяемыеСтроки Цикл
		ЗаполнитьЗначенияСвойств(Строка, Патч);
		ЕстьИзменения = Истина;
	КонецЦикла;
	
	Если ИзменяемыеСтроки.Количество() = 0 И НастройкиИзменения.Добавлять Тогда
		Строка = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Патч);
		ЕстьИзменения = Истина;
	КонецЕсли;
	
	Если НЕ ЕстьИзменения И НастройкиИзменения.ВыбрасыватьОшибкуЕслиНеНайдены Тогда
		
		КлассыОшибок = РатОбщий.КлассыОшибок();
		ПредставлениеОтбора = РатОбщий.Представление(Отбор);
		Сообщение = СтрШаблон("Не найдены записи для изменения с параметрами:
		|" + ПредставлениеОтбора);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.НеНайденаЗаписьИБ, Сообщение);
		
	КонецЕсли;
	
	Возврат ЕстьИзменения;
	
КонецФункции

Функция НастройкиИзмененияТабличнойЧасти(Тело)
	
	Настройки = Новый Структура("Добавлять, ВыбрасыватьОшибкуЕслиНеНайдены", Ложь, Истина);
	
	Если Тело.Свойство("settings") Тогда
		
		Настройки.Добавлять = РатКоллекции.ЗначениеСтруктуры(Тело.settings, "append", Настройки.Добавлять);
		Настройки.ВыбрасыватьОшибкуЕслиНеНайдены = РатКоллекции.ЗначениеСтруктуры(Тело.settings,
																				  "throwException",
																				  Настройки.ВыбрасыватьОшибкуЕслиНеНайдены);
		
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

Функция ОтфильтрованныеСтроки(ТабличнаяЧасть, Фильтр)
	
	Строки = Новый Массив();
	
	Если Фильтр.Количество() И Фильтр.Свойство("НомерСтроки") Тогда
		
		Если ТабличнаяЧасть.Количество() >= Фильтр.НомерСтроки Тогда
			Строка = ТабличнаяЧасть[Фильтр.НомерСтроки - 1];
		КонецЕсли;
		
		Для Каждого Элемент Из Фильтр Цикл
			Если Строка <> Неопределено И Строка[Элемент.Ключ] <> Элемент.Значение Тогда
				Строка = Неопределено;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Строка <> Неопределено Тогда
			Строки.Добавить(Строка);
		КонецЕсли;
		
	ИначеЕсли Фильтр.Количество() Тогда
		
		Строки = ТабличнаяЧасть.НайтиСтроки(Фильтр);
		
	Иначе
		
		Строки = ТабличнаяЧасть;
		
	КонецЕсли;
	
	Возврат Строки;
	
КонецФункции

#КонецОбласти

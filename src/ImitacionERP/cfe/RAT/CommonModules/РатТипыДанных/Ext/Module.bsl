//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

/////////////////////////////////////////////////////////////////////////////////
// Заголовок
//  Описание
/////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ПроверкиТипов

// Проверяет, является ли тип составным (содержит более одного типа).
//
// Параметры:
//  ОписаниеТипа - ОписаниеТипов - Проверяемое описание типа
//
// Возвращаемое значение:
//  Булево - Истина, если тип составной (содержит более одного типа)
//
// Пример:
//  Если РатТипыДанных.ЭтоСоставнойТип(ОписаниеТипа) Тогда ...
//
Функция ЭтоСоставнойТип(ОписаниеТипа) Экспорт
	
	МинимальноеКоличествоТипов = 1;

	Если ОписаниеТипа.СодержитТип(Тип("Null")) Тогда
		МинимальноеКоличествоТипов = 2;
	КонецЕсли;
	
	Возврат ОписаниеТипа.Типы().Количество() > МинимальноеКоличествоТипов;
	
КонецФункции

// Проверяет, является ли значение структурой.
//
// Параметры:
//  Значение - Произвольный - Проверяемое значение
//
// Возвращаемое значение:
//  Булево - Истина, если значение является структурой
//
// Пример:
//  Если РатТипыДанных.ЭтоСтруктура(Значение) Тогда ...
//
Функция ЭтоСтруктура(Значение) Экспорт
	
	Возврат ТипЗнч(Значение) = Тип("Структура");
	
КонецФункции

// Проверяет, является ли значение массивом.
//
// Параметры:
//  Значение - Произвольный - Проверяемое значение
//
// Возвращаемое значение:
//  Булево - Истина, если значение является массивом
//
// Пример:
//  Если РатТипыДанных.ЭтоМассив(Значение) Тогда ...
//
Функция ЭтоМассив(Значение) Экспорт
	
	Возврат ТипЗнч(Значение) = Тип("Массив") ИЛИ ТипЗнч(Значение) = Тип("ФиксированныйМассив");
	
КонецФункции

// Проверяет, является ли значение строкой.
//
// Параметры:
//  Значение - Произвольный - Проверяемое значение
//
// Возвращаемое значение:
//  Булево - Истина, если значение является строкой
//
// Пример:
//  Если РатТипыДанных.ЭтоСтрока(Значение) Тогда ...
//
Функция ЭтоСтрока(Значение) Экспорт
	
	Возврат ТипЗнч(Значение) = Тип("Строка");
	
КонецФункции

// Проверяет, является ли значение неопределенным или null.
//
// Параметры:
//  Значение - Произвольный - Проверяемое значение
//
// Возвращаемое значение:
//  Булево - Истина, если значение неопределено или равно "null"
//
// Пример:
//  Если РатТипыДанных.ЭтоНеопределено(Значение) Тогда ...
//
Функция ЭтоНеопределено(Значение) Экспорт
	
	Возврат Значение = Неопределено ИЛИ СтрСравнить(Значение, "null") = 0;
	
КонецФункции

// Проверяет, является ли тип системным перечислением.
//
// Параметры:
//  Тип - Тип - Проверяемый тип
//
// Возвращаемое значение:
//  Булево - Истина, если тип является системным перечислением
//
// Пример:
//  Если РатТипыДанных.ЭтоСистемноеПеречисление(Тип) Тогда ...
//
Функция ЭтоСистемноеПеречисление(Тип) Экспорт
	
	Возврат ТипыСистемныхПеречислений().СодержитТип(Тип);
	
КонецФункции

// Проверяет, является ли тип ссылочным (ссылкой на объект базы данных).
//
// Параметры:
//  Тип - Тип - Проверяемый тип
//
// Возвращаемое значение:
//  Булево - Истина, если тип является ссылочным
//
// Пример:
//  Если РатТипыДанных.ЭтоСсылочныйТип(Тип("СправочникСсылка.Номенклатура")) Тогда ...
//
Функция ЭтоСсылочныйТип(Тип) Экспорт
	
	Если Тип <> Тип("Неопределено") Тогда
		
		ПредставлениеТипа = ПредставлениеТипа(Тип);
		
		Результат = СтрНайти(ПредставлениеТипа, "Ref.") И РатПовтИсп.ЭтоСсылочныйТип(ПредставлениеТипа);
		
	Иначе
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет, является ли значение объектом типа ключ-значение.
//
// Параметры:
//  Значение - Произвольный - Проверяемое значение
//
// Возвращаемое значение:
//  Булево - Истина, если значение является объектом типа ключ-значение
//
// Пример:
//  Если РатТипыДанных.ЭтоКлючЗначение(Значение) Тогда ...
//
Функция ЭтоКлючЗначение(Значение) Экспорт
	
	ТипЗначения = ТипЗнч(Значение);
	Возврат ТипЗначения = Тип("Структура")
		ИЛИ ТипЗначения = Тип("ФиксированнаяСтруктура")
		ИЛИ ТипЗначения = Тип("Соответствие")
		ИЛИ ТипЗначения = Тип("ФиксированноеСоответствие");
	
КонецФункции

#КонецОбласти

// Возвращает коллекцию типов системных перечислений.
//
// Возвращаемое значение:
//  ОписаниеТипов - Коллекция типов системных перечислений
//
// Пример:
//  ТипыПеречислений = РатТипыДанных.ТипыСистемныхПеречислений();
//
Функция ТипыСистемныхПеречислений() Экспорт
	
	Возврат Новый ОписаниеТипов(
		"СостояниеФоновогоЗадания,
		|ВидДвиженияБухгалтерии,
		|ВидДвиженияНакопления,
		|ВидПериодаРегистраРасчета,
		|ВидСчета,
		|ВидТочкиМаршрутаБизнесПроцесса,
		|ИспользованиеГруппИЭлементов,
		|ИспользованиеСреза,
		|ИспользованиеРежимаПроведения,
		|РежимАвтоВремя,
		|РежимЗаписиДокумента,
		|РежимПроведенияДокумента,
		|ПериодичностьАгрегатаРегистраНакопления,
		|ИспользованиеАгрегатаРегистраНакопления
		|");
	
КонецФункции

// Определяет тип по его строковому представлению.
// Поддерживает работу с примитивными типами и типами объектов информационной базы.
//
// Параметры:
//  Представление - Строка - Строковый идентификатор типа текущей конфигурации или примитивного типа
//
// Возвращаемое значение:
//  Тип, Неопределено - Тип по представлению
//
// Пример:
//  Тип = РатТипыДанных.ТипПоПредставлению("СправочникСсылка.Номенклатура");
//
Функция ТипПоПредставлению(Представление) Экспорт
	
#Если ВебКлиент Тогда
	ВызватьИсключение РатИсключения.МетодНеДоступен("РатТипыДанных.ТипПоПредставлению");
#Иначе
	Если СтрНайти(Представление, ".") Тогда
		Результат = СериализаторXDTO.ИзXMLТипа(Представление, "http://v8.1c.ru/8.1/data/enterprise/current-config");
	Иначе
		Результат = СериализаторXDTO.ИзXMLТипа(Представление, "http://www.w3.org/2001/XMLSchema");
		Если Результат = Неопределено Тогда
			Результат = СериализаторXDTO.ИзXMLТипа(Представление, "http://v8.1c.ru/8.1/data/core");
		КонецЕсли;
		Если Результат = Неопределено Тогда
			Результат = СериализаторXDTO.ИзXMLТипа(Представление, "http://v8.1c.ru/8.1/data/enterprise");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
#КонецЕсли
		
КонецФункции

// Возвращает строковое представление типа данных.
//
// Параметры:
//  Тип - Тип - Тип, представление которого нужно получить
//
// Возвращаемое значение:
//  Строка - Представление типа
//
// Пример:
//  Представление = РатТипыДанных.ПредставлениеТипа(Тип("СправочникСсылка.Номенклатура"));
//
Функция ПредставлениеТипа(Тип) Экспорт
	
#Если ВебКлиент Тогда
	ВызватьИсключение РатИсключения.МетодНеДоступен("РатТипыДанных.ПредставлениеТипа");
#Иначе
	Если Тип = Тип("Неопределено") Тогда
		Возврат "null";
	КонецЕсли;
	
	ТипXML = СериализаторXDTO.XMLТип(Тип);
	
	Если ТипXML = Неопределено Тогда
		Возврат "<unknown>";
	Иначе
		Возврат ТипXML.ИмяТипа;
	КонецЕсли;
#КонецЕсли
	
КонецФункции

// Возвращает тип значения по имени таблицы данных и суффиксу.
//
// Параметры:
//  ИмяТаблицы - Строка - Полное имя таблицы
//             - см. РатМетаданные.РазложитьИмяТаблицы
//  СуффиксТипа - Строка - Суффикс типа, например: Ссылка, Менеджер, НаборЗаписей
//
// Возвращаемое значение:
//  Тип - Тип значения
//
// Пример:
//  Тип = РатТипыДанных.ТипПоИмениТаблицы("Справочник.Номенклатура", "Ссылка");
//
Функция ТипПоИмениТаблицы(ИмяТаблицы, СуффиксТипа) Экспорт
	
	Если ТипЗнч(ИмяТаблицы) = Тип("Строка") Тогда
		ЧастиИмени = СтрРазделить(ИмяТаблицы, ".");
	Иначе
		ЧастиИмени = РатКоллекции.ЗначениеВМассиве(ИмяТаблицы.ИмяТипа, ИмяТаблицы.ИмяВида);
	КонецЕсли;
	
	Возврат Тип(СтрШаблон("%1%3.%2", ЧастиИмени[0], ЧастиИмени[1], СуффиксТипа));
	
КонецФункции

#КонецОбласти

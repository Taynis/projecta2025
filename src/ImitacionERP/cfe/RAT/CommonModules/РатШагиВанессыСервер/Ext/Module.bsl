//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция ПреобразоватьЗначение(Знач Значение, Знач Тип) Экспорт
	
	Если СтрСравнить(Тип, "Время") = 0 Тогда
		Тип = Новый ОписаниеТипов("Дата", , , , , Новый КвалификаторыДаты(ЧастиДаты.Время));
	КонецЕсли;
	
	Если ТипЗнч(Тип) = Тип("Строка") Тогда
		Тип = Тип(Тип);
	КонецЕсли;
	
	Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
	Результат = РатПреобразования.ДесериализоватьЗначение(Значение, Тип, Статус, "Преобразование значения");
	
	Если НЕ Статус.Успешно Тогда
		ВызватьИсключение Статус.ОписаниеОшибки.Текст;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Табличный документ из двоичных данных.
// 
// Параметры:
//  Данные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  ТабличныйДокумент
Функция ТабличныйДокументИзДвоичныхДанных(Знач Данные) Экспорт
	
	ТабДок = Новый ТабличныйДокумент();
	
	Поток = Данные.ОткрытьПотокДляЧтения();
	ТабДок.Прочитать(Поток);
	Поток.Закрыть();
	
	Возврат ТабДок;
	
КонецФункции

Функция СверкаКартинокТабличныхДокументов(Знач Таб1, Знач Таб2) Экспорт
	
	МассивКартинокДокумента1 = ПолучитьМассивКартинок(Таб1);
	МассивКартинокДокумента2 = ПолучитьМассивКартинок(Таб2);
	
	РезультатСверки = Новый Структура;
	РезультатСверки.Вставить("Совпадают", Ложь);
	РезультатСверки.Вставить("КоличествоСовпадает", МассивКартинокДокумента1.Количество() = МассивКартинокДокумента2.Количество());
	РезультатСверки.Вставить("Количество1", МассивКартинокДокумента1.Количество());
	РезультатСверки.Вставить("Количество2", МассивКартинокДокумента2.Количество());
	РезультатСверки.Вставить("НесовпадающиеКартинки", Новый Массив);
	
	Если НЕ РезультатСверки.КоличествоСовпадает Тогда
		Возврат РезультатСверки;
	КонецЕсли;
	
	Для Индекс = 0 По МассивКартинокДокумента1.Количество() - 1 Цикл
		
		Картинка1 = МассивКартинокДокумента1[Индекс];
		Картинка2 = МассивКартинокДокумента2[Индекс];
		
		Отличия = ОтличияКартинок(Картинка1, Картинка2);
		Если ЗначениеЗаполнено(Отличия) Тогда
			Сообщение = СтрШаблон("Картинка №%1, отличия: %2", Формат(Индекс + 1, "ЧГ="), СтрСоединить(Отличия, ", "));
			РезультатСверки.НесовпадающиеКартинки.Добавить(Сообщение);
		КонецЕсли;
	КонецЦикла;
	
	РезультатСверки.Совпадают = НЕ ЗначениеЗаполнено(РезультатСверки.НесовпадающиеКартинки);
	
	Возврат РезультатСверки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Отличия картинок.
// 
// Параметры:
//  Картинка1 - см. ОписаниеКартинки
//  Картинка2 - см. ОписаниеКартинки
// 
// Возвращаемое значение:
//  Массив из Строка
Функция ОтличияКартинок(Картинка1, Картинка2)
	
	Различия = Новый Массив;
	
	Для Каждого Элемент Из Картинка1 Цикл
		
		Значение1 = Элемент.Значение;
		Значение2 = Картинка2[Элемент.Ключ];
		
		Если Значение1 <> Значение2 Тогда
			Различия.Добавить(Элемент.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Различия;
	
КонецФункции

Функция ПолучитьМассивКартинок(ТабДокумент)
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, ТабДокумент);
	XML = ЗаписьXML.Закрыть();
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(XML);
	ЧтениеXML.Прочитать();
	
	Картинки = Новый Массив;
	ДанныеКартинок = Новый Соответствие();
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
			
			Продолжить;
			
		ИначеЕсли ЧтениеXML.Имя = "drawing" Тогда
			
			Картинка = ОписаниеКартинкиДокумента(ЧтениеXML);
			Если Картинка <> Неопределено Тогда
				Картинки.Добавить(Картинка);
			КонецЕсли;
			
		ИначеЕсли ЧтениеXML.Имя = "picture" Тогда
			
			ПрочитатьДанныеКартинки(ЧтениеXML, ДанныеКартинок);
			
		Иначе
			
			ЧтениеXML.Пропустить();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Инд = 0 По Картинки.ВГраница() Цикл
		
		Картинка = Картинки[Инд];
		
		Картинка.Картинка = ДанныеКартинок[Картинка.Картинка - 1];
		Картинки[Инд] = Новый ФиксированнаяСтруктура(Картинка);
		
	КонецЦикла;
	
	Возврат Картинки;
	
КонецФункции

Функция ОписаниеКартинкиДокумента(ЧтениеXML)
	
	Уровень = 1;
	Данные = Новый Структура;
	Пропуск = Ложь;
	
	Пока Уровень > 0 И ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Уровень = Уровень + 1;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Уровень = Уровень - 1;
			Продолжить;
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если Пропуск Тогда
			Продолжить;
		КонецЕсли;
		
		Ключ = ЧтениеXML.Имя;
		ЧтениеXML.Прочитать();
		Значение = ЧтениеXML.Значение;
		
		Данные.Вставить(Ключ, Значение);
		
		Если Ключ = "drawingType" И Значение <> "Picture" Тогда
			Пропуск = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Данные.drawingType <> "Picture" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеКартинки = ОписаниеКартинки();
	ОписаниеКартинки.АвтоРазмер = РатКоллекции.ЗначениеСтруктуры(Данные, "autoSize");
	ОписаниеКартинки.Картинка = Число(РатКоллекции.ЗначениеСтруктуры(Данные, "pictureIndex", 0));
	ОписаниеКартинки.РазмерКартинки = РатКоллекции.ЗначениеСтруктуры(Данные, "pictureSize");
	
	ОписаниеКартинки.Строка = СтрШаблон("%1;%2;%3;%4;",
										РатКоллекции.ЗначениеСтруктуры(Данные, "beginRow"),
										РатКоллекции.ЗначениеСтруктуры(Данные, "beginRowOffset"),
										РатКоллекции.ЗначениеСтруктуры(Данные, "endRow"),
										РатКоллекции.ЗначениеСтруктуры(Данные, "endRowOffset"));
	
	ОписаниеКартинки.Колонка = СтрШаблон("%1;%2;%3;%4;",
										 РатКоллекции.ЗначениеСтруктуры(Данные, "beginColumn"),
										 РатКоллекции.ЗначениеСтруктуры(Данные, "beginColumnOffset"),
										 РатКоллекции.ЗначениеСтруктуры(Данные, "endColumn"),
										 РатКоллекции.ЗначениеСтруктуры(Данные, "endColumnOffset"));
	
	Возврат ОписаниеКартинки;
	
КонецФункции

Процедура ПрочитатьДанныеКартинки(ЧтениеXML, ДанныеКартинок)
	
	Уровень = 1;
	КодКартинки = Неопределено;
	ИндексКартинки = Неопределено;
	
	Пока Уровень > 0 И ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Уровень = Уровень + 1;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Уровень = Уровень - 1;
			Продолжить;
		КонецЕсли;
		
		Если ЧтениеXML.Имя = "index" Тогда
			ЧтениеXML.Прочитать();
			ИндексКартинки = ЧтениеXML.Значение;
		ИначеЕсли ЧтениеXML.Имя = "picture" Тогда
			ЧтениеXML.Прочитать();
			
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				Уровень = Уровень - 1;
				КодКартинки = ЧтениеXML.ЗначениеАтрибута("ref");
			Иначе
				КодКартинки = ЧтениеXML.Значение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИндексКартинки <> Неопределено И КодКартинки <> Неопределено Тогда
		ДанныеКартинок.Вставить(Число(ИндексКартинки), КодКартинки);
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеКартинки()
	
	Описание = Новый Структура;
	
	Описание.Вставить("Строка", "");
	Описание.Вставить("Колонка", "");
	
	Описание.Вставить("АвтоРазмер", Ложь);
	Описание.Вставить("Картинка", "");
	Описание.Вставить("РазмерКартинки", "");
	
	Возврат Описание;
	
КонецФункции

#КонецОбласти

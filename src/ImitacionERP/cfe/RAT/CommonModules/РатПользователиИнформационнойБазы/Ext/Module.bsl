//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание сервиса пользователей информационной базы, включая операции и схемы.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса);
	
	ЗарегистрироватьОписаниеСхем(СпецификацияСервиса);
	
КонецПроцедуры

// Регистрирует шаблоны адресов для обработки запросов пользователей информационной базы.
//
// Параметры:
//   ШаблоныСервиса - см. РатШаблоныАдресов.Шаблоны
//
Процедура ЗарегистрироватьШаблоны(ШаблоныСервиса) Экспорт
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Обработчики = Обработчики();
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса, Методы.PUT, "/Пользователи", Обработчики.СоздатьПользователя, 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса, Методы.GET, "/Пользователи/{Наименование}", Обработчики.ДанныеПользователя, 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса, Методы.PUT, "/Пользователи/{Наименование}", Обработчики.ИзменитьПользователя, 0);
	
КонецПроцедуры

// Вызвать обработчик.
// 
// Параметры:
//  Обработчик - Строка - Идентификатор обработчика, см. Обработчики
//  ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//  ОбработчикВыполнен - Булево - Необходимо установить если это "наш" запрос
// 
// Возвращаемое значение:
//  Произвольный - Сериализуемый результат обработки запроса
Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
	
	Обработчики = Обработчики();
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.СоздатьПользователя Тогда
	
		Результат = СоздатьПользователя(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
	
	ИначеЕсли Обработчик = Обработчики.ДанныеПользователя Тогда
	
		Результат = ДанныеПользователя(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
	
	ИначеЕсли Обработчик = Обработчики.ИзменитьПользователя Тогда
	
		Результат = ИзменитьПользователя(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Обработчики

Функция Обработчики()
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("СоздатьПользователя", "СоздатьПользователя");
	Обработчики.Вставить("ДанныеПользователя", "ДанныеПользователя");
	Обработчики.Вставить("ИзменитьПользователя", "ИзменитьПользователя");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

Функция СоздатьПользователя(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Если РатРасширениеФункциональности.ЕстьПереопределениеМетода("РатПользователиИнформационнойБазы", "СоздатьПользователя") Тогда
		ПараметрыВызова = РатКоллекции.ЗначениеВМассиве(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		Результат = РатРасширениеФункциональности.ВычислитьЗначениеМетода("РатПользователиИнформационнойБазы", "СоздатьПользователя", ПараметрыВызова);
	Иначе
		КлассыОшибок = РатОбщий.КлассыОшибок();
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
									 КлассыОшибок.НедоступнаяФункциональность,
									 "Не реализован метод создания пользователя для конфигурации " + РатРасширениеФункциональности.ИмяКонфигурации());
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИзменитьПользователя(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	ДанныеПользователя = ПреобразоватьДанныеПользователя(ПараметрыЗапроса.Тело, СтатусОбработкиЗапроса);
	
	ИмяПользователя = ПараметрыЗапроса.Наименование;
	ПользовательИБ = ПользовательИБ(ИмяПользователя);
	
	УстановитьДанныеПользователяИБ(ПользовательИБ, ДанныеПользователя, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		ПользовательИБ.Записать();
		Результат = ОписаниеПользователя(ПользовательИБ);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеПользователя(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	ИмяПользователя = ПараметрыЗапроса.Наименование;
	
	Пользователь = ПользовательИБ(ИмяПользователя);
	
	Если Пользователь = Неопределено Тогда
		КлассыОшибок = РатОбщий.КлассыОшибок();
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.НеНайденаЗаписьИБ, "Не найден пользователь " + ИмяПользователя);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОписаниеПользователя(Пользователь);
	
КонецФункции

#КонецОбласти

Процедура ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса)
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Путь = "/Пользователи";
	Группа = "Пользователи информационной базы";
	РатСпецификация.ДобавитьГруппу(СпецификацияСервиса, Группа);
	
	ИмяСхемыПользователя = "userib";
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Создание пользователя",
													"Создает пользователя информационной базы",
													Группа,
													Путь,
													Методы.PUT);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, ИмяСхемыПользователя);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, ИмяСхемыПользователя);
	
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Данные пользователя",
													"Ищет и возвращает параметры пользователя информационной базы",
													Группа,
													Путь + "/{name}",
													Методы.GET);
	РатСпецификация.ДобавитьПараметрОперации(Операция,
											 "name",
											 РатOpenAPI.НоваяСхемаДанных(РатOpenAPI.ТипыДанных().ЦелоеЧисло),
											 "Имя пользователя информационной базы");
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, ИмяСхемыПользователя);
	
КонецПроцедуры

Процедура ЗарегистрироватьОписаниеСхем(СпецификацияСервиса)
	
	ТипСтрока = Тип("Строка");
	ТипБулево = Тип("Булево");
	ТипУникальныйИдентификатор = Тип("УникальныйИдентификатор");
	
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "УникальныйИдентификатор",
											  "Уникальный идентификатор пользователя информационной базы",
											  ТипУникальныйИдентификатор,
											  Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "Имя", "Имя пользователя информационной базы", ТипСтрока, Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "ПолноеИмя", "Полное имя пользователя информационной базы", ТипСтрока, Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "АутентификацияСтандартная",
											  "Определяет использование стандартной аутентификации (пользователь-пароль)",
											  ТипБулево,
											  Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "АутентификацияOpenID",
											  "Указывает, разрешена ли пользователю аутентификация по протоколу OpenID",
											  ТипБулево,
											  Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "АутентификацияОС",
											  "Определяет, использовать ли аутентификацию Windows для данного пользователя",
											  ТипБулево,
											  Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "Пароль",
											  "Пароль, используемый при стандартной аутентификации",
											  ТипСтрока,
											  Ложь,
											  ,
											  Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "ПользовательОС",
											  "Содержит строку, идентифицирующую пользователя операционной системы.
											  |Формат строки: \\ИмяДомена\ИмяПользователя",
											  ТипСтрока,
											  Ложь);
	ОписаниеРоли = РатOpenAPI.НоваяСхемаПеречисления(ИменаКоллекцииМетаданных(Метаданные.Роли), "Роль пользователя информационной базы");
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "ЗащитаОтОпасныхДействий", "Защита от опасных действий", ТипБулево, Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "ПоказыватьВСпискеВыбора",
											  "Определяет, показывать ли данного пользователя в списке для выбора при старте системы",
											  ТипБулево,
											  Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "ЗапрещеноИзменятьПароль",
											  "Определяет возможность пользователя изменять свой пароль",
											  ТипБулево,
											  Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "Роли",
											  "Коллекция ролей пользователя информационной базы",
											  РатOpenAPI.НоваяСхемаКоллекции(ОписаниеРоли),
											  Ложь);
	
	Описание = РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "РежимЗапуска", "Режим запуска клиентского приложения", ТипСтрока, Ложь);
	Описание.ВозможныеЗначения = РатПовтИсп.КлючиСистемногоПеречисления("РежимЗапускаКлиентскогоПриложения");
	
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "ОсновнойИнтерфейс", "Основной интерфейс пользователя", ТипСтрока, Ложь);
	Описание.ВозможныеЗначения = ИменаКоллекцииМетаданных(Метаданные.Интерфейсы);
	
	Описание = РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "Язык", "Язык пользователя информационной базы", ТипСтрока, Ложь);
	Описание.ВозможныеЗначения = ИменаКоллекцииМетаданных(Метаданные.Языки);
	
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "РазделениеДанных",
											  "Содержит установленные пользователю значения разделителей",
											  Тип("Структура"),
											  Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "ПарольУстановлен",
											  "Показывает, установлен ли пароль у пользователя",
											  ТипБулево,
											  Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
											  "СохраняемоеЗначениеПароля",
											  "Содержит хранимые значения пароля (хеш-функции)
											  |и хранимые значения пароля в верхнем регистре (хеш-функции) разделенные запятой",
											  ТипСтрока,
											  Ложь,
											  ,
											  Истина);
	
	РатСпецификация.ЗарегистрироватьОписаниеОбъекта(СпецификацияСервиса, ОписаниеРеквизитов, "userib", "Описание пользователя информационной базы");
	
КонецПроцедуры

Функция ИменаКоллекцииМетаданных(Коллекция)
	
	Возврат РатКоллекции.ВыгрузитьЗначения(Коллекция, "Имя");
	
КонецФункции

Функция ЭлементМетаданных(Коллекция, Имя, СтатусОбработкиЗапроса)
	
	Если Не ЗначениеЗаполнено(Имя) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Коллекция.Найти(Имя);
	
	Если Результат = Неопределено Тогда
		
		Для Каждого Элемент Из Коллекция Цикл
			Если Элемент.Представление() = Имя Тогда
				Результат = Элемент;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Не удалось найти элемент метаданных '%1'", Имя);
		КлассОшибки = РатОбщий.КлассыОшибок().АнализПараметровЗапроса;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеПользователя(Пользователь) Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("УникальныйИдентификатор", Пользователь.УникальныйИдентификатор);
	Описание.Вставить("Имя", Пользователь.Имя);
	Описание.Вставить("ПолноеИмя", Пользователь.ПолноеИмя);
	Описание.Вставить("АутентификацияСтандартная", Пользователь.АутентификацияСтандартная);
	Описание.Вставить("АутентификацияOpenID", Пользователь.АутентификацияOpenID);
	Описание.Вставить("ПоказыватьВСпискеВыбора", Пользователь.ПоказыватьВСпискеВыбора);
	Описание.Вставить("АутентификацияОС", Пользователь.АутентификацияОС);
	Описание.Вставить("ПользовательОС", Пользователь.ПользовательОС);
	Описание.Вставить("ОсновнойИнтерфейс", ?(Пользователь.ОсновнойИнтерфейс <> Неопределено, Пользователь.ОсновнойИнтерфейс.Имя, Неопределено));
	Описание.Вставить("Язык", ?(Пользователь.Язык <> Неопределено, Пользователь.Язык.Имя, Неопределено));
	Описание.Вставить("ПарольУстановлен", Пользователь.ПарольУстановлен);
	Описание.Вставить("ЗапрещеноИзменятьПароль", Пользователь.ЗапрещеноИзменятьПароль);
	
	КлючРежимаЗапуска = РатПреобразования.КлючСистемногоПеречисления(Пользователь.РежимЗапуска);
	Описание.Вставить("РежимЗапуска", КлючРежимаЗапуска);
	
	ПредупреждатьОбОпасныхДействиях = Пользователь.ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях;
	Описание.Вставить("ЗащитаОтОпасныхДействий", ПредупреждатьОбОпасныхДействиях);
	Описание.Вставить("РазделениеДанных", Пользователь.РазделениеДанных);
	
	Описание.Вставить("Роли", Новый Массив);
	
	Для Каждого Роль Из Пользователь.Роли Цикл
		Описание.Роли.Добавить(Роль.Имя);
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции

Функция ПроверкаДанныхПользователя(ДанныеПользователя, СтатусОбработкиЗапроса) Экспорт
	
	Классы = РатОбщий.КлассыОшибок();
	
	Если Не ЗначениеЗаполнено(ДанныеПользователя) Тогда
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, Классы.АнализПараметровЗапроса, "Не указаны параметры нового пользователя");
		Возврат Ложь;
	КонецЕсли;
	
	ИмяПользователя = РатКоллекции.ЗначениеСтруктуры(ДанныеПользователя, "Имя");
	Если Не ЗначениеЗаполнено(ИмяПользователя) Тогда
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, Классы.АнализПараметровЗапроса, "Не указано имя пользователя");
	КонецЕсли;
	
	Роли = РатКоллекции.ЗначениеСтруктуры(ДанныеПользователя, "Роли");
	
	Если Не ЗначениеЗаполнено(Роли) Тогда
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, Классы.АнализПараметровЗапроса, "Не указаны роли пользователя");
	КонецЕсли;
	
	Возврат СтатусОбработкиЗапроса.Успешно;
	
КонецФункции

Функция ПреобразоватьДанныеПользователя(ДанныеПользователя, СтатусОбработкиЗапроса) Экспорт
	
	Результат = РатКоллекции.СкопироватьСтруктуру(ДанныеПользователя);
	
	ТипБулево = Тип("Булево");
	РеквизитыБулево = РатКоллекции.ЗначениеВМассиве("ПоказыватьВСпискеВыбора",
													 "ЗапрещеноИзменятьПароль",
													 "АутентификацияСтандартная",
													 "АутентификацияОС",
													 "ЗащитаОтОпасныхДействий");
	
	Для Каждого Реквизит Из РеквизитыБулево Цикл
		
		Если Результат.Свойство(Реквизит) Тогда
			Результат[Реквизит] = РатПреобразования.ДесериализоватьЗначение(Результат[Реквизит],
																			ТипБулево,
																			СтатусОбработкиЗапроса,
																			"Преобразование реквизита " + Реквизит);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Результат.Свойство("Роли") Тогда
		Результат.Роли = ПереданныеРоли(Результат.Роли, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПереданныеРоли(ИменаРолей, СтатусОбработкиЗапроса)
	
	Классы = РатОбщий.КлассыОшибок();
	ПереданныеРоли = Новый Массив();
	
	Для Каждого ИмяРоли Из ИменаРолей Цикл
		
		Если Не ЗначениеЗаполнено(ИмяРоли) Тогда
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, Классы.АнализПараметровЗапроса, "Не указано имя роли");
		Иначе
			Мета = ЭлементМетаданных(Метаданные.Роли, ИмяРоли, СтатусОбработкиЗапроса);
			ПереданныеРоли.Добавить(Мета);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПереданныеРоли;
	
КонецФункции

Процедура УстановитьДанныеПользователяИБ(ПользовательИБ, ДанныеПользователя, СтатусОбработкиЗапроса)
	
	ЗаполнитьЗначенияСвойств(ПользовательИБ, ДанныеПользователя);
	
	Если ДанныеПользователя.Свойство("Роли") Тогда
		
		ПользовательИБ.Роли.Очистить();
		
		Для Каждого Роль Из ДанныеПользователя.Роли Цикл
			ПользовательИБ.Роли.Добавить(Роль);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ДанныеПользователя.Свойство("РежимЗапуска") И ЗначениеЗаполнено(ДанныеПользователя.РежимЗапуска) Тогда
		ПользовательИБ.РежимЗапуска = РежимЗапускаКлиентскогоПриложения[ДанныеПользователя.РежимЗапуска];
	КонецЕсли;
	
	Если ДанныеПользователя.Свойство("Язык") И ЗначениеЗаполнено(ДанныеПользователя.Язык) Тогда
		ПользовательИБ.Язык = ЭлементМетаданных(Метаданные.Языки, ДанныеПользователя.Язык, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если ДанныеПользователя.Свойство("ОсновнойИнтерфейс") И ЗначениеЗаполнено(ДанныеПользователя.ОсновнойИнтерфейс) Тогда
		ПользовательИБ.ОсновнойИнтерфейс = ЭлементМетаданных(Метаданные.Интерфейсы, ДанныеПользователя.ОсновнойИнтерфейс, СтатусОбработкиЗапроса);
	КонецЕсли;
	
КонецПроцедуры

Функция ПользовательИБ(ИдентификаторПользователя) Экспорт
	
	УстановитьПривилегированныйРежим(Истина); // BSLLS:SetPrivilegedMode-off
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторПользователя) Тогда
		ВызватьИсключение "Не указан идентификатор пользователя";
	ИначеЕсли ТипЗнч(ИдентификаторПользователя) = Тип("УникальныйИдентификатор") Тогда
		Возврат ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользователя);
	Иначе
		Возврат ПользователиИнформационнойБазы.НайтиПоИмени(ИдентификаторПользователя);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

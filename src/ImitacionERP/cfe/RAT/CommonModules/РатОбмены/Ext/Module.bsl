//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание операций и схем для сервиса обмена данными.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса);
	
	ЗарегистрироватьОписаниеСхем(СпецификацияСервиса);
	
КонецПроцедуры

// Регистрирует шаблоны адресов для сервиса обмена данными.
//
// Параметры:
//   ШаблоныСервиса - см. РатШаблоныАдресов.Шаблоны
//
Процедура ЗарегистрироватьШаблоны(ШаблоныСервиса) Экспорт
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Обработчики = Обработчики();
	
	Ограничения = Новый Структура("ТипМетаданных", РатКоллекции.ЗначениеВМассиве("ПланОбмена", "ПланыОбмена"));
	Адрес = "/{ТипМетаданных}/{ВидМетаданных}/{ИдентификаторУзла}/Изменения";
	
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса, Методы.GET, Адрес, Обработчики.СписокИзменений, 0, Ограничения);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.PUT,
									 Адрес,
									 Обработчики.ЗарегистрироватьИзменения,
									 0,
									 Ограничения);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.DELETE,
									 Адрес,
									 Обработчики.УдалитьРегистрациюИзменений,
									 0,
									 Ограничения);
	
	Адрес = "/{ТипМетаданных}/{ВидМетаданных}/{ИдентификаторУзла}/Изменения/{ИдентификаторОбъекта}";
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.GET,
									 Адрес,
									 Обработчики.ИнформацияОбИзмененияхОбъекта,
									 0,
									 Ограничения);
	
КонецПроцедуры

// Вызвать обработчик.
// 
// Параметры:
//  Обработчик - Строка - Идентификатор обработчика, см. Обработчики
//  ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//  ОбработчикВыполнен - Булево - Необходимо установить если это "наш" запрос
// 
// Возвращаемое значение:
//  Произвольный - Сериализуемый результат обработки запроса
Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
	
	Обработчики = Обработчики();
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.СписокИзменений Тогда
		Результат = СписокИзменений(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
	ИначеЕсли Обработчик = Обработчики.ЗарегистрироватьИзменения Тогда
		Результат = ЗарегистрироватьИзменения(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
	ИначеЕсли Обработчик = Обработчики.УдалитьРегистрациюИзменений Тогда
		Результат = УдалитьРегистрациюИзменений(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
	ИначеЕсли Обработчик = Обработчики.ИнформацияОбИзмененияхОбъекта Тогда
		Результат = ИнформацияОбИзмененияхОбъекта(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Обработчики

Функция Обработчики()
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("ЗарегистрироватьИзменения", "ЗарегистрироватьОбъектНаОбмен");
	Обработчики.Вставить("УдалитьРегистрациюИзменений", "СнятьОбъектСРегистрацииНаОбмен");
	Обработчики.Вставить("СписокИзменений", "СписокИзмененийОбмена");
	Обработчики.Вставить("ИнформацияОбИзмененияхОбъекта", "ИнформацияОбИзмененияхОбъекта");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

Функция ЗарегистрироватьИзменения(ПараметрыЗапроса, СтатусОбработкиЗапроса) Экспорт
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса,
										"ТипМетаданных, ВидМетаданных, ИдентификаторУзла",
										СтатусОбработкиЗапроса);
	
	ИдентификаторУзла = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса,
															  СтатусОбработкиЗапроса,
															  "ИдентификаторУзла");
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Объекты = Объекты(ПараметрыЗапроса.Тело, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Объект Из Объекты Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(ИдентификаторУзла, Объект);
	КонецЦикла;
	
	Возврат РатОбщий.РезультатДанныеЗаписаны("Объекты успешно зарегистрированы на узле обмена");
	
КонецФункции

Функция УдалитьРегистрациюИзменений(ПараметрыЗапроса, СтатусОбработкиЗапроса) Экспорт
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса,
										"ТипМетаданных, ВидМетаданных, ИдентификаторУзла",
										СтатусОбработкиЗапроса);
	
	ИдентификаторУзла = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса,
															  СтатусОбработкиЗапроса,
															  "ИдентификаторУзла");
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Объекты = Объекты(ПараметрыЗапроса.Тело, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Объект Из Объекты Цикл
		ПланыОбмена.УдалитьРегистрациюИзменений(ИдентификаторУзла, Объект);
	КонецЦикла;
	
	Возврат РатОбщий.РезультатДанныеЗаписаны("Объекты успешно сняты с регистрации на узле обмена");
	
КонецФункции

Функция СписокИзменений(ПараметрыЗапроса, СтатусОбработкиЗапроса) Экспорт
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса,
										"ТипМетаданных, ВидМетаданных, ИдентификаторУзла",
										СтатусОбработкиЗапроса);
	
	ИдентификаторУзла = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса,
															  СтатусОбработкиЗапроса,
															  "ИдентификаторУзла");
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат СводкаИзмененийУзла(ИдентификаторУзла);
	
КонецФункции

Функция ИнформацияОбИзмененияхОбъекта(ПараметрыЗапроса, СтатусОбработкиЗапроса) Экспорт
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса,
										"ТипМетаданных, ВидМетаданных, ИдентификаторУзла, ИдентификаторОбъекта",
										СтатусОбработкиЗапроса);
	
	ИдентификаторУзла = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса,
															  СтатусОбработкиЗапроса,
															  "ИдентификаторУзла");
	ИдентификаторОбъекта = РатПреобразования.ДесериализоватьТипизированноеЗначение(ПараметрыЗапроса.ИдентификаторОбъекта,
																				   СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяТаблицы = ИдентификаторОбъекта.Метаданные().ПолноеИмя();
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрШаблон(
		"ВЫБРАТЬ
		|	Изменения.НомерСообщения КАК НомерСообщения
		|ИЗ
		|	%1.Изменения КАК Изменения
		|ГДЕ
		|	Узел = &Узел
		|	И Изменения.Ссылка = &Ссылка",
		ИмяТаблицы);
		
	Запрос.УстановитьПараметр("Узел", ИдентификаторУзла);
	Запрос.УстановитьПараметр("Ссылка", ИдентификаторОбъекта);
	
	НомерСообщения = РатОбщий.ЗначениеИзЗапроса(Запрос);
	
	Результат = Новый Структура("Статус, НомерСообщения", "Неизвестный", НомерСообщения);
	
	Если НомерСообщения = Неопределено Тогда
		Результат.Статус = "Незарегистрирован";
	ИначеЕсли НомерСообщения = Null Тогда
		Результат.Статус = "Зарегистрирован";
	ИначеЕсли НомерСообщения > 0 Тогда
		Результат.Статус = "Отправлен";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция Объекты(Знач Тело, СтатусОбработкиЗапроса)
	
	Если ТипЗнч(Тело) <> Тип("Массив") Тогда
		Тело = РатКоллекции.ЗначениеВМассиве(Тело);
	КонецЕсли;
	
	Объекты = Новый Массив();
	
	Для Каждого ИдентификаторОбъекта Из Тело Цикл
		
		Если СтрСравнить(ИдентификаторОбъекта, "ALL") = 0 ИЛИ СтрСравнить(ИдентификаторОбъекта, "ВСЕ") = 0 Тогда
			Объекты.Добавить(Неопределено);
			Прервать;
		КонецЕсли;
		
		Объект = РатМетаданные.ОбъектМетаданных(ИдентификаторОбъекта, Ложь);
		
		Если Объект = Неопределено Тогда
			Объект = РатПреобразования.ДесериализоватьТипизированноеЗначение(ИдентификаторОбъекта,
																			 СтатусОбработкиЗапроса);
		КонецЕсли;
		
		Объекты.Добавить(Объект);
		
	КонецЦикла;
	
	Возврат Объекты;
	
КонецФункции

#КонецОбласти

Процедура ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса)
	
	БазовыеСхемы = РатСпецификация.БазовыеСхемы();
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	ОписаниеТипа = РатМетаданные.ТипМетаданных("ПланОбмена");
	ТипСрока = РатOpenAPI.ТипыДанных().Строка;
	СхемаИдентификаторов = РатOpenAPI.СсылкаНаСхему(РатСпецификация.БазовыеСхемы().ВходнаяКоллекцияСсылок);
	
	ИмяПараметраОбъекта = "objectID";
	
	СхемаСтрока = РатOpenAPI.НоваяСхемаДанных(ТипСрока);
	РатСпецификация.ЗарегистрироватьПараметр(СпецификацияСервиса, ИмяПараметраОбъекта, ИмяПараметраОбъекта, СхемаСтрока,
		"Идентификатор объекта в формате `{ВидОбъекта}.{ТипОбъекта}.{Идентификатор}`.
		|
		|В качестве идентификатор можно использовать (при наличии):
		|* `УникальныйИдентификатор`
		|* `ИмяПредопределенного`
		|* `Код`
		|* `Номер`
		|* `Наименование`
		|
		|Например:
		|* `Документы.ПКО.1234567890`
		|* `Справочники.Пользователи.ИмяПользователя`
		|* `Справочники.Товары.a772d037-9519-4c2b-8c9b-ad9de187fe90`");
		
	Для Каждого План Из Метаданные.ПланыОбмена Цикл
		
		Группа = РатСпецификация.ГруппуОбъектаМетаданных(ОписаниеТипа, План);
		РатСпецификация.ДобавитьГруппу(СпецификацияСервиса, Группа);
		
		ПараметрИдентификатора = РатСпецификация.ПараметрИдентификаторОбъекта(План);
		БазовыйАдрес = РатСпецификация.АдресОбъектаМетаданных(ОписаниеТипа, План);
		Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса,
										БазовыйАдрес,
										СтрШаблон("{%1}/Изменения", ПараметрИдентификатора.ИмяПараметра));
		РатСпецификация.ДобавитьИзвестныйПараметрОперации(Ресурс, ПараметрИдентификатора.Ключ);
		
		// Сводная информация
		Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.GET, "Изменения: Сводная информация", , Группа);
		РатСпецификация.ДобавитьСтандартныеОтветы(Операция, "exchangeSummary");
		
		// Зарегистрировать объекты
		Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс,
														Методы.PUT,
														"Изменения: Зарегистрировать объекты",
														,
														Группа);
		РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаИдентификаторов);
		РатСпецификация.ДобавитьСтандартныеОтветы(Операция, БазовыеСхемы.ОтветУспешно);
		
		// Удалить регистрацию
		Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс,
														Методы.DELETE,
														"Изменения: Удалить регистрацию",
														,
														Группа);
		РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаИдентификаторов);
		РатСпецификация.ДобавитьСтандартныеОтветы(Операция, БазовыеСхемы.ОтветУспешно);
		
		// Статус объекта
		Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса,
										БазовыйАдрес,
										СтрШаблон("{%1}/Изменения/{%2}", ПараметрИдентификатора.ИмяПараметра, ИмяПараметраОбъекта));
		Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.GET, "Изменения: Статус объекта", , Группа);
		РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ПараметрИдентификатора.Ключ);
		РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраОбъекта);
		РатСпецификация.ДобавитьСтандартныеОтветы(Операция, "exchangeItemStatus");
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьОписаниеСхем(СпецификацияСервиса)
	
	ТипСтрока = Тип("Строка");
	ТипЧисло = Тип("Число");
	
	// Сводка
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "МетаПолноеИмя",
									 "Полное имя объекта, регистрируемого на узле",
									 ТипСтрока,
									 Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "КоличествоИзменений",
									 "Количество зарегистрированных изменений на узле",
									 ТипЧисло,
									 Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "КоличествоВыгруженных",
									 "Количество зарегистрированных и выгруженных изменений на узле",
									 ТипЧисло,
									 Истина);
	
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "КоличествоВыгруженных",
									 "Количество зарегистрированных, но не выгруженных изменений на узле",
									 ТипЧисло,
									 Истина);
	РатСпецификация.ЗарегистрироватьОписаниеОбъекта(СпецификацияСервиса,
													ОписаниеРеквизитов,
													"exchangeSummaryItem",
													"Сводка по зарегистрированным изменениям объекта метаданных");
	Схема = РатOpenAPI.НоваяСхемаКоллекции(РатOpenAPI.СсылкаНаСхему("exchangeSummaryItem"), "Сводка по зарегистрированным изменениям");
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, "exchangeSummary", Схема);
	
	// Статус регистрации объекта
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	ОписаниеРеквизита = РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "Статус", "Статус регистрации объекта", ТипСтрока, Истина);
	ОписаниеРеквизита.ВозможныеЗначения = РатКоллекции.ЗначениеВМассиве("Неизвестный, Незарегистрирован, Зарегистрирован, Отправлен");
	ОписаниеРеквизита = РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
														 "НомерСообщения",
														 "Номер сообщения, в котором отправлен объект",
														 ТипЧисло,
														 Истина);
	РатСпецификация.ЗарегистрироватьОписаниеОбъекта(СпецификацияСервиса,
													ОписаниеРеквизитов,
													"exchangeItemStatus",
													"Статус регистрации объекта");
	
КонецПроцедуры

Функция СводкаИзмененийУзла(Узел)
	
	МетаданныеПлана = Узел.Метаданные();
	
	НаборЗапросов = Новый Массив();
	ШаблонЗапроса =
		"ВЫБРАТЬ
		|	""%1"" КАК МетаПолноеИмя,
		|	КОЛИЧЕСТВО(*) КАК КоличествоИзменений,
		|	КОЛИЧЕСТВО(НомерСообщения) КАК КоличествоВыгруженных,
		|	КОЛИЧЕСТВО(*) - КОЛИЧЕСТВО(НомерСообщения) КАК КоличествоНеВыгруженных
		|ИЗ
		|	%1.Изменения
		|ГДЕ
		|	Узел = &Узел
		|СГРУППИРОВАТЬ ПО
		|	Узел";
	
	Для Каждого ЭлементСостава Из МетаданныеПлана.Состав Цикл
		
		Запрос = СтрШаблон(ШаблонЗапроса, ЭлементСостава.Метаданные.ПолноеИмя());
		НаборЗапросов.Добавить(Запрос);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(НаборЗапросов, РатОбщийКлиентСервер.РазделительОператоровЗапроса(Истина));
	Запрос.УстановитьПараметр("Узел", Узел);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

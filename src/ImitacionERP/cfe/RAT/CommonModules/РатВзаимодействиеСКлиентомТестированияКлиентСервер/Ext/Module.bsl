//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

#Область РаботаСОтветом

// Извлекает результат выполнения из ответа.
//
// Параметры:
//   Ответ - Структура - ответ от сервиса тестирования.
//
// Возвращаемое значение:
//   Произвольный - результат выполнения запроса.
//
Функция РезультатВыполненияИзОтвета(Ответ) Экспорт
	
	Возврат Ответ.Тело;
		
КонецФункции

// Формирует строку сообщений из ответа.
//
// Параметры:
//   Ответ - Структура - ответ от сервиса тестирования.
//
// Возвращаемое значение:
//   Строка - сообщения из ответа, соединенные символом переноса строки.
//
Функция СообщенияИзОтвета(Ответ) Экспорт
	
	Результат = "";
	
	Если ЗначениеЗаполнено(Ответ.Сообщения) Тогда
		
		Результат = СтрСоединить(Ответ.Сообщения, Символы.ПС);
				
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

#Область ПроверкаСтатусовОтветов

// Проверяет, является ли ответ статусом "В процессе".
//
// Параметры:
//   Ответ - Структура - ответ от сервиса тестирования.
//
// Возвращаемое значение:
//   Булево - Истина, если ответ имеет статус "В процессе".
//
Функция ЭтоОтвет_ВПроцессе(Ответ) Экспорт
	
	Возврат Ответ.КодСтатуса = КодыСтатусовВыполнения().ВПроцессе;
	
КонецФункции

// Проверяет, является ли ответ успешным.
//
// Параметры:
//   Ответ - Структура - ответ от сервиса тестирования.
//
// Возвращаемое значение:
//   Булево - Истина, если ответ имеет статус "Выполнен".
//
Функция ЭтоОтвет_Успешно(Ответ) Экспорт
	
	Возврат Ответ.КодСтатуса = КодыСтатусовВыполнения().Выполнен;
	
КонецФункции

#КонецОбласти

#Область КонструкторыОтветов

// Создает новый ответ с ошибкой.
//
// Параметры:
//   Сообщение - Строка, ИнформацияОбОшибке - сообщение об ошибке.
//
// Возвращаемое значение:
//   см. НовыйОтвет
//
Функция НовыйОтветОшибка(Сообщение) Экспорт
	
	Возврат НовыйОтвет(КодыСтатусовВыполнения().Ошибка, Сообщение);
	
КонецФункции

// Создает новый ответ с ошибкой сервиса.
//
// Параметры:
//   Сообщение - Строка, ИнформацияОбОшибке - сообщение об ошибке сервиса.
//
// Возвращаемое значение:
//   см. НовыйОтвет
//
Функция НовыйОтветОшибкаСервиса(Сообщение) Экспорт
	
	Возврат НовыйОтвет(КодыСтатусовВыполнения().ОшибкаСервиса, Сообщение);

КонецФункции

// Создает новый ответ с таймаутом ожидания.
//
// Параметры:
//   Сообщение - Строка, ИнформацияОбОшибке - сообщение о таймауте.
//
// Возвращаемое значение:
//   см. НовыйОтвет
//
Функция НовыйОтветТаймаутОжидания(Сообщение = Неопределено) Экспорт
	
	Возврат НовыйОтвет(КодыСтатусовВыполнения().Таймаут, Сообщение);
	
КонецФункции

// Создает новый ответ со статусом "В процессе".
//
// Возвращаемое значение:
//   см. НовыйОтвет
//
Функция НовыйОтветВПроцессе() Экспорт
	
	Возврат НовыйОтвет(КодыСтатусовВыполнения().ВПроцессе);
	
КонецФункции

// Создает новый успешный ответ.
//
// Параметры:
//   РезультатВыполнения - Произвольный - результат выполнения запроса.
//
// Возвращаемое значение:
//   см. НовыйОтвет
//
Функция НовыйОтветУспешно(РезультатВыполнения = Неопределено) Экспорт
	
	Результат = НовыйОтвет(КодыСтатусовВыполнения().Выполнен);
	Результат.Тело = РезультатВыполнения;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Создает новую структуру ответа.
//
// Параметры:
//   КодСтатуса - Число - код статуса ответа.
//   Сообщение - Строка, ИнформацияОбОшибке - сообщение для ответа.
//
// Возвращаемое значение:
//   Структура - новая структура ответа со следующими полями:
//     * КодСтатуса - Число - код статуса ответа
//     * Тело - Произвольный - результат выполнения
//     * Сообщения - Массив из Строка - массив сообщений
//     * ТехническаяИнформация - Массив из Строка - массив технической информации
//
Функция НовыйОтвет(Знач КодСтатуса = Неопределено, Сообщение = Неопределено) Экспорт
	
	Результат = Новый Структура;
	
	КодСтатуса = ?(КодСтатуса = Неопределено, КодыСтатусовВыполнения().Инициализация, КодСтатуса);
	
	Результат.Вставить("КодСтатуса", КодСтатуса);
	Результат.Вставить("Тело", Неопределено);
	Результат.Вставить("Сообщения", Новый Массив);
	Результат.Вставить("ТехническаяИнформация", Новый Массив);
	
	ДополнитьСообщенияОтвета(Результат, Сообщение);
	
	//@skip-check constructor-function-return-section
	Возврат Результат;
	
КонецФункции

// Возвращает структуру с кодами статусов выполнения.
//
// Возвращаемое значение:
//   Структура - структура с кодами статусов:
//     * Инициализация - Число - 100, код для инициализации ответа
//     * ВПроцессе - Число - 202, запрос получен, но еще не выполнен
//     * Ошибка - Число - 400, код для неспецифичных ошибок
//     * ОшибкаСервиса - Число - 500, ошибки транспортировки запрос-ответа
//     * Таймаут - Число - 408, код для таймаутов
//     * Выполнен - Число - 200, запрос выполнен успешно
//
Функция КодыСтатусовВыполнения()
	
	// BSLLS:MagicNumber-off
	Результат = Новый Структура;
	Результат.Вставить("Инициализация", 100); // код для инициализации ответа
	Результат.Вставить("ВПроцессе", 202); // запрос получен, но еще не выполнен (ответ не готов)
	Результат.Вставить("Ошибка", 400); // под любые не специфичные ошибки выделим один код
	Результат.Вставить("ОшибкаСервиса", 500); // любые ошибки связанные с "транспортировкой" запрос-ответа между менеджером и клиентом 
	Результат.Вставить("Таймаут", 408); // общий код под любые таймауты
	Результат.Вставить("Выполнен", 200); // общий код, указывающий на то, что запрос выполнен (ответ сформирован)
	// BSLLS:MagicNumber-on
	
	Возврат Результат;
		
КонецФункции

// Добавляет сообщение в ответ.
//
// Параметры:
//   Ответ - Структура - ответ для добавления сообщения.
//   Сообщение - Строка, ИнформацияОбОшибке - сообщение для добавления.
//
Процедура ДополнитьСообщенияОтвета(Ответ, Сообщение)
	
	ТипСообщения = ТипЗнч(Сообщение);
	
	Если ТипСообщения = Тип("Строка") Тогда
		
		Ответ.Сообщения.Добавить(Сообщение);
		
	ИначеЕсли ТипСообщения = Тип("ИнформацияОбОшибке") Тогда
		
		//@skip-check bsl-legacy-check-static-feature-access
		Ответ.Сообщения.Добавить(РатСтроки.ПредставлениеОшибки(Сообщение, Ложь));
		//@skip-check bsl-legacy-check-static-feature-access
		Ответ.ТехническаяИнформация.Добавить(РатСтроки.ПредставлениеОшибки(Сообщение));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

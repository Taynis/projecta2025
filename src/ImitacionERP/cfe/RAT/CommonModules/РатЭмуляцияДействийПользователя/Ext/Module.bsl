//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Эмулировать нажатие сочетания клавиш на клиенте тестирования
// Поддерживаемые сочетания см. ИменованныеСочетанияКлавиш
// 
// Параметры:
//  Ванесса - Произвольный - контекст фреймворка Vanessa-Automation
//  Сочетание - см. НовоеСочетаниеКлавиш
Процедура ЭмулироватьНажатиеСочетанияКлавиш(Ванесса, Сочетание) Экспорт
	
	Команда  = РатВзаимодействиеСКлиентомТестирования.НоваяКоманда();
	
	// см. РатЭмуляцияДействийПользователяВызовКлиента.ЭмулироватьНажатиеСочетанияКлавиш
	Команда.Метод = "РатЭмуляцияДействийПользователяВызовКлиента.ЭмулироватьНажатиеСочетанияКлавиш";
	Команда.Параметры.Добавить(Сочетание);
	
	РатВзаимодействиеСКлиентомТестирования.ВыполнитьКоманду(Ванесса, Команда);
	
КонецПроцедуры

// Возвращает именованные сочетания клавиш для эмуляции действий пользователя
// 
// Возвращаемое значение:
//  Структура - Именованные сочетания клавиш:
// * ВызовКонтекстногоМеню - Структура - сочетание клавиш, эмулирующее вызов контекстного меню (SHIFT+F10)
// * Ввод - Структура - сочетание клавиш, эмулирующее ввод (нажатие Enter)
Функция ИменованныеСочетанияКлавиш() Экспорт
	
	Результат = Новый Структура;
	
	ДобавитьИменованноеСочетаниеКлавиш(Результат, "ВызовКонтекстногоМеню", "+{F10}");
	ДобавитьИменованноеСочетаниеКлавиш(Результат, "Ввод", "ENTER");
	
	//@skip-check constructor-function-return-section
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Создает новое описание сочетания клавиш
// 
// Параметры:
//  Имя - Строка - имя сочетания клавиш
// 
// Возвращаемое значение:
//  Структура - Новое описание сочетания клавиш:
// * Имя - Строка - имя сочетания клавиш
// * WScriptShell - Строка - строка для отправки в WScript.Shell
Функция НовоеСочетаниеКлавиш(Имя)
	
	Результат = Новый Структура;
	Результат.Вставить("Имя", Имя);
	Результат.Вставить("WScriptShell", "");
	
	Возврат Результат;

КонецФункции

// Добавляет именованное сочетание клавиш в коллекцию
// 
// Параметры:
//  Приемник - Структура - коллекция для добавления сочетания клавиш
//  Имя - Строка - имя сочетания клавиш
//  КодWScriptShell - Строка - код сочетания клавиш для WScript.Shell
Процедура ДобавитьИменованноеСочетаниеКлавиш(Приемник, Имя, КодWScriptShell)
	
	Результат = НовоеСочетаниеКлавиш(Имя);
	Результат.WScriptShell = КодWScriptShell;
	
	Приемник.Вставить(Имя, Результат);
		
КонецПроцедуры

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание операций в спецификации сервиса.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	// TODO Пример формирования описания. Исправить на описание своего модуля
	//		Ниже приведен пример формирования описания API, полезно посмотреть на формирование описаний в других модулях
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Путь = "/АктивныеПользователи";
	Группа = "Активные пользователи";
	РатСпецификация.ДобавитьГруппу(СпецификацияСервиса, Группа);
	
	ТипСтрока = Тип("Строка");
	ТипЧисло = Тип("Число");
	ТипДата = Тип("Дата");
	
	// Описание типа данных "Активный пользователь"
	ИмяТипа = "activeUser";
	ОписаниеПользователя = РатСпецификация.ОписаниеРеквизитов();
	РатСпецификация.ДобавитьРеквизит(ОписаниеПользователя, "Имя", "Имя пользователя", ТипСтрока, Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеПользователя, "НомерСеанса", "Номер сеанса", ТипЧисло, Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеПользователя, "ТипКлиента", "Клиент", ТипСтрока, Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеПользователя, "Начало", "Время подключения", ТипДата, Истина);
	
	РатСпецификация.ЗарегистрироватьОписаниеОбъекта(СпецификацияСервиса, ОписаниеПользователя, ИмяТипа,
		"Описание активного пользователя");
	СсылкаНаОписаниеПользователя = РатOpenAPI.СсылкаНаСхему(ИмяТипа);
	
	// Описание операции получения списка
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Список активных пользователей",
													"Возвращает список активных пользователей системы",
													Группа,
													Путь,
													Методы.GET);
	
	СхемаОтвета = РатOpenAPI.НоваяСхемаКоллекции(СсылкаНаОписаниеПользователя, "Список активных пользователей");
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
	
	// Описание операции отключения пользователя по номеру сеанса
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Отключение пользователя",
													"Отключает пользователя по номеру сеанса",
													Группа,
													Путь + "/{НомерСеанса}",
													Методы.DELETE);
	
	СхемаОтвета = РатOpenAPI.НоваяСхемаКоллекции(СсылкаНаОписаниеПользователя, "Список активных пользователей");
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
	
	РатСпецификация.ДобавитьПараметрОперации(Операция, "НомерСеанса",
		РатOpenAPI.НоваяСхемаДанных(РатOpenAPI.ТипыДанных().ЦелоеЧисло), "Номер сеанса пользователя");
	
КонецПроцедуры

// Регистрирует шаблоны URL для обработки запросов.
//
// Параметры:
//   ШаблоныСервиса - см. РатШаблоныАдресов.Шаблоны
//
Процедура ЗарегистрироватьШаблоны(ШаблоныСервиса) Экспорт
	
	// TODO Пример формирования шаблонов запросов. Исправить на свои шаблоны
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Обработчики = Обработчики();
	
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса, Методы.GET, "/АктивныеПользователи", Обработчики.СписокАктивныхПользователей, 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса, Методы.DELETE, "/АктивныеПользователи/{НомерСеанса}", Обработчики.ЗавершениеСеанса, 0);
	
КонецПроцедуры

// Вызывает обработчик запроса в зависимости от его типа.
//
// Параметры:
//   Обработчик - Строка - Идентификатор обработчика, см. Обработчики
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//   ОбработчикВыполнен - Булево - Необходимо установить если это "наш" запрос
//
// Возвращаемое значение:
//   Произвольный - Сериализуемый результат обработки запроса
Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
	
	// TODO Пример алгоритма подбора обработчика. Исправить на свои обработчики
	Обработчики = Обработчики();
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.СписокАктивныхПользователей Тогда
		
		Результат = ОбработчикСписокАктивныхПользователей(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ЗавершениеСеанса Тогда
		
		Результат = ОбработчикЗавершениеСеанса(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Обработчики

Функция Обработчики()
	
	// TODO Пример списка обработчиков. Исправить на свои
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("СписокАктивныхПользователей", "СписокАктивныхПользователей");
	Обработчики.Вставить("ЗавершениеСеанса", "ЗавершениеСеанса");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

Функция ОбработчикСписокАктивныхПользователей(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	// TODO Пример обработчика
	Результат = Новый Массив(); // Необходимо возвращать объекты примитивных (сериализуемых типов)
	// Поэтому данные сеансов перекладываем в массив структур (DTO)
	
	Для Каждого Сеанс Из ПолучитьСеансыИнформационнойБазы() Цикл
		ОписаниеСеанса = Новый Структура();
		ОписаниеСеанса.Вставить("НомерСеанса", Сеанс.НомерСеанса);
		ОписаниеСеанса.Вставить("ИмяКомпьютера", Сеанс.ИмяКомпьютера);
		ОписаниеСеанса.Вставить("ИмяПриложения", Сеанс.ИмяПриложения);
		ОписаниеСеанса.Вставить("Пользователь", Сеанс.Пользователь.Имя);
		Результат.Добавить(ОписаниеСеанса);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикЗавершениеСеанса(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	// TODO Пример обработчика
	НомерСеанса = ПараметрыЗапроса.НомерСеанса; // Получение параметра запроса
	Возврат НомерСеанса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

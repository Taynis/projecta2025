//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание операций для работы с движениями документов
//
// Параметры:
//  СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса);
	
КонецПроцедуры

// Регистрирует шаблоны адресов для работы с движениями документов
//
// Параметры:
//  Шаблоны - см. РатШаблоныАдресов.Шаблоны
//
Процедура ЗарегистрироватьШаблоны(Шаблоны) Экспорт
	
	Обработчики = ОбработчикиЗапросов();
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	
	Приоритет = 0;
	
	// Движения
	ОграничениеДокументы = Новый Структура("ТипМетаданных", РатКоллекции.ЗначениеВМассиве("Документ", "Документы"));
	
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны,
									 Методы.GET,
									 "/{ТипМетаданных}/{ВидМетаданных}/{Идентификатор}/Движения",
									 Обработчики.ПолучитьВсеДвижения,
									 Приоритет,
									 ОграничениеДокументы);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны,
									 Методы.GET,
									 "/{ТипМетаданных}/{ВидМетаданных}/{Идентификатор}/Движения/{ИмяРегистра}",
									 Обработчики.ПолучитьДвиженияПоРегистру,
									 Приоритет,
									 ОграничениеДокументы);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны,
									 Методы.PUT,
									 "/{ТипМетаданных}/{ВидМетаданных}/{Идентификатор}/Движения/{ИмяРегистра}",
									 Обработчики.УстановитьДвижения,
									 Приоритет,
									 ОграничениеДокументы);
	
КонецПроцедуры

// Вызывает обработчик запроса для работы с движениями
//
// Параметры:
//  Обработчик - Строка - Имя обработчика для вызова
//  ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//  ОбработчикВыполнен - Булево - Признак выполнения обработчика
//
// Возвращаемое значение:
//  Произвольный - Результат выполнения обработчика
//
Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
	
	Обработчики = ОбработчикиЗапросов();
	
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.ПолучитьВсеДвижения Тогда
		
		Результат = ПолучитьВсеДвижения(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ПолучитьДвиженияПоРегистру Тогда
		
		Результат = ПолучитьДвиженияПоРегистру(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.УстановитьДвижения Тогда
		
		Результат = УстановитьДвижения(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса)
	
	ОписаниеТипы = Новый Структура("ИмяКоллекции", "Документы");
	
	Для Каждого Объект Из Метаданные.Документы Цикл
		
		Группа = РатСпецификация.ГруппуОбъектаМетаданных(ОписаниеТипы, Объект);
		БазовыйАдрес = РатСпецификация.АдресОбъектаМетаданных(ОписаниеТипы, Объект);
		ПараметрИдентификатора = РатСпецификация.ПараметрИдентификаторОбъекта(Объект);
		
		ДобавитьОперацииРаботыСДвижениями(СпецификацияСервиса, БазовыйАдрес, ПараметрИдентификатора, Объект, Группа);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОперацииРаботыСДвижениями(СпецификацияСервиса, БазовыйАдрес, ПараметрИдентификатора, Объект, Группа)
	
	Если НЕ Метаданные.Документы.Содержит(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	БазовыеСхемы = РатСпецификация.БазовыеСхемы();
	
	ПредставлениеОбъекта = Объект.Представление();
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	
	Если НЕ ЗначениеЗаполнено(Объект.Движения) Тогда
		Возврат;
	КонецЕсли;
	
	СхемаВсехДвижений = РатOpenAPI.НоваяСхемаОбъекта("Движения по документу");
	
	СписокДоступныхРегистров = Новый Массив;
	СхемыКоллекций = Новый Массив;
	
	Для Каждого Движение Из Объект.Движения Цикл
		Представление = СтрШаблон("`%1` (%2)", Движение.Имя, Движение.Представление());
		СхемаСтроки = РатСпецификация.СсылкуНаВыходнуюСхему(Движение);
		СхемаКоллекции = РатOpenAPI.НоваяСхемаКоллекции(СхемаСтроки, "Движения по регистру " + Представление);
		
		РатOpenAPI.ДобавитьРеквизит(СхемаВсехДвижений, Движение.Имя, СхемаКоллекции);
		СписокДоступныхРегистров.Добавить("* " + Представление);
		
		СхемыКоллекций.Добавить(СхемаКоллекции);
	КонецЦикла;
	
	СписокРегистровТекст = СтрСоединить(СписокДоступныхРегистров, Символы.ПС);
	ПредставлениеОперацииПоРегистру = СтрШаблон(" движений документа ""%1"" по выбранному регистру
												|
												|Документ делает движения по:
												|%2", ПредставлениеОбъекта, СписокРегистровТекст);
	
	// Все движения
	Описание = "Получение всех движений документа " + ПредставлениеОбъекта;
	Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес, СтрШаблон("{%1}/Движения", ПараметрИдентификатора.ИмяПараметра));
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Ресурс, ПараметрИдентификатора.Ключ);
	
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.GET, "Движения: получение всех движений документа", Описание, Группа);
	
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаВсехДвижений);
	
	// Движения по регистру
	Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес, СтрШаблон("{%1}/Движения/{name}", ПараметрИдентификатора.ИмяПараметра));
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Ресурс, ПараметрИдентификатора.Ключ);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Ресурс, "name");
	
	// Получение движений по регистру
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс,
													Методы.GET,
													"Движения: получение движений по регистру",
													"Получение" + ПредставлениеОперацииПоРегистру,
													Группа);
	СхемаОтвета = РатOpenAPI.НоваяСхемаСоставногоТипа(СхемыКоллекций, "Данные регистров. Зависит от параметра `name`");
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
	
	// Изменить движений по регистру
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс,
													Методы.PUT,
													"Движения: изменение движений по регистру",
													"Изменение" + ПредставлениеОперацииПоРегистру,
													Группа);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаЗапросаИзмененияДанных(СхемаОтвета));
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ОтветУспешно));
	
КонецПроцедуры

#Область ОбработчикиСобытий

Функция ОбработчикиЗапросов()
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("ПолучитьВсеДвижения",        "ПолучитьВсеДвижения");
	Обработчики.Вставить("ПолучитьДвиженияПоРегистру", "ПолучитьДвиженияПоРегистру");
	Обработчики.Вставить("УстановитьДвижения",         "УстановитьДвижения");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

// Получает все движения документа
//
// Параметры:
//  ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  см. РатИнформационнаяБаза.ДвиженияДокументаПоРегистрам
//
Функция ПолучитьВсеДвижения(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "ТипМетаданных, ВидМетаданных, Идентификатор", СтатусОбработкиЗапроса);
	
	ПроверитьВозможностьРаботыСДвижениями(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	ДокументСсылка = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	Результат = РатИнформационнаяБаза.ДвиженияДокументаПоРегистрам(ДокументСсылка, ПараметрыЗапроса.ИмяТаблицы, СтатусОбработкиЗапроса);
	
	Возврат Результат;
	
КонецФункции

// Получает движения документа по указанному регистру
//
// Параметры:
//  ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  см. РатИнформационнаяБаза.ДвиженияПоРегистру
//
Функция ПолучитьДвиженияПоРегистру(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "ТипМетаданных, ВидМетаданных, Идентификатор, ИмяРегистра", СтатусОбработкиЗапроса);
	
	ПроверитьВозможностьРаботыСДвижениями(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	ДокументСсылка = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		ПолноеИмяРегистра = РатМетаданные.ТаблицыДвижений(ПараметрыЗапроса.ИмяТаблицы)[ПараметрыЗапроса.ИмяРегистра];
		Результат = РатИнформационнаяБаза.ДвиженияПоРегистру(ДокументСсылка, ПолноеИмяРегистра, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Устанавливает движения документа по указанному регистру
//
// Параметры:
//  ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  см. РатОбщий.РезультатДанныеЗаписаны
//
Функция УстановитьДвижения(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса,
										"ТипМетаданных, ВидМетаданных, Идентификатор, ИмяРегистра, Тело",
										СтатусОбработкиЗапроса);
	
	ПроверитьВозможностьРаботыСДвижениями(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	ИдентификаторЗаписи = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		ПолноеИмяТаблицыДвижений = РатМетаданные.ПолноеИмяТаблицыДвижений(ПараметрыЗапроса.ИмяТаблицы, ПараметрыЗапроса.ИмяРегистра);
		Данные = РатПреобразования.ДанныеСохраняемогоОбъекта(ПараметрыЗапроса.Тело, ПолноеИмяТаблицыДвижений, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	РатИнформационнаяБаза.УстановитьДвижения(ИдентификаторЗаписи, Данные, ПолноеИмяТаблицыДвижений, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Результат = РатОбщий.РезультатДанныеЗаписаны();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Процедура ПроверитьВозможностьРаботыСДвижениями(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	КлассОшибки = РатОбщий.КлассыОшибок().НеНайденМаршрут;
	
	Если Не РатМетаданные.ЭтоТаблицаДокумента(ПараметрыЗапроса.ИмяТаблицы) Тогда
		
		ТекстОшибки = "Работа с движениями доступна только для документов";
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	ИначеЕсли ПараметрыЗапроса.Свойство("ИмяРегистра")
		И Не РатМетаданные.ТаблицыДвижений(ПараметрыЗапроса.ИмяТаблицы).Свойство(ПараметрыЗапроса.ИмяРегистра) Тогда
		
		ТекстОшибки = СтрШаблон("Этот документ (%1) не пишет в регистр(%2)", ПараметрыЗапроса.ИмяТаблицы, ПараметрыЗапроса.ИмяРегистра);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СхемаЗапросаИзмененияДанных(Знач СхемаОписанияОбъекта)
	
	БазоваяСхема = "commonWriteBody";
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта("Данные записи");
	РатOpenAPI.ДобавитьРеквизит(Схема, "data", СхемаОписанияОбъекта);
	
	Возврат РатOpenAPI.НоваяСхемаОбъединения(Неопределено, Схема, РатOpenAPI.СсылкаНаСхему(БазоваяСхема));
	
КонецФункции

#КонецОбласти

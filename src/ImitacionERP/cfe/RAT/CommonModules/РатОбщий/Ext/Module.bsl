//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Возвращает перечень используемых классов ошибок для группировки проблем.
// На основании класса определяется код возврата ответа.
//
// Параметры:
//  Кэшировать - Булево - Признак использования кэширования
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Описание используемых классов ошибок:
//    * Неизвестная - Структура - Неклассифицированная ошибка
//    * НеподдерживаемаяФункциональность - Структура - Заявленная, но не реализованная функция
//    * ЛогикаКонфигурации - Структура - Ошибка возникшая в тестируемой конфигурации
//    * РазборПараметровЗапроса - Структура - Ошибка разбора входных параметров
//    * АнализПараметровЗапроса - Структура - Логическая ошибка данных входных параметров
//    * Десериализация - Структура - Ошибка разбора входных данных (невалидный JSON)
//    * Сериализация - Структура - Ошибка формирования ответа
//    * НеНайденМаршрут - Структура - Не удалось найти обработчик для URL
//    * НеНайденаЗаписьИБ - Структура - Не найдена запись в базе по переданным параметрам
//    * МетодНеПоддерживается - Структура - Использован неподходящий HTTP метод для шаблона URL
//    * ЗаписьДанныхВБазу - Структура - Ошибка записи данных в базу
//
// Пример:
//   Классы = РатОбщий.КлассыОшибок();
//   Если Классы.НеНайденаЗаписьИБ.КодСостояния = 404 Тогда ...
Функция КлассыОшибок(Кэшировать = Истина) Экспорт
	
	Если Кэшировать Тогда
		Возврат РатПовтИсп.КлассыОшибок();
	КонецЕсли;
	
	КлассыОшибок = Новый Структура();
	
	КодыОтветов = РатОбщийКлиентСервер.КодыОтветов();
	
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок, "Неизвестная", "Ошибка внутренней логики расширения", КодыОтветов.ОшибкаСервера, "internal_extension");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок,
								 "НеподдерживаемаяФункциональность",
								 "Функционал не поддерживается. Поддержите проект создайте задачу или реализуйте",
								 КодыОтветов.ОшибкаСервера,
								 "internal_extension");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок, "ЛогикаКонфигурации", "Ошибка кода конфигурации", КодыОтветов.ОшибкаСервера, "internal_configuration");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок, "ЗаписьДанныхВБазу", "Ошибка записи данных", КодыОтветов.ОшибкаСервера, "write_object");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок,
								 "РазборПараметровЗапроса",
								 "Ошибка разбора входных параметров",
								 КодыОтветов.ПлохойЗапрос,
								 "request_parse");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок,
								 "АнализПараметровЗапроса",
								 "Ошибка данных входных параметров",
								 КодыОтветов.ПлохойЗапрос,
								 "request_analyze");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок, "Десериализация", "Ошибка входных данных", КодыОтветов.ПлохойЗапрос, "deserialize");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок, "Сериализация", "Ошибка сериализации результата", КодыОтветов.ОшибкаСервера, "serialize");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок,
								 "НедоступнаяФункциональность",
								 "Запрашиваемое действие не может быть выполнено",
								 КодыОтветов.ПлохойЗапрос,
								 "bad_action");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок, "НеНайденМаршрут", "Не верный адрес ресурса", КодыОтветов.НеНайдено, "route_not_found");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок, "НеНайденаЗаписьИБ", "Не найдена запись в базе", КодыОтветов.НеНайдено, "record_not_found");
	ДобавитьОписаниеКлассаОшибок(КлассыОшибок,
								 "МетодНеПоддерживается",
								 "Метод не поддерживается",
								 КодыОтветов.МетодНеПоддерживается,
								 "method_not_allowed");
	
	Возврат Новый ФиксированнаяСтруктура(КлассыОшибок);
	
КонецФункции

// Создает базовую структуру для отслеживания статуса обработки запроса.
//
// Параметры:
//  Результат - Неопределено - Результат обработки запроса
//
// Возвращаемое значение:
//  Структура - Статус обработки запроса:
//    * Успешно - Булево - Признак успешности обработки
//    * ВложеннаяОперацияУспешна - Булево - Признак успешности вложенной операции
//    * ОписаниеОшибки - Массив из Структура - Список возникших ошибок
//    * ОтладочныеСообщения - Массив из Строка - Список отладочных сообщений
//    * Результат - Неопределено - Результат обработки запроса
//    * КодСостояния - Число - Код HTTP-ответа, нужен для переопределения кода 200
//
// Пример:
//   Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   Если Статус.Успешно Тогда ...
//
Функция НовыйСтатусОбработкиЗапроса(Результат = Неопределено) Экспорт
	
	СтатусОбработкиЗапроса = Новый Структура;
	СтатусОбработкиЗапроса.Вставить("Успешно", Истина);
	СтатусОбработкиЗапроса.Вставить("ВложеннаяОперацияУспешна", Истина);
	СтатусОбработкиЗапроса.Вставить("ОписаниеОшибки", Новый Массив);
	СтатусОбработкиЗапроса.Вставить("ОтладочныеСообщения", Новый Массив);
	СтатусОбработкиЗапроса.Вставить("Результат", Результат);
	СтатусОбработкиЗапроса.Вставить("КодСостояния", 0);
	
	Возврат СтатусОбработкиЗапроса;
	
КонецФункции

// Сохраняет информацию об ошибке в данных статуса обработки запроса.
//
// Параметры:
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//  КлассОшибки - Структура - Класс ошибки, см. РатОбщий.КлассыОшибок
//  Ошибка - Строка, ИнформацияОбОшибке - Данные ошибки
//
// Пример:
//   Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   РатОбщий.ЗафиксироватьОшибку(Статус, КлассыОшибок().НеНайденаЗаписьИБ, "Запись не найдена");
//
Процедура ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, Ошибка) Экспорт
	
	СтатусОбработкиЗапроса.Успешно = Ложь;
	СтатусОбработкиЗапроса.ВложеннаяОперацияУспешна = Ложь;
	ОписаниеОшибки = Новый Структура("Класс, Текст", КлассОшибки, РатОбщийКлиентСервер.ТекстОшибки(Ошибка));
	СтатусОбработкиЗапроса.ОписаниеОшибки.Добавить(ОписаниеОшибки);
	
КонецПроцедуры

// Добавляет отладочное сообщение в статус обработки запроса для последующего анализа.
//
// Параметры:
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//  Сообщение - Строка - Текст отладочного сообщения
//
// Пример:
//   Статус = РатОбщий.НовыйСтатусОбработкиЗапроса();
//   РатОбщий.ДобавитьОтладочноеСообщение(Статус, "Начало обработки запроса");
//
Процедура ДобавитьОтладочноеСообщение(СтатусОбработкиЗапроса, Сообщение) Экспорт
	
	СтатусОбработкиЗапроса.ОтладочныеСообщения.Добавить(Сообщение);
	
КонецПроцедуры

// Возвращает структуру с сообщением об успешном выполнении операции.
//
// Параметры:
//   Описание - Строка - Текст сообщения об успешном выполнении операции
//
// Возвращаемое значение:
//   Структура - Данные результата:
//     * message - Строка - Сообщение об успешности операции
//
// Пример:
//   Результат = РатОбщий.РезультатДанныеЗаписаны("Документ успешно проведен");
//
Функция РезультатДанныеЗаписаны(Описание = "Данные успешно сохранены") Экспорт
	
	Возврат Новый Структура("message", Описание);
	
КонецФункции

// Возвращает строковое представление данных переданного объекта.
//
// Параметры:
//   Объект - Произвольный - Объект, данные которого нужно представить
//   Уровень - Число - Уровень вложенности, нужен для формирования отступов
//   ПредставлениеТипа - Строка, Неопределено - Подмена представления типа объекта, применяется для указания описания объекта
//
// Возвращаемое значение:
//   Строка - Представление данных объекта в виде форматированного текста
//
// Пример:
//   Текст = РатОбщий.Представление(Новый Структура("Код, Наименование", "000001", "Товар"));
//
Функция Представление(Объект, Знач Уровень = 0, ПредставлениеТипа = Неопределено) Экспорт
	
	ТипОбъекта = ТипЗнч(Объект);
	
	Шаблон = "%1 (%2)";
	Представление = "";
	
	ШагОтступа = 8;
	
	Если ПредставлениеТипа = Неопределено Тогда
		ПредставлениеТипа = ТипОбъекта;
	КонецЕсли;
	
	Если РатТипыДанных.ЭтоКлючЗначение(ТипОбъекта) Тогда
		Представление = ПредставлениеКлючЗначение(Объект, Уровень, ШагОтступа);
		Шаблон = "%2:
				 |%1";
	ИначеЕсли РатТипыДанных.ЭтоМассив(ТипОбъекта) Тогда
		Представление = ПредставлениеМассив(Объект, Уровень);
		Шаблон = "%2: %1";
	ИначеЕсли ТипОбъекта = Тип("Число") Тогда
		Представление = XMLСтрока(Объект);
	ИначеЕсли ТипОбъекта = Тип("Дата") Тогда
		Представление = Формат(Объект, "ДФ=""dd.MM.yyyy ЧЧ:мм:сс""");
	ИначеЕсли ТипОбъекта = Тип("Булево") Тогда
		Представление = ?(Объект, "Истина", "Ложь");
	ИначеЕсли ТипОбъекта = Тип("Строка") Тогда
		Представление = Объект;
	Иначе
		Представление = Строка(Объект);
		ПредставлениеТипа = РатТипыДанных.ПредставлениеТипа(ТипОбъекта);
	КонецЕсли;
	
	Если ПустаяСтрока(Представление) Тогда
		Представление = "<Пусто>";
	КонецЕсли;
	
	Возврат СтрШаблон(Шаблон, Представление, ПредставлениеТипа);
	
КонецФункции

// Возвращает значение реквизита, прочитанного из информационной базы по ссылке на объект.
//
// Если доступа к реквизиту нет, возникнет исключение прав доступа.
// Если необходимо зачитать реквизит независимо от прав текущего пользователя,
// то следует использовать предварительный переход в привилегированный режим.
//
// Параметры:
//   Ссылка - ЛюбаяСсылка - Ссылка, значение реквизита которой нужно получить
//   ИмяРеквизита - Строка, Структура - Имя реквизита, например, "Код", "Автор.Наименование", 
//                  Структура аналогично ф-ии ПолучитьЗначенияРеквизитов()
//
// Возвращаемое значение:
//   Произвольный - Значение реквизита
//
// Пример:
//   Значение = РатОбщий.ПолучитьЗначениеРеквизита(Ссылка, "Код");
//
Функция ПолучитьЗначениеРеквизита(Ссылка, ИмяРеквизита) Экспорт
	
	Результат = ПолучитьЗначенияРеквизитов(Ссылка, ИмяРеквизита);
	
	Если ТипЗнч(ИмяРеквизита) = Тип("Строка") Тогда
		
		Возврат Результат[СтрЗаменить(ИмяРеквизита, ".", "")];
		
	ИначеЕсли ИмяРеквизита.Количество() > 0 Тогда
		
		// Так как нет метода получения по индексу, делаем циклом.
		Для Каждого Поле Из ИмяРеквизита Цикл
			
			Возврат Результат[Поле.Ключ];
			
		КонецЦикла;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает значения реквизитов, прочитанных из информационной базы по ссылке на объект.
//
// Если доступа к реквизиту нет, возникнет исключение прав доступа.
// Если необходимо зачитать реквизит независимо от прав текущего пользователя,
// то следует использовать предварительный переход в привилегированный режим.
//
// Параметры:
//   Ссылка - ЛюбаяСсылка - Ссылка, значения реквизитов которой нужно получить
//   ИменаРеквизитов - Строка, Структура - Если Строка, то имена реквизитов, перечисленные через запятую, 
//                    в формате требований к свойствам структуры.
//                    Например, "Код, Наименование, Родитель". 
//                    Для строки возможен вариант "Автор.Сотрудник, Контрагент.Код". 
//                    В таком формате при формировании имени поля символ точки будет удален, 
//                    т.е. будет "АвторСотрудник" и "КонтрагентКод".
//                    Если Структура, то в качестве ключа передается псевдоним поля для возвращаемой 
//                    структуры с результатом, а в качестве значения (опционально) - 
//                    фактическое имя поля в таблице.
//                    Если значение не определено, то имя поля берется из ключа.
//
// Возвращаемое значение:
//   Структура - Значения реквизитов, ключи - имена реквизитов или псевдонимы из структуры
//
// Пример:
//   Значения = РатОбщий.ПолучитьЗначенияРеквизитов(Ссылка, "Код, Наименование, Автор.Наименование");
//   Значения = РатОбщий.ПолучитьЗначенияРеквизитов(Ссылка, Новый Структура("Код", "Номер", "Наименование", "Имя"));
//
Функция ПолучитьЗначенияРеквизитов(Ссылка, ИменаРеквизитов) Экспорт
	
	Если ТипЗнч(ИменаРеквизитов) = Тип("Структура") Тогда
		СтруктураРеквизитов = ИменаРеквизитов;
	ИначеЕсли ТипЗнч(ИменаРеквизитов) = Тип("Строка") Тогда
		
		СтруктураРеквизитов = Новый Структура;
		Массив = РатСтроки.СтрокуВМассив(ИменаРеквизитов, ",", Истина);
		Для Каждого Эл Из Массив Цикл
			
			СтруктураРеквизитов.Вставить(СтрЗаменить(Эл, ".", ""), Эл);
			
		КонецЦикла;
		
	Иначе
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Неверный тип второго параметра ИменаИлиСтруктураРеквизитов: %1'"),
			Строка(ТипЗнч(ИменаРеквизитов)));
	КонецЕсли;
	
	ТекстПолей = "";
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		ИмяПоля   = ?(ЗначениеЗаполнено(КлючИЗначение.Значение), СокрЛП(КлючИЗначение.Значение), СокрЛП(КлючИЗначение.Ключ));
		Псевдоним = СокрЛП(КлючИЗначение.Ключ);
		ТекстПолей  = ТекстПолей + ?(ПустаяСтрока(ТекстПолей), "", ",") + "
			|	ПсевдонимЗаданнойТаблицы." + ИмяПоля + " КАК " + Псевдоним;
	КонецЦикла;
	
	ПолноеИмяТаблицы = Ссылка.Метаданные().ПолноеИмя();
	
	ШаблонЗапроса = "ВЫБРАТЬ %1 ИЗ %2 КАК ПсевдонимЗаданнойТаблицы ГДЕ ПсевдонимЗаданнойТаблицы.Ссылка = &Ссылка"; // BSLLS:QueryParseError-off
	ТекстЗапроса = СтрШаблон(ШаблонЗапроса, ТекстПолей, ПолноеИмяТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Результат = Новый Структура;
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		Результат.Вставить(КлючИЗначение.Ключ);
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает значение реквизита из первой строки выборки результата запроса.
//
// Параметры:
//   Запрос - Запрос, Строка, РезультатЗапроса - Запрос, текст или результат выполнения запроса, 
//            из которого необходимо извлечь данные
//   Реквизит - Число - Индекс колонки в результате запроса (по умолчанию 0)
//   ЗначениеПоУмолчанию - Произвольный - Значение, возвращаемое если результат запроса пуст
//
// Возвращаемое значение:
//   Произвольный - Значение из результата запроса или ЗначениеПоУмолчанию
//
// Пример:
//   Код = РатОбщий.ЗначениеИзЗапроса(Запрос, "Код", "000000");
//
Функция ЗначениеИзЗапроса(Запрос, Реквизит = 0, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Если Реквизит = Неопределено Тогда
		ВызватьИсключение "Не указан получаемый из запроса реквизит";
	КонецЕсли;
	
	Возврат ПолучитьДанныеЗаписиИзЗапроса(Запрос, Реквизит, ЗначениеПоУмолчанию);
	
КонецФункции

// Возвращает структуру значений первой строки выборки.
//
// Параметры:
//  Запрос - Запрос, Строка, РезультатЗапроса - Запрос, текст или результат выполнения запроса, 
//           из которого необходимо извлечь данные
//  ЗначениеПоУмолчанию - Произвольный - Значение по умолчанию, которое будет возвращено, 
//                        если выборка не содержит данных
//
// Возвращаемое значение:
//  Структура, Неопределено, Произвольный - Значение реквизитов первой записи результата 
//                                          выполнения запроса или значение по умолчанию
//
// Пример:
//   Данные = РатОбщий.ЗначенияИзЗапроса(Запрос);
//
Функция ЗначенияИзЗапроса(Запрос, Знач ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Возврат ПолучитьДанныеЗаписиИзЗапроса(Запрос, Неопределено, ЗначениеПоУмолчанию);
	
КонецФункции

// Проверяет, является ли строка корректным идентификатором.
//
// Правила формирования идентификатора:
//   * Может содержать буквы английского и русского алфавита
//   * Может содержать цифры, но не начинаться с них
//   * Может содержать символ подчеркивания
//
// Параметры:
//   Строка - Строка - Значение, которое нужно проверить
//
// Возвращаемое значение:
//   Булево - Истина, если строка является валидным идентификатором
//
// Пример:
//   Если РатОбщий.ЭтоВалидныйИдентификатор("ИмяПоля") Тогда ...
//
Функция ЭтоВалидныйИдентификатор(Строка) Экспорт
	
	Возврат ВалидныйИдентификатор(Строка) = Строка;
	
КонецФункции

// Преобразует строку в валидный идентификатор.
//
// Правила формирования идентификатора:
//   * Может содержать буквы английского и русского алфавита
//   * Может содержать цифры, но не начинаться с них
//   * Может содержать символ подчеркивания
//
// Преобразование происходит путем:
//   * Замены всех невалидных символов на подчеркивание
//   * Добавления подчеркивания в начало, если строка начинается с цифры
//
// Параметры:
//   Строка - Строка - Произвольная строка для преобразования
//
// Возвращаемое значение:
//   Строка - Полученный валидный идентификатор
//
// Пример:
//   Идентификатор = РатОбщий.ВалидныйИдентификатор("123ИмяПоля"); // Вернет "_123ИмяПоля"
//
Функция ВалидныйИдентификатор(Знач Строка) Экспорт
	
	РазрешенныеСимволы = РатСтроки.НаборСимволов(Истина, Истина, Истина, Истина, "_");
	
	ЗапрещенныеСимволы = СтрСоединить(СтрРазделить(Строка, РазрешенныеСимволы), "");
	
	Строка = СтрСоединить(СтрРазделить(Строка, ЗапрещенныеСимволы), "_");
	
	ПервыйСимвол = Лев(Строка, 1);
	Цифры = РатСтроки.Цифры();
	
	Если СтрНайти(Цифры, ПервыйСимвол) Тогда
		
		Строка = СтрШаблон("_%1", Строка);
		
	КонецЕсли;
	
	Возврат Строка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Создает структуру с описанием класса ошибки.
//
// Параметры:
//   Описание - Строка - Текстовое описание ошибки
//   КодСостояния - Число - HTTP код состояния
//   ИдентификаторОшибки - Строка - Уникальный идентификатор ошибки
//
// Возвращаемое значение:
//   ФиксированнаяСтруктура - Структура с описанием класса ошибки:
//     * Описание - Строка - Текстовое описание ошибки
//     * КодСостояния - Число - HTTP код состояния
//     * Идентификатор - Строка - Уникальный идентификатор ошибки
//
Функция ОписаниеКласса(Описание, КодСостояния, ИдентификаторОшибки)
	
	Описание = Новый Структура("Описание, КодСостояния, Идентификатор", Описание, КодСостояния, ИдентификаторОшибки);
	Возврат Новый ФиксированнаяСтруктура(Описание);
	
КонецФункции

// Добавляет описание класса ошибки в структуру классов ошибок.
//
// Параметры:
//   КлассыОшибок - Структура - Структура классов ошибок
//   Идентификатор - Строка - Идентификатор класса ошибки
//   Описание - Строка - Текстовое описание ошибки
//   КодСостояния - Число - HTTP код состояния
//   ИдентификаторОшибки - Строка - Уникальный идентификатор ошибки
//
Процедура ДобавитьОписаниеКлассаОшибок(КлассыОшибок, Идентификатор, Описание, КодСостояния, ИдентификаторОшибки)
	
	ОписаниеКласса = ОписаниеКласса(Описание, КодСостояния, ИдентификаторОшибки);
	КлассыОшибок.Вставить(Идентификатор, ОписаниеКласса);
	
КонецПроцедуры

// Возвращает данные из результата выполнения запроса.
//
// Параметры:
//   Запрос - Запрос, Строка, РезультатЗапроса - Запрос, текст или результат выполнения запроса
//   Реквизит - Число, Неопределено - Индекс колонки в результате запроса.
//                Если Неопределено, возвращается структура со всеми колонками
//   ЗначениеПоУмолчанию - Произвольный - Значение, возвращаемое если результат запроса пуст
//
// Возвращаемое значение:
//   Произвольный - Значение из результата запроса или ЗначениеПоУмолчанию
//
Функция ПолучитьДанныеЗаписиИзЗапроса(Запрос, Реквизит, Знач ЗначениеПоУмолчанию = Неопределено)
	
	ТипЗначения = ТипЗнч(Запрос);
	
	Если ТипЗначения = Тип("Запрос") Тогда
		
		РезультатЗапроса = Запрос.Выполнить();
		
	ИначеЕсли ТипЗначения = Тип("Строка") Тогда
		
		ВыполняемыйЗапрос = Новый Запрос(Запрос);
		РезультатЗапроса = ВыполняемыйЗапрос.Выполнить();
		
	Иначе
		
		РезультатЗапроса = Запрос;
		
	КонецЕсли;
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат ЗначениеПоУмолчанию;
		
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если Реквизит = Неопределено Тогда
	
		Ключи = РатКоллекции.ВыгрузитьЗначения(РезультатЗапроса.Колонки, "Имя");
		Результат = Новый Структура(СтрСоединить(Ключи, ","));
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		
	Иначе
		
		Результат = Выборка[Реквизит];
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеКлючЗначение(Объект, Уровень, ШагОтступа)
	
	Строки = Новый Массив();
	
	Для Каждого Элемент Из Объект Цикл
		Строки.Добавить(СтрШаблон("%1 = %2", Элемент.Ключ, Представление(Элемент.Значение, Уровень + 1)));
	КонецЦикла;
	
	Отступ = РатСтроки.СформироватьСтрокуСимволов(" ", Уровень * ШагОтступа);
	Возврат Отступ + СтрСоединить(Строки, Символы.ПС + Отступ);
	
КонецФункции

Функция ПредставлениеМассив(Объект, Уровень)
	
	ПредставленияОбъектов = Новый Массив;
	
	Для Каждого Элемент Из Объект Цикл
		ПредставленияОбъектов.Добавить(Представление(Элемент, Уровень + 1));
	КонецЦикла;
	
	Возврат СтрСоединить(ПредставленияОбъектов, "; ");
	
КонецФункции

#КонецОбласти

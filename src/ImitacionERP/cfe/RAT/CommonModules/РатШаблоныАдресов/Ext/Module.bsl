//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет поиск подходящего шаблона URL для обработки запроса.
//
// Параметры:
//   Метод - Строка - HTTP метод запроса
//   ОтносительныйURL - Строка - Часть URL относительно корня сервиса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   СтрокаТабличнойЧасти, Неопределено - Описание шаблона, см. РатШаблоныАдресов.Шаблоны
//                                       Если шаблон не обнаружен возвращает Неопределено
Функция ОпределитьШаблон(Знач Метод, Знач ОтносительныйURL, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Неопределено;
	Метод = ВРег(Метод);
	
	ОтносительныйURL = НормализоватьШаблонURL(ОтносительныйURL);
	БлокиURL = СтрРазделить(ОтносительныйURL, "/");
	
	ПодходящиеШаблоны = Новый Массив;
	ДоступныеШаблоны = Шаблоны();
	
	Для Каждого Шаблон Из ДоступныеШаблоны Цикл
		Если ПроверитьШаблон(Шаблон, БлокиURL) Тогда
			ПодходящиеШаблоны.Добавить(Шаблон);
		КонецЕсли;
	КонецЦикла;
	
	Если ПодходящиеШаблоны.Количество() = 0 Тогда
		КоличествоШаблонов = ДоступныеШаблоны.Количество();
		СписокШаблонов = РатКоллекции.ВыгрузитьЗначения(ДоступныеШаблоны, "Шаблон");
		СтрокаШаблонов = СтрСоединить(СписокШаблонов, "; ");
		ТекстОшибки = СтрШаблон("Не удалось определить подходящий шаблон для `[%1] %2`
								|Доступно %3 шаблонов: %4",
								Метод,
								ОтносительныйURL,
								КоличествоШаблонов,
								СтрокаШаблонов);
		КлассОшибки = РатОбщий.КлассыОшибок().НеНайденМаршрут;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Шаблон Из ПодходящиеШаблоны Цикл
		Если Шаблон.Метод = Метод Тогда
			Результат = Шаблон;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Результат = Неопределено Тогда
		ПоддерживаемыеМетоды = РатКоллекции.ВыгрузитьЗначения(ПодходящиеШаблоны, "Метод");
		ТекстОшибки = "Метод не поддерживается. Поддерживаемые методы: " + СтрСоединить(ПоддерживаемыеМетоды, "; ");
		КлассОшибки = РатОбщий.КлассыОшибок().МетодНеПоддерживается;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает доступные к обработке шаблоны URL.
// Для добавления шаблонов в коллекцию используется метод см. РатШаблоныАдресов.ДобавитьШаблон
//
// Возвращаемое значение:
//  ТаблицаЗначений - Коллекция шаблонов адресов сервиса:
//    * Метод       - Строка - HTTP метод (GET, POST, PUT и т.д.)
//    * Шаблон      - Строка - Шаблон относительного URL. Может содержать
//                             + статические части (например, "РегламентныеЗадания")
//                             + динамические части в формате {ИмяПеременной}
//    * Обработчик  - Строка - Идентификатор обработчика запроса
//    * Приоритет   - Число - Приоритет шаблона при поиске (по умолчанию 99).
//                            Чем меньше значение, тем выше приоритет
//    * Блоки       - Массив из Строка - Разбитый на части шаблон
//    * Ограничения - Структура - Ограничения для динамических частей шаблона
//
Функция Шаблоны() Экспорт
	
	Шаблоны = Новый ТаблицаЗначений();
	Шаблоны.Колонки.Добавить("Метод", Новый ОписаниеТипов("Строка"));
	Шаблоны.Колонки.Добавить("Шаблон", Новый ОписаниеТипов("Строка"));
	Шаблоны.Колонки.Добавить("Обработчик", Новый ОписаниеТипов("Строка"));
	Шаблоны.Колонки.Добавить("Приоритет", Новый ОписаниеТипов("Число"));
	Шаблоны.Колонки.Добавить("Блоки", Новый ОписаниеТипов("Массив"));
	Шаблоны.Колонки.Добавить("Ограничения", Новый ОписаниеТипов("Структура"));
	
	Для Каждого Модуль Из РатРасширениеФункциональности.МодулиСервиса() Цикл
		
		Если РатРасширениеФункциональности.МетодОбъектаСуществует(Модуль, "ЗарегистрироватьШаблоны") Тогда
			Модуль.ЗарегистрироватьШаблоны(Шаблоны);
		КонецЕсли;
		
	КонецЦикла;
	
	Шаблоны.Сортировать("Приоритет");
	
	Возврат Шаблоны;
	
КонецФункции

// Добавляет шаблон в коллекцию шаблонов URL.
//
// Параметры:
//   Шаблоны - ТаблицаЗначений - Коллекция шаблонов, созданная методом см. РатШаблоныАдресов.Шаблоны
//   Метод - Строка - HTTP метод (GET, POST, PUT и т.д.)
//   Шаблон - Строка - Шаблон относительного URL. Может содержать:
//                     + статические части (например, "РегламентныеЗадания")
//                     + динамические части в формате {ИмяПеременной}
//   Обработчик - Строка - Идентификатор обработчика запроса
//   Приоритет - Число - Приоритет шаблона при поиске (по умолчанию 99).
//                       Чем меньше значение, тем выше приоритет
//   Ограничения - Структура, Неопределено - Ограничения для динамических частей шаблона.
//                 В качестве ключа используется имя переменной шаблона
//
Процедура ДобавитьШаблон(Шаблоны, Метод, Шаблон, Обработчик, Приоритет = 99, Ограничения = Неопределено) Экспорт
	
	СтрокаШаблона = Шаблоны.Добавить();
	СтрокаШаблона.Метод       = ВРег(Метод);
	СтрокаШаблона.Шаблон      = НормализоватьШаблонURL(Шаблон);
	СтрокаШаблона.Обработчик  = Обработчик;
	СтрокаШаблона.Приоритет   = Приоритет;
	СтрокаШаблона.Блоки       = СтрРазделить(СтрокаШаблона.Шаблон, "/");
	СтрокаШаблона.Ограничения = Ограничения;
	
КонецПроцедуры

// Извлекает параметры из URL на основе шаблона.
//
// Параметры:
//   ШаблонЗапроса - СтрокаТаблицыЗначений - Описание шаблона запроса
//   ОтносительныйURL - Строка - Относительный URL запроса
//
// Возвращаемое значение:
//   Структура - Параметры, извлеченные из URL
//
Функция ПараметрыURL(ШаблонЗапроса, ОтносительныйURL) Экспорт
	
	Параметры = Новый Структура;
	
	Если ШаблонЗапроса.Обработчик = РатСервис.ОбработчикиЗапросов().ОбработчикНеНайден Тогда
		Возврат Параметры;
	КонецЕсли;
	
	БлокиURL = СтрРазделить(НормализоватьШаблонURL(ОтносительныйURL), "/");
	БлокиШаблона = ШаблонЗапроса.Блоки;
	
	Для Инд = 0 По БлокиШаблона.ВГраница() Цикл
		
		Если ЭтоБлокПеременной(БлокиШаблона[Инд]) Тогда
			
			ИмяПеременной = ИмяПеременнойБлока(БлокиШаблона[Инд]);
			Значение = БлокиURL[Инд];
			
			Параметры.Вставить(ИмяПеременной, Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Нормализует шаблон URL, удаляя начальный и конечный слеши.
//
// Параметры:
//   URL - Строка - исходный URL
//
// Возвращаемое значение:
//   Строка - нормализованный URL
//
Функция НормализоватьШаблонURL(Знач URL)
	
	Если СтрНачинаетсяС(URL, "/") Тогда
		URL = Сред(URL, 2);
	КонецЕсли;
	
	Если СтрЗаканчиваетсяНа(URL, "/") Тогда
		URL = Лев(URL, СтрДлина(URL) - 1);
	КонецЕсли;
	
	Возврат URL;
	
КонецФункции

// Проверяет, является ли блок шаблона переменной.
//
// Параметры:
//   Текст - Строка - текст блока шаблона
//
// Возвращаемое значение:
//   Булево - Истина, если блок является переменной
//
Функция ЭтоБлокПеременной(Текст)
	
	Возврат СтрНачинаетсяС(Текст, "{") И СтрЗаканчиваетсяНа(Текст, "}");
	
КонецФункции

// Извлекает имя переменной из блока шаблона.
//
// Параметры:
//   Текст - Строка - текст блока шаблона
//
// Возвращаемое значение:
//   Строка - имя переменной
//
Функция ИмяПеременнойБлока(Текст)
	
	Сдвиг = 2;
	Возврат Сред(Текст, Сдвиг, СтрДлина(Текст) - Сдвиг);
	
КонецФункции

// Проверяет ограничения для блока шаблона.
//
// Параметры:
//   Шаблон - СтрокаТаблицыЗначений - описание шаблона
//   БлокШаблона - Строка - блок шаблона
//   БлокURL - Строка - блок URL
//
// Возвращаемое значение:
//   Булево - Истина, если блок соответствует ограничениям
//
Функция ПроверитьОграниченияБлокаШаблона(Шаблон, БлокШаблона, БлокURL)
	
	Результат = Истина;
	
	Если НЕ ЗначениеЗаполнено(Шаблон.Ограничения) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИмяПеременной = ИмяПеременнойБлока(БлокШаблона);
	Ограничение = РатКоллекции.ЗначениеСтруктуры(Шаблон.Ограничения, ИмяПеременной);
	
	Если ЗначениеЗаполнено(Ограничение) И Ограничение.Найти(БлокURL) = Неопределено Тогда
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет соответствие URL шаблону.
//
// Параметры:
//   Шаблон - СтрокаТаблицыЗначений - описание шаблона
//   БлокиURL - Массив из Строка - блоки URL
//
// Возвращаемое значение:
//   Булево - Истина, если URL соответствует шаблону
//
Функция ПроверитьШаблон(Шаблон, БлокиURL)
	
	Если Шаблон.Блоки.Количество() <> БлокиURL.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Инд = 0 По БлокиURL.ВГраница() Цикл
		БлокШаблона = Шаблон.Блоки[Инд];
		
		Если ЭтоБлокПеременной(БлокШаблона) Тогда
			Если НЕ ПроверитьОграниченияБлокаШаблона(Шаблон, БлокШаблона, БлокиURL[Инд]) Тогда
				Возврат Ложь;
			КонецЕсли;
		ИначеЕсли БлокШаблона <> БлокиURL[Инд] Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

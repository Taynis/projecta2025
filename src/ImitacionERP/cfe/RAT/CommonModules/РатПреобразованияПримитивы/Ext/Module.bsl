//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// См. РатПреобразования.ЗначениеВДату
Функция ЗначениеВДату(Значение, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Неопределено;
	
	Попытка
		
		// Позиция двоеточия в строке времени формата HH:MM
		ПозицияДвоеточияВремени = 3;
		// Длина строки времени формата HH:MM:SS
		ДлинаВремениССекундами = 8;
		Если СтрНайти(Значение, ":") = ПозицияДвоеточияВремени Тогда
			Если СтрДлина(Значение) = ДлинаВремениССекундами Тогда
				НормализованноеЗначение = "0001-01-01T" + Значение;
			Иначе
				НормализованноеЗначение = "0001-01-01T" + Значение + ":00";
			КонецЕсли;
		Иначе
			НормализованноеЗначение = Значение;
		КонецЕсли;
		
		Результат = XMLЗначение(Тип("Дата"), НормализованноеЗначение);
		
	Исключение
		ТекстОшибки = СтрШаблон("Не удалось привести значение '%1' к дате", Значение);
		КлассОшибки = РатОбщий.КлассыОшибок().Десериализация;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		Результат = '0001-01-01T00:00:00';
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// См. РатПреобразования.ЗначениеВБулево
Функция ЗначениеВБулево(Знач Значение, СтатусОбработкиЗапроса) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Булево") Тогда
		Возврат Значение;
	КонецЕсли;
	
	Результат = РатПреобразованияКлиентСервер.ВБулево(Значение);
	
	Если Результат = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Не удалось привести значение '%1' к булево", Значение);
		КлассОшибки = РатОбщий.КлассыОшибок().Десериализация;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// См. РатПреобразования.ЗначениеВЧисло
Функция ЗначениеВЧисло(Значение, СтатусОбработкиЗапроса) Экспорт
	
	Результат = ?(ПустаяСтрока(Значение), 0, РатПреобразованияКлиентСервер.ЧислоИзСтроки(Значение));
	
	Если Результат = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Не удалось привести значение '%1' к числу", Значение);
		КлассОшибки = РатОбщий.КлассыОшибок().Десериализация;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// См. РатПреобразования.ЗначениеВСтроку
Функция ЗначениеВСтроку(Значение, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Неопределено;
	
	Попытка
		Результат = Строка(Значение);
	Исключение
		ТекстОшибки = "Не удалось привести значение '%1' к строке";
		ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), ТекстОшибки);
		КлассОшибки = РатОбщий.КлассыОшибок().Десериализация;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// См. РатПреобразования.СериализоватьПримитив
Функция СериализоватьПримитив(Значение, ТипЗначения) Экспорт
	
	Если Значение = Неопределено ИЛИ Значение = Null Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если РатJSON.ЭтоПримитивныйТипJSON(ТипЗначения) Тогда
		Возврат Значение;
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		Возврат XMLСтрока(Значение);
	ИначеЕсли ТипЗначения = Тип("УникальныйИдентификатор") Тогда
		Возврат Строка(Значение);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// См. РатПреобразования.ДесериализоватьПримитив
Функция ДесериализоватьПримитив(Значение, ОжидаемыйТип, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Неопределено;
	
	Если ОжидаемыйТип = Тип("Дата") Тогда
		Результат = ЗначениеВДату(Значение, СтатусОбработкиЗапроса);
	ИначеЕсли ОжидаемыйТип = Тип("Булево") Тогда
		Результат = ЗначениеВБулево(Значение, СтатусОбработкиЗапроса);
	ИначеЕсли ОжидаемыйТип = Тип("Число") Тогда
		Результат = ЗначениеВЧисло(Значение, СтатусОбработкиЗапроса);
	ИначеЕсли ОжидаемыйТип = Тип("Строка") Тогда
		Результат = ЗначениеВСтроку(Значение, СтатусОбработкиЗапроса);
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет, является ли тип примитивным для сериализации
//
// Параметры:
//   Тип - Тип - проверяемый тип
//
// Возвращаемое значение:
//   Булево - Истина, если тип примитивный
//
Функция ЭтоПримитивныйТип(Тип) Экспорт
	
	Возврат Тип = Тип("Строка")
		ИЛИ Тип = Тип("Число")
		ИЛИ Тип = Тип("Булево")
		ИЛИ Тип = Тип("Дата")
		ИЛИ Тип = Тип("Неопределено")
		ИЛИ Тип = Тип("Null");
	
КонецФункции

// Возвращает псевдонимы примитивных типов для сериализации
//
// Возвращаемое значение:
//   ФиксированнаяСтруктура - псевдонимы типов
//
Функция ПсевдонимыПримитивныхТипов() Экспорт
	
	Псевдонимы = Новый Структура();
	Псевдонимы.Вставить("Строка", "string");
	Псевдонимы.Вставить("Число", "decimal");
	Псевдонимы.Вставить("Булево", "boolean");
	Псевдонимы.Вставить("ДатаВремя", "dateTime");
	Псевдонимы.Вставить("Неопределено", "Null");
	
	Псевдонимы.Вставить(Псевдонимы.Строка, Псевдонимы.Строка);
	Псевдонимы.Вставить(Псевдонимы.Число, Псевдонимы.Число);
	Псевдонимы.Вставить(Псевдонимы.Булево, Псевдонимы.Булево);
	Псевдонимы.Вставить(Псевдонимы.ДатаВремя, Псевдонимы.ДатаВремя);
	Псевдонимы.Вставить(Псевдонимы.Неопределено, Псевдонимы.Неопределено);
	
	Возврат Новый ФиксированнаяСтруктура(Псевдонимы);
	
КонецФункции

#КонецОбласти

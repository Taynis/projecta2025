//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

#Область МенеджерТестирования

// Проверяет свойства области табличного документа на соответствие заданному предикату.
// В случае расхождения выбрасывает исключение.
// ВНИМАНИЕ: исключение так же может быть выброшено при невозможности определить область
// табличного документа по различным причинам: нет активной формы, нет самого табличного документа,
// или произошла ошибка при взаимодействии с клиентом тестирования
// Контекст: Менеджер тестирования - выполняет вызов клиента тестирования.
// 
// Параметры:
//  Ванесса - Произвольный - контекст фреймворка Vanessa-Automation
//  ОписаниеПредиката - Строка - описание предиката см. СформироватьПредикатПоОписанию
//  ИмяОбласти - Строка, Структура - имя области табличного документа или предикат для поиска области 
//             - Неопределено - предпринимается попытка определить текущую область
//  ИмяЭлемента - Строка - Имя элемента активной формы, содержащей табличный документ
//              - Неопределено - предпринимается попытка определить по текущему элементу формы
Процедура ПроверитьСвойстваОбласти(Ванесса, ОписаниеПредиката, ИмяОбласти = Неопределено, ИмяЭлемента = Неопределено) Экспорт
	
	Команда  = РатВзаимодействиеСКлиентомТестирования.НоваяКоманда();
	
	// см. РатАнализТабличногоДокументаВызовКлиента.ПроверитьСвойстваОбласти
	Команда.Метод = "РатАнализТабличногоДокументаВызовКлиента.ПроверитьСвойстваОбласти";
	
	Команда.Параметры.Добавить(СформироватьПредикатПоОписанию(ОписаниеПредиката));
	Команда.Параметры.Добавить(ИмяОбласти);
	Команда.Параметры.Добавить(ИмяЭлемента);
	
	РатВзаимодействиеСКлиентомТестирования.ВыполнитьКоманду(Ванесса, Команда);
	
КонецПроцедуры

// Возвращает значения указанных свойств области табличного документа.
// ВНИМАНИЕ: при невозможности определить свойства, область, элемент или активную форму вызывает исключение
// Контекст: Менеджер тестирования - выполняет вызов клиента тестирования.
//
// Параметры:
//  Ванесса - Произвольный - контекст фреймворка Vanessa-Automation
//  ИменаСвойств - Строка - список свойств области, разделенных запятыми
//  ИмяОбласти - Строка - имя области табличного документа предикат для поиска области
//             - Неопределено - предпринимается попытка определить текущую область
//  ИмяЭлемента - Строка - Имя элемента активной формы, содержащей табличный документ
//              - Неопределено - предпринимается попытка определить по текущему элементу формы
//
// Возвращаемое значение:
//  Структура - коллекция значений указанных свойств
Функция ЗначенияСвойствОбласти(Ванесса, ИменаСвойств, ИмяОбласти = Неопределено, ИмяЭлемента = Неопределено) Экспорт
	
	Команда  = РатВзаимодействиеСКлиентомТестирования.НоваяКоманда();
	
	// см. РатАнализТабличногоДокументаВызовКлиента.ЗначенияСвойствОбласти
	Команда.Метод = "РатАнализТабличногоДокументаВызовКлиента.ЗначенияСвойствОбласти";
	
	Команда.Параметры.Добавить(ИменаСвойств);
	Команда.Параметры.Добавить(ИмяОбласти);
	Команда.Параметры.Добавить(ИмяЭлемента);
	
	//@skip-check constructor-function-return-section - универсальный метод: "сужаем" тип возвращаемого значения
	Возврат РатВзаимодействиеСКлиентомТестирования.ВыполнитьКоманду(Ванесса, Команда);
	
КонецФункции

#КонецОбласти

#Область ОбщегоНазначения

// Сформировать структуру (предикат) по описанию
// Вариант описания 1:
//  Ключ:Значение разделяются символом ";"
//  Ключ и значение разделяются символом ":"
//  Приведение значений определено в соответствии см. ФункцияОпределенияЗначений
// 
// Параметры:
//  ПараметрыОформления - Строка - описание свойств предиката (ключ:значение)
// 
// Возвращаемое значение:
//  Структура - сформированная структура ключ:значение, описывающая условия (предикат) проверки
Функция СформироватьПредикатПоОписанию(ПараметрыОформления) Экспорт
	
	РазделительПараметров = ";";
	РазделительКлючЗначение = ":";
	КоличествоКомпонентовПараметра = 2;
	
	Результат = Новый Структура;
	СписокПараметров = СтрРазделить(ПараметрыОформления, РазделительПараметров);
	
	Для Каждого ТекущийПараметр Из СписокПараметров Цикл
		
		ДанныеПараметра = СтрРазделить(ТекущийПараметр, РазделительКлючЗначение, Истина);
		
		Если ДанныеПараметра.Количество() <> КоличествоКомпонентовПараметра Тогда
			
			ВызватьИсключение СтрШаблон("Некорректно заданы параметры оформления: `%1`", ТекущийПараметр);
		
		КонецЕсли;
		
		ИмяПараметра = СокрЛП(ДанныеПараметра[0]);
		ОписаниеЗначенияПараметра = ДанныеПараметра[1];
		ЗначениеПараметра = ПолучитьЗначениеПараметраОформленияПоОписанию(ИмяПараметра, ОписаниеЗначенияПараметра);
		
		Результат.Вставить(ИмяПараметра, ЗначениеПараметра);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОписаниеПредикатаСвойствОбластиТабличногоДокумента

Функция ПолучитьЗначениеПараметраОформленияПоОписанию(ИмяПараметра, ОписаниеЗначенияПараметра)
	
	МетодПолученияЗначения = ФункцияОпределенияЗначений(ИмяПараметра);
	
	Возврат ВызватьФункцию(МетодПолученияЗначения, ОписаниеЗначенияПараметра);
	
КонецФункции

#Область ФункцииОпределенияЗначенийПредиката

Функция ФункцияОпределенияЗначений(ИмяПараметра)
	
	ОпределениеЦвета = "ОпределитьЦвет";
	МетодыЗначений = Новый Соответствие;
	МетодыЗначений.Вставить("ЦВЕТФОНА", ОпределениеЦвета);
	МетодыЗначений.Вставить("ЦВЕТТЕКСТА", ОпределениеЦвета);
	МетодыЗначений.Вставить("ЦВЕТРАМКИ", ОпределениеЦвета);
	МетодыЗначений.Вставить("ЦВЕТУЗОРА", ОпределениеЦвета);
	
	МетодыЗначений.Вставить("ВЕРТИКАЛЬНОЕПОЛОЖЕНИЕ", "ОпределитьВертикальноеПоложение");
	МетодыЗначений.Вставить("ГОРИЗОНТАЛЬНОЕПОЛОЖЕНИЕ", "ОпределитьГоризонтальноеПоложение");
	
	МетодыЗначений.Вставить("УЗОР", "ОпределитьУзор");
	
	МетодыЗначений.Вставить("ТЕКСТ", "ОпределитьТекст");
	
	Метод = МетодыЗначений[ВРег(ИмяПараметра)];
	
	Если Метод = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон("Неподдерживаемый параметр оформления: `%1`", ИмяПараметра);
		
	КонецЕсли;
	
	Возврат Метод;
	
КонецФункции

Функция ВызватьФункцию(ИмяМетода, Параметры)
	
	Выражение = СтрШаблон("%1(Параметры)", ИмяМетода);
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ТолстыйКлиентУправляемоеПриложение Тогда
	УстановитьБезопасныйРежим(Истина);
	Возврат Вычислить(Выражение); // BSLLS:ExecuteExternalCodeInCommonModule-off
#Иначе
	Возврат Вычислить(Выражение); // BSLLS:ExecuteExternalCodeInCommonModule-off
#КонецЕсли
	
КонецФункции

// BSLLS:MissingReturnedValueDescription-off
// BSLLS:MissingParameterDescription-off
// BSLLS:UnusedLocalMethod-off

//@skip-check module-unused-method - используется см. СформироватьПредикатПоОписанию 
Функция ОпределитьЦвет(ОписаниеЦвета)
	
	// ОписаниеЦвета должно быть задано одним из вариантов:
	// в виде трех компонент -> 255, 255, 255
	// в виде значения системного перечисления - ИмяКоллекцииЦветов.НазваниеЭлементаКоллекции, например:
	// -> WebЦвета.Красный
	// -> WindowsЦвета.ЗаголовокНеактивногоОкна
	// -> ЦветаСтиля.ЦветАктивности
	// Возможные значения системных перечислений смотри в документации (синтаксис-помощник)
	
	Если ЗначениеЗаполнено(ОписаниеЦвета) Тогда
		
		ДанныеЦвета = СтрРазделить(ОписаниеЦвета, ",", Истина);
		
		КоличествоКомпонентовЦвета = 3;
		Если ДанныеЦвета.Количество() = КоличествоКомпонентовЦвета Тогда
			
			// цвет задан как RGB
			
			Возврат Новый Цвет(Число(ДанныеЦвета[0]), Число(ДанныеЦвета[1]), Число(ДанныеЦвета[2])); // BSLLS:StyleElementConstructors-off
			
		КонецЕсли;
		
		ДанныеЦвета = СтрРазделить(ОписаниеЦвета, ".", Истина);
		
		КоличествоКомпонентовИмениЦвета = 2;
		Если ДанныеЦвета.Количество() = КоличествоКомпонентовИмениЦвета Тогда
			
			// это какое-то системное перечисление:
			// WebЦвета
			// WindowsЦвета
			// ЦветаСтиля
			
			КоллекцииСветов = Новый Структура;
			КоллекцииСветов.Вставить("WebЦвета", WebЦвета);
			КоллекцииСветов.Вставить("WindowsЦвета", "РатАнализТабличногоДокументаВызовСервера.WindowsЦвет");
			КоллекцииСветов.Вставить("ЦветаСтиля", "РатАнализТабличногоДокументаВызовСервера.ЦветСтиля");
			
			ФункцияПолученияЦвета = Неопределено;
			ИмяКоллекции = СокрЛП(ДанныеЦвета[0]);
			
			Если КоллекцииСветов.Свойство(ИмяКоллекции, ФункцияПолученияЦвета) Тогда
				
				//@skip-check module-unused-local-variable - используется в вычисляемом выражении
				ВычисляемыйПараметр = СокрЛП(ДанныеЦвета[1]);
				
				Если ТипЗнч(ФункцияПолученияЦвета) = Тип("Строка") Тогда
					
					Возврат РатАлгоритмы.ВызватьФункцию(ФункцияПолученияЦвета, ВычисляемыйПараметр);
					
				Иначе
				
					Возврат ФункцияПолученияЦвета[ВычисляемыйПараметр];
					
				КонецЕсли;
				
			Иначе
				
				ВызватьИсключение СтрШаблон("Неподдерживаемая коллекция цветов: `%1`", ИмяКоллекции);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ВызватьИсключение СтрШаблон("Некорректно задан цвет оформления: `%1`", ОписаниеЦвета);
		
	Иначе
		
		Возврат Новый Цвет; // BSLLS:StyleElementConstructors-off
		
	КонецЕсли;
	
КонецФункции

//@skip-check module-unused-method - используется см. СформироватьПредикатПоОписанию
Функция ОпределитьВертикальноеПоложение(Значение)
	
	Попытка
	
		Возврат ВертикальноеПоложение[СокрЛП(Значение)];
	
	Исключение
		
		ВызватьИсключение СтрШаблон("Некорректно задано значения свойства вертикальное положение: %1", Значение);
	
	КонецПопытки;
	
КонецФункции

//@skip-check module-unused-method - используется см. СформироватьПредикатПоОписанию
Функция ОпределитьГоризонтальноеПоложение(Значение)
	
	Попытка
	
		Возврат ГоризонтальноеПоложение[СокрЛП(Значение)];
	
	Исключение
		
		ВызватьИсключение СтрШаблон("Некорректно задано значения свойства горизонтальное положение: `%1`", Значение);
	
	КонецПопытки;
	
КонецФункции

//@skip-check module-unused-method - используется см. СформироватьПредикатПоОписанию
Функция ОпределитьУзор(Значение)
	
	Попытка
	
		Возврат ТипУзораТабличногоДокумента[СокрЛП(Значение)];
	
	Исключение
		
		ВызватьИсключение СтрШаблон("Некорректно задано значения свойства Узор: `%1`", Значение);
	
	КонецПопытки;
	
КонецФункции

//@skip-check module-unused-method - используется см. СформироватьПредикатПоОписанию
Функция ОпределитьТекст(Значение)
	
	Возврат Значение;
	
КонецФункции
// BSLLS:MissingReturnedValueDescription-on
// BSLLS:MissingParameterDescription-on
// BSLLS:UnusedLocalMethod-on
#КонецОбласти

#КонецОбласти

#КонецОбласти

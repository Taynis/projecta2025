//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет команду в клиенте тестирования и возвращает результат.
//
// Параметры:
//   Ванесса - ВнешняяОбработка - объект тестового приложения.
//   Команда - см. РатВзаимодействиеСКлиентомТестирования.НоваяКоманда
//
// Возвращаемое значение:
//   Произвольный - результат выполнения команды.
//
// Пример:
//   Результат = РатВзаимодействиеСКлиентомТестированияКлиент.ВыполнитьКоманду(Ванесса, "ОткрытьФорму");
//
Функция ВыполнитьКоманду(Ванесса, Команда) Экспорт
	
	Результат = Неопределено;
	Запрос = ОтправитьКомандуКлиентуТестирования(Ванесса, Команда);
	Ответ = ПолучитьОтветОтКлиентаТестирования(Ванесса, Запрос);
	
	Если РатВзаимодействиеСКлиентомТестированияКлиентСервер.ЭтоОтвет_Успешно(Ответ) Тогда
		
		Результат = РатВзаимодействиеСКлиентомТестированияКлиентСервер.РезультатВыполненияИзОтвета(Ответ);
		
		// TODO: добавить возможности вывода какой-то отладочной (или дополнительной) информации
		
	Иначе
		
		ВызватьИсключение РатВзаимодействиеСКлиентомТестированияКлиентСервер.СообщенияИзОтвета(Ответ);
				
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВзаимодействиеСКлиентомТестирования

// Получает ответ от клиента тестирования.
//
// Параметры:
//   Ванесса - ВнешняяОбработка - объект тестового приложения.
//   Запрос - Структура - запрос к клиенту тестирования.
//
// Возвращаемое значение:
//   Структура - ответ от клиента тестирования.
//
Функция ПолучитьОтветОтКлиентаТестирования(Ванесса, Запрос)
	
	КонтейнерЗапросОтвет = Новый Структура;
	КонтейнерЗапросОтвет.Вставить("Запрос", Запрос);
	КонтейнерЗапросОтвет.Вставить("Ответ", Неопределено);
	
	Оповещение = Новый ОписаниеОповещения("ПолученОтветКлиентаТестирования", ЭтотОбъект, КонтейнерЗапросОтвет);
	
	Если Ванесса.ПолучитьТестовоеПриложение().ОжидатьСостояния(Оповещение, Запрос.ТаймаутОжиданияОтвета) Тогда
		
		Ответ = КонтейнерЗапросОтвет.Ответ;
		
	Иначе
		
		Сообщение = СтрШаблон("Таймаут (%1 сек.) ожидания ответа клиента тестирования", Запрос.ТаймаутОжиданияОтвета);
		Ответ = РатВзаимодействиеСКлиентомТестированияКлиентСервер.НовыйОтветТаймаутОжидания(Сообщение);
				
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

// Отправляет команду клиенту тестирования.
//
// Параметры:
//   Ванесса - ВнешняяОбработка - объект тестового приложения.
//   Команда - см. РатВзаимодействиеСКлиентомТестирования.НоваяКоманда
//
// Возвращаемое значение:
//   Структура - запрос к клиенту тестирования.
//
Функция ОтправитьКомандуКлиентуТестирования(Ванесса, Команда)
	
	Запрос = РатВзаимодействиеСКлиентомТестированияВызовСервера.НовыйЗапрос(Команда);
	
	Ванесса.ОткрытьНавигационнуюСсылку(Запрос.НавигационнаяСсылка);
	
	Возврат Запрос;
	
КонецФункции

// Обработчик получения ответа от клиента тестирования.
//
// Параметры:
//   КонтейнерЗапросОтвет - Структура - контейнер с запросом и ответом:
//     * Запрос - Структура - запрос к клиенту тестирования
//     * Ответ - Структура - ответ от клиента тестирования
//
// Возвращаемое значение:
//   Булево - Истина, если ответ получен и обработан.
//
Функция ПолученОтветКлиентаТестирования(КонтейнерЗапросОтвет) Экспорт
		
	Результат = Ложь;
	Ответ = РатВзаимодействиеСКлиентомТестированияВызовСервера.ПолучитьОтветОтКлиентаТестирования(КонтейнерЗапросОтвет.Запрос);
	
	Если Не РатВзаимодействиеСКлиентомТестированияКлиентСервер.ЭтоОтвет_ВПроцессе(Ответ) Тогда
		
		Результат = Истина;
		КонтейнерЗапросОтвет.Ответ = Ответ;
		
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

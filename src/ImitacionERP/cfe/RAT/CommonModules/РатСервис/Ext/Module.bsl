//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Обрабатывает HTTP запросы к сервису RAT. Проверяет возможность переопределения через РатСервисПереопределяемый,
// определяет шаблон запроса на основе HTTP метода и URL, обрабатывает запрос согласно найденному шаблону,
// формирует ответ с учетом статуса обработки. Поддерживает как HTTP запросы, так и локальные вызовы.
//
// Параметры:
//  Запрос - HTTPСервисЗапрос - Данные HTTP запроса
//         - Структура - Данные запроса при локальном вызове
//
// Возвращаемое значение:
//  HTTPСервисОтвет - Ответ сервиса
Функция ЗапросAPI(Запрос) Экспорт
	
	ОтказОсновнойФункции = Ложь;
	Ответ = РатСервисПереопределяемый.ЗапросAPI(Запрос, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Ответ;
	КонецЕсли;
	
	ЭтоЛокальныйВызов = ТипЗнч(Запрос) = Тип("Структура");
	СтатусОбработкиЗапроса = РатОбщий.НовыйСтатусОбработкиЗапроса();
	
	ШаблонЗапроса = РатШаблоныАдресов.ОпределитьШаблон(Запрос.HTTPМетод,
													   Запрос.ОтносительныйURL,
													   СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		ОбработатьЗапрос(ШаблонЗапроса, Запрос, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	ОбработатьСтатус(СтатусОбработкиЗапроса);
	
	Если ЭтоЛокальныйВызов И НЕ Запрос.ИспользуетсяСериализация Тогда
		Возврат ЛокальныйОтвет(СтатусОбработкиЗапроса);
	Иначе
		Возврат Ответ(СтатусОбработкиЗапроса);
	КонецЕсли;
	
КонецФункции

// Возвращает описание API сервиса в формате OpenAPI v3 для документирования и тестирования
//
// Параметры:
//  Запрос - HTTPСервисЗапрос - Данные запроса
//
// Возвращаемое значение:
//  HTTPСервисОтвет - JSON документ с описанием API
Функция Спецификация(Запрос) Экспорт
	
	Описание = СпецификацияСервиса(Запрос);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("БезПреобразования", Истина);
	
	Возврат Ответ(РатОбщий.НовыйСтатусОбработкиЗапроса(Описание), ПараметрыЗапроса);
	
КонецФункции

// Возвращает HTML страницу для визуализации API документации. Получает HTML шаблон из общего макета "РатИнтерфейс"
// и устанавливает тип содержимого как HTML.
//
// Параметры:
//  Запрос - HTTPСервисЗапрос - Данные запроса
// Возвращаемое значение:
//  HTTPСервисОтвет - HTML страница с документацией
Функция СпецификацияИнтерфейс(Запрос) Экспорт
	
	// Список компонент для генерация HTML https://openapi.tools/#documentation
	Результат = ПолучитьОбщийМакет("РатИнтерфейс").ПолучитьТекст();
	
	Ответ = Ответ(РатОбщий.НовыйСтатусОбработкиЗапроса(Результат));
	УстановитьТипСодержимого(Ответ, РатОбщийКлиентСервер.ТипыСодержимого().HTML);
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиЗапросов

// Возвращает идентификаторы обработчиков запросов
//
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Обработчики запросов:
//   * ОбработчикНеНайден - Строка - Идентификатор обработчика для несуществующих маршрутов
Функция ОбработчикиЗапросов() Экспорт
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("ОбработчикНеНайден",      "ОбработчикНеНайден");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

#КонецОбласти

// МетаданныеЗапроса
// Определяет метаданные для запроса на основе параметров. Проверяет наличие обязательных параметров,
// определяет тип метаданных через РатМетаданные.ТипМетаданных, находит метаданные в списке по виду
// и формирует полное имя таблицы.
//
// Параметры:
//  ПараметрыЗапроса - Структура - Параметры запроса:
//   * ТипМетаданных - Строка - Тип метаданных
//   * ВидМетаданных - Строка - Вид метаданных
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  Структура, Неопределено - Метаданные запроса:
//  * ИмяТаблицы - Строка - Полное имя таблицы метаданных
//  * ОписаниеТипаМетаданных - Структура - Описание типа метаданных
Функция МетаданныеЗапроса(ПараметрыЗапроса, СтатусОбработкиЗапроса) Экспорт
	
	Результат = Неопределено;
	
	ТипМетаданных = РатКоллекции.ЗначениеСтруктуры(ПараметрыЗапроса, "ТипМетаданных");
	ВидМетаданных = РатКоллекции.ЗначениеСтруктуры(ПараметрыЗапроса, "ВидМетаданных");
	
	КлассОшибки = РатОбщий.КлассыОшибок().НеНайденМаршрут;
	Если НЕ ЗначениеЗаполнено(ТипМетаданных) Тогда
		ТекстОшибки = "Тип метаданных не указан";
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидМетаданных) Тогда
		ТекстОшибки = "Вид метаданных не указан";
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
	ОписаниеТипа = РатМетаданные.ТипМетаданных(ТипМетаданных);
	
	Если ОписаниеТипа = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Неизвестная коллекция метаданных [%1]", ТипМетаданных);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Результат;
	КонецЕсли;
	
	МетаданныеСписка = Метаданные[ОписаниеТипа.ИмяКоллекции].Найти(ПараметрыЗапроса.ВидМетаданных);
	
	Если МетаданныеСписка = Неопределено Тогда
		ТекстОшибки = СтрШаблон("Неизвестная таблица %1.%2", ТипМетаданных, ВидМетаданных);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		Возврат Результат;
	КонецЕсли;
	
	ИмяТаблицы = СтрШаблон("%1.%2", ОписаниеТипа.Имя, ПараметрыЗапроса.ВидМетаданных);
	
	//@skip-check constructor-function-return-section
	Возврат Новый Структура("ИмяТаблицы, ОписаниеТипаМетаданных", ИмяТаблицы, ОписаниеТипа);
	
КонецФункции

// Проверяет параметры входящего запроса. Проверяет наличие всех обязательных полей,
// проверяет формат тела запроса (должно быть структурой) и фиксирует ошибки в статусе обработки.
//
// Параметры:
//  ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//  ОбязательныеПоля - Строка - Список обязательных полей через запятую
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
Процедура ПроверитьПараметрыЗапроса(ПараметрыЗапроса, ОбязательныеПоля, СтатусОбработкиЗапроса) Экспорт
	
	СписокПолей = СтрРазделить(ОбязательныеПоля, ", ", Ложь);
	
	КлассОшибки = РатОбщий.КлассыОшибок().РазборПараметровЗапроса;
	
	Для Каждого ИмяПоля Из СписокПолей Цикл
		
		Значение = РатКоллекции.ЗначениеСтруктуры(ПараметрыЗапроса, ИмяПоля);
		
		Если НЕ ЗначениеЗаполнено(Значение) Тогда
			ТекстОшибки = "Не указан " + ИмяПоля;
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтатусОбработкиЗапроса.Успешно
		И СписокПолей.Найти("Тело") <> Неопределено
		И Не РатТипыДанных.ЭтоСтруктура(ПараметрыЗапроса.Тело) Тогда
		
		ТекстОшибки = "Данные тела должны быть в виде структуры";
		КлассОшибки = РатОбщий.КлассыОшибок().АнализПараметровЗапроса;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьЗапрос(ШаблонЗапроса, Запрос, СтатусОбработкиЗапроса)
	
	ПараметрыЗапроса = ПараметрыВходящегоЗапроса(ШаблонЗапроса, Запрос, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно И ПараметрыЗапроса.Свойство("ТипМетаданных") Тогда
		МетаданныеЗапроса = МетаданныеЗапроса(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		
		Если СтатусОбработкиЗапроса.Успешно Тогда
			РатКоллекции.ОбъединитьВСтруктуру(ПараметрыЗапроса, МетаданныеЗапроса);
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		
		Если СтатусОбработкиЗапроса.Успешно Тогда
			ВызватьОбработчик(ШаблонЗапроса.Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса);
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), "Не удалось обработать запрос");
		КлассОшибки = РатОбщий.КлассыОшибок().Неизвестная;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

// Извлекает и обрабатывает параметры из HTTP запроса. Извлекает параметры из URL,
// обрабатывает тело запроса (десериализация JSON) и объединяет все параметры в одну структуру.
//
// Параметры:
//  ШаблонЗапроса - Структура - Шаблон запроса
//  Запрос - HTTPСервисЗапрос - Запрос
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  Структура - Параметры запроса:
//   * Параметры - ФиксированноеСоответствие из Строка - Параметры URL
//   * Тело - Структура, Массив из Произвольный - Десериализованные данные тела
Функция ПараметрыВходящегоЗапроса(ШаблонЗапроса, Запрос, СтатусОбработкиЗапроса) Экспорт
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатСервисПереопределяемый.ПараметрыВходящегоЗапроса(ШаблонЗапроса,
																	Запрос,
																	СтатусОбработкиЗапроса,
																	ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура("Параметры, Тело");
	ПараметрыЗапроса.Параметры = Запрос.ПараметрыЗапроса;
	
	ПараметрыURL = РатШаблоныАдресов.ПараметрыURL(ШаблонЗапроса, Запрос.ОтносительныйURL);
	
	РатКоллекции.ОбъединитьВСтруктуру(ПараметрыЗапроса, ПараметрыURL);
	
	Если ТипЗнч(Запрос) = Тип("Структура") Тогда
		Тело = Запрос.Тело;
	Иначе
		Тело = Запрос.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Тело) Тогда
		ПараметрыЗапроса.Тело = Неопределено;
	ИначеЕсли ТипЗнч(Тело) = Тип("Строка") Тогда
		ПараметрыЗапроса.Тело = ДанныеЗапроса(Тело, СтатусОбработкиЗапроса);
	Иначе
		ПараметрыЗапроса.Тело = Тело;
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	Возврат ПараметрыЗапроса;
	
КонецФункции

Процедура ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	ОтказОсновнойФункции = Ложь;
	РатСервисПереопределяемый.ВызватьОбработчик(Обработчик,
												ПараметрыЗапроса,
												СтатусОбработкиЗапроса,
												ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат;
	КонецЕсли;
	
	Обработчики = ОбработчикиЗапросов();
	
	Результат = Неопределено;
	ОбработчикВыполнен = Ложь;
	
	Если Обработчик = Обработчики.ОбработчикНеНайден Тогда
		
		ТекстОшибки = "Не найден шаблон запроса";
		КлассОшибки = РатОбщий.КлассыОшибок().НеНайденМаршрут;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		ОбработчикВыполнен = Истина;
		
	КонецЕсли;
	
	Для Каждого Модуль Из РатРасширениеФункциональности.МодулиСервиса() Цикл
		
		Если ОбработчикВыполнен Тогда
			Прервать;
		КонецЕсли;
		
		Если РатРасширениеФункциональности.МетодОбъектаСуществует(Модуль, "ВызватьОбработчик") Тогда
			
			Результат = Модуль.ВызватьОбработчик(Обработчик,
												 ПараметрыЗапроса,
												 СтатусОбработкиЗапроса,
												 ОбработчикВыполнен);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ОбработчикВыполнен Тогда
		ТекстОшибки = "Обработчик запроса не реализован";
		КлассОшибки = РатОбщий.КлассыОшибок().Неизвестная;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
	Если СтатусОбработкиЗапроса.Успешно
		И Результат = Неопределено
		И СтатусОбработкиЗапроса.КодСостояния <> РатОбщийКлиентСервер.КодыОтветов().НетСодержимого Тогда
		
		ПараметрыЗапросаJSON = РатJSON.ОбъектВСтроку(ПараметрыЗапроса);
		ТекстОшибки = СтрШаблон("Ошибок не выявлено, но обработчик не вернул результат.
		|Обработчик: %1
		|Параметры запроса: %2", Обработчик, ПараметрыЗапросаJSON);
		
		КлассОшибки = РатОбщий.КлассыОшибок().Неизвестная;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		
	КонецЕсли;
	
	СтатусОбработкиЗапроса.Результат = Результат;
	
КонецПроцедуры

// Формирует спецификацию REST API сервиса. Создает описание сервиса, добавляет базовый URL,
// настраивает аутентификацию и регистрирует операции.
//
// Параметры:
//  Запрос - HTTPСервисЗапрос - Запрос к сервису
//
// Возвращаемое значение:
//  Структура - Спецификация сервиса в формате OpenAPI
//
// Пример:
//  Спецификация = СпецификацияСервиса(Запрос);
Функция СпецификацияСервиса(Запрос)
	
	Спецификация = РатOpenAPI.СпецификацияСервиса();
	
	ИмяКонфигурации = Метаданные.Имя;
	ПредставлениеКонфигурации = Метаданные.Представление();
	ЗаголовокСервиса = СтрШаблон("REST API для тестирования конфигурации %1 (%2)", ИмяКонфигурации, ПредставлениеКонфигурации);
	ПояснениеСервиса = СтрШаблон("# Описание сервиса для выполнения интеграционного тестирования %1.
															|", ИмяКонфигурации);
	РатСпецификация.ОписаниеСервиса(Спецификация, ЗаголовокСервиса, ПояснениеСервиса);
	РатСпецификация.ДобавитьСервер(Спецификация, Запрос.БазовыйURL);
	
	РатСпецификация.УстановитьНастройкуБазовойАутентификации(Спецификация);
	
	РатСпецификация.ДобавитьБазовыеКомпоненты(Спецификация);
	
	РатСпецификация.ЗарегистрироватьДополнительныеОперации(Спецификация);
	
	Возврат Спецификация;
	
КонецФункции

#Область HTTPОтвет

// Обрабатывает статус обработки запроса. Проверяет наличие описания ошибки
// и фиксирует ошибку если описание отсутствует.
//
// Параметры:
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Пример:
//  ОбработатьСтатус(СтатусОбработкиЗапроса);
Процедура ОбработатьСтатус(СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно И НЕ ЗначениеЗаполнено(СтатусОбработкиЗапроса.ОписаниеОшибки) Тогда
		ТекстОшибки = "Ошибка внутренней логики расширения, не указано описание ошибки";
		КлассОшибки = РатОбщий.КлассыОшибок().Неизвестная;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Формирует HTTP ответ на основе статуса обработки запроса. Определяет тип содержимого ответа,
// формирует тело ответа, устанавливает код состояния и добавляет заголовки ответа.
//
// Параметры:
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//  ПараметрыЗапроса - Структура - Дополнительные параметры формирования ответа
//
// Возвращаемое значение:
//  HTTPСервисОтвет - Ответ сервиса
Функция Ответ(СтатусОбработкиЗапроса, ПараметрыЗапроса = Неопределено)
	
	ОтказОсновнойФункции = Ложь;
	Результат = РатСервисПереопределяемый.Ответ(СтатусОбработкиЗапроса, ПараметрыЗапроса, ОтказОсновнойФункции);
	
	Если ОтказОсновнойФункции Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТипыСодержимого = РатОбщийКлиентСервер.ТипыСодержимого();
	
	ТипСодержимого = ТипыСодержимого.JSON;
	КодСостояния = РатОбщийКлиентСервер.КодыОтветов().Успешно;
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		ТелоОтвета = ТелоОтвета(СтатусОбработкиЗапроса.Результат, СтатусОбработкиЗапроса, ПараметрыЗапроса);
	КонецЕсли;
	
	Если СтатусОбработкиЗапроса.Успешно И ТипЗнч(СтатусОбработкиЗапроса.Результат) = Тип("Строка") Тогда
		
		ТипСодержимого = ТипыСодержимого.Текст;
		
	ИначеЕсли НЕ СтатусОбработкиЗапроса.Успешно Тогда
		
		КодСостояния = СтатусОбработкиЗапроса.ОписаниеОшибки[0].Класс.КодСостояния;
		ДанныеОшибки = РатСериализация.ПредставлениеОписанияОшибок(СтатусОбработкиЗапроса);
		ТелоОтвета = ТелоОтвета(ДанныеОшибки, СтатусОбработкиЗапроса);
		
	КонецЕсли;
	
	Ответ = Новый HTTPСервисОтвет(КодСостояния);
	Ответ.Заголовки.Вставить("Cache-Control", "no-cache, no-store, must-revalidate");
	УстановитьТипСодержимого(Ответ, ТипСодержимого);
	
	Если ТелоОтвета = Неопределено Тогда
		ТелоОтвета = "Нет данных";
	КонецЕсли;
	
	Ответ.УстановитьТелоИзСтроки(ТелоОтвета, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
	
	Возврат Ответ;
	
КонецФункции

// Формирует локальный ответ для тестирования. Создает структуру с данными ответа,
// включая код состояния и тело ответа.
//
// Параметры:
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  Структура - См. РатШагиВанессыКлиентСервер.ДанныеОтвета
//
// Пример:
//  ДанныеОтвета = ЛокальныйОтвет(СтатусОбработкиЗапроса);
Функция ЛокальныйОтвет(СтатусОбработкиЗапроса)
	
	ДанныеОтвета = РатШагиВанессыКлиентСервер.ДанныеОтвета();
	ДанныеОтвета.Успешно = Истина;
	
	КодСостояния = ?(ЗначениеЗаполнено(СтатусОбработкиЗапроса.КодСостояния), СтатусОбработкиЗапроса.КодСостояния, 200);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		ДанныеОтвета.Тело = СтатусОбработкиЗапроса.Результат;
	Иначе
		ДанныеОшибки = РатСериализация.ПредставлениеОписанияОшибок(СтатусОбработкиЗапроса);
		ДанныеОтвета.Тело = ТелоОтвета(ДанныеОшибки, СтатусОбработкиЗапроса);
		КодСостояния = СтатусОбработкиЗапроса.ОписаниеОшибки[0].Класс.КодСостояния;
	КонецЕсли;
	
	ДанныеОтвета.КодСостояния = КодСостояния;
	
	Возврат ДанныеОтвета;
	
КонецФункции

// Устанавливает тип содержимого в заголовках HTTP ответа
//
// Параметры:
//  Ответ - HTTPСервисОтвет - Ответ сервиса
//  ТипСодержимого - Строка - MIME-тип содержимого
Процедура УстановитьТипСодержимого(Ответ, ТипСодержимого)
	
	Ответ.Заголовки.Вставить("Content-Type", СтрШаблон("%1; charset=utf-8", ТипСодержимого));
	
КонецПроцедуры

// Формирует тело HTTP ответа. Проверяет тип данных тела, сериализует данные в JSON если необходимо
// и обрабатывает ошибки сериализации.
//
// Параметры:
//  Тело - Произвольный - Данные для формирования тела
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//  ПараметрыЗапроса - Структура - Дополнительные параметры формирования
//
// Возвращаемое значение:
//  Строка - Сериализованное тело ответа
Функция ТелоОтвета(Тело, СтатусОбработкиЗапроса, ПараметрыЗапроса = Неопределено)
	
	Попытка
		
		Если ТипЗнч(Тело) = Тип("Строка") Тогда
			Возврат Тело;
		Иначе
			Возврат РатJSON.ОбъектВСтроку(Тело, ПараметрыЗапроса);
		КонецЕсли;
		
	Исключение
		
		Класс = РатОбщий.КлассыОшибок().Сериализация;
		ТестОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), "Не удалось сериализовать данные ответа");
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, Класс, ТестОшибки);
		Возврат Неопределено;

	КонецПопытки;
	
КонецФункции

// Десериализует тело HTTP запроса. Десериализует JSON в объект и обрабатывает ошибки десериализации.
//
// Параметры:
//  Тело - Строка - Тело запроса в формате JSON
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//  Произвольный - Десериализованные данные
//
// Пример:
//  Данные = ДанныеЗапроса(ТелоЗапроса, СтатусОбработкиЗапроса);
Функция ДанныеЗапроса(Тело, СтатусОбработкиЗапроса)
	
	Попытка
		
		Возврат РатJSON.ОбъектИзСтроки(Тело);
		
	Исключение
		
		Классы = РатОбщий.КлассыОшибок();
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, Классы.Десериализация, "Не удалось разобрать тело запроса");
		//@skip-check bsl-legacy-check-static-feature-access
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
									 Классы.Десериализация,
									 РатСтроки.ПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

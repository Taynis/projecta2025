//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

/////////////////////////////////////////////////////////////////////////////////
// 
// ПРЕДПОЛАГАЕТСЯ ВЫПОЛНЕНИЕ ТОЛЬКО НА КЛИЕНТЕ ТЕСТИРОВАНИЯ
// 
/////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиКомандМенеджераТестирования

// Описание методов, параметров и возвращаемых значений
// смотри в одноименных методах модуля РатАнализТабличногоДокумента

// Проверяет соответствие свойств области табличного документа заданному предикату.
//
// Параметры:
//   Предикат - Структура - условия проверки свойств области
//   ИмяОбласти - Неопределено, Строка - имя области табличного документа (Неопределено - текущая область)
//   ИмяЭлемента - Неопределено, Строка - имя элемента формы, отображающей табличный документ (Неопределено - текущий элемент)
//
// Возвращаемое значение:
//   Неопределено - если проверка успешна
//
// Пример:
//   ПроверитьСвойстваОбласти(Новый Структура("Текст", "Итоги"), "ИтоговаяСтрока", "ТабличныйДокумент1")
//
Функция ПроверитьСвойстваОбласти(Предикат, ИмяОбласти = Неопределено, ИмяЭлемента = Неопределено) Экспорт
	
	// см. РатАнализТабличногоДокумента.ПроверитьСвойстваОбласти
	
	ТекущаяОбласть = ПолучитьОбластьТабличногоДокументаАктивнойФормы(ИмяЭлемента, ИмяОбласти);
	Результат = РатКоллекции.СравнитьСвойстваИсточникаСПредикатом(ТекущаяОбласть, Предикат);
	
	Если Не Результат.ПолноеСоответствие Тогда
		
		Сообщения = Новый Массив;
		Сообщения.Добавить("Значения свойств не соответствуют предикату:");
		Сообщения.Добавить(Результат.ОписаниеРасхождений);
		
		ВызватьИсключение СтрСоединить(Сообщения, Символы.ПС);
	
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает значения свойств области табличного документа.
//
// Параметры:
//   ИменаСвойств - Массив из Строка - имена свойств, значения которых необходимо получить
//   ИмяОбласти - Неопределено, Строка - имя области табличного документа (Неопределено - текущая область)
//   ИмяЭлемента - Неопределено, Строка - имя элемента формы, отображающей табличный документ (Неопределено - текущий элемент)
//
// Возвращаемое значение:
//   Структура - структура с запрошенными свойствами области
//
// Пример:
//   ЗначенияСвойствОбласти(Новый Массив("Текст", "ЦветФона"), "ИтоговаяСтрока", "ТабличныйДокумент1")
//
Функция ЗначенияСвойствОбласти(ИменаСвойств, ИмяОбласти = Неопределено, ИмяЭлемента = Неопределено) Экспорт
	
	Попытка
		
		Результат = Новый Структура(ИменаСвойств);
		
	Исключение
		
		ВызватьИсключение СтрШаблон("Некорректно заданы имена свойств для получения значений: `%1`", ИменаСвойств);
		
	КонецПопытки;
	
	ТекущаяОбласть = ПолучитьОбластьТабличногоДокументаАктивнойФормы(ИмяЭлемента, ИмяОбласти);
	ЗаполнитьЗначенияСвойств(Результат, ТекущаяОбласть);
	
	Возврат Результат;
		
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьОбластьТабличногоДокументаАктивнойФормы(ИмяЭлемента, ИмяОбласти)
	
	Попытка
	
		АктивнаяФорма = ОпределитьАктивнуюФорму();
		ЭлементФормы = ПолучитьЭлементТабличногоДокументаФормы(АктивнаяФорма, ИмяЭлемента);
		Результат = ОпределитьОбластьТабличногоДокумента(АктивнаяФорма, ЭлементФормы, ИмяОбласти);
	
	Исключение
		
		ВызватьИсключение РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), "Не удалось получить область табличного документа");
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Определить область табличного документа.
// 
// Параметры:
//  ФормаВладелец - ФормаКлиентскогоПриложения - форма, на которой размещен элемент управления
//  ТабличныйДокумент - ПолеФормы, ТабличныйДокумент - поле формы расширение табличного документа
//  ПредикатОбласти - Неопределено, Строка, Структура - Имя области или условия поиска (если Неопределено - текущая область)
// 
// Возвращаемое значение:
//	ОбластьЯчеекТабличногоДокумента - полученная область табличного документа  
Функция ОпределитьОбластьТабличногоДокумента(ФормаВладелец, ТабличныйДокумент, ПредикатОбласти)
	
	Результат = Неопределено;
	ТипПредиката = ТипЗнч(ПредикатОбласти);
	СообщениеОбОшибке = "Не удалось определить область табличного документа";
		
	Если Не ЗначениеЗаполнено(ПредикатОбласти) Тогда

		// определяем текущую область табличного документа
		
		Результат = ТабличныйДокумент.ТекущаяОбласть;
		СообщениеОбОшибке = "Не удалось определить текущую область табличного документа";
				
	ИначеЕсли ТипПредиката = Тип("Строка") Тогда
		
		СообщениеОбОшибке = СтрШаблон("Не удалось найти область табличного документа по имени ""%1""", ПредикатОбласти);
		
		// определяем область по имени
		
		// для обращения по имени к области табличном документе необходимо перейти от элемента к реквизиту формы
		// при этом возможности получить серверный контекст формы у нас нет
		//	
		// TODO:
		// возможно в будущем необходимо будет делать более хитро как-то:
		// например передавать и имя элемента и имя реквизита
		// по имени элемента проверять всякие там доступности и т.п.
		// а по реквизиту уже более низкоуровневые вещи (типа поиска)
			
		Попытка
		
			Реквизит = РеквизитФормы(ФормаВладелец, ТабличныйДокумент.Имя);
			Результат =	Реквизит.Область(ПредикатОбласти);
			
		Исключение
			
			СообщениеОбОшибке = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), СообщениеОбОшибке);
			
		КонецПопытки;

	ИначеЕсли ТипПредиката = Тип("Структура") Тогда
		
		// выполняем поиск области по условиям
		// на текущий момент выполняем только поиск
		// первой ячейки (области), содержащей заданный текст
		
		Если ПредикатОбласти.Свойство("Текст") Тогда
			
			СообщениеОбОшибке = СтрШаблон("Не удалось найти область табличного документа с текстом ""%1""", ПредикатОбласти.Текст);
			
			// для выполнения поиска в табличном документе необходимо перейти от элемента к реквизиту формы
			// при этом возможности получить серверный контекст формы у нас нет
			//	
			// TODO:
			// возможно в будущем необходимо будет делать более хитро как-то:
			// например передавать и имя элемента и имя реквизита
			// по имени элемента проверять всякие там доступности и т.п.
			// а по реквизиту уже более низкоуровневые вещи (типа поиска)
			
			Попытка
			
				Реквизит = РеквизитФормы(ФормаВладелец, ТабличныйДокумент.Имя);
				
				// TODO: пока ищем по всему документу
				// с начала табличного документа
				// с обходом по строкам
				//
				// реализовать более гибкие возможности фильтрации (?)
			
				Результат = Реквизит.НайтиТекст(ПредикатОбласти.Текст, , , , Истина, , Истина);
								
			Исключение
				
				СообщениеОбОшибке = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), СообщениеОбОшибке);
				
			КонецПопытки;
			
		Иначе
			
			СообщениеОбОшибке = "Не заданы условия поиска ячейки табличного документа";
			
		КонецЕсли;
		
	Иначе
		
		СообщениеОбОшибке = СтрШаблон("Не поддерживаемый формат указания ячейки: ""%1""", ПредикатОбласти);
		
	КонецЕсли;
	
	Если ТипЗнч(Результат) <> Тип("ОбластьЯчеекТабличногоДокумента") Тогда
				
		ВызватьИсключение СообщениеОбОшибке;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получить элемент табличного документа формы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в рамках которой определяется элемент табличного документа
//  ИмяЭлемента - Неопределено, Строка - имя элемента формы, отображающей табличный документ (Неопределено - используется текущий элемент формы) 
// 
// Возвращаемое значение:
//  ПолеФормы - элемент формы (расширение поле формы для табличного документа)
Функция ПолучитьЭлементТабличногоДокументаФормы(Форма, ИмяЭлемента)
	
	ОпределяемТекущийЭлемент = Не ЗначениеЗаполнено(ИмяЭлемента);
	ТекущийЭлемент = ?(ОпределяемТекущийЭлемент, Форма.ТекущийЭлемент, Форма.Элементы.Найти(ИмяЭлемента));
		
	Если ТекущийЭлемент = Неопределено Тогда
		
		Если ОпределяемТекущийЭлемент Тогда
			
			ВызватьИсключение СтрШаблон("Не удалось определить текущий элемент формы (""%1"")", Форма.ИмяФормы);
		
		Иначе
			
			ВызватьИсключение СтрШаблон("Не удалось найти элемент (""%1"") формы (""%2"")", ИмяЭлемента, Форма.ИмяФормы);
			
		КонецЕсли;
			
	ИначеЕсли Не ЭтоПолеТабличногоДокумента(ТекущийЭлемент) Тогда
		
		ВызватьИсключение СтрШаблон("Элемент (""%1"") формы (""%2"") не является табличным документом", ТекущийЭлемент.Имя, Форма.ИмяФормы);
		
	КонецЕсли;
	
	Возврат ТекущийЭлемент;
	
КонецФункции

Функция ЭтоПолеТабличногоДокумента(Элемент)
	
	Возврат ТипЗнч(Элемент) = Тип("ПолеФормы")
		И Элемент.Вид = ВидПоляФормы.ПолеТабличногоДокумента;
	
КонецФункции

Функция ОпределитьАктивнуюФорму()

	ТекущееОкно = АктивноеОкно(); // version_lock (8.3.20)
	Результат = Неопределено;
	
	Если ТекущееОкно = Неопределено Тогда
		
		ВызватьИсключение "Не удалось определить активное окно клиента тестирования";
		
	КонецЕсли;

	КоличествоФормОкна = ТекущееОкно.Содержимое.Количество();

	Если КоличествоФормОкна = 1 Тогда
		
		Результат = ТекущееОкно.Содержимое[0]; // окно содержит единственную форму - она и будет "активной"
		
	ИначеЕсли КоличествоФормОкна > 1 Тогда
	
		// попытаемся найти активную форму проверяя доступность ввода
		// работает не всегда надежно - при потере фокуса приложением
		// клиента тестирования будет возвращать ложь -
		// в таком случае будем детектировать ошибку
	
		Для Каждого ФормаТекущегоОкна Из ТекущееОкно.Содержимое Цикл
			
			Если ФормаТекущегоОкна.ВводДоступен() Тогда
				
				Результат = ФормаТекущегоОкна;
				Прервать;
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		
		ВызватьИсключение "Не удалось определить активную форму клиента тестирования";
		
	КонецЕсли;
			
	Возврат Результат;
	                  	
КонецФункции

Функция РеквизитФормы(Форма, ИмяРеквизита)
	
	Результат = Неопределено;
	
	Попытка
		
		РеквизитыФормы = Новый Структура(ИмяРеквизита);
		ЗаполнитьЗначенияСвойств(РеквизитыФормы, Форма);
		
		Если ТипЗнч(РеквизитыФормы[ИмяРеквизита]) <> Тип("ТабличныйДокумент") Тогда
			
			ВызватьИсключение "Не найден реквизит с типом табличный документ";
		
		КонецЕсли;
		
		Результат = РеквизитыФормы[ИмяРеквизита];
		
	Исключение
		
		ВызватьИсключение СтрШаблон("Не удалось получить реквизит (табличный документ) формы ""%1"" по имени ""%2""", Форма.ИмяФормы, ИмяРеквизита);
				
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

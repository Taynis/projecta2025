//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Если Не ВебКлиент Тогда
#Область СлужебныйПрограммныйИнтерфейс

// Выполняет HTTP запрос с заданными параметрами и возвращает результат его выполнения.
//
// Параметры:
//  ОписаниеЗапроса - см. РатВызовСервисаКлиент.ОписаниеЗапроса
//
// Возвращаемое значение:
//  см. РатШагиВанессыКлиентСервер.ДанныеОтвета
//
// Пример:
//  ОписаниеЗапроса = РатВызовСервисаКлиент.ОписаниеЗапроса(НастройкиПодключения, "GET", "/api/v1/users");
//  Ответ = РатКоннекторHTTP.ВыполнитьЗапрос(ОписаниеЗапроса);
Функция ВыполнитьЗапрос(ОписаниеЗапроса) Экспорт
	
	ДанныеОтвета = РатШагиВанессыКлиентСервер.ДанныеОтвета();
	
	Попытка
		
		Соединение = Соединение(ОписаниеЗапроса.НастройкиПодключения);
		Запрос = Запрос(ОписаниеЗапроса);
		
		Ответ = Соединение.ВызватьHTTPМетод(ОписаниеЗапроса.Метод, Запрос);
		
		ДанныеОтвета.Успешно = Истина;
		ДанныеОтвета.КодСостояния = Ответ.КодСостояния;
		ДанныеОтвета.Заголовки = Ответ.Заголовки;
		ДанныеОтвета.Тело = Ответ.ПолучитьТелоКакСтроку();
		УстановитьРасшифровкуКодаСостояния(ДанныеОтвета);
		
	Исключение
		
		ДанныеОтвета.Успешно = Ложь;
		ДанныеОтвета.Вставить("Ошибка", ИнформацияОбОшибке());
		
	КонецПопытки;
	
	//@skip-check constructor-function-return-section
	Возврат ДанныеОтвета;
	
КонецФункции

// Преобразует HTTP ответ в структуру с данными.
//
// Параметры:
//  Ответ - HTTPОтвет - Ответ HTTP запроса
//
// Возвращаемое значение:
//  см. РатШагиВанессыКлиентСервер.ДанныеОтвета
//
Функция ОтветВСтруктуру(Ответ) Экспорт
	
	ДанныеОтвета = РатШагиВанессыКлиентСервер.ДанныеОтвета();
	ДанныеОтвета.Успешно = Истина;
	ДанныеОтвета.КодСостояния = Ответ.КодСостояния;
	ДанныеОтвета.Заголовки = Ответ.Заголовки;
	ДанныеОтвета.Тело = Ответ.ПолучитьТелоКакСтроку();
	УстановитьРасшифровкуКодаСостояния(ДанныеОтвета);
	
	Возврат ДанныеОтвета;
	
КонецФункции

// Устанавливает расшифровку кода состояния HTTP в данные ответа
//
// Параметры:
//  ДанныеОтвета - Структура - Данные ответа HTTP запроса, в которые будет добавлена расшифровка кода
//
Процедура УстановитьРасшифровкуКодаСостояния(ДанныеОтвета) Экспорт
	
	ДанныеОтвета.РасшифровкаКода = РасшифровкаКодаСостоянияHTTP(ДанныеОтвета.КодСостояния);
	
КонецПроцедуры

// Разбирает URL на составные части
//
// Параметры:
//  URL - Строка - URL для разбора
//
// Возвращаемое значение:
//  Структура - Составные части URL:
//   * Схема - Строка - Протокол (http/https)
//   * Аутентификация - см. РатКоннекторHTTP.ПараметрыАутентификации
//   * Сервер - Строка - Имя сервера
//   * Порт - Число - Порт
//   * Путь - Строка - Путь
//   * Фрагмент - Строка - Фрагмент URL
//
// Пример:
//  ЧастиURL = РатКоннекторHTTP.РазобратьURL("https://user:pass@example.com:8080/path?query#fragment");
Функция РазобратьURL(Знач URL) Экспорт
	
	// Источник: https://github.com/vbondarevsky/Connector
	
	Схема = "";
	Путь = "";
	Аутентификация = ПараметрыАутентификации();
	Сервер = "";
	Порт = "";
	Фрагмент = "";
	
	ДопустимыеСхемы = СтрРазделить("http,https", ",");
	
	URLБезСхемы = URL;
	РазбитьСтрокуПоРазделителю(Схема, URLБезСхемы, "://");
	Если ДопустимыеСхемы.Найти(НРег(Схема)) <> Неопределено Тогда
		URL = URLБезСхемы;
	Иначе
		Схема = "";
	КонецЕсли;
	
	Результат = РазделитьПоПервомуНайденномуРазделителю(URL, СтрРазделить("/,?,#", ","));
	URL = Результат[0];
	Если ЗначениеЗаполнено(Результат[2]) Тогда
		Путь = Результат[2] + Результат[1];
	КонецЕсли;
	
	АутентификацияСтрока = "";
	РазбитьСтрокуПоРазделителю(АутентификацияСтрока, URL, "@");
	Если ЗначениеЗаполнено(АутентификацияСтрока) Тогда
		АутентификацияЧасти = СтрРазделить(АутентификацияСтрока, ":");
		Аутентификация.Пользователь = АутентификацияЧасти[0];
		Если АутентификацияЧасти.Количество() > 1 Тогда
			Аутентификация.Пароль = АутентификацияЧасти[1];
		КонецЕсли;
	КонецЕсли;
	
	// IPv6
	РазбитьСтрокуПоРазделителю(Сервер, URL, "]");
	Если ЗначениеЗаполнено(Сервер) Тогда
		Сервер = Сервер + "]";
	КонецЕсли;
	
	URL = СтрЗаменить(URL, "/", "");
	
	РазбитьСтрокуПоРазделителю(Порт, URL, ":", Истина);
	
	Если Не ЗначениеЗаполнено(Сервер) Тогда
		Сервер = URL;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Порт) Тогда
		Порт = Число(Порт);
	Иначе
		Порт = 0;
	КонецЕсли;
	
	РазбитьСтрокуПоРазделителю(Фрагмент, Путь, "#", Истина);
	
	Если Не ЗначениеЗаполнено(Схема) Тогда
		Схема = "http";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Путь) Тогда
		Путь = "/";
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Аутентификация", Аутентификация);
	Результат.Вставить("Сервер", Сервер);
	Результат.Вставить("Порт", Порт);
	Результат.Вставить("Путь", Путь);
	Результат.Вставить("Фрагмент", Фрагмент);
	
	//@skip-check constructor-function-return-section
	Возврат Результат;
	
КонецФункции

// Возвращает структуру с параметрами аутентификации для HTTP запроса.
//
// Возвращаемое значение:
//  Структура - Параметры аутентификации:
//   * Пользователь - Строка - Имя пользователя
//   * Пароль - Строка - Пароль пользователя
//
Функция ПараметрыАутентификации() Экспорт
	
	Возврат Новый Структура("Пользователь, Пароль", "", "");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Создает HTTP соединение с заданными параметрами.
//
// Параметры:
//  НастройкиПодключения - см. РатШагиВанессыКлиентСервер.ОписаниеНастройкиПодключения
//
// Возвращаемое значение:
//  HTTPСоединение - Объект соединения с HTTP сервером
//
Функция Соединение(НастройкиПодключения)
	
	Порт = НастройкиПодключения.Порт;
	ИспользоватьЗащищенноеСоединение = СтрСравнить(НастройкиПодключения.Схема, "https") = 0;
	Если НЕ ЗначениеЗаполнено(Порт) Тогда
		Порт = ?(ИспользоватьЗащищенноеСоединение, 443, 80);
	КонецЕсли;
	
	Если ИспользоватьЗащищенноеСоединение Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	Прокси = Прокси(НастройкиПодключения);
	
	Возврат Новый HTTPСоединение(НастройкиПодключения.Сервер,
								 Порт,
								 НастройкиПодключения.Пользователь,
								 НастройкиПодключения.Пароль,
								 Прокси,
								 НастройкиПодключения.Таймаут,
								 ЗащищенноеСоединение);
	
КонецФункции

// Создает прокси для HTTP запроса.
//
// Параметры:
//  НастройкиПодключения - см. РатШагиВанессыКлиентСервер.ОписаниеНастройкиПодключения
//
// Возвращаемое значение:
//  ИнтернетПрокси - Прокси для HTTP запроса
//
Функция Прокси(НастройкиПодключения)
	
	Если НЕ НастройкиПодключения.Прокси.Использовать Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НастройкиПрокси = НастройкиПодключения.Прокси;
	Прокси = Новый ИнтернетПрокси(НастройкиПрокси.ПроксиПоУмолчанию);
	
	Прокси.Установить(НастройкиПодключения.Схема,
					  НастройкиПрокси.Сервер,
					  НастройкиПрокси.Порт,
					  НастройкиПрокси.Пользователь,
					  НастройкиПрокси.Пароль,
					  НастройкиПрокси.АутентификацияОС);
	
	Возврат Прокси;
	
КонецФункции

// Создает HTTP запрос с заданными параметрами.
//
// Параметры:
//  ОписаниеЗапроса - см. РатВызовСервисаКлиент.ОписаниеЗапроса
//
// Возвращаемое значение:
//  HTTPЗапрос - Объект HTTP запроса с настроенными параметрами
//
Функция Запрос(ОписаниеЗапроса)
	
	АдресРесурса = РатСтроки.ОбъединитьПути("/", ОписаниеЗапроса.НастройкиПодключения.Ресурс, ОписаниеЗапроса.АдресРесурса);
	
	ПараметрыЗапроса = КоллекцияПараметровВСтроку(ОписаниеЗапроса.ПараметрыЗапроса);
	АдресРесурса = РатСтроки.ДобавитьСтроку(АдресРесурса, ПараметрыЗапроса, "?");
	
	Запрос = Новый HTTPЗапрос();
	Запрос.АдресРесурса = АдресРесурса;
	
	Если ЗначениеЗаполнено(ОписаниеЗапроса.Тело) Тогда
		
		Если ТипЗнч(ОписаниеЗапроса.Тело) = Тип("Строка") Тогда
			Запрос.УстановитьТелоИзСтроки(ОписаниеЗапроса.Тело);
		Иначе
			Запрос.УстановитьТелоИзСтроки(РатJSON.ОбъектВСтроку(ОписаниеЗапроса.Тело));
			Запрос.Заголовки.Вставить("Content-Type", "application/json");
		КонецЕсли;
		
	КонецЕсли;
	
	РатКоллекции.ОбъединитьВСоответствие(Запрос.Заголовки, ОписаниеЗапроса.НастройкиПодключения.Заголовки);
	
	Если ОписаниеЗапроса.Свойство("Заголовки") Тогда
		РатКоллекции.ОбъединитьВСоответствие(Запрос.Заголовки, ОписаниеЗапроса.Заголовки);
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

// Преобразует коллекцию параметров в строку для URL запроса.
//
// Параметры:
//  КоллекцияПараметров - Соответствие из Строка, Число - Коллекция параметров запроса
//
// Возвращаемое значение:
//  Строка - Параметры запроса в формате "ключ1=значение1&ключ2=значение2"
//
Функция КоллекцияПараметровВСтроку(КоллекцияПараметров)
	
	СтрокаПараметров = "";
	
	Если НЕ ЗначениеЗаполнено(КоллекцияПараметров) Тогда
		Возврат СтрокаПараметров;
	КонецЕсли;
	
	Для Каждого Элемент Из КоллекцияПараметров Цикл
		
		ЗначениеПараметра = Элемент.Значение;
		
		Если ТипЗнч(ЗначениеПараметра) = Тип("Число") Тогда
			ЗначениеПараметра = Формат(ЗначениеПараметра, "ЧГ=");
		КонецЕсли;
		
		ЗначениеПараметра = ЗакодироватьСтрокуURL(Элемент.Ключ) + "=" + ЗакодироватьСтрокуURL(ЗначениеПараметра);
		СтрокаПараметров = РатСтроки.ДобавитьСтроку(СтрокаПараметров, ЗначениеПараметра, "&");
		
	КонецЦикла;
	
	Возврат СтрокаПараметров;
	
КонецФункции

// Кодирует строку для использования в URL.
//
// Параметры:
//  URL - Строка - Строка для кодирования
//
// Возвращаемое значение:
//  Строка - Закодированная строка в формате URL
//
Функция ЗакодироватьСтрокуURL(URL)
	
#Если Клиент Тогда
	СтрокаВозвратаURL = "";
	ДлинаURL = СтрДлина(URL);
	
	Для НомерСимвола = 1 По ДлинаURL Цикл
		
		КодОчередногоСимвола = КодСимвола(URL, НомерСимвола);
		
		// Получим код в кодировке Utf8 вместе со специальным знаком процента
		СтрокаВозвратаURL = СтрокаВозвратаURL + UnicodeToUtf8(КодОчередногоСимвола);
		
	КонецЦикла;
#Иначе
	СтрокаВозвратаURL = КодироватьСтроку(URL, СпособКодированияСтроки.КодировкаURL);
#КонецЕсли
	
	Возврат СтрокаВозвратаURL;
	
КонецФункции

// Преобразует Unicode символ в UTF-8 последовательность.
//
// Параметры:
//  Unicode - Число - Код Unicode символа
//
// Возвращаемое значение:
//  Строка - UTF-8 последовательность в формате URL (с префиксом %)
//
Функция UnicodeToUtf8(Знач Unicode)
	// BSLLS:MagicNumber-off
	Bin = 0;
	ПрефиксПродолжения = "%10";
	
	Если Unicode <= 127 Тогда
		Bin = ПеревестиВСистему(Unicode, 2, 8);
	ИначеЕсли (Unicode >= 128) И (Unicode <= 2047) Тогда
		Bin = ПеревестиВСистему(Unicode, 2, 11);
		Bin = "110" + Сред(Bin, 1, 5)
			+ ПрефиксПродолжения + Сред(Bin, 6);
	ИначеЕсли (Unicode >= 2048) И (Unicode <= 65535) Тогда
		Bin = ПеревестиВСистему(Unicode, 2, 16);
		Bin = "1110" + Сред(Bin, 1, 4)
			+ ПрефиксПродолжения + Сред(Bin, 5, 6)
			+ ПрефиксПродолжения + Сред(Bin, 11);
	ИначеЕсли (Unicode >= 65536) И (Unicode <= 2097151) Тогда
		Bin = ПеревестиВСистему(Unicode, 2, 21);
		Bin = "11110" + Сред(Bin, 1, 3)
			+ ПрефиксПродолжения + Сред(Bin, 4, 6)
			+ ПрефиксПродолжения + Сред(Bin, 10, 6)
			+ ПрефиксПродолжения + Сред(Bin, 16);
	ИначеЕсли (Unicode >= 2097152) И (Unicode <= 67108863) Тогда
		Bin = ПеревестиВСистему(Unicode, 2, 26);
		Bin = "111110" + Сред(Bin, 1, 2)
			+ ПрефиксПродолжения + Сред(Bin, 3, 6)
			+ ПрефиксПродолжения + Сред(Bin, 9, 6)
			+ ПрефиксПродолжения + Сред(Bin, 15, 6)
			+ ПрефиксПродолжения + Сред(Bin, 21);
	ИначеЕсли (Unicode >= 67108864) И (Unicode <= 2147483647) Тогда
		Bin = ПеревестиВСистему(Unicode, 2, 31);
		Bin = "1111110" + Сред(Bin, 1, 1)
			+ ПрефиксПродолжения + Сред(Bin, 2, 6)
			+ ПрефиксПродолжения + Сред(Bin, 8, 6)
			+ ПрефиксПродолжения + Сред(Bin, 14, 6)
			+ ПрефиксПродолжения + Сред(Bin, 20, 6)
			+ ПрефиксПродолжения + Сред(Bin, 26);
	КонецЕсли;
	
	Utf8 = "";
	НомерСимвола = 1;
	Пока НомерСимвола < СтрДлина(Bin) Цикл
		
		ДвоичноеЧисло = Сред(Bin, НомерСимвола, 8);
		ДесятичноеЧисло = 0;
		
		Для Степень = 0 По 7 Цикл
			ДесятичноеЧисло = ДесятичноеЧисло + Число(Сред(ДвоичноеЧисло, Степень + 1, 1)) * Pow(2, 7 - Степень);
		КонецЦикла;
		
		Hex = ПеревестиВСистему(ДесятичноеЧисло, 16, 2);
		
		Utf8 = Utf8 + "%" + Hex;
		
		НомерСимвола = НомерСимвола + 9;
	КонецЦикла;
	// BSLLS:MagicNumber-on
	
	Возврат Utf8;
	
КонецФункции // UnicodeToUtf8

// Разбивает строку на две части по первому вхождению разделителя.
//
// Параметры:
//  ИзвлекаемаяЧасть - Строка - Первая часть строки до разделителя
//  ОстальнаяЧасть - Строка - Вторая часть строки после разделителя
//  Разделитель - Строка - Строка-разделитель
//  Инверсия - Булево - Признак инвертирования результата (по умолчанию Ложь)
//
Процедура РазбитьСтрокуПоРазделителю(ИзвлекаемаяЧасть, ОстальнаяЧасть, Разделитель, Инверсия = Ложь)

	Индекс = СтрНайти(ОстальнаяЧасть, Разделитель);
	Если Индекс Тогда
		ИзвлекаемаяЧасть = Лев(ОстальнаяЧасть, Индекс - 1);
		ОстальнаяЧасть = Сред(ОстальнаяЧасть, Индекс + СтрДлина(Разделитель));
		Если Инверсия Тогда
			ДляОбмена = ИзвлекаемаяЧасть;
			ИзвлекаемаяЧасть = ОстальнаяЧасть;
			ОстальнаяЧасть = ДляОбмена;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Разделяет строку по первому найденному разделителю из списка.
//
// Параметры:
//  Строка - Строка - Исходная строка
//  Разделители - Массив из Строка - Массив строк-разделителей
//
// Возвращаемое значение:
//  Массив из Строка - Результат разделения:
//   [0] - Строка - Часть строки до разделителя
//   [1] - Строка - Часть строки после разделителя
//   [2] - Строка - Найденный разделитель или Неопределено, если разделитель не найден
//
Функция РазделитьПоПервомуНайденномуРазделителю(Строка, Разделители)

	МинимальныйИндекс = СтрДлина(Строка);
	ПервыйРазделитель = "";

	Для Каждого Разделитель Из Разделители Цикл
		Индекс = СтрНайти(Строка, Разделитель);
		Если Индекс = 0 Тогда
			Продолжить;
		КонецЕсли;
		Если Индекс < МинимальныйИндекс Тогда
			МинимальныйИндекс = Индекс;
			ПервыйРазделитель = Разделитель;
		КонецЕсли;
	КонецЦикла;

	Результат = Новый Массив;
	Если ЗначениеЗаполнено(ПервыйРазделитель) Тогда
		Результат.Добавить(Лев(Строка, МинимальныйИндекс - 1));
		Результат.Добавить(Сред(Строка, МинимальныйИндекс + СтрДлина(ПервыйРазделитель)));
		Результат.Добавить(ПервыйРазделитель);
	Иначе
		Результат.Добавить(Строка);
		Результат.Добавить("");
		Результат.Добавить(Неопределено);
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает расшифровку HTTP кода состояния.
//
// Параметры:
//  КодСостояния - Число - HTTP код состояния
//
// Возвращаемое значение:
//  Строка - Расшифровка кода состояния в формате "[код] описание"
//
Функция РасшифровкаКодаСостоянияHTTP(КодСостояния)
	
	// BSLLS:MagicNumber-off
	КодыСостояния = Новый Соответствие;
	КодыСостояния.Вставить(200, "Запрос выполнился успешно");
	КодыСостояния.Вставить(304, "Нет необходимости повторно передавать запрошенные ресурсы.");
	КодыСостояния.Вставить(400, "Запрос не может быть исполнен.");
	КодыСостояния.Вставить(401, "Попытка авторизации на сервере была отклонена.");
	КодыСостояния.Вставить(402, "Требуется оплата.");
	КодыСостояния.Вставить(403, "К запрашиваемому ресурсу нет доступа.");
	КодыСостояния.Вставить(404, "Запрашиваемый ресурс не найден на сервере.");
	КодыСостояния.Вставить(405, "Метод запроса не поддерживается сервером.");
	КодыСостояния.Вставить(406, "Запрошенный формат данных не поддерживается сервером.");
	КодыСостояния.Вставить(407, "Ошибка аутентификации на прокси-сервере");
	КодыСостояния.Вставить(408, "Время ожидания сервером передачи от клиента истекло.");
	КодыСостояния.Вставить(409, "Запрос не может быть выполнен из-за конфликтного обращения к ресурсу.");
	КодыСостояния.Вставить(410, "Ресурс на сервере был перемешен.");
	КодыСостояния.Вставить(411, "Сервер требует указание ""Content-length."" в заголовке запроса.");
	КодыСостояния.Вставить(412, "Запрос не применим к ресурсу");
	КодыСостояния.Вставить(413, "Сервер отказывается обработать, слишком большой объем передаваемых данных.");
	КодыСостояния.Вставить(414, "Сервер отказывается обработать, слишком длинный URL.");
	КодыСостояния.Вставить(415, "Сервер заметил, что часть запроса была сделана в неподдерживаемом формате.");
	КодыСостояния.Вставить(416, "Часть запрашиваемого ресурса не может быть предоставлена");
	КодыСостояния.Вставить(417, "Сервер не может предоставить ответ на указанный запрос.");
	КодыСостояния.Вставить(429, "Слишком много запросов за короткое время.");
	КодыСостояния.Вставить(500, "Внутренняя ошибка сервера.");
	КодыСостояния.Вставить(501, "Сервер не поддерживает метод запроса.");
	КодыСостояния.Вставить(502, "Сервер, выступая в роли шлюза или прокси-сервера, получил недействительное "
		+ "ответное сообщение от вышестоящего сервера.");
	КодыСостояния.Вставить(503, "Сервер временно не доступен.");
	КодыСостояния.Вставить(504, "Сервер в роли шлюза или прокси-сервера не дождался ответа от вышестоящего сервера "
		+ "для завершения текущего запроса.");
	КодыСостояния.Вставить(505, "Сервер не поддерживает указанную в запросе версию протокола HTTP");
	КодыСостояния.Вставить(506, "Сервер настроен некорректно, и не способен обработать запрос.");
	КодыСостояния.Вставить(507, "На сервере недостаточно места для выполнения запроса.");
	КодыСостояния.Вставить(509, "Сервер превысил отведенное ограничение на потребление трафика.");
	КодыСостояния.Вставить(510, "Сервер требует больше информации о совершаемом запросе.");
	КодыСостояния.Вставить(511, "Требуется авторизация на сервере.");
	// BSLLS:MagicNumber-on
	
	Ответ = КодыСостояния.Получить(КодСостояния);
	
	Ответ = ?(Ответ = Неопределено, "Неизвестный код состояния", Ответ);
	
	Возврат СтрШаблон(НСтр("ru = '[%1] %2'"), КодСостояния, Ответ);
	
КонецФункции

// Преобразует число в строку в указанной системе счисления.
//
// Параметры:
//  Unicode - Число - Исходное число
//  Base - Число - Основание системы счисления (2-16)
//  Length - Число - Минимальная длина результата (по умолчанию Неопределено)
//
// Возвращаемое значение:
//  Строка - Число в указанной системе счисления
//
Функция ПеревестиВСистему(Знач Unicode, Base, Length = Неопределено)
	
	Если Unicode = 0 Тогда
		Возврат "0";
	КонецЕсли;
	
	Соответствие = Новый Соответствие;
	Для Номер = 0 По 9 Цикл
		Соответствие.Вставить(Номер, Строка(Номер));
	КонецЦикла;
	// BSLLS:MagicNumber-off
	Соответствие.Вставить(10, "A");
	Соответствие.Вставить(11, "B");
	Соответствие.Вставить(12, "C");
	Соответствие.Вставить(13, "D");
	Соответствие.Вставить(14, "E");
	Соответствие.Вставить(15, "F");
	
	Результат = "";
	Пока Unicode <> 0 Цикл
		Остаток = Unicode - (Окр((Unicode / Base) - 0.5) * Base);
		Результат = Соответствие.Получить(Остаток) + Результат;
		Unicode = Окр((Unicode / Base) - 0.5);
	КонецЦикла;
	// BSLLS:MagicNumber-on
	
	Если Length <> Неопределено Тогда
		Пока Length > СтрДлина(Результат) Цикл
			Результат = "0" + Результат;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
#КонецЕсли

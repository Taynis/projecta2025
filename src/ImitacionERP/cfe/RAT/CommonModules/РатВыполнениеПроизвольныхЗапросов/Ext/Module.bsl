//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание сервиса в спецификации OpenAPI.
// Описывает операции для работы с произвольными алгоритмами.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	БазовыеСхемы = РатСпецификация.БазовыеСхемы();
	
	Путь = "/ВыполнитьАлгоритм";
	Группа = "Выполнение алгоритмов";
	РатСпецификация.ДобавитьГруппу(СпецификацияСервиса, Группа);
	
	СхемаМассиваПараметров = РатOpenAPI.НоваяСхемаКоллекции(РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ВходнаяСсылкаСоставное),
															"Массив типизированных параметров алгоритма");
	СхемаСтруктурыПараметров = РатOpenAPI.НоваяСхемаСтруктуры("Структура типизированных параметров алгоритма");
	СхемаПараметров = РатOpenAPI.НоваяСхемаОбъединения("Параметры алгоритма",
													   СхемаМассиваПараметров,
													   СхемаСтруктурыПараметров);
	
	ОписаниеТелаЗапроса = РатСпецификация.ОписаниеРеквизитов();
	РатСпецификация.ДобавитьРеквизит(ОписаниеТелаЗапроса, "Алгоритм", "Выполняемый алгоритм", Тип("Строка"), Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеТелаЗапроса,
									 "Параметры",
									 "Параметры алгоритма",
									 СхемаПараметров,
									 Истина,
									 Ложь);
	
	СхемаЗапроса = РатСпецификация.СхемаПоОписаниюОбъекта(ОписаниеТелаЗапроса, "Схема запроса выполнения произвольного алгоритма");
	
	// Описание операции
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"ВыполнитьАлгоритм",
													"Выполняет произвольный алгоритма с указанными параметрами",
													Группа,
													Путь,
													Методы.POST);
	
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаЗапроса);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, РатOpenAPI.СсылкаНаСхему(БазовыеСхемы.ОтветУспешно));
	
КонецПроцедуры

// Регистрирует шаблоны адресов для обработки запросов.
// Регистрирует шаблон для выполнения произвольного алгоритма.
//
// Параметры:
//   ШаблоныСервиса - см. РатШаблоныАдресов.Шаблоны
//
Процедура ЗарегистрироватьШаблоны(ШаблоныСервиса) Экспорт
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Обработчики = Обработчики();
	
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/ВыполнитьАлгоритм",
									 Обработчики.ВыполнитьПроизвольныйАлгоритм,
									 0);
	
КонецПроцедуры

// Вызывает обработчик запроса по его идентификатору.
// Обрабатывает запросы на выполнение произвольного алгоритма.
//
// Параметры:
//   Обработчик - Строка - Идентификатор обработчика, см. Обработчики
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//   ОбработчикВыполнен - Булево - Необходимо установить если это "наш" запрос
//
// Возвращаемое значение:
//   Произвольный - Сериализуемый результат обработки запроса
Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
	
	Обработчики = Обработчики();
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.ВыполнитьПроизвольныйАлгоритм Тогда
		
		Результат = ОбработчикВыполнитьПроизвольныйАлгоритм(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Обработчики

// Возвращает структуру с идентификаторами обработчиков запросов.
// Содержит идентификаторы обработчиков для выполнения произвольных алгоритмов.
//
// Возвращаемое значение:
//   ФиксированнаяСтруктура - Структура с идентификаторами обработчиков:
//     * ВыполнитьПроизвольныйАлгоритм - Строка - Идентификатор обработчика произвольного алгоритма
//
Функция Обработчики()
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("ВыполнитьПроизвольныйАлгоритм", "ВыполнитьПроизвольныйАлгоритм");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

// Обрабатывает запрос на выполнение произвольного алгоритма.
// Выполняет произвольный алгоритм с заданными параметрами в безопасном режиме.
//
// Параметры:
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Структура - Результат выполнения алгоритма:
//     * success - Булево - Признак успешного выполнения
//     * messages - Массив из Строка - Сообщения пользователю
//
Функция ОбработчикВыполнитьПроизвольныйАлгоритм(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	СырыеПараметры = РатКоллекции.ЗначениеСтруктуры(ПараметрыЗапроса.Тело, "Параметры");
	Параметры = ПодготовленныеПараметры(СырыеПараметры, СтатусОбработкиЗапроса);
	
	РатАлгоритмы.ВыполнитьАлгоритм(ПараметрыЗапроса.Тело.Алгоритм, Параметры);
	
	Сообщения = ПолучитьСообщенияПользователю();
	
	ТекстСообщений = РатКоллекции.ВыгрузитьЗначения(Сообщения, "Текст");
	
	Возврат Новый Структура("success, messages", Истина, ТекстСообщений);
	
КонецФункции

#КонецОбласти

// Подготавливает параметры для выполнения алгоритма.
// Преобразует параметры в формат, пригодный для выполнения алгоритма.
//
// Параметры:
//   СырыеПараметры - Произвольный - Исходные параметры
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Произвольный - Подготовленные параметры
//
Функция ПодготовленныеПараметры(СырыеПараметры, СтатусОбработкиЗапроса)
	
	Если НЕ ЗначениеЗаполнено(СырыеПараметры) Тогда
		Возврат СырыеПараметры;
	КонецЕсли;
	
	Возврат ПреобразованноеЗначение(СырыеПараметры, СтатусОбработкиЗапроса);
	
КонецФункции

// Преобразует значение в соответствии с его типом.
// Выполняет преобразование значений различных типов для корректного выполнения алгоритма.
//
// Параметры:
//   СыроеЗначение - Произвольный - Исходное значение
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Произвольный - Преобразованное значение
//
Функция ПреобразованноеЗначение(СыроеЗначение, СтатусОбработкиЗапроса)
	
	Тип = ТипЗнч(СыроеЗначение);
	
	Если Тип = Тип("Массив") Тогда
		Возврат ПреобразованныйМассив(СыроеЗначение, СтатусОбработкиЗапроса);
	ИначеЕсли Тип <> Тип("Структура") Тогда
		Возврат СыроеЗначение;
	КонецЕсли;
	
	ТипИЗначение = РатПреобразования.ИзвлечьТипИЗначение(СыроеЗначение);
	
	Если НЕ ЗначениеЗаполнено(ТипИЗначение.ПредставлениеТипа) Тогда
		Результат = ПреобразованнаяСтруктура(СыроеЗначение, СтатусОбработкиЗапроса);
	Иначе
		Результат = РатПреобразования.ДесериализоватьТипизированноеЗначение(СыроеЗначение, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Преобразует структуру значений.
// Рекурсивно преобразует все значения в структуре.
//
// Параметры:
//   СырыеЗначения - Структура - Исходная структура
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Структура - Преобразованная структура
//
Функция ПреобразованнаяСтруктура(СырыеЗначения, СтатусОбработкиЗапроса)
	
	Результат = Новый Структура;
	
	Для Каждого СыроеЗначение Из СырыеЗначения Цикл
		Результат.Вставить(СыроеЗначение.Ключ, ПреобразованноеЗначение(СыроеЗначение.Значение, СтатусОбработкиЗапроса));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Преобразует массив значений.
//
// Параметры:
//   СырыеЗначения - Массив из Произвольный - Исходный массив
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Массив из Произвольный - Преобразованный массив
//
Функция ПреобразованныйМассив(СырыеЗначения, СтатусОбработкиЗапроса)
	
	Результат = Новый Массив;
	
	Для Каждого СыроеЗначение Из СырыеЗначения Цикл
		Результат.Добавить(ПреобразованноеЗначение(СыроеЗначение, СтатусОбработкиЗапроса));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

/////////////////////////////////////////////////////////////////////////////////
// Коллекции
//  Набор методов для работы с коллекциями данных
/////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс
// Возвращает массив значений поля элементов коллекции.
//
// Параметры:
//   Коллекция - Произвольный - значение, для которого определен итератор, и возможно обращение к полям элементов через квадратные скобки.
//                Также можно передать запрос, результат запроса или выборку.
//   ИмяРеквизита - Строка, Число - имя поля элемента коллекции, значения которого необходимо выгрузить. Также можно использовать индекс поля.
//
// Возвращаемое значение:
//   Массив из Произвольный- коллекция значений поля.
//
// Пример:
//   МассивЗначений = РатКоллекции.ВыгрузитьЗначения(СписокПользователей, "Имя");
Функция ВыгрузитьЗначения(Знач Коллекция, Знач ИмяРеквизита) Экспорт
	
	Результат = Новый Массив();
	
#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
	
	Если ТипЗнч(Коллекция) = Тип("Запрос") Тогда
		
		Коллекция = Коллекция.Выполнить();
		
	КонецЕсли;
	
	Если ТипЗнч(Коллекция) = Тип("РезультатЗапроса") Тогда
		
		Если НЕ Коллекция.Пустой() Тогда
			
			Коллекция = Коллекция.Выбрать();
			
		Иначе
			
			Коллекция = Новый Массив();
			
		КонецЕсли;
		
	КонецЕсли;
		
	Если ТипЗнч(Коллекция) = Тип("ВыборкаИзРезультатаЗапроса") Тогда
		
		Пока Коллекция.Следующий() Цикл
			
			Результат.Добавить(Коллекция[ИмяРеквизита]);
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецЕсли;
	
#КонецЕсли

	Для Каждого ЭлементКоллекции Из Коллекция Цикл
		
		Результат.Добавить(ЭлементКоллекции[ИмяРеквизита]);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Преобразует коллекцию в соответствие, где ключами являются значения указанного поля элементов.
//
// Параметры:
//   Коллекция - Произвольный - значение, для которого определен итератор
//                     и возможно обращение к полям элементов через квадратные скобки
//   ИмяПоляКлюча - Строка - имя поля элемента коллекции, которое будет ключом соответствия.
//   ИмяПоляЗначения - Строка, Неопределено - если указан, значениями результата будут не элементы, 
//                     а значения соответствующих полей элементов коллекции
//
// Возвращаемое значение:
//   Соответствие из Произвольный - полученное соответствие.
//
// Пример:
//   Соответствие = РатКоллекции.ВСоответствие(СписокПользователей, "Код", "Имя");
//   // Результат: {"001": "Иван", "002": "Петр", "003": "Сергей"}
Функция ВСоответствие(Коллекция, ИмяПоляКлюча, ИмяПоляЗначения = Неопределено) Экспорт
	
	Результат = Новый Соответствие();
	
	Для Каждого ЭлементКоллекции Из Коллекция Цикл
		
		Значение = ?(ИмяПоляЗначения = Неопределено, ЭлементКоллекции, ЭлементКоллекции[ИмяПоляЗначения]);
		
		Результат.Вставить(ЭлементКоллекции[ИмяПоляКлюча], Значение);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Создает рекурсивную копию экземпляра указанного объекта.
//
// Параметры:
//   Источник - Произвольный - объект, который необходимо скопировать.
//   ФиксированнаяКоллекцияВОбычную - Булево - если значение равно Истина, то из Фиксированной коллекции формируется обычная.
//
// Возвращаемое значение:
//   Произвольный - копия объекта, переданного в параметре Источник.
//
// Примечание:
//   Функцию нельзя использовать для объектных типов (СправочникОбъект, ДокументОбъект и т.п.).
//
// Пример:
//   КопияСтруктуры = РатКоллекции.СкопироватьРекурсивно(ИсходнаяСтруктура);
Функция СкопироватьРекурсивно(Знач Источник, ФиксированнаяКоллекцияВОбычную = Ложь) Экспорт
	
	Перем Приемник;
	
	СкопироватьПрисвоением = Ложь;
	
	ТипИсточника = ТипЗнч(Источник);
	Если ТипИсточника = Тип("Структура") ИЛИ ТипИсточника = Тип("ФиксированнаяСтруктура") Тогда
		
		Приемник = СкопироватьСтруктуру(Источник, ФиксированнаяКоллекцияВОбычную);
		
	ИначеЕсли ТипИсточника = Тип("Соответствие") ИЛИ ТипИсточника = Тип("ФиксированноеСоответствие") Тогда
		
		Приемник = СкопироватьСоответствие(Источник, ФиксированнаяКоллекцияВОбычную);
		
	ИначеЕсли ТипИсточника = Тип("Массив") ИЛИ ТипИсточника = Тип("ФиксированныйМассив") Тогда
		
		Приемник = СкопироватьМассив(Источник, ФиксированнаяКоллекцияВОбычную);
		
	Иначе
		
		СкопироватьПрисвоением = Истина;
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Если ТипИсточника = Тип("ТаблицаЗначений") Или ТипИсточника = Тип("ДеревоЗначений") Тогда
			
			СкопироватьПрисвоением = Ложь;
			Приемник = Источник.Скопировать();
			
		КонецЕсли;
#КонецЕсли
		
	КонецЕсли;
	
	Если СкопироватьПрисвоением Тогда
		Приемник = Источник;
	КонецЕсли;
	
	Возврат Приемник;
	
КонецФункции // СкопироватьРекурсивно

// Объединяет два массива в один с возможностью контроля уникальности значений.
//
// Параметры:
//   Коллекция1 - Массив из Произвольный- первый массив, участвующий в объединении.
//   Коллекция2 - Массив из Произвольный - второй массив, участвующий в объединении.
//   КонтрольУникальности - Булево - если Истина, то в результате будет массив с уникальными значениями,
//                                  если Ложь, то в результате будет просто объединенный массив.
//   РезультатВФиксированнуюКоллекцию - Булево - если Истина, возвращает фиксированный массив.
//   Дополнить - Булево - если Истина, то добавит элементы Коллекции2 в Коллекцию1.
//
// Возвращаемое значение:
//   Массив из Произвольный, ФиксированныйМассив из Произвольный - результат объединения двух массивов.
//
// Пример:
//   Массив1 = Новый Массив("А", "Б", "В");
//   Массив2 = Новый Массив("В", "Г", "Д");
//   Результат = РатКоллекции.ОбъединитьМассивы(Массив1, Массив2, Истина);
//   // Результат: ["А", "Б", "В", "Г", "Д"]
Функция ОбъединитьМассивы(Коллекция1, Коллекция2, КонтрольУникальности = Истина, РезультатВФиксированнуюКоллекцию = Ложь, Дополнить = Ложь) Экспорт
	
	МассивРезультат = ?(Дополнить, Коллекция1, СкопироватьМассив(Коллекция1));
	
	Для Каждого Элемент Из Коллекция2 Цикл
		ДобавитьЗначениеВМассив(МассивРезультат, Элемент, КонтрольУникальности);
	КонецЦикла;
	
	Если РезультатВФиксированнуюКоллекцию Тогда
		МассивРезультат = Новый ФиксированныйМассив(МассивРезультат);
	КонецЕсли;
	
	Возврат МассивРезультат;
	
КонецФункции // ОбъединитьМассивы

// Объединяет две коллекции типа Структура или Соответствие в одну структуру.
//
// Параметры:
//   Коллекция1 - Соответствие из Произвольный, Структура - первая коллекция для объединения.
//   Коллекция2 - Соответствие из Произвольный, Структура - вторая коллекция для объединения.
//   РезультатВФиксированнуюКоллекцию - Булево - если Истина, возвращает фиксированную структуру.
//
// Возвращаемое значение:
//   Структура, ФиксированнаяСтруктура - результат объединения двух коллекций.
//
// Пример:
//   Структура1 = Новый Структура("А", 1, "Б", 2);
//   Структура2 = Новый Структура("В", 3, "Г", 4);
//   Результат = РатКоллекции.ОбъединитьВСтруктуру(Структура1, Структура2);
//   // Результат: {"А": 1, "Б": 2, "В": 3, "Г": 4}
Функция ОбъединитьВСтруктуру(Знач Коллекция1, Коллекция2, РезультатВФиксированнуюКоллекцию = Ложь) Экспорт
	
	Если ТипЗнч(Коллекция1) <> Тип("Структура") Тогда
		Коллекция1 = СкопироватьСтруктуру(Коллекция1);
	КонецЕсли;
	
	СтруктураРезультат = ДобавитьКлючИЗначениеВКоллекцию(Коллекция1, Коллекция2);
	
	//@skip-check constructor-function-return-section
	Возврат ?(РезультатВФиксированнуюКоллекцию, Новый ФиксированнаяСтруктура(СтруктураРезультат), СтруктураРезультат);
	
КонецФункции

// Объединяет две коллекции типа Структура или Соответствие в одно соответствие.
//
// Параметры:
//   Коллекция1 - Соответствие из Произвольный, Структура - первая коллекция для объединения.
//   Коллекция2 - Соответствие из Произвольный, Структура - вторая коллекция для объединения.
//   РезультатВФиксированнуюКоллекцию - Булево - если Истина, возвращает фиксированное соответствие.
//
// Возвращаемое значение:
//   Соответствие из Произвольный, ФиксированноеСоответствие из Произвольный - результат объединения двух коллекций.
//
// Пример:
//   Соответствие1 = Новый Соответствие;
//   Соответствие1.Вставить("А", 1);
//   Соответствие1.Вставить("Б", 2);
//   Соответствие2 = Новый Соответствие;
//   Соответствие2.Вставить("В", 3);
//   Соответствие2.Вставить("Г", 4);
//   Результат = РатКоллекции.ОбъединитьВСоответствие(Соответствие1, Соответствие2);
//   // Результат: {"А": 1, "Б": 2, "В": 3, "Г": 4}
Функция ОбъединитьВСоответствие(Знач Коллекция1, Коллекция2, РезультатВФиксированнуюКоллекцию = Ложь) Экспорт
	
	Если ТипЗнч(Коллекция1) <> Тип("Соответствие") Тогда
		Коллекция1 = СкопироватьСоответствие(Коллекция1);
	КонецЕсли;
	
	СоответствиеРезультат = ДобавитьКлючИЗначениеВКоллекцию(Коллекция1, Коллекция2);
	
	Возврат ?(РезультатВФиксированнуюКоллекцию, Новый ФиксированноеСоответствие(СоответствиеРезультат), СоответствиеРезультат);
	
КонецФункции

// ДополнитьСтруктуру
// 	Дополняет структуру значениями из другой структуры.
//
// Параметры:
//   Приемник - Структура - коллекция, в которую будут добавляться новые значения.
//   Источник - Структура - коллекция, из которой будут считываться пары Ключ и Значение для заполнения.
//   Заменять - Булево - что делать в местах пересечения ключей источника и приемника:
//                                       Истина - заменять значения приемника (самый быстрый способ),
//                                       Ложь   - не заменять значения приемника (пропускать)
//
Процедура ДополнитьСтруктуру(Приемник, Источник, Заменять = Истина) Экспорт
	
	Для Каждого Элемент Из Источник Цикл
		
		Если НЕ Заменять И Приемник.Свойство(Элемент.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		Приемник.Вставить(Элемент.Ключ, Элемент.Значение);
		
	КонецЦикла;
	
КонецПроцедуры

// СкопироватьСтруктуру
//  Создает копию значения типа Структура
//
// Параметры:
//  СтруктураИсточник				 - Структура - копируемая структура
//  ФиксированнаяКоллекцияВОбычную	 - Булево	 - Если значение равно Истина, то из Фиксированной коллекции формируется обычная
// 
// Возвращаемое значение:
//  Структура, ФиксированнаяСтруктура - копия исходной структуры.
//
Функция СкопироватьСтруктуру(СтруктураИсточник, ФиксированнаяКоллекцияВОбычную = Ложь) Экспорт
	
	СтруктураРезультат = Новый Структура;
	
	Для Каждого КлючИЗначение Из СтруктураИсточник Цикл
		СтруктураРезультат.Вставить(КлючИЗначение.Ключ, СкопироватьРекурсивно(КлючИЗначение.Значение, ФиксированнаяКоллекцияВОбычную));
	КонецЦикла;
	
	Если ТипЗнч(СтруктураИсточник) = Тип("ФиксированнаяСтруктура") И Не ФиксированнаяКоллекцияВОбычную Тогда
		СтруктураРезультат = Новый ФиксированнаяСтруктура(СтруктураРезультат);
	КонецЕсли;
	
	Возврат СтруктураРезультат;
	
КонецФункции // СкопироватьСтруктуру

// СкопироватьСоответствие
//  Создает копию значения типа Соответствие.
//
// Параметры:
//  СоответствиеИсточник           - Соответствие из Произвольный - соответствие, копию которого необходимо получить.
//  ФиксированнаяКоллекцияВОбычную - Булево - Если значение равно Истина, то из Фиксированной коллекции формируется обычная
// 
// Возвращаемое значение:
//  Соответствие из Произвольный - копия исходного соответствия.
//
Функция СкопироватьСоответствие(СоответствиеИсточник, ФиксированнаяКоллекцияВОбычную = Ложь) Экспорт
	
	СоответствиеРезультат = Новый Соответствие;
	
	Для Каждого КлючИЗначение Из СоответствиеИсточник Цикл
		
		СоответствиеРезультат.Вставить(СкопироватьРекурсивно(КлючИЗначение.Ключ, ФиксированнаяКоллекцияВОбычную),
								СкопироватьРекурсивно(КлючИЗначение.Значение, ФиксированнаяКоллекцияВОбычную));
		
	КонецЦикла;
	
	Если ТипЗнч(СоответствиеИсточник) = Тип("ФиксированноеСоответствие") И Не ФиксированнаяКоллекцияВОбычную Тогда
		СоответствиеРезультат = Новый ФиксированноеСоответствие(СоответствиеРезультат);
	КонецЕсли;
	
	Возврат СоответствиеРезультат;
	
КонецФункции // СкопироватьСоответствие

// СкопироватьМассив
//  Создает копию значения типа Массив.
//
// Параметры:
//  МассивИсточник                 - Массив из Произвольный - массив, копию которого необходимо получить
//  ФиксированнаяКоллекцияВОбычную - Булево - Если значение равно Истина, то из Фиксированной коллекции формируется обычная
// 
// Возвращаемое значение:
//  Массив из Произвольный - копия исходного массива.
//
Функция СкопироватьМассив(МассивИсточник, ФиксированнаяКоллекцияВОбычную = Ложь) Экспорт
	
	МассивРезультат = Новый Массив;
	
	Для Каждого Элемент Из МассивИсточник Цикл
		МассивРезультат.Добавить(СкопироватьРекурсивно(Элемент, ФиксированнаяКоллекцияВОбычную));
	КонецЦикла;
	
	Если ТипЗнч(МассивИсточник) = Тип("ФиксированныйМассив") И Не ФиксированнаяКоллекцияВОбычную Тогда
		МассивРезультат = Новый ФиксированныйМассив(МассивРезультат);
	КонецЕсли;
	
	Возврат МассивРезультат;
	
КонецФункции // СкопироватьМассив

// ЗначениеВМассиве
//  Создает массив и помещает в него переданное значение
//
// Параметры:
//  Значение  - Произвольный - значение, которое будет добавлено в массив
//  Значение2 - Произвольный - значение, которое будет добавлено в массив
//  Значение3 - Произвольный - значение, которое будет добавлено в массив
//  Значение4 - Произвольный - значение, которое будет добавлено в массив
//  Значение5 - Произвольный - значение, которое будет добавлено в массив
//  Значение6 - Произвольный - значение, которое будет добавлено в массив
//  Значение7 - Произвольный - значение, которое будет добавлено в массив
//  Значение8 - Произвольный - значение, которое будет добавлено в массив
//  Значение9 - Произвольный - значение, которое будет добавлено в массив
// 
// Возвращаемое значение:
//  Массив из Произвольный - массив, заполненный значениями, переданными в параметрах функции
//
//@skip-check method-too-many-params
Функция ЗначениеВМассиве(Значение, // BSLLS:NumberOfOptionalParams-off
						 Значение2 = Неопределено,
						 Значение3 = Неопределено,
						 Значение4 = Неопределено,
						 Значение5 = Неопределено,
						 Значение6 = Неопределено,
						 Значение7 = Неопределено,
						 Значение8 = Неопределено,
						 Значение9 = Неопределено) Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(Значение);
	
	Если Не Значение2 = Неопределено Тогда
		Массив.Добавить(Значение2);
	КонецЕсли;
	
	Если Не Значение3 = Неопределено Тогда
		Массив.Добавить(Значение3);
	КонецЕсли;
	
	Если Не Значение4 = Неопределено Тогда
		Массив.Добавить(Значение4);
	КонецЕсли;
	
	Если Не Значение5 = Неопределено Тогда
		Массив.Добавить(Значение5);
	КонецЕсли;
	
	Если Не Значение6 = Неопределено Тогда
		Массив.Добавить(Значение6);
	КонецЕсли;
	
	Если Не Значение7 = Неопределено Тогда
		Массив.Добавить(Значение7);
	КонецЕсли;
	
	Если Не Значение8 = Неопределено Тогда
		Массив.Добавить(Значение8);
	КонецЕсли;
	
	Если Не Значение9 = Неопределено Тогда
		Массив.Добавить(Значение9);
	КонецЕсли;
	
	Возврат Массив;
	
КонецФункции // ЗначениеВМассиве

// ДобавитьЗначениеВМассив
//  процедура, для добавления в массив значений
//
// Параметры:
//  Коллекция            - Массив из Произвольный, ФиксированныйМассив из Произвольный - Коллекция, в которую добавляется значение
//  Значение             - Произвольный - добавляемое значение
//  КонтрольУникальности - Булево - необязательный, если Истина - при добавлении значения 
//  	производится контроль уникальности значений, по умолчанию Истина
//
Процедура ДобавитьЗначениеВМассив(Коллекция, Значение, КонтрольУникальности = Истина) Экспорт
		
	Если КонтрольУникальности И Коллекция.Найти(Значение) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Коллекция) = Тип("ФиксированныйМассив") Тогда
		
		Коллекция = СкопироватьМассив(Коллекция, Истина);
		Коллекция.Добавить(Значение);
		Коллекция = Новый ФиксированныйМассив(Коллекция);
		
	Иначе
		
		Коллекция.Добавить(Значение);
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьЗначениеВМассив

// Возвращает требуемое поле структуры. В случае отсутствия поля возвращает значение по умолчанию.
//
// Параметры:
// 	ИсходнаяСтруктура - Структура - исходная структура.
// 	ИмяПоля - Строка - имя поля структуры.
// 	ЗначениеПоУмолчанию - Произвольный - значение, которое будет возвращено, если поля в структуре нет.
// 	ПроверятьЗаполнение - Булево - если Истина, и поле в структуре есть, и его значение не заполнено, будет возвращено ЗначениеПоУмолчанию.
// Возвращаемое значение:
//  Произвольный - значение искомого поля структуры или ЗначениеПоУмолчанию.
Функция ЗначениеСтруктуры(Знач ИсходнаяСтруктура, ИмяПоля, Знач ЗначениеПоУмолчанию = Неопределено, ПроверятьЗаполнение = Ложь) Экспорт
	
	Если ПустаяСтрока(ИмяПоля) Тогда
		
		Возврат ЗначениеПоУмолчанию;
		
	КонецЕсли;
	
	ЗначениеПоля = Неопределено;
	Если ИсходнаяСтруктура.Свойство(ИмяПоля, ЗначениеПоля)
		И (Не ПроверятьЗаполнение Или ЗначениеЗаполнено(ЗначениеПоля)) Тогда
		
		Возврат ЗначениеПоля;
		
	КонецЕсли;
	
	Возврат ЗначениеПоУмолчанию;
	
КонецФункции

// Сравнивает свойства источника с эталонной структурой (предикатом) на соответствие.
// Внимание: проверяется именно по принципу предиката - т.е. должны соответствовать
// именно те свойства, что описываются предикатом, а не полное совпадение ВСЕХ свойств источника.
// 
// Параметры:
//  Источник - Произвольный - источник, свойства (атрибуты, реквизиты) которого требуется проверить на соответствие
//  Предикат - Структура - описание эталонного набора свойств и значений
// 
// Возвращаемое значение:
//  Структура - Сравнить свойства источника с предикатом:
// * ПолноеСоответствие - Булево - результат сравнения
// * ОписаниеРасхождений - Строка - строковое представление обнаруженных расхождений
Функция СравнитьСвойстваИсточникаСПредикатом(Источник, Предикат) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ПолноеСоответствие", Истина);
	Результат.Вставить("ОписаниеРасхождений", "");
	
	КлючОтсутствияЗначения = Новый УникальныйИдентификатор(); // контрольное значение, для индикации отсутствия свойства в источнике
	
	КонтрольныеСвойства = СкопироватьКлючиСтруктуры(Предикат, КлючОтсутствияЗначения);
	ЗаполнитьЗначенияСвойств(КонтрольныеСвойства, Источник);
	
	ПереченьРасхождений = Новый Массив;
		
	Для Каждого КлючЗначение Из Предикат Цикл
		
		ИмяСвойства = КлючЗначение.Ключ;
		КонтролируемоеЗначение = КонтрольныеСвойства[ИмяСвойства];
		
		Если КонтролируемоеЗначение <> КлючЗначение.Значение Тогда
			
			ПредставлениеКонтролируемого = ПредставлениеЗначения(КонтролируемоеЗначение, КлючОтсутствияЗначения);
			ПредставлениеКонтрольного = ПредставлениеЗначения(КлючЗначение.Значение, КлючОтсутствияЗначения);
			
			ПереченьРасхождений.Добавить(СтрШаблон("%1 : '%2' <> '%3'", ИмяСвойства, ПредставлениеКонтролируемого, ПредставлениеКонтрольного));
					
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПереченьРасхождений) Тогда
		
		Результат.ПолноеСоответствие = Ложь;
		Результат.ОписаниеРасхождений = СтрСоединить(ПереченьРасхождений, Символы.ПС);
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Скопировать ключи структуры.
// 
// Параметры:
//  Источник - Структура - структура источник для копирования ключей
//  ЗначениеПоУмолчанию - Произвольный - Значение по умолчанию для значений свойств структуры
// 
// Возвращаемое значение:
//  Структура - структура, содержащая копию ключей источника
Функция СкопироватьКлючиСтруктуры(Источник, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Результат = Новый Структура;
	
	Для Каждого КлючЗначение Из Источник Цикл
		
		Результат.Вставить(КлючЗначение.Ключ, ЗначениеПоУмолчанию);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьКлючИЗначениеВКоллекцию(КоллекцияРезультат, Коллекция)
	
	Для Каждого Элемент Из Коллекция Цикл
		КоллекцияРезультат.Вставить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
		
	Возврат КоллекцияРезультат;
	
КонецФункции

Функция ПредставлениеЗначения(Значение, КлючОтсутствующегоЗначения = NULL)
	
	Если Значение = КлючОтсутствующегоЗначения Тогда
		
		Результат = "<значение отсутствует>";
		
	Иначе
		
		Результат = Строка(Значение);
	
	КонецЕсли;
	
	Возврат Результат;
	                  	
КонецФункции

#КонецОбласти

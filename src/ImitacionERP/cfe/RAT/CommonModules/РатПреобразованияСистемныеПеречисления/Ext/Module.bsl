//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// См. РатПреобразования.ИмяСистемногоПеречисления
Функция ИмяСистемногоПеречисления(Тип) Экспорт
	
	ИмяПеречисления = Строка(Тип);
	Если СтрНачинаетсяС(ИмяПеречисления, "Перечисление") Тогда
		Возврат Сред(ИмяПеречисления, СтрДлина("Перечисление") + 1);
	Иначе
		Возврат ИмяПеречисления;
	КонецЕсли;
	
КонецФункции

// См. РатПреобразования.МенеджерСистемногоПеречисления
Функция МенеджерСистемногоПеречисления(ИмяПеречисления) Экспорт
	
	Если РатТипыДанных.ЭтоСтрока(ИмяПеречисления) Тогда
		Возврат РатАлгоритмы.ВычислитьВыражение(ИмяПеречисления);
	Иначе
		Возврат ИмяПеречисления;
	КонецЕсли;
	
КонецФункции

// См. РатПреобразования.КлючСистемногоПеречисления
Функция КлючСистемногоПеречисления(ЗначениеПеречисления) Экспорт
	
	ИмяПеречисления = ИмяСистемногоПеречисления(ТипЗнч(ЗначениеПеречисления));
	
	Возврат РатПовтИсп.КлючСистемногоПеречисления(ИмяПеречисления, Строка(ЗначениеПеречисления));
	
КонецФункции

// См. РатПреобразования.КлючиСистемногоПеречисления
Функция КлючиСистемногоПеречисления(МенеджерПеречисления) Экспорт
	
	Если РатТипыДанных.ЭтоСтрока(МенеджерПеречисления) Тогда
		Возврат РатПовтИсп.КлючиСистемногоПеречисления(МенеджерПеречисления);
	Иначе
		ИмяПеречисления = ИмяСистемногоПеречисления(МенеджерПеречисления);
		Возврат РатПовтИсп.КлючиСистемногоПеречисления(ИмяПеречисления);
	КонецЕсли;
	
КонецФункции

// См. РатПреобразования.ЗначениеСистемногоПеречисления
Функция ЗначениеСистемногоПеречисления(ОжидаемыйТип, Значение, СтатусОбработкиЗапроса) Экспорт
	
	ИмяПеречисления = ИмяСистемногоПеречисления(ОжидаемыйТип);
	Менеджер = МенеджерСистемногоПеречисления(ИмяПеречисления);
	
	Попытка
		Возврат Менеджер[Значение];
	Исключение
		ТекстОшибки = СтрШаблон("Не удалось найти значение системного перечисления '%1' (%2)", Значение, ОжидаемыйТип);
		ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), ТекстОшибки);
		КлассОшибки = РатОбщий.КлассыОшибок().Десериализация;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

#КонецОбласти

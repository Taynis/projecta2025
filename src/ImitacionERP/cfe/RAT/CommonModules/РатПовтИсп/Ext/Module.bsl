//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает таблицу описания типов метаданных, используемых при работе расширения.
//
// Возвращаемое значение:
//   см. РатМетаданные.ТипыМетаданных
//
// Пример:
//   ТипыМетаданных = РатПовтИсп.ТипыМетаданных();
//
Функция ТипыМетаданных() Экспорт
	
	Возврат РатМетаданные.ТипыМетаданных();
	
КонецФункции

// Возвращает структуру данных по имени таблицы информационной базы.
//
// Параметры:
//   ИмяТаблицы - Строка - Имя таблицы информационной базы (напр. Справочник.Пользователи)
//
// Возвращаемое значение:
//   см. РатМетаданные.СтруктураТаблицы
//
// Пример:
//   Структура = РатПовтИсп.СтруктураТаблицы("Справочник.Пользователи");
//
Функция СтруктураТаблицы(ИмяТаблицы) Экспорт
	
	Возврат РатМетаданные.СтруктураТаблицы(ИмяТаблицы);
	
КонецФункции

// Возвращает описание типа реквизита по полному имени.
// Используется для определения типа значения отбора.
//
// Параметры:
//   Путь - Строка - Полный путь к реквизиту, [ТипМетаданных].[ВидМетаданных].[ИмяРеквизита]{.ИмяРеквизитаСсылки}
//
// Возвращаемое значение:
//   ОписаниеТипов - Тип указанного реквизита
//
// Пример:
//   Тип = РатПовтИсп.ТипРеквизита("Справочник.Пользователи.Код");
//
Функция ТипРеквизита(Путь) Экспорт

	Возврат РатМетаданные.ТипРеквизита(Путь);

КонецФункции

// Возвращает перечень используемых классов ошибок.
// Используется для группировки проблем.
// На основании класса определяется код возврата ответа.
//
// Возвращаемое значение:
//   см. РатОбщий.КлассыОшибок
//
// Пример:
//   Классы = РатПовтИсп.КлассыОшибок();
//
Функция КлассыОшибок() Экспорт
	
	Возврат РатОбщий.КлассыОшибок(Ложь);
	
КонецФункции

// Возвращает имена значений системного перечисления в формате camel case.
//
// Параметры:
//   ИмяПеречисления - Строка - Имя системного перечисления
//
// Возвращаемое значение:
//   Массив из Строка - Имена значений системного перечисления в формате camel case
//
// Пример:
//   Ключи = РатПовтИсп.КлючиСистемногоПеречисления("СостояниеФоновогоЗадания");
//   // Результат: ["Активно", "Завершено", "ЗавершеноАварийно", "Отменено"]
//
Функция КлючиСистемногоПеречисления(ИмяПеречисления) Экспорт
	
	Ключи = Неопределено;
	
	Если ИмяПеречисления = "СостояниеФоновогоЗадания" Тогда
		Ключи = СтрРазделить("Активно,Завершено,ЗавершеноАварийно,Отменено", ",");
	ИначеЕсли ИмяПеречисления = "ВидСчета" Тогда
		Ключи = СтрРазделить("АктивноПассивный,Активный,Пассивный", ",");
	Иначе
		Менеджер = РатПреобразования.МенеджерСистемногоПеречисления(ИмяПеречисления);
		Ключи = Новый Массив;
		
		Для Каждого Значение Из Менеджер Цикл
			ПредставлениеЗначения = Строка(Значение);
			Идентификатор = РатСтроки.ПреобразоватьВИдентификатор(ПредставлениеЗначения);
			Ключи.Добавить(Идентификатор);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ключи;
	
КонецФункции

// Возвращает имя значения системного перечисления по представлению в формате camel case.
//
// Параметры:
//   ИмяПеречисления - Строка - Имя системного перечисления
//   ПредставленияЗначения - Строка - Представления значения
//
// Возвращаемое значение:
//   Строка - Имя значения системного перечисления в формате camel case
//
// Пример:
//   Ключ = РатПовтИсп.КлючСистемногоПеречисления("СостояниеФоновогоЗадания", "Активно");
//   // Результат: "Активно"
//
Функция КлючСистемногоПеречисления(ИмяПеречисления, ПредставленияЗначения) Экспорт
	
	Менеджер = РатПреобразования.МенеджерСистемногоПеречисления(ИмяПеречисления);
	
	Ключи = РатПовтИсп.КлючиСистемногоПеречисления(ИмяПеречисления);
	
	Для Каждого Ключ Из Ключи Цикл
		
		// Преобразуем ключ обратно в представление для сравнения
		ПредставлениеКлюча = Строка(Менеджер[Ключ]);
		
		Если ПредставлениеКлюча = ПредставленияЗначения Тогда
			
			Возврат Ключ;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВызватьИсключение СтрШаблон("Не удалось определить значение системного перечисления %2 (%1)", ИмяПеречисления, ПредставленияЗначения);
	
КонецФункции

// Возвращает значение ОписаниеТипов содержащее массив типов на все имеющиеся объекты ссылочного типа.
//
// Возвращаемое значение:
//   ОписаниеТипов - описание типов, содержащее весь набор типов пространства "http://v8.1c.ru/8.1/data/core"
//
// Пример:
//   ОписаниеТипов = РатПовтИсп.ПолучитьОписаниеТиповЛюбаяСсылка();
//
Функция ПолучитьОписаниеТиповЛюбаяСсылка() Экспорт
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(
	"<TypeDescription xmlns=""http://v8.1c.ru/8.1/data/core"">
	|      <TypeSet xmlns:cc=""http://v8.1c.ru/8.1/data/enterprise/current-config"">cc:AnyRef</TypeSet>
	|</TypeDescription>");
	
	Возврат СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	
КонецФункции

// Проверяет, относится ли тип к ссылочным типам данных конфигурации.
//
// Параметры:
//   ПредставлениеТипа - Строка - Представление типа в формате XML
//
// Возвращаемое значение:
//   Булево - Истина, если тип является ссылочным
//
// Пример:
//   Если РатПовтИсп.ЭтоСсылочныйТип("СправочникСсылка.Пользователи") Тогда ...
//
Функция ЭтоСсылочныйТип(ПредставлениеТипа) Экспорт
	
	Результат = Ложь;
	
	Если СтрНайти(ПредставлениеТипа, "Ref.") Тогда
		
		Тип = РатТипыДанных.ТипПоПредставлению(ПредставлениеТипа);
		Результат = РатПовтИсп.ПолучитьОписаниеТиповЛюбаяСсылка().СодержитТип(Тип);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание сервиса виртуальных таблиц.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
// Пример:
//   РатВиртуальныеТаблицы.ЗарегистрироватьОписание(СпецификацияСервиса);
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	ЗарегистрироватьСхемы(СпецификацияСервиса);
	
	ЗарегистрироватьОперации(СпецификацияСервиса);
	
КонецПроцедуры

// Регистрирует шаблоны адресов для виртуальных таблиц.
//
// Параметры:
//   ШаблоныСервиса - см. РатШаблоныАдресов.Шаблоны
//
// Пример:
//   РатВиртуальныеТаблицы.ЗарегистрироватьШаблоны(ШаблоныСервиса);
Процедура ЗарегистрироватьШаблоны(ШаблоныСервиса) Экспорт
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Обработчики = Обработчики();
	
	Ограничение = Новый Структура("ТипМетаданных", РатКоллекции.ЗначениеВМассиве("РегистрНакопления", "РегистрыНакопления"));
	
	МинимальныйПриоритет = 99;
	
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/{ТипМетаданных}/{ВидМетаданных}/Остатки",
									 Обработчики.Остатки,
									 МинимальныйПриоритет,
									 Ограничение);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/{ТипМетаданных}/{ВидМетаданных}/Обороты",
									 Обработчики.Обороты,
									 МинимальныйПриоритет,
									 Ограничение);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/{ТипМетаданных}/{ВидМетаданных}/ОстаткиИОбороты",
									 Обработчики.ОстаткиИОбороты,
									 МинимальныйПриоритет,
									 Ограничение);
	
	Ограничение = Новый Структура("ТипМетаданных", РатКоллекции.ЗначениеВМассиве("РегистрСведений", "РегистрыСведений"));
	
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/{ТипМетаданных}/{ВидМетаданных}/СрезПоследних",
									 Обработчики.СрезПоследних,
									 МинимальныйПриоритет,
									 Ограничение);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/{ТипМетаданных}/{ВидМетаданных}/СрезПервых",
									 Обработчики.СрезПервых,
									 МинимальныйПриоритет,
									 Ограничение);
	
КонецПроцедуры

// Вызывает обработчик запроса к виртуальной таблице.
//
// Параметры:
//   Обработчик - Строка - идентификатор обработчика запроса
//   ПараметрыЗапроса - Структура - параметры запроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//   ОбработчикВыполнен - Булево - признак выполнения обработчика
//
// Возвращаемое значение:
//   Произвольный - результат выполнения обработчика
//
// Пример:
//   Результат = РатВиртуальныеТаблицы.ВызватьОбработчик("Остатки", ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен);
Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
// BSLLS:FunctionOutParameter-off
	Обработчики = Обработчики();
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.Остатки Тогда
		
		Результат = ОбработчикОстатки(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.Обороты Тогда
		
		Результат = ОбработчикОбороты(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ОстаткиИОбороты Тогда
		
		Результат = ОбработчикОстаткиИОбороты(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.СрезПервых Тогда
		
		Результат = ОбработчикСрезПервых(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.СрезПоследних Тогда
		
		Результат = ОбработчикСрезПоследних(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	КонецЕсли; // BSLLS:IfElseIfEndsWithElse-off
	
	Возврат Результат;
	
// BSLLS:FunctionOutParameter-on
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Обработчики

// Возвращает структуру с идентификаторами обработчиков запросов.
//
// Возвращаемое значение:
//   ФиксированнаяСтруктура - структура с идентификаторами обработчиков:
//     * Остатки - Строка - получение остатков
//     * Обороты - Строка - получение оборотов
//     * ОстаткиИОбороты - Строка - получение остатков и оборотов
//     * СрезПоследних - Строка - получение среза последних
//     * СрезПервых - Строка - получение среза первых
//
Функция Обработчики() Экспорт
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("Остатки",         "Остатки");
	Обработчики.Вставить("Обороты",         "Обороты");
	Обработчики.Вставить("ОстаткиИОбороты", "ОстаткиИОбороты");
	
	Обработчики.Вставить("СрезПоследних",   "СрезПоследних");
	Обработчики.Вставить("СрезПервых",      "СрезПервых");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

// Обрабатывает запрос к виртуальной таблице остатков.
//
// Параметры:
//   ПараметрыЗапроса - Структура - параметры запроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Массив из Структура - результат запроса к виртуальной таблице
//
Функция ОбработчикОстатки(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Описание = "Получение остатков регистра ";
	
	Возврат ОбработатьЗапросКВиртуальнойТаблице(ПараметрыЗапроса, СтатусОбработкиЗапроса, "Остатки", Описание);
	
КонецФункции

// Обрабатывает запрос к виртуальной таблице оборотов.
//
// Параметры:
//   ПараметрыЗапроса - Структура - параметры запроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Массив из Структура - результат запроса к виртуальной таблице
//
Функция ОбработчикОбороты(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Описание = "Получение оборотов регистра ";
	
	Возврат ОбработатьЗапросКВиртуальнойТаблице(ПараметрыЗапроса, СтатусОбработкиЗапроса, "Обороты", Описание);
	
КонецФункции

// Обрабатывает запрос к виртуальной таблице остатков и оборотов.
//
// Параметры:
//   ПараметрыЗапроса - Структура - параметры запроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Массив из Структура - результат запроса к виртуальной таблице
//
Функция ОбработчикОстаткиИОбороты(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Описание = "Получение остатков и оборотов регистра ";
	
	Возврат ОбработатьЗапросКВиртуальнойТаблице(ПараметрыЗапроса, СтатусОбработкиЗапроса, "ОстаткиИОбороты", Описание);
	
КонецФункции

// Обрабатывает запрос к виртуальной таблице среза последних.
//
// Параметры:
//   ПараметрыЗапроса - Структура - параметры запроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Массив из Структура - результат запроса к виртуальной таблице
//
Функция ОбработчикСрезПоследних(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Описание = "Получение среза последних регистра ";
	
	Возврат ОбработатьЗапросКВиртуальнойТаблице(ПараметрыЗапроса, СтатусОбработкиЗапроса, "СрезПоследних", Описание);
	
КонецФункции

// Обрабатывает запрос к виртуальной таблице среза первых.
//
// Параметры:
//   ПараметрыЗапроса - Структура - параметры запроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Массив из Структура - результат запроса к виртуальной таблице
//
Функция ОбработчикСрезПервых(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Описание = "Получение среза первых регистра ";
	
	Возврат ОбработатьЗапросКВиртуальнойТаблице(ПараметрыЗапроса, СтатусОбработкиЗапроса, "СрезПервых", Описание);
	
КонецФункции

#КонецОбласти

#Область СтруктураТелаЗапроса

// Возвращает структуру тела запроса для виртуальной таблицы.
//
// Параметры:
//   Регистр - ОбъектМетаданных - регистр, для которого формируется структура
//   ВиртуальнаяТаблица - Строка - тип виртуальной таблицы
//
// Возвращаемое значение:
//   Массив из см. РатПреобразования.ОписаниеРеквизита - структура тела запроса
//
Функция СтруктураТелаЗапроса(Регистр, ВиртуальнаяТаблица)
	
	Если ВиртуальнаяТаблица = "Остатки" Тогда
		
		Возврат СтруктураТелаЗапросаОстатки(Регистр);
		
	ИначеЕсли ВиртуальнаяТаблица = "Обороты" Тогда
		
		Возврат СтруктураТелаЗапросаОбороты(Регистр);
		
	ИначеЕсли ВиртуальнаяТаблица = "ОстаткиИОбороты" Тогда
		
		Возврат СтруктураТелаЗапросаОстаткиИОбороты(Регистр);
		
	ИначеЕсли ВиртуальнаяТаблица = "СрезПервых" ИЛИ ВиртуальнаяТаблица = "СрезПоследних" Тогда
		
		Возврат СтруктураТелаЗапросаСрез(Регистр);
		
	Иначе
		
		ВызватьИсключение "Неизвестный тип виртуальной таблицы " + ВиртуальнаяТаблица;
		
	КонецЕсли;
	
КонецФункции

// Возвращает структуру тела запроса для виртуальной таблицы остатков.
//
// Параметры:
//   Регистр - ОбъектМетаданных - регистр, для которого формируется структура
//
// Возвращаемое значение:
//   Массив из см. РатПреобразования.ОписаниеРеквизита - структура тела запроса
//
Функция СтруктураТелаЗапросаОстатки(Регистр)
	
	ОписаниеРеквизитов = БазоваяСтруктураТелаЗапроса(Регистр);
	
	ОписаниеРеквизитов.Добавить(РатПреобразования.ОписаниеРеквизита("Период", Тип("Дата")));
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

// Возвращает структуру тела запроса для виртуальной таблицы среза.
//
// Параметры:
//   Регистр - ОбъектМетаданных - регистр, для которого формируется структура
//
// Возвращаемое значение:
//   Массив из см. РатПреобразования.ОписаниеРеквизита - структура тела запроса
//
Функция СтруктураТелаЗапросаСрез(Регистр)
	
	ОписаниеРеквизитов = БазоваяСтруктураТелаЗапроса(Регистр);
	
	ОписаниеРеквизитов.Добавить(РатПреобразования.ОписаниеРеквизита("Период", Тип("Дата")));
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

// Возвращает структуру тела запроса для виртуальной таблицы оборотов.
//
// Параметры:
//   Регистр - ОбъектМетаданных - регистр, для которого формируется структура
//
// Возвращаемое значение:
//   Массив из см. РатПреобразования.ОписаниеРеквизита - структура тела запроса
//
Функция СтруктураТелаЗапросаОбороты(Регистр)
	
	ОписаниеРеквизитов = БазоваяСтруктураТелаЗапроса(Регистр);
	
	ОписаниеРеквизитов.Добавить(РатПреобразования.ОписаниеРеквизита("НачалоПериода", Тип("Дата")));
	ОписаниеРеквизитов.Добавить(РатПреобразования.ОписаниеРеквизита("КонецПериода", Тип("Дата")));
	
	ОписаниеРеквизита = РатПреобразования.ОписаниеРеквизита("Периодичность", Тип("Строка"));
	ОписаниеРеквизита.ДопустимыеЗначения = ЗначенияПериодичности();
	ОписаниеРеквизитов.Добавить(ОписаниеРеквизита);
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

// Возвращает структуру тела запроса для виртуальной таблицы остатков и оборотов.
//
// Параметры:
//   Регистр - ОбъектМетаданных - регистр, для которого формируется структура
//
// Возвращаемое значение:
//   Массив из см. РатПреобразования.ОписаниеРеквизита - структура тела запроса
//
Функция СтруктураТелаЗапросаОстаткиИОбороты(Регистр)
	
	ОписаниеРеквизитов = БазоваяСтруктураТелаЗапроса(Регистр);
	
	ОписаниеРеквизитов.Добавить(РатПреобразования.ОписаниеРеквизита("НачалоПериода", Тип("Дата")));
	ОписаниеРеквизитов.Добавить(РатПреобразования.ОписаниеРеквизита("КонецПериода", Тип("Дата")));
	
	ОписаниеРеквизита = РатПреобразования.ОписаниеРеквизита("Периодичность", Тип("Строка"));
	ОписаниеРеквизита.ДопустимыеЗначения = ЗначенияПериодичности();
	ОписаниеРеквизитов.Добавить(ОписаниеРеквизита);
	
	ОписаниеРеквизита = РатПреобразования.ОписаниеРеквизита("МетодДополнения", Тип("Строка"));
	ОписаниеРеквизита.ДопустимыеЗначения = ЗначенияМетодаДополнения();
	ОписаниеРеквизитов.Добавить(ОписаниеРеквизита);
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

// Возвращает базовую структуру тела запроса.
//
// Параметры:
//   Регистр - ОбъектМетаданных - регистр, для которого формируется структура
//
// Возвращаемое значение:
//   Массив из см. РатПреобразования.ОписаниеРеквизита- базовая структура тела запроса
//
Функция БазоваяСтруктураТелаЗапроса(Регистр)
	
	ОписаниеРеквизитов = Новый Массив;
	
	ОписаниеРеквизита = РатПреобразования.ОписаниеРеквизита("Условия", Тип("Структура"));
	ОписаниеРеквизита.ВложенныеРеквизиты = РатПреобразования.ОписаниеРеквизитовОбъектаМетаданных(Регистр.Измерения);
	ОписаниеРеквизитов.Добавить(ОписаниеРеквизита);
	
	ОписаниеРеквизитов.Добавить(РатПреобразования.ОписаниеКоллекции("Поля", Тип("Массив"), Тип("Строка"), СтрРазделить("*", ",")));
	
	ОписаниеТипа = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(3, 0, ДопустимыйЗнак.Неотрицательный));
	ОписаниеРеквизитов.Добавить(РатПреобразования.ОписаниеРеквизита("Количество", ОписаниеТипа, 10));
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

#КонецОбласти

#Область Схемы

// Регистрирует схемы для виртуальных таблиц.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьСхемы(СпецификацияСервиса)
	
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, "periodicity", СхемаПериодичность());
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, "completionMethod", СхемаМетодДополнения());
	
	Для Каждого Регистр Из Метаданные.РегистрыНакопления Цикл
		
		Схема = СхемаИзмерения(Регистр, Ложь);
		ИмяСхемы = ИмяСхемыУсловийРегистра(Регистр);
		РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, ИмяСхемы, Схема);
		
		Схема = СхемаИзмерения(Регистр, Истина);
		ИмяСхемы = РатСпецификация.ИмяСхемыОписанияОбъектаМетаданных(Регистр, "Измерения");
		РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, ИмяСхемы, Схема);
		
	КонецЦикла;
	
	Для Каждого Регистр Из Метаданные.РегистрыСведений Цикл
		
		Если Регистр.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			Продолжить;
		КонецЕсли;
		
		Схема = СхемаИзмерения(Регистр, Ложь);
		ИмяСхемы = ИмяСхемыУсловийРегистра(Регистр);
		РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, ИмяСхемы, Схема);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает имя схемы условий регистра.
//
// Параметры:
//   Регистр - ОбъектМетаданных - регистр, для которого формируется имя схемы
//
// Возвращаемое значение:
//   Строка - имя схемы условий регистра
//
Функция ИмяСхемыУсловийРегистра(Регистр)
	
	Возврат РатСпецификация.ИмяСхемыОписанияОбъектаМетаданных(Регистр, "Условия");
	
КонецФункции

// Возвращает схему измерений регистра.
//
// Параметры:
//   Регистр - ОбъектМетаданных - регистр, для которого формируется схема
//   ДляОтвета - Булево - признак формирования схемы для ответа
//
// Возвращаемое значение:
//   Структура - схема измерений регистра
//
Функция СхемаИзмерения(Регистр, ДляОтвета)
	
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	
	Для Каждого Реквизит Из Регистр.Измерения Цикл
		
		Представление = Реквизит.Представление();
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, Реквизит.Имя, Представление, Реквизит.Тип, Ложь, Ложь);
		
	КонецЦикла;
	
	Представление = Регистр.Представление();
	
	Возврат РатСпецификация.СхемаПоОписаниюОбъекта(ОписаниеРеквизитов, "Измерения регистра " + Представление, ДляОтвета);
	
КонецФункции

Функция СхемаТелаЗапросаОстатки(Регистр)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	Схема = БазоваяСхемаЗапроса(Регистр, "Настройки получения остатков");
	
	РатOpenAPI.ДобавитьРеквизит(Схема, "Период", ТипыДанных.Дата);
	
	Возврат Схема;
	
КонецФункции

Функция СхемаТелаЗапросаОбороты(Регистр)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	Схема = БазоваяСхемаЗапроса(Регистр, "Настройки получения оборотов");
	
	РатOpenAPI.ДобавитьРеквизит(Схема, "НачалоПериода", ТипыДанных.Дата);
	РатOpenAPI.ДобавитьРеквизит(Схема, "КонецПериода", ТипыДанных.Дата);
	РатOpenAPI.ДобавитьРеквизит(Схема, "Периодичность", РатOpenAPI.СсылкаНаСхему("periodicity"));
	
	Возврат Схема;
	
КонецФункции

Функция СхемаТелаЗапросаОстаткиИОбороты(Регистр)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	Схема = БазоваяСхемаЗапроса(Регистр, "Настройки получения остатков и оборотов");
	
	РатOpenAPI.ДобавитьРеквизит(Схема, "НачалоПериода", ТипыДанных.Дата);
	РатOpenAPI.ДобавитьРеквизит(Схема, "КонецПериода", ТипыДанных.Дата);
	РатOpenAPI.ДобавитьРеквизит(Схема, "Периодичность", РатOpenAPI.СсылкаНаСхему("periodicity"));
	РатOpenAPI.ДобавитьРеквизит(Схема, "МетодДополнения", РатOpenAPI.СсылкаНаСхему("completionMethod"));
	
	Возврат Схема;
	
КонецФункции

Функция СхемаТелаОтветаОстатки(Регистр, ОписаниеКоллекции)
	
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	
	Для Каждого Реквизит Из Регистр.Ресурсы Цикл
		
		Представление = Реквизит.Представление();
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, Реквизит.Имя + "Остаток", "Остаток " + Представление, Реквизит.Тип, Истина, Истина);
		
	КонецЦикла;
	
	Описание = "Остатки по регистру";
	СхемаРесурсы = РатСпецификация.СхемаПоОписаниюОбъекта(ОписаниеРеквизитов, Описание, Истина);
	СсылкуНаСхему = РатСпецификация.СсылкуНаСхему(Регистр, "Измерения");
	
	СхемаОбъединения = РатOpenAPI.НоваяСхемаОбъединения(Описание, СсылкуНаСхему, СхемаРесурсы);
	Возврат РатOpenAPI.НоваяСхемаКоллекции(СхемаОбъединения, ОписаниеКоллекции);
	
КонецФункции

Функция СхемаТелаОтветаОбороты(Регистр, ОписаниеКоллекции)
	
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	
	Для Каждого Реквизит Из Регистр.Ресурсы Цикл
		
		Представление = Реквизит.Представление();
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, Реквизит.Имя + "Оборот", "Оборот по " + Представление, Реквизит.Тип, Истина, Истина);
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, Реквизит.Имя + "Приход", "Приход по " + Представление, Реквизит.Тип, Истина, Истина);
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, Реквизит.Имя + "Расход", "Расход по " + Представление, Реквизит.Тип, Истина, Истина);
		
	КонецЦикла;
	
	Описание = "Обороты по регистру";
	СхемаРесурсы = РатСпецификация.СхемаПоОписаниюОбъекта(ОписаниеРеквизитов, Описание, Истина);
	СсылкуНаСхему = РатСпецификация.СсылкуНаСхему(Регистр, "Измерения");
	
	СхемаОбъединения = РатOpenAPI.НоваяСхемаОбъединения(Описание, СсылкуНаСхему, СхемаРесурсы);
	Возврат РатOpenAPI.НоваяСхемаКоллекции(СхемаОбъединения, ОписаниеКоллекции);

КонецФункции

Функция СхемаТелаОтветаОстаткиИОбороты(Регистр, ОписаниеКоллекции)
	
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	
	Для Каждого Реквизит Из Регистр.Ресурсы Цикл
		
		Представление = Реквизит.Представление();
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
										 Реквизит.Имя + "НачальныйОстаток",
										 "Начальный остаток " + Представление,
										 Реквизит.Тип,
										 Истина,
										 Истина);
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
										 Реквизит.Имя + "КонечныйОстаток",
										 "Конечный остаток " + Представление,
										 Реквизит.Тип,
										 Истина,
										 Истина);
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, Реквизит.Имя + "Оборот", "Оборот по " + Представление, Реквизит.Тип, Истина, Истина);
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, Реквизит.Имя + "Приход", "Приход по " + Представление, Реквизит.Тип, Истина, Истина);
		РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, Реквизит.Имя + "Расход", "Расход по " + Представление, Реквизит.Тип, Истина, Истина);
		
	КонецЦикла;
	
	Описание = "Остатки и обороты по регистру";
	СхемаРесурсы = РатСпецификация.СхемаПоОписаниюОбъекта(ОписаниеРеквизитов, Описание, Истина);
	СсылкуНаСхему = РатСпецификация.СсылкуНаСхему(Регистр, "Измерения");
	
	СхемаОбъединения = РатOpenAPI.НоваяСхемаОбъединения(Описание, СсылкуНаСхему, СхемаРесурсы);
	Возврат РатOpenAPI.НоваяСхемаКоллекции(СхемаОбъединения, ОписаниеКоллекции);
	
КонецФункции

Функция БазоваяСхемаЗапроса(Регистр, Описание)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	СхемаИмяРеквизита = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка, "Имя реквизита объекта");
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта(Описание);
	
	РатOpenAPI.ДобавитьРеквизит(Схема, "Условия", РатСпецификация.СсылкуНаСхему(Регистр, "Условия"));
	РатOpenAPI.ДобавитьРеквизит(Схема, "Количество", ТипыДанных.ЦелоеЧисло, "Ограничение количества возвращаемых записей, если не указано - 10");
	
	СхемаСписокПолей = РатOpenAPI.НоваяСхемаКоллекции(СхемаИмяРеквизита, "Список выбираемых полей. Можно указывать имена измерений и ресурсов,
																		 |а также значения вложенных полей через точку, например Регистратор.Дата");
	РатOpenAPI.ДобавитьРеквизит(Схема, "Поля", СхемаСписокПолей);
	
	Возврат Схема;
	
КонецФункции

Функция СхемаПериодичность()
	
	Возврат РатOpenAPI.НоваяСхемаПеречисления(ЗначенияПериодичности(), "Периодичность");

КонецФункции

Функция СхемаМетодДополнения()
	
	Возврат РатOpenAPI.НоваяСхемаПеречисления(ЗначенияМетодаДополнения(), "Метод дополнения");

КонецФункции

Функция СхемаТелаЗапросаСреза(Регистр)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	Схема = БазоваяСхемаЗапроса(Регистр, "Настройки получения среза");
	РатOpenAPI.ДобавитьРеквизит(Схема, "Период", ТипыДанных.Дата);
	
	Возврат Схема;
	
КонецФункции

Функция СхемаТелаОтветаСрез(Регистр)
	
	Возврат РатСпецификация.СсылкуНаВыходнуюСхему(Регистр);
	
КонецФункции

Функция ЗначенияПериодичности()
	
	ЗначенияПериодичности = Новый Массив;
	ЗначенияПериодичности.Добавить("Период");
	ЗначенияПериодичности.Добавить("Запись");
	ЗначенияПериодичности.Добавить("Регистратор");
	ЗначенияПериодичности.Добавить("Секунда");
	ЗначенияПериодичности.Добавить("Минута");
	ЗначенияПериодичности.Добавить("Час");
	ЗначенияПериодичности.Добавить("День");
	ЗначенияПериодичности.Добавить("Неделя");
	ЗначенияПериодичности.Добавить("Декада");
	ЗначенияПериодичности.Добавить("Месяц");
	ЗначенияПериодичности.Добавить("Квартал");
	ЗначенияПериодичности.Добавить("Полугодие");
	ЗначенияПериодичности.Добавить("Год");
	ЗначенияПериодичности.Добавить("Авто");
	
	Возврат ЗначенияПериодичности;
	
КонецФункции

Функция ЗначенияМетодаДополнения()
	
	Значения = Новый Массив;
	Значения.Добавить("Движения");
	Значения.Добавить("ДвиженияИГраницыПериода");
	
	Возврат Значения;
	
КонецФункции

#КонецОбласти

Процедура ЗарегистрироватьОперации(СпецификацияСервиса)
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	ОписаниеТипа = РатМетаданные.ТипМетаданных("РегистрыНакопления");
	
	ВидРегистраНакопления = Метаданные.СвойстваОбъектов.ВидРегистраНакопления;
	
	Для Каждого Регистр Из Метаданные.РегистрыНакопления Цикл
		
		Остатки = Регистр.ВидРегистра = ВидРегистраНакопления.Остатки;
		Обороты = Истина;
		
		Если НЕ Остатки И НЕ Обороты Тогда
			Продолжить;
		КонецЕсли;
		
		Группа = РатСпецификация.ГруппуОбъектаМетаданных(ОписаниеТипа, Регистр);
		РатСпецификация.ДобавитьГруппу(СпецификацияСервиса, Группа);
		БазовыйАдрес = РатСпецификация.АдресОбъектаМетаданных(ОписаниеТипа, Регистр);
		
		ПрефиксПолучение = "Получение ";
		ПрефиксКоллекция = "Коллекция ";
		
		Если Остатки Тогда
			
			// Остатки
			Описание = СтрШаблон("остатков регистра %1 на дату по указанным условиям ", Регистр.Представление());
			Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес, "Остатки");
			
			Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.POST, "Вирт таблицы: Остатки", ПрефиксПолучение + Описание, Группа);
			РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаТелаЗапросаОстатки(Регистр));
			РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаТелаОтветаОстатки(Регистр, ПрефиксКоллекция + Описание));
			
		КонецЕсли;
		
		Если Обороты Тогда
			
			// Обороты
			Описание = СтрШаблон("оборотов регистра %1 за период по указанным условиям ", Регистр.Представление());
			Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес, "Обороты");
			
			Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.POST, "Вирт таблицы: Обороты", ПрефиксПолучение + Описание, Группа);
			РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаТелаЗапросаОбороты(Регистр));
			РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаТелаОтветаОбороты(Регистр, ПрефиксКоллекция + Описание));
			
		КонецЕсли;
		
		Если Остатки И Обороты Тогда
			
			// Остатки и обороты
			Описание = СтрШаблон("остатков и оборотов регистра %1 за период по указанным условиям ", Регистр.Представление());
			Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес, "ОстаткиИОбороты");
			
			Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс,
															Методы.POST,
															"Вирт таблицы: Остатки и обороты",
															ПрефиксПолучение + Описание,
															Группа);
			РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаТелаЗапросаОстаткиИОбороты(Регистр));
			РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаТелаОтветаОстаткиИОбороты(Регистр, ПрефиксКоллекция + Описание));
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОписаниеТипа = РатМетаданные.ТипМетаданных("РегистрыСведений");
	
	Для Каждого Регистр Из Метаданные.РегистрыСведений Цикл
		
		Если Регистр.ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			Продолжить;
		КонецЕсли;
		
		Группа = РатСпецификация.ГруппуОбъектаМетаданных(ОписаниеТипа, Регистр);
		
		СхемаОтвета = РатOpenAPI.НоваяСхемаКоллекции(СхемаТелаОтветаСрез(Регистр), "Коллекция значений среза по указанным условиям");
		БазовыйАдрес = РатСпецификация.АдресОбъектаМетаданных(ОписаниеТипа, Регистр);
		
		ПрефиксПолучение = "Получение ";
		
		// СрезПервых
		Описание = СтрШаблон(ПрефиксПолучение + "среза первых регистра %1", Регистр.Представление());
		Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес, "СрезПервых");
		
		Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.POST, "Вирт таблицы: Срез первых", Описание, Группа);
		РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаТелаЗапросаСреза(Регистр));
		РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
		
		// СрезПоследних
		Описание = СтрШаблон(ПрефиксПолучение + "среза последних регистра %1", Регистр.Представление());
		Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес, "СрезПоследних");
		
		Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.POST, "Вирт таблицы: Срез последних", Описание, Группа);
		РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаТелаЗапросаСреза(Регистр));
		РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОбработатьЗапросКВиртуальнойТаблице(ПараметрыЗапроса, СтатусОбработкиЗапроса, ВиртуальнаяТаблица, Описание)
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "ТипМетаданных, ВидМетаданных, Тело", СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Регистр = РатМетаданные.ОбъектМетаданных(ПараметрыЗапроса.ИмяТаблицы);
	ОписаниеОперации = Описание + Регистр.Представление();
	
	СтруктураТела = СтруктураТелаЗапроса(Регистр, ВиртуальнаяТаблица);
	
	НормализованноеИмяТаблицы = Регистр.ПолноеИмя();
	
	ДанныеТела = РатПреобразования.ПреобразоватьДанные(ПараметрыЗапроса.Тело, СтруктураТела, СтатусОбработкиЗапроса, ОписаниеОперации);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыборки = ПараметрыЗапросаВиртуальнойТаблицы(НормализованноеИмяТаблицы, ВиртуальнаяТаблица);
	
	РатКоллекции.ДополнитьСтруктуру(ПараметрыВыборки, ДанныеТела);
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыборки.Сортировка) Тогда
		
		СортировкаПоПериоду = СтрНачинаетсяС(ВиртуальнаяТаблица, "Срез")
			ИЛИ (ВиртуальнаяТаблица = "Обороты" ИЛИ ВиртуальнаяТаблица = "ОстаткиИОбороты")
				И ЗначениеЗаполнено(ПараметрыВыборки.Периодичность) И СтрСравнить(ПараметрыВыборки.Периодичность, "Период");
		
		Если СортировкаПоПериоду Тогда
			ПараметрыВыборки.Сортировка = "Период";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВыполнитьЗапрос(ПараметрыВыборки, ОписаниеОперации, СтатусОбработкиЗапроса);
	
КонецФункции

Функция ПараметрыЗапросаВиртуальнойТаблицы(ИмяТаблицы, ВиртуальнаяТаблица)
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ИмяТаблицы", ИмяТаблицы);
	Параметры.Вставить("ВиртуальнаяТаблица", ВиртуальнаяТаблица);
	Параметры.Вставить("Условия", Новый Структура);
	Параметры.Вставить("Поля", Новый Массив);
	Параметры.Вставить("КоличествоЗаписей", 0);
	Параметры.Вставить("Сортировка", Неопределено);
	Параметры.Вставить("Периодичность", Неопределено);
	
	Возврат Параметры;
	
КонецФункции

// Формирует параметры виртуальной таблицы для запроса.
//
// Параметры:
//  ПараметрыЗапроса - Структура - параметры запроса
//  Запрос - Запрос - объект запроса
//
// Возвращаемое значение:
//  Массив из Строка - параметры виртуальной таблицы
//
Функция СформироватьПараметрыВиртуальнойТаблицы(ПараметрыЗапроса, Запрос)
	
	ПараметрыВиртуальнойТаблицы = Новый Массив();
	
	Если ПараметрыЗапроса.ВиртуальнаяТаблица = "Обороты" ИЛИ ПараметрыЗапроса.ВиртуальнаяТаблица = "ОстаткиИОбороты" Тогда
		ДобавитьПараметрыПериода(ПараметрыЗапроса, Запрос, ПараметрыВиртуальнойТаблицы);
		ПараметрыВиртуальнойТаблицы.Добавить(РатКоллекции.ЗначениеСтруктуры(ПараметрыЗапроса, "Периодичность", ""));
		
		Если ПараметрыЗапроса.ВиртуальнаяТаблица = "ОстаткиИОбороты" Тогда
			ПараметрыВиртуальнойТаблицы.Добавить(РатКоллекции.ЗначениеСтруктуры(ПараметрыЗапроса, "МетодДополнения", ""));
		КонецЕсли;
	Иначе
		ДобавитьПараметрПериода(ПараметрыЗапроса, Запрос, ПараметрыВиртуальнойТаблицы);
	КонецЕсли;
	
	ДобавитьУсловия(ПараметрыЗапроса, Запрос, ПараметрыВиртуальнойТаблицы);
	
	Возврат ПараметрыВиртуальнойТаблицы;
	
КонецФункции

// Добавляет параметры периода в запрос.
//
// Параметры:
//  ПараметрыЗапроса - Структура - параметры запроса
//  Запрос - Запрос - объект запроса
//  ПараметрыВиртуальнойТаблицы - Массив из Строка - параметры виртуальной таблицы
//
Процедура ДобавитьПараметрыПериода(ПараметрыЗапроса, Запрос, ПараметрыВиртуальнойТаблицы)
	
	Если ПараметрыЗапроса.Свойство("НачалоПериода") И ЗначениеЗаполнено(ПараметрыЗапроса.НачалоПериода) Тогда
		ПараметрыВиртуальнойТаблицы.Добавить("&НачалоПериода");
		Запрос.УстановитьПараметр("НачалоПериода", ПараметрыЗапроса.НачалоПериода);
	Иначе
		ПараметрыВиртуальнойТаблицы.Добавить("");
	КонецЕсли;
	
	Если ПараметрыЗапроса.Свойство("КонецПериода") И ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
		ПараметрыВиртуальнойТаблицы.Добавить("&КонецПериода");
		Запрос.УстановитьПараметр("КонецПериода", ПараметрыЗапроса.КонецПериода);
	Иначе
		ПараметрыВиртуальнойТаблицы.Добавить("");
	КонецЕсли;
	
КонецПроцедуры

// Добавляет параметр периода в запрос.
//
// Параметры:
//  ПараметрыЗапроса - Структура - параметры запроса
//  Запрос - Запрос - объект запроса
//  ПараметрыВиртуальнойТаблицы - Массив из Строка - параметры виртуальной таблицы
//
Процедура ДобавитьПараметрПериода(ПараметрыЗапроса, Запрос, ПараметрыВиртуальнойТаблицы)
	
	Если ПараметрыЗапроса.Свойство("Период") И ЗначениеЗаполнено(ПараметрыЗапроса.Период) Тогда
		ПараметрыВиртуальнойТаблицы.Добавить("&Период");
		Запрос.УстановитьПараметр("Период", ПараметрыЗапроса.Период);
	Иначе
		ПараметрыВиртуальнойТаблицы.Добавить("");
	КонецЕсли;
	
КонецПроцедуры

// Добавляет условия в запрос.
//
// Параметры:
//  ПараметрыЗапроса - Структура - параметры запроса
//  Запрос - Запрос - объект запроса
//  ПараметрыВиртуальнойТаблицы - Массив из Строка - параметры виртуальной таблицы
//
Процедура ДобавитьУсловия(ПараметрыЗапроса, Запрос, ПараметрыВиртуальнойТаблицы)
	
	Если НЕ ЗначениеЗаполнено(ПараметрыЗапроса.Условия) Тогда
		Возврат;
	КонецЕсли;
	
	Условия = Новый Массив();
	
	Для Каждого Отбор Из ПараметрыЗапроса.Условия Цикл
		ИмяПараметра = СтрЗаменить(Отбор.Ключ, ".", "");
		Условия.Добавить(СтрШаблон("%1 = &%2", Отбор.Ключ, ИмяПараметра));
		Запрос.УстановитьПараметр(ИмяПараметра, Отбор.Значение);
	КонецЦикла;
	
	ПараметрыВиртуальнойТаблицы.Добавить(СтрСоединить(Условия, " И "));
	
КонецПроцедуры

// Формирует запрос к виртуальной таблице.
//
// Параметры:
//  ПараметрыЗапроса - Структура - параметры запроса
//
// Возвращаемое значение:
//  Запрос - сформированный запрос
//
Функция СформироватьЗапросКВиртуальнойТаблице(ПараметрыЗапроса)
	
	Запрос = Новый Запрос;
	
	БлокиТекстаЗапроса = Новый Массив;
	
	БлокиТекстаЗапроса.Добавить("ВЫБРАТЬ ПЕРВЫЕ " + РатСтроки.ЧислоВСтроку(ПараметрыЗапроса.Количество));
	БлокиТекстаЗапроса.Добавить("	" + СтрСоединить(ПараметрыЗапроса.Поля, РазделительПолейЗапроса()));
	
	ПараметрыВиртуальнойТаблицы = СформироватьПараметрыВиртуальнойТаблицы(ПараметрыЗапроса, Запрос);
	
	ТекстПараметровВиртуальнойТаблицы = СтрСоединить(ПараметрыВиртуальнойТаблицы, ", ");
	ТекстБлока = СтрШаблон(
	"ИЗ
	|	%1.%2(%3) КАК Выборка", ПараметрыЗапроса.ИмяТаблицы, ПараметрыЗапроса.ВиртуальнаяТаблица, ТекстПараметровВиртуальнойТаблицы);
	БлокиТекстаЗапроса.Добавить(ТекстБлока);
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Сортировка) Тогда
		ДобавитьСортировку(ПараметрыЗапроса, БлокиТекстаЗапроса);
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(БлокиТекстаЗапроса, Символы.ПС);
	
	Возврат Запрос;
	
КонецФункции

// Добавляет сортировку в запрос.
//
// Параметры:
//  ПараметрыЗапроса - Структура - параметры запроса
//  БлокиТекстаЗапроса - Массив из Строка - блоки текста запроса
//
Процедура ДобавитьСортировку(ПараметрыЗапроса, БлокиТекстаЗапроса)
	
	БлокиТекстаЗапроса.Добавить("УПОРЯДОЧИТЬ ПО");
	
	Если ТипЗнч(ПараметрыЗапроса.Сортировка) = Тип("Массив") Тогда
		ТекстБлока = СтрСоединить(ПараметрыЗапроса.Сортировка, РазделительПолейЗапроса());
		БлокиТекстаЗапроса.Добавить("	" + ТекстБлока);
	Иначе
		БлокиТекстаЗапроса.Добавить("	" + ПараметрыЗапроса.Сортировка);
	КонецЕсли;
	
КонецПроцедуры

Функция РазделительПолейЗапроса(Разделитель = ",")
	
	Возврат Разделитель + Символы.ПС + Символы.Таб;
	
КонецФункции

Функция ВыполнитьЗапрос(ПараметрыЗапроса, Описание, СтатусОбработкиЗапроса)
	
	Попытка
		
		Запрос = СформироватьЗапросКВиртуальнойТаблице(ПараметрыЗапроса);
		РезультатЗапроса = Запрос.Выполнить();
	
	Исключение
		
		КлассыОшибок = РатОбщий.КлассыОшибок();
		
		Текст = СтрШаблон("%1. Не удалось выполнить запрос к данным", Описание);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.Неизвестная, Текст);
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.Неизвестная, ИнформацияОбОшибке());
		Возврат Неопределено;
		
	КонецПопытки;
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Массив;
	Иначе
		Возврат РезультатЗапроса.Выгрузить();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Сериализует значение в структуру для передачи в JSON. Преобразует значение в формат,
// пригодный для сериализации в JSON, включая специальную обработку ссылочных типов.
//
// Параметры:
//   Значение - Произвольный - значение для сериализации
//
// Возвращаемое значение:
//   Структура - со свойствами:
//     * type - Строка - тип значения
//     * id - Строка - идентификатор значения
//     * presentation - Строка - представление значения
//
Функция СериализованноеЗначение(Значение) Экспорт
	
	ТипЗначения = ТипЗнч(Значение);
	
	Если РатТипыДанных.ЭтоСсылочныйТип(ТипЗначения) Тогда
		
		Результат = СериализоватьСсылку(Значение, ТипЗначения);
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру ошибки для ответа сервиса. Формирует структурированное описание
// ошибок, включая основную ошибку, стек ошибок и отладочные сообщения.
//
// Параметры:
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Структура - Представление описания ошибок:
//     * id - Строка - Идентификатор категории ошибки
//     * description - Строка - Описание категории ошибки
//     * message - Строка - Текст ошибки
//     * stack - Массив из Структура - Описание прочих ошибок возникших при обработке запроса
//     * debugMessages - Массив из Строка - Отладочные сообщения (опционально)
//
Функция ПредставлениеОписанияОшибок(СтатусОбработкиЗапроса) Экспорт
	
	Результат = Новый Структура("id, description, message");
	
	ОписаниеОшибок = СтатусОбработкиЗапроса.ОписаниеОшибки;
	ОписаниеОшибки = ОписаниеОшибок[0];
	
	Результат.id = ОписаниеОшибки.Класс.Идентификатор;
	Результат.description = ОписаниеОшибки.Класс.Описание;
	Результат.message = ОписаниеОшибки.Текст;
	
	Если ОписаниеОшибок.Количество() > 1 Тогда
		
		Результат.Вставить("stack", Новый Массив);
		
		Для Инд = 1 По ОписаниеОшибок.ВГраница() Цикл
			ОписаниеОшибки = ОписаниеОшибок[Инд];
			
			ПредставлениеОшибки = Новый Структура("id, description, message");
			ПредставлениеОшибки.id = ОписаниеОшибки.Класс.Идентификатор;
			ПредставлениеОшибки.description = ОписаниеОшибки.Класс.Описание;
			ПредставлениеОшибки.message = ОписаниеОшибки.Текст;
			
			Результат.stack.Добавить(ПредставлениеОшибки);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтатусОбработкиЗапроса.ОтладочныеСообщения) Тогда
		Результат.Вставить("debugMessages", СтатусОбработкиЗапроса.ОтладочныеСообщения);
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Сериализует ссылочное значение в структуру. Преобразует ссылочные типы данных
// в структуру с информацией о типе, идентификаторе и представлении.
//
// Параметры:
//   Значение - Произвольный - ссылочное значение для сериализации
//   Тип - Тип - тип значения
//
// Возвращаемое значение:
//   Структура - со свойствами:
//     * type - Строка - тип значения
//     * id - Строка - идентификатор значения
//     * presentation - Строка - представление значения
//
Функция СериализоватьСсылку(Значение, Тип)
	
	Результат = Новый Структура("type, id, presentation");
	Результат.type = РатТипыДанных.ПредставлениеТипа(Тип);
	Результат.presentation = Строка(Значение);
	
	ЧастиИмени = СтрРазделить(Результат.type, ".");
	
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Результат.id = "";
	ИначеЕсли ЧастиИмени[0] = "EnumRef" Или ЧастиИмени[0] = "BusinessProcessRoutePointRef" Тогда
		Результат.id = XMLСтрока(Значение);
	Иначе
		Результат.id = Строка(Значение.УникальныйИдентификатор());
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание операций и схем сервиса для REST API регламентных заданий.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса);
	
	ЗарегистрироватьОписаниеСхем(СпецификацияСервиса);
	
КонецПроцедуры

// Регистрирует шаблоны HTTP-обработчиков для REST API регламентных заданий.
//
// Параметры:
//   ШаблоныСервиса - см. РатШаблоныАдресов.Шаблоны
//
Процедура ЗарегистрироватьШаблоны(ШаблоныСервиса) Экспорт
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Обработчики = Обработчики();
	
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.GET,
									 "/РегламентныеЗадания",
									 Обработчики.СписокРегламентныхЗаданий,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/РегламентныеЗадания/Выключить",
									 Обработчики.ВыключитьРегламентныеЗадания,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.GET,
									 "/РегламентныеЗадания/{Идентификатор}",
									 Обработчики.ДанныеРегламентногоЗадания,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.PUT,
									 "/РегламентныеЗадания/{Идентификатор}",
									 Обработчики.НастроитьРегламентноеЗадание,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/РегламентныеЗадания/{Идентификатор}/Включить",
									 Обработчики.ВключитьРегламентноеЗадание,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/РегламентныеЗадания/{Идентификатор}/Выключить",
									 Обработчики.ВыключитьРегламентноеЗадание,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/РегламентныеЗадания/{Идентификатор}/Запустить",
									 Обработчики.ЗапуститьРегламентноеЗадание,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/РегламентныеЗадания/{Идентификатор}/Остановить",
									 Обработчики.ОстановитьРегламентноеЗадание,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.POST,
									 "/РегламентныеЗадания/{Идентификатор}/Перезапустить",
									 Обработчики.ПерезапуститьРегламентноеЗадание,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.GET,
									 "/РегламентныеЗадания/{Идентификатор}/ФоновоеЗадание",
									 Обработчики.АктивноеФоновоеЗаданиеРегламентного,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.GET,
									 "/ФоновыеЗадания/{Идентификатор}",
									 Обработчики.ФоновоеЗадание,
									 0);
	РатШаблоныАдресов.ДобавитьШаблон(ШаблоныСервиса,
									 Методы.DELETE,
									 "/ФоновыеЗадания/{Идентификатор}",
									 Обработчики.ЗавершитьФоновоеЗадание,
									 0);
	
КонецПроцедуры

// Вызвать обработчик.
// 
// Параметры:
//  Обработчик -  см. Обработчики
//  ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//  СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//  ОбработчикВыполнен - Булево - Признак того, что выполнена обработка запроса
// 
// Возвращаемое значение:
//  Произвольный - Результат обработки запроса 
Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
	
	Обработчики = Обработчики();
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.СписокРегламентныхЗаданий Тогда
		
		Результат = ОбработчикСписокРегламентныхЗаданий(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ДанныеРегламентногоЗадания Тогда
		
		Результат = ОбработчикДанныеРегламентногоЗадания(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ЗапуститьРегламентноеЗадание Тогда
		
		Результат = ОбработчикЗапуститьРегламентноеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ОстановитьРегламентноеЗадание Тогда
		
		Результат = ОбработчикОстановитьРегламентноеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ПерезапуститьРегламентноеЗадание Тогда
		
		Результат = ОбработчикПерезапуститьРегламентноеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.НастроитьРегламентноеЗадание Тогда
		
		Результат = ОбработчикНастроитьРегламентноеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.АктивноеФоновоеЗаданиеРегламентного Тогда
		
		Результат = ОбработчикАктивноеФоновоеЗаданиеРегламентного(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ФоновоеЗадание Тогда
		
		Результат = ОбработчикФоновоеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ЗавершитьФоновоеЗадание Тогда
		
		Результат = ОбработчикЗавершитьФоновоеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ВключитьРегламентноеЗадание Тогда
		
		Результат = ОбработчикВключенностьРегламентногоЗадания(ПараметрыЗапроса, Истина, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ВыключитьРегламентноеЗадание Тогда
		
		Результат = ОбработчикВключенностьРегламентногоЗадания(ПараметрыЗапроса, Ложь, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ВыключитьРегламентныеЗадания Тогда
		
		Результат = ОбработчикВключенностьРегламентныхЗаданий(ПараметрыЗапроса, Ложь, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Обработчики

Функция Обработчики()
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("СписокРегламентныхЗаданий",        "СписокРегламентныхЗаданий");
	Обработчики.Вставить("ДанныеРегламентногоЗадания",       "ДанныеРегламентногоЗадания");
	Обработчики.Вставить("НастроитьРегламентноеЗадание",     "НастроитьРегламентноеЗадание");
	
	Обработчики.Вставить("ЗапуститьРегламентноеЗадание",     "ЗапуститьРегламентноеЗадание");
	Обработчики.Вставить("ОстановитьРегламентноеЗадание",    "ОстановитьРегламентноеЗадание");
	Обработчики.Вставить("ПерезапуститьРегламентноеЗадание", "ПерезапуститьРегламентноеЗадание");
	
	Обработчики.Вставить("ФоновоеЗадание", "ФоновоеЗадание");
	Обработчики.Вставить("ЗавершитьФоновоеЗадание", "ЗавершитьФоновоеЗадание");
	Обработчики.Вставить("АктивноеФоновоеЗаданиеРегламентного", "АктивноеФоновоеЗаданиеРегламентного");
	
	Обработчики.Вставить("ВключитьРегламентноеЗадание",  "ВключитьРегламентноеЗадание");
	Обработчики.Вставить("ВыключитьРегламентноеЗадание", "ВыключитьРегламентноеЗадание");
	Обработчики.Вставить("ВыключитьРегламентныеЗадания", "ВыключитьРегламентныеЗадания");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);

КонецФункции

Функция ОбработчикСписокРегламентныхЗаданий(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Отбор = ПараметрыЗапроса.Параметры;
	СтруктураОтбора = СтруктураОтбораРегламентныхЗаданий(Отбор, СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Массив;
	Задания = РегламентныеЗадания.ПолучитьРегламентныеЗадания(СтруктураОтбора);
	
	Для Каждого Задание Из Задания Цикл
		Результат.Добавить(ОписаниеРегламентногоЗадания(Задание));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикДанныеРегламентногоЗадания(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	
	Идентификатор = ДесериализоватьИдентификатор(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	Задание = НайтиРегламентноеЗадание(Идентификатор, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Результат = ОписаниеРегламентногоЗадания(Задание);
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикЗапуститьРегламентноеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	ДатаОбработки = ТекущаяДатаСеанса();
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	
	Идентификатор = ДесериализоватьИдентификатор(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	Задание = НайтиРегламентноеЗадание(Идентификатор, СтатусОбработкиЗапроса);
	
	Результат = ЗапуститьРегламентноеЗадание(Задание, ПараметрыЗапроса, СтатусОбработкиЗапроса, ДатаОбработки);
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикОстановитьРегламентноеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	
	Идентификатор = ДесериализоватьИдентификатор(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	Задание = НайтиРегламентноеЗадание(Идентификатор, СтатусОбработкиЗапроса);
	
	Результат = ОстановитьРегламентноеЗадание(Задание, СтатусОбработкиЗапроса);
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикПерезапуститьРегламентноеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	ДатаОбработки = ТекущаяДатаСеанса();
	Результат = Новый Структура("Остановлено, Запущено");
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	
	Идентификатор = ДесериализоватьИдентификатор(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	Задание = НайтиРегламентноеЗадание(Идентификатор, СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат.Остановлено = ОстановитьРегламентноеЗадание(Задание, СтатусОбработкиЗапроса);
	Результат.Запущено = ЗапуститьРегламентноеЗадание(Задание, ПараметрыЗапроса, СтатусОбработкиЗапроса, ДатаОбработки);
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикНастроитьРегламентноеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	Идентификатор = ДесериализоватьИдентификатор(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	
	Результат = НастроитьРегламентноеЗадание(Идентификатор, ПараметрыЗапроса.Тело, СтатусОбработкиЗапроса);
	
	Возврат Результат;

КонецФункции

Функция ОбработчикАктивноеФоновоеЗаданиеРегламентного(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	
	Идентификатор = ДесериализоватьИдентификатор(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	Задание = НайтиРегламентноеЗадание(Идентификатор, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		
		АктивныеФоновыеЗадания = АктивныеФоновыеЗадания(Задание);
		АктивноеФоновоеЗадание = Неопределено;
		ДатаЗапуска = '00010101';
		
		Для Каждого ФоновоеЗадание Из АктивныеФоновыеЗадания Цикл
			Если ФоновоеЗадание.Начало > ДатаЗапуска Тогда
				АктивноеФоновоеЗадание = ФоновоеЗадание;
			КонецЕсли;
		КонецЦикла;
		
		Если АктивноеФоновоеЗадание = Неопределено Тогда
			СтатусОбработкиЗапроса.КодСостояния = РатОбщийКлиентСервер.КодыОтветов().НетСодержимого;
		Иначе
			Результат = ОписаниеФоновогоЗадания(АктивноеФоновоеЗадание);
		КонецЕсли;
		
	Иначе
		
		Результат = Неопределено;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикФоновоеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	
	Задание = НайтиФоновоеЗадание(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Результат = ОписаниеФоновогоЗадания(Задание);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикЗавершитьФоновоеЗадание(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	
	Задание = НайтиФоновоеЗадание(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		
		ОтменитьФоновоеЗадание(Задание, 5);
		Результат = ОписаниеФоновогоЗадания(Задание);
		
	Иначе
		
		Результат = Неопределено;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОбработчикВключенностьРегламентногоЗадания(ПараметрыЗапроса, Использовать, СтатусОбработкиЗапроса)
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "Идентификатор", СтатусОбработкиЗапроса);
	Идентификатор = ДесериализоватьИдентификатор(ПараметрыЗапроса.Идентификатор, СтатусОбработкиЗапроса);
	
	Задание = НайтиРегламентноеЗадание(Идентификатор, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Задание.Использование = Использовать;
		Задание.Записать();
		Возврат РатОбщий.РезультатДанныеЗаписаны();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ОбработчикВключенностьРегламентныхЗаданий(ПараметрыЗапроса, Использовать, СтатусОбработкиЗапроса)
	
	Для Каждого Задание Из РегламентныеЗадания.ПолучитьРегламентныеЗадания() Цикл
		
		Если Задание.Использование = Использовать Тогда
			Продолжить;
		КонецЕсли;
		
		Задание.Использование = Использовать;
		Задание.Записать();
		
	КонецЦикла;
	
	Возврат РатОбщий.РезультатДанныеЗаписаны();
	
КонецФункции

#КонецОбласти

#Область РеализацияОбработчиков

Функция ЗапуститьРегламентноеЗадание(Задание, ПараметрыЗапроса, СтатусОбработкиЗапроса, ДатаЗапуска)
	
	ВремяОжидания = ВремяОжидания(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФоновоеЗадание = ЗапуститьФоновоеЗадание(Задание, ДатаЗапуска, СтатусОбработкиЗапроса);
	
	Если ФоновоеЗадание <> Неопределено Тогда
		ОжидатьЗавершения(ФоновоеЗадание, ВремяОжидания);
		ОписаниеЗадания = ОписаниеФоновогоЗадания(ФоновоеЗадание);
	Иначе
		ОписаниеЗадания = Неопределено;
	КонецЕсли;
	
	Возврат ОписаниеЗадания;
	
КонецФункции

Функция ЗапуститьФоновоеЗадание(Задание, ДатаЗапуска, СтатусОбработкиЗапроса)
	
	Представление = ?(ЗначениеЗаполнено(Задание.Наименование), Задание.Наименование, Задание.Метаданные.Представление());
	НаименованиеФоновогоЗадания = СтрШаблон("Запуск вручную: %1", Представление);
	
	Попытка
		ФоновоеЗадание = ФоновыеЗадания.Выполнить(Задание.Метаданные.ИмяМетода,
												  Задание.Параметры,
												  Задание.Ключ,
												  НаименованиеФоновогоЗадания);
	Исключение
		Ошибка = ИнформацияОбОшибке();
	КонецПопытки;
	
	Если ФоновоеЗадание = Неопределено Тогда
		СтатусОбработкиЗапроса.ОтладочныеСообщения.Добавить("Поиск запущенного фонового задания после ошибки запуска");
		ФоновоеЗадание = ЗапущенноеЗадание(Задание, ДатаЗапуска, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если ФоновоеЗадание = Неопределено Тогда
		КлассыОшибок = РатОбщий.КлассыОшибок();
		
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
									 КлассыОшибок.ЛогикаКонфигурации,
									 "Не удалось запустить фоновое задание");
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
									 КлассыОшибок.ЛогикаКонфигурации,
									 Ошибка);
	КонецЕсли;
	
	Возврат ФоновоеЗадание;
	
КонецФункции

Функция ЗапущенноеЗадание(Задание, ДатаЗапуска, СтатусОбработкиЗапроса)
	
	НайденныеЗадания = АктивныеФоновыеЗадания(Задание);
	СтатусОбработкиЗапроса.ОтладочныеСообщения.Добавить(СтрШаблон("Найдено %1 запущенных заданий", НайденныеЗадания.Количество()));
	
	Для Каждого Задание Из НайденныеЗадания Цикл
		
		СтатусОбработкиЗапроса.ОтладочныеСообщения.Добавить(СтрШаблон("Задание %1 (%2) запушено %3",
																	  Задание.Наименование,
																	  Задание.Ключ,
																	  Задание.Начало));
		Если Задание.Начало >= ДатаЗапуска Тогда
			СтатусОбработкиЗапроса.ОтладочныеСообщения.Добавить("Задание подошло и будет использовано как результат запуска");
			Возврат Задание;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ОстановитьРегламентноеЗадание(Задание, СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Массив;
	
	АктивныеФоновыеЗадания = АктивныеФоновыеЗадания(Задание);
	
	Пока АктивныеФоновыеЗадания.Количество() Цикл
		
		Для Каждого ФоновоеЗадание Из АктивныеФоновыеЗадания Цикл
			ОтменитьФоновоеЗадание(ФоновоеЗадание);
		КонецЦикла;
		
		ВремяОжиданияЗавершенияЗаданий = 10;
		//@skip-check empty-except-statement
		Попытка
			//@skip-check bsl-legacy-check-dynamic-feature-access
			ФоновыеЗадания.ОжидатьЗавершения(АктивныеФоновыеЗадания, ВремяОжиданияЗавершенияЗаданий);
		Исключение
			// Ошибку таймаута и ошибку задания игнорируем
		КонецПопытки;
		
		Для Инд = -АктивныеФоновыеЗадания.ВГраница() По 0 Цикл
			ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(АктивныеФоновыеЗадания[-Инд].УникальныйИдентификатор);
			
			Если Не ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
				Результат.Добавить(ОписаниеФоновогоЗадания(ФоновоеЗадание));
				АктивныеФоновыеЗадания.Удалить(-Инд);
			КонецЕсли;
		КонецЦикла;
		
		АктивныеФоновыеЗадания = АктивныеФоновыеЗадания(Задание);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция НастроитьРегламентноеЗадание(Идентификатор, ДанныеЗадания, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	Задание = НайтиРегламентноеЗадание(Идентификатор, СтатусОбработкиЗапроса);
	УстановитьДанныеРегламентногоЗадания(Задание, ДанныеЗадания, СтатусОбработкиЗапроса);
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Задание.Записать();
		Результат = РатОбщий.РезультатДанныеЗаписаны();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СтруктурыДанных

Функция СтруктураОписанияРегламентногоЗадания()
	
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	
	ТипСтрока = Тип("Строка");
	ТипБулево = Тип("Булево");
	ТипЧисло = Тип("Число");
	ТипМассив = Тип("Массив");
	ТипУникальныйИдентификатор = Тип("УникальныйИдентификатор");
	ТипРасписание = Тип("РасписаниеРегламентногоЗадания");
	ТипФоновоеЗадание = Тип("ФоновоеЗадание");
	
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"УникальныйИдентификатор",
									"Уникальный идентификатор регламентного задания",
									ТипУникальныйИдентификатор,
									Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"Наименование",
									"Наименование регламентного задания",
									ТипСтрока);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"Использование",
									"Признак использования регламентного задания",
									ТипБулево);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"Предопределенное",
									"Указывает, является ли регламентное задание предопределенным",
									ТипБулево);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"Ключ",
									"Прикладной идентификатор. Ключ используется при запуске фонового задания на основе регламентного задания",
									ТипСтрока);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"Параметры",
									"Массив параметров регламентного задания.
									|Количество и состав параметров должны соответствовать параметрам метода регламентного задания",
									ТипМассив,
									Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"Расписание",
									"Расписание",
									ТипРасписание);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"ПоследнееЗадание",
									"Последнее фоновое задание",
									ТипФоновоеЗадание,
									Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"ИмяПользователя",
									"Имя пользователя, под которым будет выполняться данное регламентное задание",
									ТипСтрока);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"ИнтервалПовтораПриАварийномЗавершении",
									"Интервал в секундах, через который нужно перезапускать задание в случае его аварийного завершения",
									ТипЧисло);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"КоличествоПовторовПриАварийномЗавершении",
									"Количество повторов при аварийном завершении задания",
									ТипЧисло);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									"Метаданные",
									"Метаданные регламентного задания",
									ТипСтрока,
									Истина);
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

Функция СтруктураОписанияРасписания(РасписаниеДня = Ложь)
	
	ТипДата = Тип("Дата");
	ТипЧисло = Тип("Число");
	ТипМассив = Тип("Массив");
	
	СтруктураРасписания = Новый Структура();
	
	Если НЕ РасписаниеДня Тогда
		СтруктураРасписания.Вставить("ДатаНачала", ТипДата);
		СтруктураРасписания.Вставить("ДатаКонца", ТипДата);
		СтруктураРасписания.Вставить("ДетальныеРасписанияДня", СтруктураОписанияРасписания(Истина));
		СтруктураРасписания.Вставить("ДеньВМесяце", ТипЧисло);
		СтруктураРасписания.Вставить("ДеньНеделиВМесяце", ТипЧисло);
		СтруктураРасписания.Вставить("ДниНедели", ТипМассив);
		СтруктураРасписания.Вставить("Месяцы", ТипМассив);
		СтруктураРасписания.Вставить("ПериодПовтораДней", ТипЧисло);
		СтруктураРасписания.Вставить("ПериодНедель", ТипЧисло);
	КонецЕсли;
	
	СтруктураРасписания.Вставить("ВремяНачала", ТипДата);
	СтруктураРасписания.Вставить("ВремяКонца", ТипДата);
	СтруктураРасписания.Вставить("ВремяЗавершения", ТипДата);
	СтруктураРасписания.Вставить("ИнтервалЗавершения", ТипЧисло);
	СтруктураРасписания.Вставить("ПериодПовтораВТечениеДня", ТипЧисло);
	СтруктураРасписания.Вставить("ПаузаПовтора", ТипЧисло);
	
	Возврат СтруктураРасписания;
	
КонецФункции

Функция СтруктураФоновогоЗадания()
	
	ТипСтрока = Тип("Строка");
	ТипУникальныйИдентификатор = Тип("УникальныйИдентификатор");
	ТипДата = Тип("Дата");
	ТипСостояниеФоновогоЗадания = Тип("СостояниеФоновогоЗадания");
	
	СтруктураФоновогоЗадания = Новый Структура();
	СтруктураФоновогоЗадания.Вставить("УникальныйИдентификатор", ТипУникальныйИдентификатор);
	СтруктураФоновогоЗадания.Вставить("Наименование", ТипСтрока);
	СтруктураФоновогоЗадания.Вставить("Состояние", ТипСостояниеФоновогоЗадания);
	СтруктураФоновогоЗадания.Вставить("ИмяМетода", ТипСтрока);
	СтруктураФоновогоЗадания.Вставить("Начало", ТипДата);
	СтруктураФоновогоЗадания.Вставить("Конец", ТипДата);
	СтруктураФоновогоЗадания.Вставить("ИнформацияОбОшибке", ТипСтрока);
	СтруктураФоновогоЗадания.Вставить("Ключ", ТипСтрока);
	СтруктураФоновогоЗадания.Вставить("Расположение", ТипСтрока);
	
	Возврат СтруктураФоновогоЗадания;
	
КонецФункции

#КонецОбласти

#Область ОписаниеAPI

Процедура ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса)
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Группа = "Регламентные задания";
	
	РатСпецификация.ДобавитьГруппу(СпецификацияСервиса, Группа);
	
	СхемаBackgroundJob = РатOpenAPI.СсылкаНаСхему("backgroundJob");
	СхемаScheduledJob = РатOpenAPI.СсылкаНаСхему("scheduledJob");
	ИмяПараметраИдентификатора = "path_uid";
	
	// Список регламентных заданий
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Список регламентных заданий",
													"Возвращает список всех зарегистрированных регламентных заданий",
													Группа,
													"/РегламентныеЗадания",
													Методы.GET);
	СхемаОтвета = РатOpenAPI.НоваяСхемаКоллекции(СхемаScheduledJob, "Коллекция описания регламентных заданий");
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
	
	// Регламентное задание
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Описание регламентного задания",
													"Возвращает описание регламентного задания",
													Группа,
													"/РегламентныеЗадания/{uid}",
													Методы.GET);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраИдентификатора);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаScheduledJob);

	// Изменить регламентное задание
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Настройка регламентного задания",
													"Выполняет настройку регламентного задание по описанию и возвращает результат",
													Группа,
													"/РегламентныеЗадания/{uid}",
													Методы.PUT);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраИдентификатора);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаScheduledJob);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаScheduledJob);
	
	// Запустить регламентное задание
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Запустить регламентное задание",
													"Запускает регламентное задание и возвращает результат",
													Группа,
													"/РегламентныеЗадания/{uid}/Запустить",
													Методы.POST);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраИдентификатора);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаBackgroundJob);
	
	// Остановить регламентное задание
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Остановить регламентное задание",
													"Останавливает регламентное задание и возвращает результат",
													Группа,
													"/РегламентныеЗадания/{uid}/Остановить",
													Методы.POST);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраИдентификатора);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаBackgroundJob);
	
	// Перезапустить регламентное задание
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Перезапустить регламентное задание",
													"Перезапускает регламентное задание и возвращает результат",
													Группа,
													"/РегламентныеЗадания/{uid}/Перезапустить",
													Методы.POST);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраИдентификатора);
	
	СхемаОтвета = РатOpenAPI.НоваяСхемаОбъекта("Описание результат перезапуска");
	СхемаОстановленныхЗаданий = РатOpenAPI.НоваяСхемаКоллекции(СхемаBackgroundJob, "Описание остановленных фоновых заданий");
	РатOpenAPI.ДобавитьРеквизит(СхемаОтвета, "Остановлено", СхемаОстановленныхЗаданий);
	РатOpenAPI.ДобавитьРеквизит(СхемаОтвета, "Запущено", РатOpenAPI.СсылкаНаСхему("backgroundJob"), "Описание запущенного фонового задания");
	
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
	
	// Активное фоновое задание регламентное задание
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Последнее фоновое задание регламентного задания",
													"Возвращает описание фонового задания запущенного на основании регламентного",
													Группа,
													"/РегламентныеЗадания/{uid}/ФоновоеЗадание",
													Методы.GET);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраИдентификатора);
	
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаBackgroundJob);
	ОписаниеОтвета = Новый Структура("description", "Пустое тело ответа, если нет данных о последнем фоновом задании");
	РатСпецификация.ДобавитьОтвет(Операция, 204, ОписаниеОтвета);
	
	// Данные фонового задания по его UID 
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Фоновое задание",
													"Возвращает описание фонового задания",
													Группа,
													"ФоновыеЗадания/{uid}",
													Методы.GET);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраИдентификатора);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаBackgroundJob);
	
	// Завершение фонового задания по его UID 
	Операция = РатСпецификация.НоваяОперацияСервиса(СпецификацияСервиса,
													"Завершить фоновое задание",
													"Отменяет выполнение фонового задания",
													Группа,
													"ФоновыеЗадания/{uid}",
													Методы.DELETE);
	РатСпецификация.ДобавитьИзвестныйПараметрОперации(Операция, ИмяПараметраИдентификатора);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаBackgroundJob);
	
КонецПроцедуры

Процедура ЗарегистрироватьОписаниеСхем(СпецификацияСервиса)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	ТипСтрока = Тип("Строка");
	ТипУникальныйИдентификатор = Тип("УникальныйИдентификатор");
	ТипЧисло = Тип("Число");
	ТипДата = Тип("Дата");
	
	// Описание регламентного задания
	СуществующиеСхемы = Новый Соответствие;
	СуществующиеСхемы.Вставить(Тип("РасписаниеРегламентногоЗадания"), РатOpenAPI.СсылкаНаСхему("schedule"));
	СуществующиеСхемы.Вставить(Тип("ФоновоеЗадание"), РатOpenAPI.СсылкаНаСхему("backgroundJob"));
	ОписаниеРеквизитов = СтруктураОписанияРегламентногоЗадания();
	РатСпецификация.ЗарегистрироватьОписаниеОбъекта(СпецификацияСервиса,
													ОписаниеРеквизитов,
													"scheduledJob",
													"Описание регламентного задания",
													СуществующиеСхемы);
	
	// Описание фонового задания
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "УникальныйИдентификатор",
									 "Уникальный идентификатор задания",
									 ТипУникальныйИдентификатор,
									 Истина);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "Наименование", "Описание задания", ТипСтрока);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "Ключ",
									 "Прикладной уникальный идентификатор.
									 |Ключ задается разработчиком и должен быть уникальным среди активных фоновых заданий,
									 |имеющих такое же имя метода",
									 ТипСтрока);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ИмяМетода",
									 "Имя метода общего модуля в форме ИмяМодуля.ИмяМетода",
									 ТипСтрока);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ИнформацияОбОшибке",
									 "Информация об ошибке, если задание было завершено аварийно",
									 ТипСтрока,
									 ,
									 Ложь);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "Начало", "Дата запуска задания.", ТипДата);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "Конец", "Дата завершения задания (только для завершенных заданий)", ТипДата);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "Расположение",
									 "URL сервера, на котором выполняется или выполнялось задание",
									 ТипСтрока,
									 Истина);
	ОписаниеРеквизита = РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "Состояние", "Состояние задания", ТипСтрока);
	ОписаниеРеквизита.ВозможныеЗначения = РатПовтИсп.КлючиСистемногоПеречисления("СостояниеФоновогоЗадания");
	
	РатСпецификация.ЗарегистрироватьОписаниеОбъекта(СпецификацияСервиса, ОписаниеРеквизитов, "backgroundJob", "Описание фонового задания");
	
	// Описание расписания
	ОписаниеРеквизитов = РатСпецификация.ОписаниеРеквизитов();
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "ДатаНачала", "Дата начала расписания", ТипДата);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "ВремяНачала", "Время начала расписания", ТипДата);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "ДатаКонца", "Дата конца расписания", ТипДата);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов, "ВремяКонца", "Время конца расписания", ТипДата);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ВремяЗавершения",
									 "Время, после которого задание будет принудительно завершено",
									 ТипДата);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ДеньВМесяце",
									 "Номер дня в месяце, когда задание может быть запущено",
									 ТипЧисло);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ДеньНеделиВМесяце",
									 "Номер дня недели в месяце, когда задание может быть запущено ",
									 ТипЧисло);
	СхемаРеквизита = РатOpenAPI.НоваяСхемаКоллекции(ТипыДанных.ЦелоеЧисло);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ДниНедели",
									 "Массив номеров дней недели, по которым задание может быть запущено (Понедельник = 1)",
									 СхемаРеквизита);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ИнтервалЗавершения",
									 "Интервал времени в секундах от начала запуска регламентного задания,
									 |после которого задание будет принудительно завершено",
									 ТипЧисло);
	СхемаРеквизита = РатOpenAPI.НоваяСхемаКоллекции(ТипыДанных.ЦелоеЧисло);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "Месяцы",
									 "Массив номеров месяцев, по которым задание может быть запущено (январь - 1)",
									 СхемаРеквизита);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ПаузаПовтора",
									 "Минимальный интервал времени (в секундах) между повторными запусками задания",
									 ТипЧисло);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ПериодНедель",
									 "Период времени в неделях, через который нужно повторять запуск регламентного задания",
									 ТипЧисло);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ПериодПовтораВТечениеДня",
									 "Период времени в секундах, через который нужно запускать задание в течение дня",
									 ТипЧисло);
	РатСпецификация.ДобавитьРеквизит(ОписаниеРеквизитов,
									 "ПериодПовтораДней",
									 "Период времени в днях, через который нужно повторять запуск регламентного задания",
									 ТипЧисло);
	
	ИмяСхемыРасписанияДня = "scheduleDay";
	РатСпецификация.ЗарегистрироватьОписаниеОбъекта(СпецификацияСервиса,
													ОписаниеРеквизитов,
													ИмяСхемыРасписанияДня,
													"Описание детального расписания на день");
	
	СхемаДетальныеРасписанияДня = РатOpenAPI.НоваяСхемаОбъекта();
	СсылкаНаСхемуРасписанияДня = РатOpenAPI.СсылкаНаСхему(ИмяСхемыРасписанияДня);
	СхемаКоллекцииРасписанийДня = РатOpenAPI.НоваяСхемаКоллекции(СсылкаНаСхемуРасписанияДня,
																 "Массив детальных расписаний на день");
	РатOpenAPI.ДобавитьРеквизит(СхемаДетальныеРасписанияДня, "ДетальныеРасписанияДня", СхемаКоллекцииРасписанийДня);
	
	СхемаРасписания = РатOpenAPI.НоваяСхемаОбъединения("Описание расписания регламентного задания",
													   СсылкаНаСхемуРасписанияДня,
													   СхемаДетальныеРасписанияДня);
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, "schedule", СхемаРасписания);
	
КонецПроцедуры

#КонецОбласти

#Область DTO

Функция ОписаниеРегламентногоЗадания(РегламентноеЗадание)
	
	Если РегламентноеЗадание = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеРеквизитов = СтруктураОписанияРегламентногоЗадания();
	ОписаниеЗадания = Новый Структура;
	
	Для Каждого Реквизит Из ОписаниеРеквизитов Цикл
		ОписаниеЗадания.Вставить(Реквизит.Имя);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ОписаниеЗадания, РегламентноеЗадание);
	
	ОписаниеЗадания.Расписание = ОписаниеРасписания(РегламентноеЗадание.Расписание);
	ОписаниеЗадания.ПоследнееЗадание = ОписаниеФоновогоЗадания(РегламентноеЗадание.ПоследнееЗадание);
	ОписаниеЗадания.Метаданные = РегламентноеЗадание.Метаданные.Имя;
	
	Возврат ОписаниеЗадания;
	
КонецФункции

Функция ОписаниеРасписания(Расписание)
	
	Если Расписание = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Ключи = РатКоллекции.ВыгрузитьЗначения(СтруктураОписанияРасписания(), "Ключ");
	ОписаниеРасписания = Новый Структура(СтрСоединить(Ключи, ","));
		
	ЗаполнитьЗначенияСвойств(ОписаниеРасписания, Расписание);
	
	Если ЗначениеЗаполнено(Расписание.ДетальныеРасписанияДня) Тогда
		
		ОписаниеРасписания.Вставить("ДетальныеРасписанияДня");
		
		ОписаниеРасписания.ДетальныеРасписанияДня = Новый Массив();
		
		Для Каждого ДетальноеРасписанияДня Из Расписание.ДетальныеРасписанияДня Цикл
			
			ОписаниеРасписания.ДетальныеРасписанияДня.Добавить(ОписаниеРасписания(ДетальноеРасписанияДня));
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ОписаниеРасписания;
	
КонецФункции

Функция ОписаниеФоновогоЗадания(Задание)
	
	Если Задание = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Ключи = РатКоллекции.ВыгрузитьЗначения(СтруктураФоновогоЗадания(), "Ключ");
	ОписаниеЗадания = Новый Структура(СтрСоединить(Ключи, ","));
	
	ЗаполнитьЗначенияСвойств(ОписаниеЗадания, Задание, , "ИнформацияОбОшибке");
	
	Если Задание.ИнформацияОбОшибке <> Неопределено Тогда
		ОписаниеЗадания.ИнформацияОбОшибке = РатСтроки.ПредставлениеОшибки(Задание.ИнформацияОбОшибке);
	КонецЕсли;
	
	Возврат ОписаниеЗадания;
	
КонецФункции

#КонецОбласти

Функция СтруктураОтбораРегламентныхЗаданий(ДанныеЗапроса, СтатусОбработкиЗапроса)
	
	СтруктураОтбора = Новый Структура;
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗапроса) Тогда
		Возврат СтруктураОтбора;
	КонецЕсли;
	
	КлассыОшибок = РатОбщий.КлассыОшибок();
	ОписаниеРеквизитов = СтруктураОписанияРегламентногоЗадания();
	
	Ключи = "УникальныйИдентификатор, Ключ, Метаданные, Предопределенное, Использование, Наименование";
	ВозможныеКлючиОтбора = Новый Структура(Ключи);
	
	Для Каждого ЭлементЗапроса Из ДанныеЗапроса Цикл
		
		Если ВозможныеКлючиОтбора.Свойство(ЭлементЗапроса.Ключ) Тогда
			
			Реквизит = ОписаниеРеквизитов.Найти(ЭлементЗапроса.Ключ, "Имя");
			Если Реквизит <> Неопределено Тогда
				Значение = РатПреобразования.ДесериализоватьЗначение(ЭлементЗапроса.Значение,
																	 Реквизит.Тип,
																	 СтатусОбработкиЗапроса,
																	 "значение отбора " + ЭлементЗапроса.Ключ);
				СтруктураОтбора.Вставить(ЭлементЗапроса.Ключ, Значение);
			КонецЕсли;
		
		Иначе
			Сообщение = СтрШаблон("Неизвестный параметр отбора регламентных заданий `%1`. Доступно: %2", ЭлементЗапроса.Ключ, Ключи);
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.АнализПараметровЗапроса, Сообщение);
		КонецЕсли;
		
	КонецЦикла;
	
	ИмяМетаданныеРегламентногоЗадания = Неопределено;
	Если СтруктураОтбора.Свойство("Метаданные", ИмяМетаданныеРегламентногоЗадания)
		И ЗначениеЗаполнено(ИмяМетаданныеРегламентногоЗадания) Тогда
		СтруктураОтбора.Метаданные = Метаданные.РегламентныеЗадания.Найти(ИмяМетаданныеРегламентногоЗадания);
		Если СтруктураОтбора.Метаданные = Неопределено Тогда
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
										 КлассыОшибок.АнализПараметровЗапроса,
										 "Неизвестное имя метаданных регламентного задания " + ИмяМетаданныеРегламентногоЗадания);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураОтбора;
	
КонецФункции

Функция НайтиРегламентноеЗадание(Идентификатор, СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипИдентификатора = ТипЗнч(Идентификатор);
	КлассыОшибок = РатОбщий.КлассыОшибок();
	Если ТипИдентификатора = Тип("УникальныйИдентификатор") Тогда
		
		Задание = РегламентныеЗадания.НайтиПоУникальномуИдентификатору(Идентификатор);
		
	Иначе
		
		Отбор = Неопределено;
		Если ТипИдентификатора = Тип("Строка") Тогда
			Отбор = Новый Структура("Наименование", Идентификатор);
		ИначеЕсли ТипИдентификатора = Тип("Структура") Тогда
			Отбор = Идентификатор;
		Иначе
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
										 КлассыОшибок.РазборПараметровЗапроса,
										 "Не верный тип идентификатора регламентного задания");
			Возврат Неопределено;
		КонецЕсли;
		
		НайденныеРЗ = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Отбор);
		
		Если НайденныеРЗ.Количество() = 0 Тогда
			ПредставлениеОтбора = РатОбщий.Представление(Отбор);
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
										 КлассыОшибок.НеНайденаЗаписьИБ,
										 "Не удалось найти регламентное задание по отбору " + ПредставлениеОтбора);
		ИначеЕсли НайденныеРЗ.Количество() > 1 Тогда
			ПредставлениеОтбора = РатОбщий.Представление(Отбор);
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
										 КлассыОшибок.РазборПараметровЗапроса,
										 "Найдено несколько регламентных заданий по отбору " + ПредставлениеОтбора);
		Иначе
			Задание = НайденныеРЗ[0];
		КонецЕсли;
		
	КонецЕсли;
	
	Если Задание = Неопределено ИЛИ НЕ СтатусОбработкиЗапроса.Успешно  Тогда
		ТекстОшибки = "Не удалось найти регламентное задание с идентификатором " + Идентификатор;
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассыОшибок.НеНайденаЗаписьИБ, ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Задание;
	
КонецФункции

Функция НайтиФоновоеЗадание(Идентификатор, СтатусОбработкиЗапроса)
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		Идентификатор = РатПреобразования.ДесериализоватьЗначение(Идентификатор,
																  Тип("УникальныйИдентификатор"),
																  СтатусОбработкиЗапроса,
																  "Идентификатор фонового задания");
	КонецЕсли;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(Идентификатор);
	
	Если Задание = Неопределено Тогда
		КлассыОшибок = РатОбщий.КлассыОшибок();
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса,
									 КлассыОшибок.НеНайденаЗаписьИБ,
									 "Не найдено фоновое задание с идентификатором " + Идентификатор);
	КонецЕсли;
	
	Возврат Задание;
	
КонецФункции

Функция ДесериализоватьИдентификатор(ПредставлениеИдентификатора, СтатусОбработкиЗапроса)
	
	ТипУникальныйИдентификатор = Тип("УникальныйИдентификатор");
	
	Если РатСтроки.ЭтоУникальныйИдентификатор(ПредставлениеИдентификатора) Тогда
		Идентификатор = РатПреобразования.ДесериализоватьЗначение(ПредставлениеИдентификатора,
																  ТипУникальныйИдентификатор,
																  СтатусОбработкиЗапроса,
																  "Идентификатор регламентного задания");
	ИначеЕсли СтрНайти(ПредставлениеИдентификатора, "=") Тогда
		ДанныеОтбора = РатПреобразования.ЗначенияИдентификатора(ПредставлениеИдентификатора);
		Идентификатор = СтруктураОтбораРегламентныхЗаданий(ДанныеОтбора, СтатусОбработкиЗапроса);
	Иначе
		Идентификатор = ПредставлениеИдентификатора;
	КонецЕсли;
	
	Возврат Идентификатор;
	
КонецФункции

Функция ДесериализоватьДанныеРегламентногоЗадания(ДанныеЗадания, СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	
	Для Каждого Реквизит Из СтруктураОписанияРегламентногоЗадания() Цикл
		
		Если НЕ ДанныеЗадания.Свойство(Реквизит.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Реквизит.ТолькоЧтение Тогда
			
			Класс = РатОбщий.КлассыОшибок().АнализПараметровЗапроса;
			Сообщение = СтрШаблон("Реквизит '%1' нельзя изменить, он доступен только для чтения", Реквизит.Имя);
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, Класс, Сообщение);
			Продолжить;
		КонецЕсли;
		
		Значение = ДанныеЗадания[Реквизит.Имя];
		
		Если НЕ ЗначениеЗаполнено(Значение) Или Реквизит.Имя = "Расписание" Тогда
			
			Результат.Вставить(Реквизит.Имя, Значение);
			
		Иначе
			
			ЗначениеРЗ = РатПреобразования.ДесериализоватьЗначение(Значение, Реквизит.Тип, СтатусОбработкиЗапроса, Реквизит.Имя);
			Результат.Вставить(Реквизит.Имя, ЗначениеРЗ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьДанныеРегламентногоЗадания(РегламентноеЗадание, ДанныеЗадания, СтатусОбработкиЗапроса)
	
	Если СтатусОбработкиЗапроса.Успешно Тогда
		ДесериализованныеДанные = ДесериализоватьДанныеРегламентногоЗадания(ДанныеЗадания, СтатусОбработкиЗапроса);
	КонецЕсли;
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из ДесериализованныеДанные Цикл
		
		Если КлючИЗначение.Ключ = "Расписание" Тогда
			УстановитьДанныеРасписания(РегламентноеЗадание.Расписание, КлючИЗначение.Значение, СтатусОбработкиЗапроса);
		Иначе
			РегламентноеЗадание[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьДанныеРасписания(Расписание, ДанныеРасписания, СтатусОбработкиЗапроса, РасписаниеДня = Ложь)
	
	Для Каждого Реквизит Из СтруктураОписанияРасписания(РасписаниеДня) Цикл
	
		Если НЕ ДанныеРасписания.Свойство(Реквизит.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		Значение = ДанныеРасписания[Реквизит.Ключ];
		
		Если НЕ ЗначениеЗаполнено(Значение) Тогда
			
			Расписание[Реквизит.Ключ] = Значение;
			Продолжить;
			
		ИначеЕсли Реквизит.Ключ = "ДетальныеРасписанияДня" Тогда
			
			ДетальныеРасписанияДня = Новый Массив();
			
			Для Каждого Элемент Из Значение Цикл
				
				ДетальноеРасписанияДня = Новый РасписаниеРегламентногоЗадания();
				УстановитьДанныеРасписания(ДетальноеРасписанияДня, Элемент, СтатусОбработкиЗапроса, Истина);
				ДетальныеРасписанияДня.Добавить(ДетальноеРасписанияДня);
				
				Расписание.ДетальныеРасписанияДня = ДетальныеРасписанияДня;
				
			КонецЦикла;
			
		Иначе
			
			ЗначениеРЗ = РатПреобразования.ДесериализоватьЗначение(Значение, Реквизит.Значение, СтатусОбработкиЗапроса, Реквизит.Ключ);
			Расписание[Реквизит.Ключ] = ЗначениеРЗ;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОжидатьЗавершения(ФоновоеЗадание, ВремяОжидания)
	
	Если ВремяОжидания <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	//@skip-check empty-except-statement
	Попытка
		//@skip-check bsl-legacy-check-dynamic-feature-access
		ФоновоеЗадание.ОжидатьЗавершения(ВремяОжидания);
	Исключение
		// Игнорируем ошибки
	КонецПопытки;
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ФоновоеЗадание.УникальныйИдентификатор);
	
КонецПроцедуры

Процедура ОтменитьФоновоеЗадание(Задание, ВремяОжидания = 0)
	
	//@skip-check empty-except-statement
	Попытка
		Задание.Отменить();
	Исключение
		// Ошибку игнорируем
	КонецПопытки;
	
	ОжидатьЗавершения(Задание, ВремяОжидания);
	
КонецПроцедуры

Функция АктивныеФоновыеЗадания(РегламентноеЗадание)
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяМетода", РегламентноеЗадание.Метаданные.ИмяМетода);
	Отбор.Вставить("Ключ", РегламентноеЗадание.Ключ);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	Возврат ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
КонецФункции

Функция ВремяОжидания(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Если ПараметрыЗапроса.Параметры.Получить("ВремяОжидания") <> Неопределено Тогда
		ВремяОжидания = ПараметрыЗапроса.Параметры["ВремяОжидания"];
		Возврат РатПреобразования.ДесериализоватьЗначение(ВремяОжидания, Тип("Число"), СтатусОбработкиЗапроса, "Время ожидания");
	Иначе
		Возврат 5;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

// Регистрирует описание сервиса информационной базы,
// включая базовые схемы, операции и схемы для объектов метаданных.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
Процедура ЗарегистрироватьОписание(СпецификацияСервиса) Экспорт
	
	ЗарегистрироватьБазовыеСхемы(СпецификацияСервиса);
	
	ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса);
	
	ЗарегистрироватьОписаниеСхем(СпецификацияСервиса);
	
КонецПроцедуры

// Регистрирует шаблоны адресов для обработки запросов,
// определяя маршруты для CRUD-операций с объектами метаданных.
//
// Параметры:
//   Шаблоны - см. РатШаблоныАдресов.Шаблоны
Процедура ЗарегистрироватьШаблоны(Шаблоны) Экспорт
	
	Обработчики = ОбработчикиЗапросов();
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	
	МинимальныйПриоритет = 99;
	
	ТипыМетаданных = РатМетаданные.ТипыМетаданных();
	ИменаТиповМетаданных = ТипыМетаданных.ВыгрузитьКолонку("Имя");
	ИменаКоллекцийТиповМетаданных = ТипыМетаданных.ВыгрузитьКолонку("ИмяКоллекции");
	
	ПоддерживаемыеТипыМетаданных = РатКоллекции.ОбъединитьМассивы(ИменаТиповМетаданных, ИменаКоллекцийТиповМетаданных);
	
	ОграничениеПоТипу = Новый Структура("ТипМетаданных", ПоддерживаемыеТипыМетаданных);
	
	ШаблонСИдентификатором = "/{ТипМетаданных}/{ВидМетаданных}/{Идентификатор}";
	
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны, Методы.GET,
									 "/{ТипМетаданных}/{ВидМетаданных}",
									 Обработчики.СписокДанныхТаблицы,
									 ,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны,
									 Методы.GET,
									 "/{ТипМетаданных}/{ВидМетаданных}/Метаданные",
									 Обработчики.МетаданныеТаблицы,
									 1,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны,
									 Методы.POST,
									 "/{ТипМетаданных}/{ВидМетаданных}/Поиск",
									 Обработчики.СписокДанныхТаблицы,
									 1,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны, Методы.POST, "/{ТипМетаданных}/{ВидМетаданных}/", 
									 Обработчики.СоздатьЗаписьТаблицы,
									 ,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны,
									 Методы.GET,
									 ШаблонСИдентификатором,
									 Обработчики.ПолучитьЗаписьТаблицы,
									 МинимальныйПриоритет,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны,
									 Методы.PUT,
									 ШаблонСИдентификатором,
									 Обработчики.ИзменитьЗаписьТаблицы,
									 МинимальныйПриоритет,
									 ОграничениеПоТипу);
	РатШаблоныАдресов.ДобавитьШаблон(Шаблоны,
									 Методы.DELETE,
									 ШаблонСИдентификатором,
									 Обработчики.УдалитьЗаписьТаблицы,
									 МинимальныйПриоритет,
									 ОграничениеПоТипу);
	
КонецПроцедуры

// Вызывает соответствующий обработчик запроса в зависимости от его типа, 
// обеспечивая единую точку входа для всех операций с данными.
//
// Параметры:
//   Обработчик - Строка - идентификатор обработчика запроса
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//   ОбработчикВыполнен - Булево - признак выполнения обработчика
//
// Возвращаемое значение:
//   Произвольный - результат выполнения обработчика
Функция ВызватьОбработчик(Обработчик, ПараметрыЗапроса, СтатусОбработкиЗапроса, ОбработчикВыполнен) Экспорт
	
	Обработчики = ОбработчикиЗапросов();
	
	Результат = Неопределено;
	
	Если Обработчик = Обработчики.СписокДанныхТаблицы Тогда
		
		Результат = ПолучитьДанныеСписка(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ПолучитьЗаписьТаблицы Тогда
		
		Результат = ПолучитьЭлемент(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.СоздатьЗаписьТаблицы Тогда
		
		Результат = СоздатьДанные(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.ИзменитьЗаписьТаблицы Тогда
		
		Результат = ИзменитьДанные(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.УдалитьЗаписьТаблицы Тогда
		
		Результат = УдалитьДанные(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	ИначеЕсли Обработчик = Обработчики.МетаданныеТаблицы Тогда
		
		Результат = МетаданныеТаблицы(ПараметрыЗапроса, СтатусОбработкиЗапроса);
		ОбработчикВыполнен = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СхемыДанных

// Регистрирует базовые схемы для спецификации сервиса, 
// включая схемы параметров записи, тела поиска и расширенного поиска.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
Процедура ЗарегистрироватьБазовыеСхемы(СпецификацияСервиса)
	
	БазоваяСхемаПараметровЗаписи(СпецификацияСервиса);
	БазоваяСхемаТелаПоиска(СпецификацияСервиса);
	БазоваяСхемаРасширенногоПоиска(СпецификацияСервиса);
	
КонецПроцедуры

// Регистрирует описание схем для объектов метаданных,
// формируя входные и выходные схемы для каждого объекта и его табличных частей.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
Процедура ЗарегистрироватьОписаниеСхем(СпецификацияСервиса)
	
	ОписаниеМетаданных = РатПовтИсп.ТипыМетаданных();
	
	РеквизитыТолькоДляЧтения = СтрРазделить("Ссылка, Предопределенный, ИмяПредопределенныхДанных, ЭтоГруппа", ", ", Ложь);
	РеквизитыТолькоДляЧтенияТЧ = СтрРазделить("Ссылка, НомерСтроки", ", ", Ложь);
	
	Для Каждого Описание Из ОписаниеМетаданных Цикл
		
		Коллекция = Метаданные[Описание.ИмяКоллекции];
		
		Для Каждого Объект Из Коллекция Цикл
			
			ПредставлениеОбъекта = Объект.Представление();
			РеквизитыОбъекта = РатМетаданные.РеквизитыОбъектаМетаданных(Объект, Описание);
			
			РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса,
				РатСпецификация.ИмяСхемыОписанияОбъектаМетаданных(Объект, "Реквизиты"),
				РатOpenAPI.НоваяСхемаПеречисления(РатКоллекции.ВыгрузитьЗначения(РеквизитыОбъекта, "Имя"), "Реквизиты объекта"));
			
			СхемаВыходная = ОписаниеСхемыРеквизитов(ПредставлениеОбъекта, РеквизитыОбъекта, РеквизитыТолькоДляЧтения, Истина);
			РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса,
				РатСпецификация.ИмяВыходнойСхемыОбъектаМетаданных(Объект),
				СхемаВыходная);
			
			Если Описание.Имя <> "Перечисление" Тогда
				СхемаВходная = ОписаниеСхемыРеквизитов(ПредставлениеОбъекта, РеквизитыОбъекта, РеквизитыТолькоДляЧтения, Ложь);
				РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса,
					РатСпецификация.ИмяВходнойСхемыОбъектаМетаданных(Объект),
					СхемаВходная);
			КонецЕсли;
			
			Если Описание.ТабличныеЧасти Тогда
				ЗарегистрироватьСхемыТабличныхЧастей(СпецификацияСервиса,
													 Объект,
													 Описание,
													 СхемаВыходная,
													 СхемаВходная,
													 РеквизитыТолькоДляЧтенияТЧ);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Регистрирует схемы для табличных частей объекта метаданных.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//   Объект - ОбъектМетаданных - объект метаданных
//   Описание - см. РатМетаданные.ТипыМетаданных
//   СхемаВыходная - см. РатOpenAPI.НоваяСхемаОбъекта
//   СхемаВходная - см. РатOpenAPI.НоваяСхемаОбъекта
//   РеквизитыТолькоДляЧтенияТЧ - Массив из Строка - имена реквизитов только для чтения табличных частей
Процедура ЗарегистрироватьСхемыТабличныхЧастей(СпецификацияСервиса,
											   Объект,
											   Описание,
											   СхемаВыходная,
											   СхемаВходная,
											   РеквизитыТолькоДляЧтенияТЧ)
	
	Для Каждого ТабличнаяЧасть Из Объект.ТабличныеЧасти Цикл
		
		ПредставлениеТЧ = ТабличнаяЧасть.Представление();
		
		РеквизитыТЧ = Новый Массив;
		РатКоллекции.ОбъединитьМассивы(РеквизитыТЧ, ТабличнаяЧасть.Реквизиты, Ложь, , Истина);
		РатКоллекции.ОбъединитьМассивы(РеквизитыТЧ, ТабличнаяЧасть.СтандартныеРеквизиты, Ложь, , Истина);
		
		// Выходная схема
		ИмяСхемыВыходнойСтроки = РатСпецификация.ИмяВыходнойСхемыОбъектаМетаданных(ТабличнаяЧасть, "Строка");
		СхемаСтрокиТЧ = ОписаниеСхемыРеквизитов("Строка табличной части: " + ПредставлениеТЧ,
												РеквизитыТЧ,
												РеквизитыТолькоДляЧтенияТЧ,
												Истина);
		РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, ИмяСхемыВыходнойСтроки, СхемаСтрокиТЧ);
		
		ИмяВыходнойСхемыТабличнойЧасти = РатСпецификация.ИмяВыходнойСхемыОбъектаМетаданных(ТабличнаяЧасть);
		СхемаТЧ = РатOpenAPI.НоваяСхемаКоллекции(РатOpenAPI.СсылкаНаСхему(ИмяСхемыВыходнойСтроки),
												 "Набор строк табличной части: " + ПредставлениеТЧ);
		РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, ИмяВыходнойСхемыТабличнойЧасти, СхемаТЧ);
		
		РатOpenAPI.ДобавитьРеквизит(СхемаВыходная, ТабличнаяЧасть.Имя, РатOpenAPI.СсылкаНаСхему(ИмяВыходнойСхемыТабличнойЧасти));
		
		// Входная схема
		Если Описание.Редактирование Тогда
			ИмяВходнойСхемыСтроки = РатСпецификация.ИмяВходнойСхемыОбъектаМетаданных(ТабличнаяЧасть, "Строка");
			СхемаСтрокиТЧ = ОписаниеСхемыРеквизитов("Строка табличной части: " + ПредставлениеТЧ,
													РеквизитыТЧ,
													РеквизитыТолькоДляЧтенияТЧ,
													Истина);
			РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, ИмяВходнойСхемыСтроки, СхемаСтрокиТЧ);
			
			ИмяВходнойСхемыТабличнойЧасти = РатСпецификация.ИмяВходнойСхемыОбъектаМетаданных(ТабличнаяЧасть);
			СхемаТЧ = РатOpenAPI.НоваяСхемаКоллекции(РатOpenAPI.СсылкаНаСхему(ИмяВходнойСхемыСтроки),
													 "Набор строк табличной части: " + ПредставлениеТЧ);
			РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, ИмяВходнойСхемыТабличнойЧасти, СхемаТЧ);
			
			РатOpenAPI.ДобавитьРеквизит(СхемаВходная,
										ТабличнаяЧасть.Имя,
										РатOpenAPI.СсылкаНаСхему(ИмяВходнойСхемыТабличнойЧасти));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Формирует описание схемы реквизитов объекта, создавая структуру для сериализации/десериализации данных объекта.
//
// Параметры:
//   Описание - Строка - описание схемы
//   Реквизиты - Массив из ОбъектМетаданных - реквизиты объекта
//   РеквизитыТолькоДляЧтения - Массив из Строка - имена реквизитов только для чтения
//   ЭтоВыходнаяСхема - Булево - признак выходной схемы
//
// Возвращаемое значение:
//   см. РатOpenAPI.НоваяСхемаОбъекта
//
Функция ОписаниеСхемыРеквизитов(Описание, Реквизиты, РеквизитыТолькоДляЧтения, ЭтоВыходнаяСхема = Ложь)
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта(Описание);
	
	Для Каждого Реквизит Из Реквизиты Цикл
		
		РеквизитТолькоДляЧтения = РеквизитыТолькоДляЧтения <> Неопределено
			И РеквизитыТолькоДляЧтения.Найти(Реквизит.Имя) <> Неопределено;
		Если НЕ ЭтоВыходнаяСхема И РеквизитТолькоДляЧтения Тогда
			Продолжить;
		КонецЕсли;
		
		СхемаРеквизита = ОписаниеРеквизита(Реквизит, РеквизитТолькоДляЧтения, , ЭтоВыходнаяСхема);
		РатOpenAPI.ДобавитьРеквизит(Схема, Реквизит.Имя, СхемаРеквизита);
		
	КонецЦикла;
	
	Возврат Схема;
	
КонецФункции

// Формирует описание реквизита для схемы, определяя его тип, обязательность и доступность для чтения/записи.
//
// Параметры:
//   Реквизит - см. РатМетаданные.РеквизитыОбъектаМетаданных
//   ТолькоДляЧтения - Булево - признак реквизита только для чтения
//   Обязательный - Булево - признак обязательного реквизита
//   ЭтоВыходнаяСхема - Булево - признак выходной схемы
//
// Возвращаемое значение:
//   см. РатСпецификация.СхемаРеквизита
//
Функция ОписаниеРеквизита(Реквизит, ТолькоДляЧтения = Ложь, Обязательный = Ложь, ЭтоВыходнаяСхема = Ложь)
	
	Представление = Реквизит.Представление();
	
	Если ПустаяСтрока(Представление) Тогда
		Представление = Реквизит.Имя;
	КонецЕсли;
	
	ОписаниеПоля = РатСпецификация.СхемаРеквизита(Реквизит.Тип, Представление, ЭтоВыходнаяСхема);
	
	Если ТолькоДляЧтения Тогда
		ОписаниеПоля.Вставить("readOnly", Истина);
	КонецЕсли;
	
	Если НЕ Обязательный Тогда
		ОписаниеПоля.Вставить("nullable", Истина);
	КонецЕсли;
	
	Возврат ОписаниеПоля;
	
КонецФункции

// Формирует схему запроса изменения данных объекта, объединяя схему описания объекта с базовой схемой параметров записи.
//
// Параметры:
//   Объект - ОбъектМетаданных - объект метаданных
//
// Возвращаемое значение:
//   см. РатOpenAPI.НоваяСхемаОбъединения
//
Функция СхемаЗапросаИзмененияДанных(Знач Объект)
	
	СхемаОписанияОбъекта = РатСпецификация.СсылкуНаВходнуюСхему(Объект);
	
	Если Метаданные.Документы.Содержит(Объект) Тогда
		БазоваяСхема = "documentWriteBody";
	ИначеЕсли Метаданные.РегистрыСведений.Содержит(Объект) Тогда
		БазоваяСхема = "registryWriteBody";
	Иначе
		БазоваяСхема = "commonWriteBody";
	КонецЕсли;
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта("Данные записи");
	РатOpenAPI.ДобавитьРеквизит(Схема, "data", СхемаОписанияОбъекта);
	
	Возврат РатOpenAPI.НоваяСхемаОбъединения(Неопределено, Схема, РатOpenAPI.СсылкаНаСхему(БазоваяСхема));
	
КонецФункции

// Регистрирует базовую схему параметров записи, определяя структуру для настройки процесса сохранения данных.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура БазоваяСхемаПараметровЗаписи(СпецификацияСервиса)
	
	КлючПараметрыЗаписиДокумента = "documentWriteBody";
	КлючПараметрыЗаписиРегистра = "registryWriteBody";
	КлючПараметрыЗаписи = "commonWriteBody";
	КлючПараметрыЗаписи = "writeSettings";
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	// Базовые Параметры записи
	СхемаДополнительныхСвойств = РатOpenAPI.НоваяСхемаСтруктуры("Доп. свойства объекта, записываются в Объект.ДополнительныеСвойства");
	СхемаДополнительныхПараметров = РатOpenAPI.НоваяСхемаСтруктуры("Доп. параметры, могут быть использованы в алгоритмах расширения");
	
	СхемаПараметровЗаписи = ПараметрыЗаписиОбъекта(ТипыДанных);
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта();
	РатOpenAPI.ДобавитьРеквизит(Схема, "additionalProperties", СхемаДополнительныхСвойств);
	РатOpenAPI.ДобавитьРеквизит(Схема, "additionalParameters", СхемаДополнительныхПараметров);
	РатOpenAPI.ДобавитьРеквизит(Схема, КлючПараметрыЗаписи, СхемаПараметровЗаписи);
	
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, КлючПараметрыЗаписи, Схема);
	
	// Параметры записи документа
	СхемаПараметровЗаписи = ПараметрыЗаписиОбъекта(ТипыДанных);
	
	РежимЗаписи = РатСпецификация.СхемаСистемногоПеречисления(РежимЗаписиДокумента, "Режим записи документа");
	РатOpenAPI.ДобавитьРеквизит(СхемаПараметровЗаписи, "writeMode", РежимЗаписи);
	
	РежимПроведения = РатСпецификация.СхемаСистемногоПеречисления(РежимПроведенияДокумента, "Режим проведения документа");
	РатOpenAPI.ДобавитьРеквизит(СхемаПараметровЗаписи, "postingMode", РежимПроведения);
	
	СхемаЗаписиДокумента = РатКоллекции.СкопироватьСтруктуру(Схема);
	РатOpenAPI.ДобавитьРеквизит(СхемаЗаписиДокумента, КлючПараметрыЗаписи, СхемаПараметровЗаписи);
	
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, КлючПараметрыЗаписиДокумента, СхемаЗаписиДокумента);
	
	// Параметры записи регистра
	СхемаЗаписиРегистра = РатКоллекции.СкопироватьСтруктуру(Схема);
	СхемаПараметровЗаписи = ПараметрыЗаписиОбъекта(ТипыДанных);
	РатOpenAPI.ДобавитьРеквизит(СхемаЗаписиРегистра, КлючПараметрыЗаписи, СхемаПараметровЗаписи);
	РатOpenAPI.ДобавитьРеквизит(СхемаПараметровЗаписи, "replace", ТипыДанных.Булево, "Замещать при записи, только для регистров");
	
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, КлючПараметрыЗаписиРегистра, СхемаЗаписиРегистра);
	
КонецПроцедуры

// Формирует схему параметров записи объекта, определяя базовые настройки для сохранения данных.
//
// Параметры:
//   ТипыДанных - см. РатOpenAPI.ТипыДанных
//
// Возвращаемое значение:
//   см. РатOpenAPI.НоваяСхемаОбъекта
//
Функция ПараметрыЗаписиОбъекта(ТипыДанных)
	
	СхемаПараметровЗаписи = РатOpenAPI.НоваяСхемаОбъекта("Параметры записи объекта");
	РатOpenAPI.ДобавитьРеквизит(СхемаПараметровЗаписи,
								"dataExchangeParametersLoad",
								ТипыДанных.Булево,
								"Флаг `ОбменДанными.Загрузка`");
	
	Возврат СхемаПараметровЗаписи;
	
КонецФункции

#КонецОбласти

#Область ОписаниеОпераций

// Регистрирует описание операций для объектов метаданных, создавая REST API для работы с данными.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура ЗарегистрироватьОписаниеОпераций(СпецификацияСервиса)
	
	ОписаниеМетаданных = РатПовтИсп.ТипыМетаданных();
	
	Для Каждого Описание Из ОписаниеМетаданных Цикл
		
		Коллекция = Метаданные[Описание.ИмяКоллекции];
		
		Если НЕ Коллекция.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Объект Из Коллекция Цикл
			
			Группа = РатСпецификация.ГруппуОбъектаМетаданных(Описание, Объект);
			РатСпецификация.ДобавитьГруппу(СпецификацияСервиса, Группа, Объект.Представление());
			БазовыйАдрес = РатСпецификация.АдресОбъектаМетаданных(Описание, Объект);
			
			ПараметрИдентификатора = РатСпецификация.ПараметрИдентификаторОбъекта(Объект);
			
			РесурсЭлемента = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес,
													СтрШаблон("{%1}", ПараметрИдентификатора.ИмяПараметра));
			РатСпецификация.ДобавитьИзвестныйПараметрОперации(РесурсЭлемента, ПараметрИдентификатора.Ключ);
			
			ДобавитьОперациюПоискаОбъектов(СпецификацияСервиса, БазовыйАдрес, Объект, Описание, Группа);
			
			ДобавитьОперациюПолученияОбъекта(РесурсЭлемента, Объект, Группа);
			
			Если Описание.Редактирование Тогда
				
				ДобавитьОперациюСозданияОбъекта(СпецификацияСервиса, БазовыйАдрес, Объект, Группа);
				
				ДобавитьОперациюИзмененияОбъекта(РесурсЭлемента, Объект, Группа);
				
				Если Описание.ОбъектныеДанные Тогда
					ДобавитьОперациюУдаленияОбъекта(РесурсЭлемента, Объект, Группа);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет операцию поиска объектов в спецификацию сервиса, определяя эндпоинты для получения списка данных.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//   БазовыйАдрес - Строка - базовый адрес ресурса
//   Объект - ОбъектМетаданных - объект метаданных
//   ОписаниеТипа - см. РатМетаданные.ТипыМетаданных
//   Группа - Строка - группа операций
//
Процедура ДобавитьОперациюПоискаОбъектов(СпецификацияСервиса, БазовыйАдрес, Объект, ОписаниеТипа, Группа)
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	СхемаОтвета = ?(ОписаниеТипа.ОбъектныеДанные,
					РатOpenAPI.СсылкаНаСхему(РатСпецификация.БазовыеСхемы().ВыходнаяКоллекцияСсылок),
					РатСпецификация.СсылкуНаВыходнуюСхему(Объект));
	
	Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес);
	ОписаниеОперации = СтрШаблон("Получение списка объектов `%1`", Объект.Представление());
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.GET, "Список: базовые возможности", ОписаниеОперации, Группа);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
	
	Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес, "Поиск");
	ПредставлениеОбъекта = Объект.Представление();
	ОписаниеОперации = СтрШаблон("Расширенный поиск `%1`
								 |Структура ответа зависит от параметра `fields`,
								 |1. Если он не указан, то в ответе будет массив найденных ссылок
								 |2. Если в `fields` указано только одно поле, то ответ будет содержать массив этих значений
								 |3. Если в `fields` указанного несколько полей, то ответ будет содержать массив структур со значениями полей",
								 ПредставлениеОбъекта);
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс,
													Методы.POST,
													"Список: расширенные возможности",
													ОписаниеОперации,
													Группа);
	
	СхемаТела = СхемаТелаПоиска(Объект);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаТела);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
	
КонецПроцедуры

// Добавляет операцию получения объекта в спецификацию сервиса, определяя эндпоинт для получения данных по идентификатору.
//
// Параметры:
//   РесурсЭлемента - см. РатOpenAPI.БлокПуть
//   Объект - ОбъектМетаданных - объект метаданных
//   Группа - Строка - группа операций
//
Процедура ДобавитьОперациюПолученияОбъекта(РесурсЭлемента, Объект, Группа)
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	
	ОписаниеОперации = СтрШаблон("Получение записей (не списком, штучно) для `%1`", Объект.Представление());
	Операция = РатOpenAPI.НоваяОперация("Объект: получение", ОписаниеОперации, Группа);
	СхемаОтвета = РатСпецификация.СсылкуНаВыходнуюСхему(Объект);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, СхемаОтвета);
	
	РатСпецификация.ЗарегистрироватьОперациюРесурса(РесурсЭлемента, Методы.GET, Операция);
	
КонецПроцедуры

// Добавляет операцию создания объекта в спецификацию сервиса, определяя эндпоинт для создания новых записей.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//   БазовыйАдрес - Строка - базовый адрес ресурса
//   Объект - ОбъектМетаданных - объект метаданных
//   Группа - Строка - группа операций
//
Процедура ДобавитьОперациюСозданияОбъекта(СпецификацияСервиса, БазовыйАдрес, Объект, Группа)
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	Ресурс = РатСпецификация.Ресурс(СпецификацияСервиса, БазовыйАдрес);
	
	ОписаниеОперации = СтрШаблон("Создание записей для `%1`", Объект.Представление());
	Операция = РатСпецификация.НоваяОперацияРесурса(Ресурс, Методы.POST, "Объект: создание", ОписаниеОперации, Группа);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаЗапросаИзмененияДанных(Объект));
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, РатСпецификация.БазовыеСхемы().ВыходнаяСсылка);
	
КонецПроцедуры

// Добавляет операцию изменения объекта в спецификацию сервиса, определяя эндпоинт для обновления существующих записей.
//
// Параметры:
//   РесурсЭлемента - см. РатOpenAPI.БлокПуть
//   Объект - ОбъектМетаданных - объект метаданных
//   Группа - Строка - группа операций
//
Процедура ДобавитьОперациюИзмененияОбъекта(РесурсЭлемента, Объект, Группа)
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	
	ОписаниеОперации = СтрШаблон("Изменение записей для `%1`", Объект.Представление());
	Операция = РатСпецификация.НоваяОперацияРесурса(РесурсЭлемента, Методы.PUT, "Объект: изменение", ОписаниеОперации, Группа);
	РатСпецификация.УстановитьОписаниеТелаОперации(Операция, СхемаЗапросаИзмененияДанных(Объект));
	
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, РатСпецификация.БазовыеСхемы().ОтветУспешно);
	
КонецПроцедуры

// Добавляет операцию удаления объекта в спецификацию сервиса, определяя эндпоинт для удаления записей.
//
// Параметры:
//   РесурсЭлемента - см. РатOpenAPI.БлокПуть
//   Объект - ОбъектМетаданных - объект метаданных
//   Группа - Строка - группа операций
//
Процедура ДобавитьОперациюУдаленияОбъекта(РесурсЭлемента, Объект, Группа)
	
	Методы = РатОбщийКлиентСервер.МетодыHTTP();
	
	ОписаниеОперации = СтрШаблон("Удаление записей для `%1`", Объект.Представление());
	Операция = РатСпецификация.НоваяОперацияРесурса(РесурсЭлемента, Методы.DELETE, "Объект: удаление", ОписаниеОперации, Группа);
	РатСпецификация.ДобавитьСтандартныеОтветы(Операция, РатСпецификация.БазовыеСхемы().ОтветУспешно);
	
КонецПроцедуры

// Формирует схему тела запроса поиска, объединяя базовую схему поиска со схемой фильтрации для конкретного объекта.
//
// Параметры:
//   Объект - ОбъектМетаданных - объект метаданных
//
// Возвращаемое значение:
//   см. РатOpenAPI.НоваяСхемаОбъединения
//
Функция СхемаТелаПоиска(Объект)
	
	СхемыФильтра = РатКоллекции.ЗначениеВМассиве(
		РатOpenAPI.СсылкаНаСхему("extendedFilter"),
		РатСпецификация.СсылкуНаВходнуюСхему(Объект));
	
	СхемаТела = РатOpenAPI.НоваяСхемаОбъекта();
	РатOpenAPI.ДобавитьРеквизит(СхемаТела, "filter", РатOpenAPI.НоваяСхемаСоставногоТипа(СхемыФильтра));
	
	Возврат РатOpenAPI.НоваяСхемаОбъединения(Неопределено, РатOpenAPI.СсылкаНаСхему("searchItemsBody"), СхемаТела);
	
КонецФункции

// Регистрирует базовую схему расширенного поиска, определяя структуру для сложных условий фильтрации данных.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура БазоваяСхемаРасширенногоПоиска(СпецификацияСервиса)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	ДоступныеТипыДанных = РатКоллекции.ЗначениеВМассиве(
		ТипыДанных.Строка,
		ТипыДанных.Булево,
		ТипыДанных.ДробноеЧисло,
		ТипыДанных.Массив,
		ТипыДанных.Объект);
	
	СхемаЛюбойТип = РатOpenAPI.НоваяСхемаДанных(ДоступныеТипыДанных, "Произвольный");
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта();
	
	СхемаПоляКлюч = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка,
												"Имя реквизита объекта или путь к вложенному реквизиту, например: `Сумма`, `Номенклатура.Артикул`");
	РатOpenAPI.ДобавитьРеквизит(Схема, "key", СхемаПоляКлюч);
	РатOpenAPI.ДобавитьРеквизит(Схема, "value", СхемаЛюбойТип);
	
	СхемаПоляТип = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка, "Имя типа значения, XML тип или имя объекта метаданных.
																  |Примеры: `decimal`, `dateTime`, `AccountingRecordType`, `CatalogRef.Номенклатура`");
	РатOpenAPI.ДобавитьРеквизит(Схема, "type", СхемаПоляТип);
	
	СхемаПоляУсловие = РатOpenAPI.НоваяСхемаПеречисления(РатКоллекции.ВыгрузитьЗначения(РатОбщийКлиентСервер.ВидыСравнения(), "Ключ"));
	РатOpenAPI.ДобавитьРеквизит(Схема, "condition", СхемаПоляУсловие);
	
	СхемаКоллекции = РатOpenAPI.НоваяСхемаКоллекции(Схема, "Расширенный фильтр");
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, "extendedFilter", СхемаКоллекции);
	
КонецПроцедуры

// Регистрирует базовую схему тела поиска, определяя структуру для параметров выборки и сортировки данных.
//
// Параметры:
//   СпецификацияСервиса - см. РатOpenAPI.СпецификацияСервиса
//
Процедура БазоваяСхемаТелаПоиска(СпецификацияСервиса)
	
	ТипыДанных = РатOpenAPI.ТипыДанных();
	
	СхемаИмяРеквизита = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка,
		"Имя реквизита объекта");
	СхемыСортировки = Новый Массив;
	СхемаИменаПолейСортировки = РатOpenAPI.НоваяСхемаКоллекции(СхемаИмяРеквизита,
		"Список реквизитов по которым нужно отсортировать список.
		|Можно указывать направление через пробел: `""Наименование Возр""`, `""Дата Убыв""`");
	СхемыСортировки.Добавить(СхемаИменаПолейСортировки);
	СхемаИменаПолейСортировки = РатOpenAPI.НоваяСхемаДанных(ТипыДанных.Строка,
		"Имена полей сортировки разделенные запятой. Пример: `""Наименование, Дата Убыв""`");
	СхемыСортировки.Добавить(СхемаИменаПолейСортировки);
	
	СхемаСписокПолей = РатOpenAPI.НоваяСхемаКоллекции(СхемаИмяРеквизита,
		"Список выбираемых полей. Поддерживается:
		|* Имя реквизита объекта, `Родитель`
		|* Имя вложенного реквизита, `Родитель.Наименование`
		|* Звездочка - все реквизиты, `*`");
	
	Схема = РатOpenAPI.НоваяСхемаОбъекта();
	
	СхемаСортировка = РатOpenAPI.НоваяСхемаСоставногоТипа(СхемыСортировки);
	РатOpenAPI.ДобавитьРеквизит(Схема, "order", СхемаСортировка, "Сортировка");
	РатOpenAPI.ДобавитьРеквизит(Схема, "fields", СхемаСписокПолей);
	РатOpenAPI.ДобавитьРеквизит(Схема, "limit", ТипыДанных.ЦелоеЧисло,
		"Ограничение количества возвращаемых записей, если не указа - 10");
	
	РатСпецификация.ЗарегистрироватьСхему(СпецификацияСервиса, "searchItemsBody", Схема);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// 	Возвращает идентификаторы обработчиков запросов
// 	Используется в шаблонах для связки шаблона запроса и обработчика
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Обработчики запросов
Функция ОбработчикиЗапросов()
	
	Обработчики = Новый Структура;
	
	Обработчики.Вставить("МетаданныеТаблицы",       "МетаданныеТаблицы");
	Обработчики.Вставить("СписокДанныхТаблицы",     "СписокДанныхТаблицы");
	Обработчики.Вставить("ПолучитьЗаписьТаблицы",   "ПолучитьЗаписьТаблицы");
	Обработчики.Вставить("СоздатьЗаписьТаблицы",    "СоздатьЗаписьТаблицы");
	Обработчики.Вставить("ИзменитьЗаписьТаблицы",   "ИзменитьЗаписьТаблицы");
	Обработчики.Вставить("УдалитьЗаписьТаблицы",    "УдалитьЗаписьТаблицы");
	
	Возврат Новый ФиксированнаяСтруктура(Обработчики);
	
КонецФункции

#Область CRUD

// Получает список данных таблицы по параметрам выборки, обеспечивая фильтрацию, сортировку и выборку полей.
//
// Параметры:
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   см. РатИнформационнаяБаза.ДанныеСписка
//
Функция ПолучитьДанныеСписка(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "ТипМетаданных, ВидМетаданных", СтатусОбработкиЗапроса);
	
	ПараметрыВыборки = ПодготовитьПараметрыВыборки(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	Результат = РатИнформационнаяБаза.ДанныеСписка(ПараметрыВыборки, СтатусОбработкиЗапроса);
		
	Возврат Результат;
	
КонецФункции

// Получает элемент таблицы по идентификатору.
//
// Параметры:
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   см. РатИнформационнаяБаза.ДанныеЭлемента
Функция ПолучитьЭлемент(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса,
										"ТипМетаданных, ВидМетаданных, Идентификатор",
										СтатусОбработкиЗапроса);
	
	ИдентификаторЗаписи = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	Результат = РатИнформационнаяБаза.ДанныеЭлемента(ИдентификаторЗаписи,
													 ПараметрыЗапроса.ИмяТаблицы,
													 СтатусОбработкиЗапроса);
	
	Возврат Результат;
	
КонецФункции

// Создает новый элемент таблицы.
//
// Параметры:
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Строка - идентификатор созданного элемента
Функция СоздатьДанные(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "ТипМетаданных, ВидМетаданных, Тело", СтатусОбработкиЗапроса);
	
	ДанныеСохраняемогоОбъекта = РатПреобразования.ДанныеСохраняемогоОбъекта(ПараметрыЗапроса.Тело,
		ПараметрыЗапроса.ИмяТаблицы,
		СтатусОбработкиЗапроса);
	
	Результат = РатИнформационнаяБаза.СоздатьЭлемент(ДанныеСохраняемогоОбъекта, СтатусОбработкиЗапроса);
	
	Возврат Результат;
	
КонецФункции

// Изменяет существующий элемент таблицы.
//
// Параметры:
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Булево - Истина, если изменение выполнено успешно
Функция ИзменитьДанные(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса,
										"ТипМетаданных, ВидМетаданных, Идентификатор, Тело",
										СтатусОбработкиЗапроса);
	
	ИдентификаторЗаписи = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	ДанныеСохраняемогоОбъекта = РатПреобразования.ДанныеСохраняемогоОбъекта(ПараметрыЗапроса.Тело,
		ПараметрыЗапроса.ИмяТаблицы,
		СтатусОбработкиЗапроса);
	
	Результат = РатИнформационнаяБаза.ОбновитьЭлемент(ИдентификаторЗаписи,
													  ДанныеСохраняемогоОбъекта,
													  СтатусОбработкиЗапроса);
	
	Возврат Результат;
	
КонецФункции

// Удаляет элемент таблицы.
//
// Параметры:
//   ПараметрыЗапроса - см. РатСервис.ПараметрыВходящегоЗапроса
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
// Возвращаемое значение:
//   Булево - Истина, если удаление выполнено успешно
//
Функция УдалитьДанные(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "ТипМетаданных, ВидМетаданных, Идентификатор", СтатусОбработкиЗапроса);
	
	ИдентификаторЗаписи = РатПреобразования.ИдентификаторЗаписи(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	Результат = РатИнформационнаяБаза.УдалитьЭлемент(ИдентификаторЗаписи, ПараметрыЗапроса.ИмяТаблицы, СтатусОбработкиЗапроса);
	
	Возврат Результат;

КонецФункции

#КонецОбласти

Функция МетаданныеТаблицы(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Результат = Неопределено;
	
	РатСервис.ПроверитьПараметрыЗапроса(ПараметрыЗапроса, "ТипМетаданных, ВидМетаданных", СтатусОбработкиЗапроса);
	
	ИмяТаблицы = ПараметрыЗапроса.ИмяТаблицы;
	
	Результат = РатМетаданные.МетаданныеТаблицы(ИмяТаблицы, СтатусОбработкиЗапроса);
	
	Возврат Результат;

КонецФункции

#КонецОбласти

Функция ПодготовитьПараметрыВыборки(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	Если НЕ СтатусОбработкиЗапроса.Успешно Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыборки = ИзвлечьПараметрыВыборкиИзЗапроса(ПараметрыЗапроса, СтатусОбработкиЗапроса);
	
	ИмяТаблицы = ПараметрыВыборки.ИмяТаблицы;
	
	Для Каждого Предикат Из ПараметрыВыборки.Отбор Цикл
		
		Попытка
			ТипЗначения = РатПовтИсп.ТипРеквизита(СтрШаблон("%1.%2", ИмяТаблицы, Предикат.Реквизит));
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось определить тип значения отбора '%1'", Предикат.Реквизит);
			ТекстОшибки = РатОбщийКлиентСервер.ТекстОшибки(ИнформацияОбОшибке(), ТекстОшибки);
			КлассОшибки = РатОбщий.КлассыОшибок().РазборПараметровЗапроса;
			РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, КлассОшибки, ТекстОшибки);
			Продолжить;
		КонецПопытки;
		
		Описание = СтрШаблон("значение отбора '%1'", Предикат.Реквизит);
		
		Если РатТипыДанных.ЭтоМассив(Предикат.Значение) Тогда
			Предикат.Значение = РатПреобразования.ДесериализоватьМассивЗначений(Предикат.Значение,
																				ТипЗначения,
																				СтатусОбработкиЗапроса,
																				Описание);
		Иначе
			Предикат.Значение = РатПреобразования.ДесериализоватьЗначение(Предикат.Значение,
																		  ТипЗначения,
																		  СтатусОбработкиЗапроса,
																		  Описание);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПараметрыВыборки.ВыбираемыеПоля.Количество() = 0 Тогда
		РатКоллекции.ОбъединитьМассивы(ПараметрыВыборки.ВыбираемыеПоля,
									   РатМетаданные.КлючевыеПоляТаблицы(ИмяТаблицы),
									   ,
									   ,
									   Истина);
	КонецЕсли;
	
	Возврат ПараметрыВыборки;
	
КонецФункции

Функция ИзвлечьПараметрыВыборкиИзЗапроса(ПараметрыЗапроса, СтатусОбработкиЗапроса)
	
	ПараметрыВыборки = РатИнформационнаяБаза.ПараметрыВыборки();
	ПараметрыВыборки.ИмяТаблицы = ПараметрыЗапроса.ИмяТаблицы;
	
	ТелоЗапроса = ПараметрыЗапроса.Тело;
	
	Если ТелоЗапроса = Неопределено Тогда
		Возврат ПараметрыВыборки;
	КонецЕсли;
	
	ИзвлечьПараметрыФильтрации(ТелоЗапроса, ПараметрыВыборки, СтатусОбработкиЗапроса);
	ИзвлечьПараметрыСортировки(ТелоЗапроса, ПараметрыВыборки, СтатусОбработкиЗапроса);
	ИзвлечьВыбираемыеПоля(ТелоЗапроса, ПараметрыВыборки, СтатусОбработкиЗапроса);
	ИзвлечьЛимит(ТелоЗапроса, ПараметрыВыборки);
	
	Возврат ПараметрыВыборки;
	
КонецФункции

// Обрабатывает параметр фильтрации из тела запроса.
//
// Параметры:
//   ТелоЗапроса - Структура - тело запроса
//   ПараметрыВыборки - см. РатИнформационнаяБаза.ПараметрыВыборки
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
Процедура ИзвлечьПараметрыФильтрации(ТелоЗапроса, ПараметрыВыборки, СтатусОбработкиЗапроса)
	
	Если РатКоллекции.ЗначениеСтруктуры(ТелоЗапроса, "filter") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеФильтра = ТелоЗапроса.filter;
	ОписаниеОперации = РатОбщий.КлассыОшибок().РазборПараметровЗапроса;
	
	Если РатТипыДанных.ЭтоСтруктура(ДанныеФильтра) Тогда
		Для Каждого Элемент Из ДанныеФильтра Цикл
			ПараметрыВыборки.Отбор.Добавить(РатИнформационнаяБаза.Предикат(Элемент.Ключ, Элемент.Значение));
		КонецЦикла;
	ИначеЕсли РатТипыДанных.ЭтоМассив(ДанныеФильтра) Тогда
		Для Каждого Элемент Из ДанныеФильтра Цикл
			Предикат = РатИнформационнаяБаза.Предикат(Элемент.key, Элемент.value);
			Предикат.Тип = РатКоллекции.ЗначениеСтруктуры(Элемент, "type");
			Предикат.Условие = РатКоллекции.ЗначениеСтруктуры(Элемент, "condition");
			ПараметрыВыборки.Отбор.Добавить(Предикат);
		КонецЦикла;
	Иначе
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, ОписаниеОперации, "Не верный тип элемента 'filter'");
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает параметр сортировки из тела запроса.
//
// Параметры:
//   ТелоЗапроса - Структура - тело запроса
//   ПараметрыВыборки - см. РатИнформационнаяБаза.ПараметрыВыборки
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
Процедура ИзвлечьПараметрыСортировки(ТелоЗапроса, ПараметрыВыборки, СтатусОбработкиЗапроса)
	
	Если РатКоллекции.ЗначениеСтруктуры(ТелоЗапроса, "order") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСортировки = ТелоЗапроса.order;
	ОписаниеОперации = РатОбщий.КлассыОшибок().РазборПараметровЗапроса;
	
	Если ТипЗнч(ДанныеСортировки) = Тип("Массив") Тогда
		ПараметрыВыборки.Сортировка = ДанныеСортировки;
	ИначеЕсли ТипЗнч(ДанныеСортировки) = Тип("Строка") Тогда
		ПараметрыВыборки.Сортировка.Добавить(ДанныеСортировки);
	Иначе
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, ОписаниеОперации, "Не верный тип элемента 'order'");
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает параметр выбираемых полей из тела запроса.
//
// Параметры:
//   ТелоЗапроса - Структура - тело запроса
//   ПараметрыВыборки - см. РатИнформационнаяБаза.ПараметрыВыборки
//   СтатусОбработкиЗапроса - см. РатОбщий.НовыйСтатусОбработкиЗапроса
//
Процедура ИзвлечьВыбираемыеПоля(ТелоЗапроса, ПараметрыВыборки, СтатусОбработкиЗапроса)
	
	Если РатКоллекции.ЗначениеСтруктуры(ТелоЗапроса, "fields") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбираемыеПоля = ТелоЗапроса.fields;
	ОписаниеОперации = РатОбщий.КлассыОшибок().РазборПараметровЗапроса;
	
	Если ТипЗнч(ДанныеВыбираемыеПоля) = Тип("Массив") Тогда
		ПараметрыВыборки.ВыбираемыеПоля = ДанныеВыбираемыеПоля;
	Иначе
		РатОбщий.ЗафиксироватьОшибку(СтатусОбработкиЗапроса, ОписаниеОперации, "Не верный тип элемента 'fields'");
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает параметр лимита из тела запроса.
//
// Параметры:
//   ТелоЗапроса - Структура - тело запроса
//   ПараметрыВыборки - см. РатИнформационнаяБаза.ПараметрыВыборки
//
Процедура ИзвлечьЛимит(ТелоЗапроса, ПараметрыВыборки)
	
	Если РатКоллекции.ЗначениеСтруктуры(ТелоЗапроса, "limit") <> Неопределено Тогда
		ПараметрыВыборки.КоличествоЭлементов = ТелоЗапроса.limit;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

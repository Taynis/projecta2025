Перем гл_булВнешняяКомпонентаПодключена;
Перем гл_стрИмяКомпоненты Экспорт;  

Перем глProducer;
Перем глConsumer Экспорт;  
Перем глAdminClient;   

Перем Запись_НастройкиГлобальные, Просмотр_НастройкиГлобальные; 
Перем Чтение_НастройкиГлобальные, тзДанные_Запись;
Перем Запись_НастройкиТопика, Чтение_НастройкиТопика; 
перем глТаблицаНаОтправку; 
Перем чслПрочитатьКоличество_Чтение, чслТаймаутПоУмолчанию; 

#Область ОбработкиЗагрузки   


Функция ЗагрузитьНоменклатуру() Экспорт
	ИнициализацияМодуляЧтение("erp.tovar");

	чслЧтение = Число(XMLСтрока(чслПрочитатьКоличество_Чтение));
	чслТаймаут = Число(XMLСтрока(чслТаймаутПоУмолчанию));
	ЧитатьДД = ?(ЧитатьДвоичныеДанные, Истина, Ложь);
	
	сткРезультат = Получатель_ЭкранироватьЗначениеСообщения(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьКлючСообщения(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьКлючиЗаголовков(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьЗначенияЗаголовков(НЕ булНеЭкранироватьСообщение);
	
	чНачало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	сткРезультат = Получатель_ПрочитатьСписокСообщенийВПул(чслЧтение, чслТаймаут, ?(ПодпискаТопики_Чтение, 10, 1));
	сткРезультат = Получатель_ПолучитьСообщенияИзПулаJSON(ЧитатьДД);
	Если Не сткРезультат.булУспешно Тогда
		Возврат сткРезультат;
	КонецЕсли;	
		
	чОкончание = ТекущаяУниверсальнаяДатаВМиллисекундах();

	сткРезультат = JSONВСтруктуру(сткРезультат.стрСообщения);
	Если Не сткРезультат.булУспешно Тогда
		Возврат сткРезультат;
	КонецЕсли;	
	
	Если сткРезультат.масДанные.Количество() > 0 Тогда		
		сткРезультатФиксации = Получатель_ЗафиксироватьСмещения();
		Если Не сткРезультатФиксации.булУспешно Тогда
			Возврат сткРезультат;
		КонецЕсли;	
	КонецЕсли;

	
	тзДанные_Чтение.Очистить();	
	тзДанные = Получатель_ПолучитьТаблицуСообщения();
	Для Каждого Элемент Из сткРезультат.масДанные Цикл
		
		стрДобавить = тзДанные.Добавить();
		ЗаполнитьЗначенияСвойств(стрДобавить, Элемент);	
		стрДобавить.headers = Получатель_ПолучитьТаблицуЗаголовки();
		
			ИД = "";   
			Код = "";   
			Наименование = ""; 
			Пометка = ложь;
		
		Для Каждого СтрокаЗаголовок Из Элемент.Headers Цикл
			ЗаполнитьЗначенияСвойств(стрДобавить.headers.Добавить(), СтрокаЗаголовок); 
			Если СтрокаЗаголовок.Key = "ID" Тогда
				 ИД = СтрокаЗаголовок.Value; 
	        ИначеЕсли СтрокаЗаголовок.Key = "name" Тогда
				 Наименование = СтрокаЗаголовок.Value;
		    ИначеЕсли СтрокаЗаголовок.Key = "code" Тогда
				 Код = СтрокаЗаголовок.Value; 
			КонецЕсли;
		КонецЦикла;        
		
		Если код = ""  Тогда
			Чтение = Новый ЧтениеJSON;
			Чтение.УстановитьСтроку(Элемент.value);
			Пока Чтение.Прочитать() Цикл
				 
				Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
					Если Чтение.ТекущееЗначение = "Ref" Тогда
						  Чтение.Прочитать();
						  ИД = Чтение.ТекущееЗначение;  
					КонецЕсли;  
					Если Чтение.ТекущееЗначение = "DeletionMark" Тогда
						  Чтение.Прочитать();  
						  Если Строка(Чтение.ТекущееЗначение) = "true" Тогда
						  	Пометка = истина; 
						  КонецЕсли;
					  КонецЕсли;  
					  Если Чтение.ТекущееЗначение = "Code" Тогда
						  Чтение.Прочитать();
						  Код = Чтение.ТекущееЗначение;  
					  КонецЕсли;
					  Если Чтение.ТекущееЗначение = "Description" Тогда
						  Чтение.Прочитать();
						  Наименование = Чтение.ТекущееЗначение;  
					КонецЕсли;

				КонецЕсли;
			КонецЦикла;
			Чтение.Закрыть();
		КонецЕсли; 
		
		СоздатьНоменклатуру(Наименование, Код, ИД);
		
	КонецЦикла;
	
	тзДанные_Чтение.Загрузить( тзДанные);
	
	ОбновитьСмещенияРазделов();
КонецФункции  

Процедура  СоздатьНоменклатуру(Наименование, Код, ИД)
	номссылка = Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(ИД));
	Если НЕ ЗначениеЗаполнено(номссылка) или номссылка.ПолучитьОбъект() = неопределено Тогда
		ном = Справочники.Номенклатура.СоздатьЭлемент();
		ном.ОбменДанными.Загрузка = истина;  
		ном.УстановитьСсылкуНового(номссылка);
	Иначе
		ном = номссылка.ПолучитьОбъект();
	КонецЕсли;
	
	ном.ИдЕРП = ИД;
	ном.Наименование = Наименование;
	ном.Код = Код;
	Ном.Записать();
	
	
КонецПроцедуры


Функция ЗагрузитьМарки()  Экспорт 
	ЗагрузитьНоменклатуру();
	ИнициализацияМодуляЧтение("erp.marki");
	чслЧтение = Число(XMLСтрока(чслПрочитатьКоличество_Чтение));
	чслТаймаут = Число(XMLСтрока(чслТаймаутПоУмолчанию));
	ЧитатьДД = ?(ЧитатьДвоичныеДанные, Истина, Ложь);
	
	сткРезультат = Получатель_ЭкранироватьЗначениеСообщения(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьКлючСообщения(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьКлючиЗаголовков(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьЗначенияЗаголовков(НЕ булНеЭкранироватьСообщение);
	
	чНачало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	сткРезультат = Получатель_ПрочитатьСписокСообщенийВПул(чслЧтение, чслТаймаут, ?(ПодпискаТопики_Чтение, 10, 1));
	сткРезультат = Получатель_ПолучитьСообщенияИзПулаJSON(ЧитатьДД);
	Если Не сткРезультат.булУспешно Тогда
		Возврат сткРезультат;
	КонецЕсли;	
		
	чОкончание = ТекущаяУниверсальнаяДатаВМиллисекундах();

	сткРезультат = JSONВСтруктуру(сткРезультат.стрСообщения);
	Если Не сткРезультат.булУспешно Тогда
		Возврат сткРезультат;
	КонецЕсли;	
	
	Если сткРезультат.масДанные.Количество() > 0 Тогда		
		сткРезультатФиксации = Получатель_ЗафиксироватьСмещения();
		Если Не сткРезультатФиксации.булУспешно Тогда
			Возврат сткРезультат;
		КонецЕсли;	
	КонецЕсли;
			
	тзДанные_Чтение.Очистить();	
	тзДанные = Получатель_ПолучитьТаблицуСообщения();
	Для Каждого Элемент Из сткРезультат.масДанные Цикл
		
		стрДобавить = тзДанные.Добавить();
		ЗаполнитьЗначенияСвойств(стрДобавить, Элемент);	
		стрДобавить.headers = Получатель_ПолучитьТаблицуЗаголовки();   
		
		ТипДокумента = "";
		ИдДокумента = "";
		ИДНоменклатуры = "";
		Марка = "";           
		ННоменклатуры = "";

		
		Для Каждого СтрокаЗаголовок Из Элемент.Headers Цикл
			ЗаполнитьЗначенияСвойств(стрДобавить.headers.Добавить(), СтрокаЗаголовок); 
			Если СтрокаЗаголовок.Key = "TipD" Тогда
				 ТипДокумента = СтрокаЗаголовок.Value; 
	        ИначеЕсли СтрокаЗаголовок.Key = "IdDok" Тогда
				 ИдДокумента = СтрокаЗаголовок.Value;
		    ИначеЕсли СтрокаЗаголовок.Key = "IdNom" Тогда
				 ИДНоменклатуры = СтрокаЗаголовок.Value;  
			ИначеЕсли СтрокаЗаголовок.Key = "Nom" Тогда
				 нНоменклатуры = СтрокаЗаголовок.Value; 
			ИначеЕсли СтрокаЗаголовок.Key = "Marka"	 Тогда
				 Марка = СтрокаЗаголовок.Value;
			КонецЕсли;
		КонецЦикла;   
		
		Док = ПодобратьДокумент(ИдДокумента, ТипДокумента);      
		Если Док = Неопределено Тогда
			Док = Документы.ДокументЕРП.СоздатьДокумент();
			Док.ТипДокумента = ТипДокумента;
			Док.ИдЕРП = ИдДокумента; 
			Док.ОбменДанными.Загрузка = истина;     
			докссылка = документы.ДокументЕРП.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдДокумента));
			Док.Дата = ТекущаяДатаСеанса();
			Док.УстановитьСсылкуНового(докссылка);
			Док.Записать();
		КонецЕсли;    
		
		ШтрихКод = ПолучитьШтрихКод(ИДНоменклатуры, ННоменклатуры, Марка);
		
		Если НЕ ЕстьМарка(Док.Ссылка, ШтрихКод) Тогда
			 строкаМ = Док.НВД_ШтрихкодыУпаковок.Добавить();
			 строкаМ.ШтрихкодУпаковки = ШтрихКод;
			 Док.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	
	ОбновитьСмещенияРазделов();


КонецФункции

Функция ПодобратьДокумент(ИдДокумента, ТипДокумента)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументЕРП.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ДокументЕРП КАК ДокументЕРП
		|ГДЕ
		|	ДокументЕРП.ИдЕРП = &ИдЕРП
		|	И ДокументЕРП.ТипДокумента = &ТипДокумента";
	
	Запрос.УстановитьПараметр("ИдЕРП", ИдДокумента);
	Запрос.УстановитьПараметр("ТипДокумента", ТипДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
	КонецЦикла;
	
	Возврат неопределено;

	
КонецФункции

Функция ЕстьМарка(Док, Марка)
  	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументЕРПНВД_ШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
		|ИЗ
		|	Документ.ДокументЕРП.НВД_ШтрихкодыУпаковок КАК ДокументЕРПНВД_ШтрихкодыУпаковок
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|		ПО ДокументЕРПНВД_ШтрихкодыУпаковок.ШтрихкодУпаковки = ШтрихкодыУпаковокТоваров.Ссылка
		|ГДЕ
		|	ДокументЕРПНВД_ШтрихкодыУпаковок.Ссылка = &Ссылка
		|	И ШтрихкодыУпаковокТоваров.Ссылка = &Марка";
	
	Запрос.УстановитьПараметр("Ссылка", Док);       
	Запрос.УстановитьПараметр("Марка", Марка);

	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат истина
	КонецЦикла;
	
	Возврат ложь;
	
КонецФункции

Функция ПолучитьШтрихКод(ИДНоменклатуры, ННоменклатуры, Марка)

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШтрихкодыУпаковокТоваров.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|ГДЕ
		|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода = &Марка";
	
	Запрос.УстановитьПараметр("Марка", Марка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка; 
	КонецЦикла;
	
	Спр = Справочники.ШтрихкодыУпаковокТоваров.СоздатьЭлемент(); 
	если ИДНоменклатуры <> "" Тогда
		НоменклатураС = Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(ИДНоменклатуры));
		Если НоменклатураС.ПолучитьОбъект() = Неопределено Тогда   
			Номенклатура = Справочники.Номенклатура.СоздатьЭлемент();
			Номенклатура.Наименование = ННоменклатуры;    
			Номенклатура.ИдЕРП =  ИДНоменклатуры;
			Номенклатура.ОбменДанными.Загрузка = истина;
			Номенклатура.УстановитьСсылкуНового(НоменклатураС);
			Номенклатура.Записать();
			НоменклатураС= Номенклатура.Ссылка;
		КонецЕсли;	
		спр.Номенклатура = НоменклатураС;
	КонецЕсли;
	спр.ЗначениеШтрихкода = Марка;
	спр.ТипШтрихкода = Перечисления.ТипыШтрихкодов.DataMatrix;
	спр.ТипУпаковки = Перечисления.ТипыУпаковок.МонотоварнаяУпаковка;   
	спр.Записать();
	Возврат спр.Ссылка;
	
КонецФункции

#КонецОбласти


#Область ОбработкиВыгрузки
 Процедура ВыгрузитьНоменклатуру(МасН) Экспорт
    мДанныеНаЗапись = Новый Массив();

	Для каждого Ном из МасН Цикл
		мЗаголовки = Новый Массив();
		//мЗаголовки.Добавить(Новый Структура("ID", Строка(Ном.УникальныйИдентификатор())));
		//мЗаголовки.Добавить(Новый Структура("name", Ном.Наименование));  
		//мЗаголовки.Добавить(Новый Структура("code", Ном.Код));  
		
		ЗаписьJson = Новый ЗаписьJson;
		ЗаписьJSON.УстановитьСтроку(); 
		СериализаторXDTO.ЗаписатьJSON(ЗаписьJSON, Ном.ПолучитьОбъект());

		стрСообщение = ЗаписьJSON.Закрыть();
		мДанныеНаЗапись.Добавить(Новый Структура("Key, Value, Headers", XMLСтрока(Ном), стрСообщение, мЗаголовки));
	КонецЦикла;		 
	
    ИнициализацияМодуляЗапись("erp.tovar");  
    ОтправитьСообщение(мДанныеНаЗапись);
    Отправить_ЗаписьНажатие();
	 
КонецПроцедуры   

 Процедура ВыгрузитьМарки(МасН) Экспорт
    мДанныеНаЗапись = Новый Массив();

	Для каждого СМ из МасН Цикл
		мЗаголовки = Новый Массив();                             
		мЗаголовки.Добавить(Новый Структура("TipD", СМ.TipD));
		мЗаголовки.Добавить(Новый Структура("IdDok", СМ.IdDok));
		мЗаголовки.Добавить(Новый Структура("IdNom", СМ.IdNom));  
		мЗаголовки.Добавить(Новый Структура("Marka", СМ.Marka));  

		стрСообщение = СМ.Marka;
		мДанныеНаЗапись.Добавить(Новый Структура("Key, Value, Headers", СМ.Marka, стрСообщение, мЗаголовки));
	КонецЦикла;		 
	
    ИнициализацияМодуляЗапись("erp.marki");  
    ОтправитьСообщение(мДанныеНаЗапись);
    Отправить_ЗаписьНажатие();
	 
 КонецПроцедуры

#КонецОбласти


#Область ПроцедурыИзФормы    

Функция ПрочитатьJSON_ЧтениеНажатие() Экспорт
	
	чслЧтение = Число(XMLСтрока(чслПрочитатьКоличество_Чтение));
	чслТаймаут = Число(XMLСтрока(чслТаймаутПоУмолчанию));
	ЧитатьДД = ?(ЧитатьДвоичныеДанные, Истина, Ложь);
	
	сткРезультат = Получатель_ЭкранироватьЗначениеСообщения(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьКлючСообщения(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьКлючиЗаголовков(НЕ булНеЭкранироватьСообщение);
	сткРезультат = Получатель_ЭкранироватьЗначенияЗаголовков(НЕ булНеЭкранироватьСообщение);
	
	чНачало = ТекущаяУниверсальнаяДатаВМиллисекундах();
	сткРезультат = Получатель_ПрочитатьСписокСообщенийВПул(чслЧтение, чслТаймаут, ?(ПодпискаТопики_Чтение, 10, 1));
	сткРезультат = Получатель_ПолучитьСообщенияИзПулаJSON(ЧитатьДД);
	Если Не сткРезультат.булУспешно Тогда
		Возврат сткРезультат;
	КонецЕсли;	
		
	чОкончание = ТекущаяУниверсальнаяДатаВМиллисекундах();

	сткРезультат = JSONВСтруктуру(сткРезультат.стрСообщения);
	Если Не сткРезультат.булУспешно Тогда
		Возврат сткРезультат;
	КонецЕсли;	
	
	Если сткРезультат.масДанные.Количество() > 0 Тогда		
		сткРезультатФиксации = Получатель_ЗафиксироватьСмещения();
		Если Не сткРезультатФиксации.булУспешно Тогда
			Возврат сткРезультат;
		КонецЕсли;	
	КонецЕсли;
			
	тзДанные_Чтение.Очистить();	
	тзДанные = Получатель_ПолучитьТаблицуСообщения();
	Для Каждого Элемент Из сткРезультат.масДанные Цикл
		
		стрДобавить = тзДанные.Добавить();
		ЗаполнитьЗначенияСвойств(стрДобавить, Элемент);	
		стрДобавить.headers = Получатель_ПолучитьТаблицуЗаголовки();
		
		Для Каждого СтрокаЗаголовок Из Элемент.Headers Цикл
			ЗаполнитьЗначенияСвойств(стрДобавить.headers.Добавить(), СтрокаЗаголовок);
		КонецЦикла;
	КонецЦикла;
	
	тзДанные_Чтение.Загрузить( тзДанные);
	
	ОбновитьСмещенияРазделов();
КонецФункции  

Процедура ОбновитьСмещенияРазделов()
	
	мОшибки = Новый Массив();
	чслТаймаут = Число(XMLСтрока(чслТаймаутПоУмолчанию));
	
	Для Каждого Строка Из тзТопикиРазделы_Чтение Цикл
		
		Топик = XMLСтрока(Строка.Topic);
		Раздел = Число(XMLСтрока(Строка.Partition));
			
		сткРезультатПолучения = Получатель_ПолучитьТекущееСмещениеРаздела(Топик, Раздел, чслТаймаут);
		Если НЕ сткРезультатПолучения.булУспешно Тогда
			мОшибки.Добавить(сткРезультатПолучения.стрОписаниеОшибки);	
			Продолжить;
		КонецЕсли;
		
		Строка.offset = сткРезультатПолучения.чСмещение; 
	Конеццикла;
	
	Если мОшибки.Количество() > 0 Тогда
		Сообщить("Ошибки получения текущих смещений: ");
		
		Для Каждого стрОшибка Из мОшибки Цикл
			Сообщить(Символы.Таб + стрОшибка);	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


Процедура Инициализация_ЧтениеНажатие(топик) Экспорт
		
	Получатель_Закрыть();
	
	сткРезультат = СоздатьПолучателя();
	Если Не сткРезультат.булУспешно Тогда
		Сообщить(сткРезультат.стрОписаниеОшибки);	
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Чтение_НастройкиГлобальные Цикл
		сткРезультат = Получатель_УстановитьПараметрГлобальный(Строка.Key, Строка.Value);
		Если Не сткРезультат.булУспешно Тогда
			Сообщить(сткРезультат.стрОписаниеОшибки);	
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из Чтение_НастройкиТопика Цикл
		сткРезультат = Получатель_УстановитьПараметрТопика(Строка.Key, Строка.Value);
		Если НЕ сткРезультат.булУспешно Тогда
			Сообщить(сткРезультат.стрОписаниеОшибки);	
			Возврат;
		КонецЕсли;
	КонецЦикла;
		
	сткРезультат = Получатель_Инициализировать(Адрес_Чтение, GroupID_Чтение);
	Если Не сткРезультат.булУспешно Тогда
		Сообщить(сткРезультат.стрОписаниеОшибки);	
		Возврат;
	КонецЕсли;
	
	Если ПодпискаТопики_Чтение = 1 Тогда
		мТопики = Новый Массив();
		//Для Каждого стрТопик Из тзТопики_Чтение Цикл
		//	Если Не мТопики.Найти(стрТопик.Topic) = Неопределено Тогда
		//		Продолжить;
		//	КонецЕсли;
			
			//мТопики.Добавить(стрТопик.Topic);	
		мТопики.Добавить(Топик);		
		//КонецЦикла;
		
		сткРезультат = Получатель_Подписаться(мТопики);
		Если Не сткРезультат.булУспешно Тогда
			Сообщить(сткРезультат.стрОписаниеОшибки);	
			Возврат;
		КонецЕсли;
	Иначе
		сткРезультат = Получатель_ОчиститьСписокНазначений();
		Если Не сткРезультат.булУспешно Тогда
			Сообщить(сткРезультат.стрОписаниеОшибки);	
			Возврат;
		КонецЕсли;

		Для Каждого стрТопик Из тзТопикиРазделы_Чтение Цикл			
			сткРезультат = Получатель_ДобавитьНазначение(стрТопик.Topic, стрТопик.Partition, ?(стрТопик.Offset = -1, Неопределено, стрТопик.Offset));
			Если Не сткРезультат.булУспешно Тогда
				Сообщить(сткРезультат.стрОписаниеОшибки);	
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		сткРезультат = Получатель_Назначить();
		Если Не сткРезультат.булУспешно Тогда
			Сообщить(сткРезультат.стрОписаниеОшибки);	
			Возврат;
		КонецЕсли;		
	КонецЕсли;	
КонецПроцедуры


Процедура ЗагрузитьDemoJSONНажатие() Экспорт
	
	мДанныеНаЗапись = Новый Массив();
	Для чСчетчик = 0 По 1000 Цикл
		
		мЗаголовки = Новый Массив();
		мЗаголовки.Добавить(Новый Структура("Заголовок1", "Значение" + чСчетчик));
		мЗаголовки.Добавить(Новый Структура("Заголовок2", "Значение" + чСчетчик + 1));
		
		стрСообщение = "Тестовое сообщение " + чСчетчик;
		мДанныеНаЗапись.Добавить(Новый Структура("Key, Value, Headers", Строка(Новый УникальныйИдентификатор()), стрСообщение, мЗаголовки));
	КонецЦикла;
	
	сткРезультатПреобразования = СтруктуруВJSON(мДанныеНаЗапись);   
	стрДанные = "";
	стрДанные = стрДанные + сткРезультатПреобразования.стрДанные;
	сткРезультат = Отправитель_ЗагрузитьСообщенияИзJSONВСписокНаОтправку(стрДанные);
	Если Не сткРезультат.булУспешно Тогда
		Сообщить(сткРезультат.стрОписаниеОшибки);	
		Возврат;
	КонецЕсли;
КонецПроцедуры         

Процедура ОтправитьСообщение(мДанныеНаЗапись) Экспорт

	сткРезультатПреобразования = СтруктуруВJSON(мДанныеНаЗапись);   
	стрДанные = "";
	стрДанные = стрДанные + сткРезультатПреобразования.стрДанные;
	сткРезультат = Отправитель_ЗагрузитьСообщенияИзJSONВСписокНаОтправку(стрДанные);
	Если Не сткРезультат.булУспешно Тогда
		Сообщить(сткРезультат.стрОписаниеОшибки);	
		Возврат;
	КонецЕсли;   
	
КонецПроцедуры


Процедура Инициализация_ЗаписьНажатие() Экспорт
	
	Отправитель_Закрыть();
	
	сткРезультат = СоздатьОтправителя();
	Если Не сткРезультат.булУспешно Тогда
		Сообщить(сткРезультат.стрОписаниеОшибки);	
		Возврат;
	КонецЕсли;

	Для Каждого Строка Из Запись_НастройкиГлобальные Цикл
		сткРезультат = Отправитель_УстановитьПараметрГлобальный(Строка.Key, Строка.Value);
		Если Не сткРезультат.булУспешно Тогда
			Сообщить(сткРезультат.стрОписаниеОшибки);	
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из Запись_НастройкиТопика Цикл
		сткРезультат = Отправитель_УстановитьПараметрТопика(Строка.Key, Строка.Value);
		Если НЕ сткРезультат.булУспешно Тогда
			Сообщить(сткРезультат.стрОписаниеОшибки);	
			Возврат;
		КонецЕсли;
	КонецЦикла;
		
	сткРезультат = Отправитель_Инициализировать(Адрес_Запись, Топик_Запись, Раздел_Запись);
	Если Не сткРезультат.булУспешно Тогда
		Сообщить(сткРезультат.стрОписаниеОшибки);	
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗаполнитьНастройкиГлобальные()
	Чтение_НастройкиГлобальные = Новый ТаблицаЗначений;
	Чтение_НастройкиГлобальные.Колонки.Добавить("Key");   
	Чтение_НастройкиГлобальные.Колонки.Добавить("Value"); 
	
	Запись_НастройкиГлобальные = Новый ТаблицаЗначений;
	Запись_НастройкиГлобальные.Колонки.Добавить("Key");   
	Запись_НастройкиГлобальные.Колонки.Добавить("Value");  
	
	Просмотр_НастройкиГлобальные = Новый ТаблицаЗначений;
	Просмотр_НастройкиГлобальные.Колонки.Добавить("Key");   
	Просмотр_НастройкиГлобальные.Колонки.Добавить("Value");


	//Получатель
	стрНастройка = Чтение_НастройкиГлобальные.Добавить();
	стрНастройка.Key =  "message.max.bytes";
	стрНастройка.Value = "10000"; 
		
	стрНастройка = Чтение_НастройкиГлобальные.Добавить();
	стрНастройка.Key =  "socket.timeout.ms";
	стрНастройка.Value = "30000"; 	
	
	стрНастройка = Чтение_НастройкиГлобальные.Добавить();
	стрНастройка.Key =  "enable.auto.commit";
	стрНастройка.Value = "false"; 
	
	стрНастройка = Чтение_НастройкиГлобальные.Добавить();
	стрНастройка.Key =  "auto.commit.interval.ms";
	стрНастройка.Value = "0"; 
	
	//Отправитель			
	стрНастройка = Запись_НастройкиГлобальные.Добавить();
	стрНастройка.Key =  "queue.buffering.max.messages";
	стрНастройка.Value = "1000000"; 
		
	стрНастройка = Запись_НастройкиГлобальные.Добавить();
	стрНастройка.Key =  "socket.timeout.ms";
	стрНастройка.Value = "30000"; 
	
	стрНастройка = Запись_НастройкиГлобальные.Добавить();
	стрНастройка.Key =  "delivery.report.only.error";
	стрНастройка.Value = "false"; 	
	
	//Клиент администрирования   
	стрНастройка = Просмотр_НастройкиГлобальные.Добавить();
	стрНастройка.Key =  "socket.timeout.ms";
	стрНастройка.Value = "30000"; 	
		
КонецПроцедуры

Процедура ЗаполнитьПоУмолчанию(топик) Экспорт
		
	Адрес_Чтение = "Kafka:9092";
	GroupID_Чтение = Константы.НаименованиеВКафке.Получить();
	
	
	чслПрочитатьКоличество_Чтение = 1000;
	чслТаймаутПоУмолчанию = 3000;
	
	Адрес_Запись = "Kafka:9092";
	Топик_Запись = топик;
	Раздел_Запись = -1;  
	
	Адрес_Просмотр = "Kafka:9092";          
	GroupID_Просмотр = Константы.НаименованиеВКафке.Получить();

	
	ЗаполнитьНастройкиГлобальные();
	ЗаполнитьНастройкиТопика();
	ЗаполнитьТопикиРазделы(топик);
	
	глТаблицаНаОтправку = Новый ТаблицаЗначений();
	глТаблицаНаОтправку.Колонки.Добавить("Key", );
	глТаблицаНаОтправку.Колонки.Добавить("Value", );
	глТаблицаНаОтправку.Колонки.Добавить("Headers", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	тзДанные_Запись = глТаблицаНаОтправку;
КонецПроцедуры

Процедура ЗаполнитьНастройкиТопика() Экспорт
	Чтение_НастройкиТопика = Новый ТаблицаЗначений;
	Чтение_НастройкиТопика.Колонки.Добавить("Key");   
	Чтение_НастройкиТопика.Колонки.Добавить("Value");      
	
	Запись_НастройкиТопика = Новый ТаблицаЗначений;
	Запись_НастройкиТопика.Колонки.Добавить("Key");   
	Запись_НастройкиТопика.Колонки.Добавить("Value");
	
	//Получатель
	стрНастройка = Чтение_НастройкиТопика.Добавить();
	стрНастройка.Key =  "compression.codec";
	стрНастройка.Value = "lz4"; 
	
	стрНастройка = Чтение_НастройкиТопика.Добавить();
	стрНастройка.Key =  "auto.offset.reset";
	стрНастройка.Value = "smallest"; 
	
	//Отправитель
	стрНастройка = Запись_НастройкиТопика.Добавить();
	стрНастройка.Key =  "message.timeout.ms";
	стрНастройка.Value = "30000"; 
	
	стрНастройка = Запись_НастройкиТопика.Добавить();
	стрНастройка.Key =  "compression.codec";
	стрНастройка.Value = "lz4"; 
	
	стрНастройка = Запись_НастройкиТопика.Добавить();
	стрНастройка.Key =  "request.required.acks";
	стрНастройка.Value = "-1"; 
	
КонецПроцедуры

Процедура ЗаполнитьТопикиРазделы(топик) Экспорт
	//тзТопикиРазделы_Чтение = Новый ТаблицаЗначений;
	//тзТопикиРазделы_Чтение.Колонки.Добавить("Topic");   
	//тзТопикиРазделы_Чтение.Колонки.Добавить("Partition");   
	//тзТопикиРазделы_Чтение.Колонки.Добавить("Offset");          
	//   
	тзТопикиРазделы_Чтение.Очистить();
	тзТопики_Чтение = Новый ТаблицаЗначений;
	тзТопики_Чтение.Колонки.Добавить("Topic");   
	тзТопики_Чтение.Колонки.Добавить("Partition");   
	тзТопики_Чтение.Колонки.Добавить("Offset"); 

	//стрДобавить = тзТопикиРазделы_Чтение.Добавить();
	//
	//стрДобавить.Topic = "prices.dv";
	//стрДобавить.Partition = 0;
	//стрДобавить.Offset = -1;
	//
	//стрДобавить = тзТопикиРазделы_Чтение.Добавить();
	//
	//стрДобавить.Topic = "prices.dv";
	//стрДобавить.Partition = 1;
	//стрДобавить.Offset = -1;
	//
	//стрДобавить = тзТопикиРазделы_Чтение.Добавить();

	//стрДобавить.Topic = "prices.dv";
	//стрДобавить.Partition = 2;
	//стрДобавить.Offset = -1;	
	                                 
	
	стрДобавить = тзТопикиРазделы_Чтение.Добавить();
	стрДобавить.Topic = топик;
	стрДобавить.Partition = 0;
	стрДобавить.Offset = -1;

		
	стрДобавить = тзТопики_Чтение.Добавить();	
	стрДобавить.Topic = топик;

КонецПроцедуры

Функция Отправить_ЗаписьНажатие() Экспорт
	
	сткРезультат = Отправитель_ДекодироватьКлючСообщенияКакBase64Строку(булДанныеПоданыВBase64);
	Если Не сткРезультат.булУспешно Тогда 
		Возврат сткРезультат;
	КонецЕсли;
	сткРезультат = Отправитель_ДекодироватьТелоСообщенияКакBase64Строку(булДанныеПоданыВBase64);
	Если Не сткРезультат.булУспешно Тогда 
		Возврат сткРезультат;
	КонецЕсли;

	сткРезультат = Отправитель_ДекодироватьЗначенияЗаголовковКакBase64Строку(булДанныеПоданыВBase64);
	Если Не сткРезультат.булУспешно Тогда 
		Возврат сткРезультат; 
	КонецЕсли;
			
	сткРезультат = Отправитель_Отправить();
	Если Не сткРезультат.булУспешно Тогда
		Возврат сткРезультат;
	КонецЕсли;

	сткРезультат = Отправитель_ВсеСообщенияДоставлены();
	Если Не сткРезультат.булУспешно Тогда
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат = Отправитель_ПолучитьJSONОтчетОДоставке();
	
	Возврат сткРезультат;

КонецФункции

#КонецОбласти


#Область ТипыМодуля

//Получатель
Функция Получатель_ПолучитьТаблицуСообщения() Экспорт
	
	тзРезультат = Новый ТаблицаЗначений();
	
	тзРезультат.Колонки.Добавить("key", 		);
	тзРезультат.Колонки.Добавить("value", 		);
	тзРезультат.Колонки.Добавить("topic", 		Новый ОписаниеТипов("Строка"));
	тзРезультат.Колонки.Добавить("partition", 	Новый ОписаниеТипов("Число"));
	тзРезультат.Колонки.Добавить("offset", 		Новый ОписаниеТипов("Число"));
	тзРезультат.Колонки.Добавить("timestamp", 	Новый ОписаниеТипов("Число"));
	тзРезультат.Колонки.Добавить("headers", 	Новый ОписаниеТипов("ТаблицаЗначений"));
	
	Возврат тзРезультат;	
КонецФункции

Функция Получатель_ПолучитьТаблицуЗаголовки() Экспорт
	
	тзРезультат = Новый ТаблицаЗначений();
	
	тзРезультат.Колонки.Добавить("key", 	);
	тзРезультат.Колонки.Добавить("value",	);
		
	Возврат тзРезультат;	
КонецФункции

//Отправитель
Функция Отправитель_ПолучитьТаблицуСообщения()
	
	тзРезультат = Новый ТаблицаЗначений();
	                                                                
	тзРезультат.Колонки.Добавить("index", 		Новый ОписаниеТипов("Число"));
	тзРезультат.Колонки.Добавить("key", 		);
	тзРезультат.Колонки.Добавить("value", 		);

	Возврат тзРезультат;	
КонецФункции

Функция Отправитель_ПолучитьТаблицуЗаголовки() Экспорт
	
	тзРезультат = Новый ТаблицаЗначений();
	
	тзРезультат.Колонки.Добавить("key", 	);
	тзРезультат.Колонки.Добавить("value",	);
		
	Возврат тзРезультат;	
КонецФункции

Функция Отправитель_ПолучитьТаблицуОтчетОДоставке() Экспорт
	
	тзРезультат = Новый ТаблицаЗначений();
	
	тзРезультат.Колонки.Добавить("key", 		);
	тзРезультат.Колонки.Добавить("status", 		);
	тзРезультат.Колонки.Добавить("topic", 		);
	тзРезультат.Колонки.Добавить("partition", 	);
	тзРезультат.Колонки.Добавить("timestamp",	);
	тзРезультат.Колонки.Добавить("offset", 	 	);
	тзРезультат.Колонки.Добавить("error", 		);	
		
	Возврат тзРезультат;	
КонецФункции

//Клиент администрирования
Функция КлиентАдминистрирования_ПолучитьТаблицуСписокГруппПолучателей() Экспорт   
	
	тзРезультат = Новый ТаблицаЗначений();
	
	тзРезультат.Колонки.Добавить("group_id", 	);
	тзРезультат.Колонки.Добавить("state", 		);
	тзРезультат.Колонки.Добавить("is_simple", 	);
		
	Возврат тзРезультат;	
КонецФункции   

Функция КлиентАдминистрирования_ПолучитьТаблицуТопикПартицияСмещение() Экспорт   
	
	тзРезультат = Новый ТаблицаЗначений();
	
	тзРезультат.Колонки.Добавить("topic",		);
	тзРезультат.Колонки.Добавить("partition",	);
	тзРезультат.Колонки.Добавить("offset",		);      
	тзРезультат.Колонки.Добавить("error",		);
		
	Возврат тзРезультат;	
КонецФункции        

Функция КлиентАдминистрирования_ПолучитьТаблицуWatermarkOffsets() Экспорт    
	
	тзРезультат = Новый ТаблицаЗначений();
	
	тзРезультат.Колонки.Добавить("topic",		);
	тзРезультат.Колонки.Добавить("partition",	);
	тзРезультат.Колонки.Добавить("low",			);      
	тзРезультат.Колонки.Добавить("hight",		);
		
	Возврат тзРезультат;	
КонецФункции


#КонецОбласти


Процедура ИнициализацияМодуляЧтение(топик = "orders" )	Экспорт
	
	гл_булВнешняяКомпонентаПодключена = Ложь;
	
	глProducer = Неопределено;
	глConsumer = Неопределено;	
	
	гл_стрИмяКомпоненты	= "librdkafka"; 
	Адрес_Чтение = "Kafka:9092";
	Адрес_Просмотр = "Kafka:9092";
	Адрес_Запись = "Kafka:9092";
	GroupID_Чтение = Константы.НаименованиеВКафке.Получить();
	
	ЗаполнитьПоУмолчанию(топик);  
	//Инициализация_ЗаписьНажатие();   
	Инициализация_ЧтениеНажатие(топик)
КонецПроцедуры  


Процедура ИнициализацияМодуляЗапись(топик = "orders" )	Экспорт
	
	гл_булВнешняяКомпонентаПодключена = Ложь;
	
	глProducer = Неопределено;
	глConsumer = Неопределено;	
	
	гл_стрИмяКомпоненты	= "librdkafka"; 
	Адрес_Чтение = "Kafka:9092";
	Адрес_Просмотр = "Kafka:9092";
	Адрес_Запись = "Kafka:9092";
	GroupID_Чтение = Константы.НаименованиеВКафке.Получить();
	
	ЗаполнитьПоУмолчанию(топик);  
	Инициализация_ЗаписьНажатие();   
	//Инициализация_ЧтениеНажатие(топик)
КонецПроцедуры  



Функция ВыполнитьПодключениеВнешнейКомпоненты(булИзОбщегоМакета)
	Возврат ВыполнитьПодключениеВнешнейКомпонентыМакет();
КонецФункции

Функция ВыполнитьПодключениеВнешнейКомпонентыМакет()	
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");     
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 Тогда	
		МакетКомпоненты = ПолучитьМакет("librdkafka_win32");
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		МакетКомпоненты = ПолучитьМакет("librdkafka_win64");
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86 Тогда
		МакетКомпоненты = ПолучитьМакет("librdkafka_linux32");	
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64 Тогда
		МакетКомпоненты = ПолучитьМакет("librdkafka_linux64");		
	Иначе
		сткРезультат.стрОписаниеОшибки = "Не поддерживаемая архитектура платформы";
		Возврат сткРезультат;
	КонецЕсли;
	
	Попытка
		_РезультатПодключенияВнешнейКомпоненты = ПодключитьВнешнююКомпоненту(ПоместитьВоВременноеХранилище(МакетКомпоненты), "librdkafka", ТипВнешнейКомпоненты.Native);
		Если НЕ _РезультатПодключенияВнешнейКомпоненты Тогда
			сткРезультат.стрОписаниеОшибки = "Ошибка подключения внешней компоненты";
			Возврат сткРезультат;
		КонецЕсли;
	Исключение
		сткРезультат.стрОписаниеОшибки = ОписаниеОшибки();
		Возврат сткРезультат;
	КонецПопытки;
	
	гл_булВнешняяКомпонентаПодключена = Истина;	
	сткРезультат.булУспешно = Истина;
	
	Возврат сткРезультат;
КонецФункции


#Область Получтатель


// Функция - Создать получателя
// 
// Возвращаемое значение:
//   - Структура - (булУспешно, стрОписнаиеОшибки)
//
//Создает объект получателя в глобальной переменной модуля
Функция СоздатьПолучателя(булФлагДляСовместимости = Истина) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	Если глConsumer <> Неопределено Тогда
		сткРезультат.булУспешно = Истина;
		Возврат сткРезультат;	
	КонецЕсли;
	
	Если Не  гл_булВнешняяКомпонентаПодключена Тогда
		 сткРезультатПодключения = ВыполнитьПодключениеВнешнейКомпоненты(булФлагДляСовместимости);
		 Если Не сткРезультатПодключения.булУспешно Тогда
			 сткРезультат.стрОписаниеОшибки = сткРезультатПодключения.стрОписаниеОшибки;
			Возврат сткРезультат;	 
		 КонецЕсли;
	 КонецЕсли;
	 
	Попытка
		 глConsumer = Неопределено;
		 глConsumer = Новый("AddIn." + гл_стрИмяКомпоненты + ".KafkaConsumer");
	Исключение
		сткРезультат.стрОписаниеОшибки = ОписаниеОшибки();
		Возврат сткРезультат;
	КонецПопытки;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;
КонецФункции
 
// Функция - Получтатель проверить доступность объекта 
// 
// Возвращаемое значение:
//   - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Если объект существует
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Проверяет, существует ли объект получателя и возможна ли работа с ним
Функция Получтатель_ПроверитьДоступностьОбъекта() Экспорт

	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	Если НЕ гл_булВнешняяКомпонентаПодключена Тогда
		сткРезультат.стрОписаниеОшибки = "Не подключена внешняя компонента";
		Возврат сткРезультат;
	КонецЕсли;
	
	Если глConsumer = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = "Не создан получатель";
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФункции
 
// Функция - Получатель установить параметр глобальный
//
// Параметры:
//  стрКлюч		 - 	Строка - Имя параметра
//  стрЗначение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если параметр установлен
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр глабольного контекста для получателя (глConsumer)
//	Пример: стрКлюч = socket.timeout.ms стрЗначение = 30000  
//!!Для применения установленных параметров - необходимка повторная инициализация получателя с помощью функции "Получатель_Инициализировать"
Функция Получатель_УстановитьПараметрГлобальный(Знач стрКлюч, Знач стрЗначение) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глConsumer.SetGlobalConf(стрКлюч, стрЗначение);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель установить параметр топика
//
// Параметры:
//  стрКлюч		 - 	Строка - Имя параметра
//  стрЗначение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если параметр установлен
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр топика для получателя (глConsumer) 
//	Пример: стрКлюч = auto.offset.reset стрЗначение = smallest  
//!!Для применения установленных параметров - необходимка повторная инициализация получателя с помощью функции "Получатель_Инициализировать"
Функция Получатель_УстановитьПараметрТопика(Знач стрКлюч, Знач стрЗначение) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глConsumer.SetTopicConf(стрКлюч, стрЗначение);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель сбросить параметры
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Сбрасывает установенные параметры топика и параметры глобального контекста (установленные функциями Получатель_УстановитьПараметрГлобальный, Получатель_УстановитьПараметрТопика)
//!!Для применения - необходимка повторная инициализация получателя с помощью функции "Получатель_Инициализировать"
Функция Получатель_СброситьПараметры() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глConsumer.ConfReset();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции
	
// Функция - Получатель инициализировать
//
// Параметры:
//  стрСписокБрокеров		 - Строка 	 - Адреса брокеров в формате "10.0.5.187:9092,10.0.5.85:9092,10.0.5.86:9092"
//  стрИмяГруппыПолучателей	 - Стркоа	 - Произвольная строка - идентификатор группы получателей
//
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Применяет параметры, установленные функциями "Получатель_УстановитьПараметрГлобальный", "Получатель_УстановитьПараметрТопика", "Получатель_СброситьПараметры" 
//Открывает соединения до брокера, создает управляющие потоки внутри ВК, полностью подготавливает получателя для чтения данных
Функция Получатель_Инициализировать(Знач стрСписокБрокеров, Знач стрИмяГруппыПолучателей) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глConsumer.Инициализация(стрСписокБрокеров, стрИмяГруппыПолучателей);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;

	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель подписаться
//
// Параметры:
//  мТопики	 - Массив	 - Массив строк, содержащих имена топиков, на которые необходимо подписаться
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Выполняет подписку на список переданных топиков
//Необходимо вызывать после инициализации
//Предполагает автоматическое распределение партиций топиков на получателей внутри группы
//	(т.е. если подписано на топик несколько получателей, с одной и той же группой - партиции будут распределены между ними брокером автоматически)
Функция Получатель_Подписаться(Знач мТопики) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	Результат = глConsumer.ClearSubscribeList();
	Если Результат <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = "Ошибка очистки списка топиков: " + глConsumer.ErrorDescription;	
		Возврат сткРезультат;
	КонецЕсли;
	
	Для Каждого стрТопик Из мТопики Цикл 
		Результат = глConsumer.AddTopicToSubscribeList(стрТопик);
		Если Результат <> Истина Тогда
			сткРезультат.стрОписаниеОшибки = "Ошибка добавления топика в список: " + глConsumer.ErrorDescription;	
			Возврат сткРезультат;
		КонецЕсли;	
	КонецЦикла;
	
	Результат = глConsumer.Subscribe();
	Если Результат <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = "Ошибка подписания на топики: " + глConsumer.ErrorDescription;	
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель отписаться
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Отписывается от всех топиков, на которые ранее была выполнена подписка с помощью функции "Получатель_Подписаться"
Функция Получатель_Отписаться() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глConsumer.Unsubscribe();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;

	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель добавить назначение
//
// Параметры:
//  стрТопик	 - Строка	 -  Имя топика, который необходимо добавить в список назначений
//  чслРаздел	 - Число	 -  Номер раздела топика
//  чслСмещение	 - Число,Неопределено	 - Смещение с которого необходимо начать чтение, если Неопределено - будут получено последнее смещение, закоммиченное в брокер
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Добавляет назначение в специальный список, внутри ВК
//Метод не выполняет назначение, а только добавляет назначение в специальный список внуутри ВК
//Непосредственное назначение выполняется функцией "Получатель_Назначить"
Функция Получатель_ДобавитьНазначение(Знач стрТопик, Знач чслРаздел, Знач чслСмещение = Неопределено) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глConsumer.AddRecordToTopicPartitionList(стрТопик, чслРаздел, чслСмещение);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;

	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель очистить список назначений
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Очищает список назначений внутри ВК, сформированный посредставм вызова функции "Получатель_ДобавитьНазначение"
//Функция не выполняет сброс назначений, а только очищает список внутри ВК, назначения сбрасываются функией "Получатель_СброситьНазначения"
Функция Получатель_ОчиститьСписокНазначений() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глConsumer.ClearTopicPartitionList();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;

	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель назначить
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Выполняет назначения "марштрутов", для чтения из них данных
//Берет маршруты из внутреннего списка ВК, сформированного вызовами функции "Получатель_ДобавитьНазначение"
Функция Получатель_Назначить() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глConsumer.Assign();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель сбросить назначения
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Выполняет сброс назначений, сделанных функцией "Получатель_Назначить"
Функция Получатель_СброситьНазначения() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глConsumer.Unassign();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель прочитать список сообщения в пул
//		Читает список сообщений из брокера - цикл получения организован внутри ВК, что быстее чем использование метода "Получатель_ПрочитатьСообщенияВПул"
//
// Параметры:
//  чслКоличество				 - Число	 - Количество сообщений, которые необходимо получить
//  чслТайимаутНаОдно			 - Число	 - Ожидание оджного сообщения
//  чслМаксимумТаймаутов		 - Число	 - Количество ошибок, в том числе таймаутов, после которого получение будет прервано
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 				- Истина, если успешно
//		чКоличествоПрочитано	- Количество прочитанных сообщений
//		стрОписаниеОшибки 		- содержит описание ошибки, в случае неудачи, при выпонении операции
//  
//Получает сообщения из брокера и помещает их во внутренний пул ВК, получение выполняется из "маршрутов", которые назначены получателю (c помощью фунцкии "Получатель_Назначить") 
//или из разделов назначенных получателю брокером (для топиков, на которые выполнялась подписка с помощью функции "Получатель_Подписаться")
//
//
//При подписании на топик - мы читаем из всех его партиций, и в какой-либо могут закочиться сообщения,
// а в любой другой - нет, при этом при чтении из той, где сообщения закончились - мы получим таймаут
// поэтому если мы подписывались на топик, а не назначали партицию для чтения - имеет смысл после первого таймаута
// - делать еще попытки, также таймауты могут быть при перебалансировке нагрузки брокером - RebalanceCb 
// - при подписке на топик еще одного получателя с такой же группой, как у текущего - партиции будут перераспределены
// брокером между получателями
Функция Получатель_ПрочитатьСписокСообщенийВПул(Знач чслКоличество, Знач чслТайимаутНаОдно, Знач ErrorCountToInterrupt = 1) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("булТаймаут", Ложь);
	сткРезультат.Вставить("FatalError", Ложь);

	сткРезультат.Вставить("чКоличествоПрочитано", 0);	
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	

	Результат = глConsumer.ConsumePool(чслТайимаутНаОдно, чслКоличество, ErrorCountToInterrupt);
	Если Результат <> 1 Тогда
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	сткРезультат.булТаймаут = (сткРезультат.стрОписаниеОшибки = "Local: Timed out") ИЛИ (сткРезультат.стрОписаниеОшибки = "Timeout: Local: Timed out")
		ИЛИ (сткРезультат.стрОписаниеОшибки = "Local queue is full");
	сткРезультат.булУспешно = (сткРезультат.булТаймаут ИЛИ Результат = 1) И НЕ глConsumer.FatalError;
	сткРезультат.FatalError = глConsumer.FatalError;
	
	сткРезультат.чКоличествоПрочитано = глConsumer.GetMessagePoolLength();		
	Возврат сткРезультат;	
КонецФункции

// Функция - Получатель прочитать список сообщений
//		
//
// Параметры:
//  булПолучатьBase64	- Булево	 - Есдли ИСТИНА - тело сообщения, ключи и значения заголовков -  будут содержать строки в формате Base64 
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрСообщения, стрОписнаиеОшибки)
//      булУспешно 			- Истина, если успешно
//		стрСообщения		- Сообщения в формате JSON
//		стрОписаниеОшибки 	- Cодержит описание ошибки, в случае неудачи, при выпонении операции
// 
//Получает сообщения из внутреннего пула ВК в формате JSON
//Если сообщения содержат двоичные данные - необходимо вызывать функцию с флагом булПолучатьBase64 = ИСТИНА, иначе JSON сформировать не удастся
Функция Получатель_ПолучитьСообщенияИзПулаJSON(Знач булПолучатьBase64) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрСообщения", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Читаем из пула компоненты	
	сткРезультат.стрСообщения = глConsumer.ReceiveJSONMessages(булПолучатьBase64);
	Если  сткРезультат.стрСообщения = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	

	//Очищаем пул компоненты	
	Результат = глConsumer.ClearMessagePool();
	Если Результат <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции   

// Функция - Получить сообщения из пула СтрокаВнутр
//		
//
// Параметры:
//  булПолучатьBase64	- Булево	 - Есдли ИСТИНА - тело сообщения, ключи и значения заголовков -  будут содержать строки в формате Base64 
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрСообщения, стрОписнаиеОшибки)
//      булУспешно 			- Истина, если успешно
//		стрСообщения		- Сообщения в формате JSON
//		стрОписаниеОшибки 	- Cодержит описание ошибки, в случае неудачи, при выпонении операции
// 
//Получает сообщения из внутреннего пула ВК в формате ЗначениеВСтрокуВнутр 1С
//Если сообщения содержат двоичные данные - необходимо вызывать функцию с флагом булПолучатьBase64 = ИСТИНА, иначе сформировать не удастся
Функция Получатель_ПолучитьСообщенияИзПулаСтрокаВнутр(Знач булПолучатьBase64) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрСообщения", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Читаем из пула компоненты	
	сткРезультат.стрСообщения = глConsumer.ReceiveOnecInternalMessages(булПолучатьBase64);
	Если  сткРезультат.стрСообщения = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	

	//Очищаем пул компоненты	
	Результат = глConsumer.ClearMessagePool();
	Если Результат <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

// Функция - Получатель получить смещения раздела
//
// Параметры:
//  стрТопик - Строка	 - Имя топика
//  чРаздел	 - Число 	 - Номер раздела
//  чТаймаут - Число	 - Время в миллисекундах после которого выполнение будет прервано
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		чМинимальное 		- Минимальное смещение раздела
//		чМинимальное 		- Максимальное смещение раздела
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Возвращает минимальное и максимальное смещение раздела
Функция Получатель_ПолучитьСмещенияРаздела(Знач стрТопик, Знач чРаздел, Знач чТаймаут) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("чМинимальное", -1);
	сткРезультат.Вставить("чМаксимальное", -1);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	Результат = глConsumer.QueryWatermarkOffsets(стрТопик, чРаздел, чТаймаут);
	Если Результат = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	

	сткРезультатПреобразования = JSONВСтруктуру(Результат);
	Если Не сткРезультатПреобразования.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПреобразования.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;
	
	Попытка 
		сткРезультат.чМинимальное = сткРезультатПреобразования.масДанные[0].Low;
		сткРезультат.чМаксимальное = сткРезультатПреобразования.масДанные[0].Hight;
		
	Исключение
		сткРезультат.стрОписаниеОшибки = "Ошибка разбора ответа";
		Возврат сткРезультат;
	КонецПопытки;

	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

// Функция - Получатель получить текущее смещение раздела
//
// Параметры:
//  стрТопик - Строка	 - Имя топика
//  чРаздел	 - Число 	 - Номер раздела
//  чТаймаут - Число	 - Время в миллисекундах после которого выполнение будет прервано
//
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		чСмещение 			- текущее смещение для группы
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Получает текущее смещение раздела для группы
Функция Получатель_ПолучитьТекущееСмещениеРаздела(Знач стрТопик, Знач чРаздел, Знач чТаймаут) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("чСмещение", -1);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.чСмещение = глConsumer.CommittedOffset(стрТопик, чРаздел, чТаймаут);
	Если сткРезультат.чСмещение = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	

	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

// Функция - Получатель зафиксировать смещения
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Записывает в брокер последние прочитанные смещения разделов
//Нужно вызывать после вызово функций чтения "Получатель_ПолучитьСообщения", "Получатель_ПрочитатьСписокСообщений", "Получатель_ПолучитьСообщенияJSON"
Функция Получатель_ЗафиксироватьСмещения() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глConsumer.Commit();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФУнкции

Функция Получатель_УдалятьСообщениеИзЛокальнойОчередиПриФормированииJSON(булУдалять = Ложь) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глConsumer.RemoveMessagesFromLocalQueueOnJSONBuild = булУдалять;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

Функция Получатель_МаксимальноеКоличествоСообщенийВЛокальнойОчереди(чслКоличество = 100000) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глConsumer.MaxMessagesInLocalQueue = чслКоличество;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

Функция Получатель_ЭкранироватьКлючСообщения(булЭкранировать = Истина) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глConsumer.EscapeMessageKey = булЭкранировать;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

Функция Получатель_ЭкранироватьЗначениеСообщения(булЭкранировать = Истина) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глConsumer.EscapeMessageValue = булЭкранировать;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

Функция Получатель_ЭкранироватьКлючиЗаголовков(булЭкранировать = Истина) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глConsumer.EscapeMessageHeaderKey = булЭкранировать;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

Функция Получатель_ЭкранироватьЗначенияЗаголовков(булЭкранировать = Истина) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глConsumer.EscapeMessageHeaderValue = булЭкранировать;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

Функция Получатель_ДобавитьФильтрЗаголовка(Знач стрКлючЗаголовка) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глConsumer.AppendHeaderFilter(стрКлючЗаголовка);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

Функция Получатель_ОчиститьФильтрыЗаголовков() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Получтатель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глConsumer.ClearHeaderFilter();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции


// Функция - Получатель закрыть
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Закрывает соединения, завершает управялющие потоки внутри ВК, закрывает объект получателя
Функция Получатель_Закрыть() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	булОшибка = Ложь;
	Если глConsumer <> Неопределено Тогда
		сткРезультатСбросаНазначений = Получатель_СброситьНазначения();
		Если НЕ сткРезультатСбросаНазначений.булУспешно Тогда
			булОшибка = Истина;
			сткРезультат.стрОписаниеОшибки = сткРезультат.стрОписаниеОшибки + сткРезультатСбросаНазначений.стрОписаниеОшибки + "; ";
		КонецЕсли;
		сткРезультатОтписки = Получатель_Отписаться();
		Если НЕ сткРезультатОтписки.булУспешно Тогда
			булОшибка = Истина;
			сткРезультат.стрОписаниеОшибки = сткРезультат.стрОписаниеОшибки + сткРезультатСбросаНазначений.стрОписаниеОшибки + "; ";
		КонецЕсли;	
	КонецЕсли;
	
	глConsumer = Неопределено;
	
	сткРезультат.булУспешно = НЕ булОшибка;
	Возврат сткРезультат;	
КонецФункции


#КонецОбласти

#Область Отправитель
	
	
// Функция - Создать отправителя
// 
// Возвращаемое значение:
//   - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 			- Если объект существует
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Создает объект отправителя в глобальной переменной модуля
Функция СоздатьОтправителя(булФлагДляСовместимости = Истина ) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	Если глProducer <> Неопределено Тогда
		сткРезультат.булУспешно = Истина;
		Возврат сткРезультат;	
	КонецЕсли;
		
	Если Не  гл_булВнешняяКомпонентаПодключена Тогда
		 сткРезультатПодключения = ВыполнитьПодключениеВнешнейКомпоненты(булФлагДляСовместимости);
		 Если Не сткРезультатПодключения.булУспешно Тогда
			 сткРезультат.стрОписаниеОшибки = сткРезультатПодключения.стрОписаниеОшибки;
			Возврат сткРезультат;	 
		 КонецЕсли;
	 КонецЕсли;
	 
	Попытка
		 глProducer = Неопределено;
		 глProducer = Новый("AddIn." + гл_стрИмяКомпоненты + ".KafkaProducer");
	Исключение
		сткРезультат.стрОписаниеОшибки = ОписаниеОшибки();
		Возврат сткРезультат;
	КонецПопытки;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;
КонецФункции

// Функция - Отправитель проверить доступность объекта 
// 
// Возвращаемое значение:
//   - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 			- Если объект существует
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Проверяет, существует ли объект получателя и возможна ли работа с ним
Функция Отправитель_ПроверитьДоступностьОбъекта() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	Если НЕ гл_булВнешняяКомпонентаПодключена Тогда
		сткРезультат.стрОписаниеОшибки = "Не подключена внешняя компонента";
		Возврат сткРезультат;
	КонецЕсли;
	
	Если глProducer = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = "Не создан отправитель";
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель установить параметр глобальный
//
// Параметры:
//  стрКлюч		 - 	Строка - Имя параметра
//  стрЗначение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 			- Истина, если параметр установлен
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр глабольного контекста для отправителя (глProducer)
//	Пример: стрКлюч = socket.timeout.ms стрЗначение = 30000  
//!!Для применения установленных параметров - необходимка повторная инициализация получателя с помощью функции "Отправитель_Инициализировать"
Функция Отправитель_УстановитьПараметрГлобальный(Знач стрКлюч, Знач стрЗначение) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глProducer.SetGlobalConf(стрКлюч, стрЗначение);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глProducer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель установить параметр топика
//
// Параметры:
//  стрКлюч		 - 	Строка - Имя параметра
//  стрЗначение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно			- Истина, если параметр установлен
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр топика для отправителя (глProducer) 
//	Пример: стрКлюч = auto.offset.reset стрЗначение = smallest  
//!!Для применения установленных параметров - необходимка повторная инициализация получателя с помощью функции "Отправитель_Инициализировать"
Функция Отправитель_УстановитьПараметрТопика(Знач стрКлюч, Знач стрЗначение) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глProducer.SetTopicConf(стрКлюч, стрЗначение);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глProducer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель сбросить параметры
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Сбрасывает установенные параметры топика и параметры глобального контекста (установленные функциями Отправитель_УстановитьПараметрГлобальный, Отправитель_УстановитьПараметрТопика)
//!!Для применения - необходимка повторная инициализация получателя с помощью функции "Отправитель_Инициализировать"
Функция Отправитель_СброситьПараметры() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глProducer.ConfReset();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глConsumer.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель инициализировать
//
// Параметры:
//  Адрес	 - Строка 	 - Адреса брокера в формате "10.0.5.187:9092" 
//  Топик	 - Стркоа	 - Имя топика
//	Раздел   - Число	 - Номер раздела, если -1 - будет выполнено партиционирование по ключу
//
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Применяет параметры, установленные функциями "Отправитель_УстановитьПараметрГлобальный", "Отправитель_УстановитьПараметрТопика", "Отправитель_СброситьПараметры" 
//Открывает соединения до брокера, создает управляющие потоки внутри ВК, полностью подготавливает отправителя
Функция Отправитель_Инициализировать(Знач Адрес, Знач Топик, Знач Раздел = 0) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	

	сткРезультат.булУспешно = глProducer.Initialize(Адрес, Топик, Раздел);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глProducer.ErrorDescription;
	КонецЕсли;              	
	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель загрузить сообщения из JSON в список на отправку
//
// Параметры:
//  стрJSON		 - Строка	 - JSON в формате "[{"Key": "Ключ", "Value": "Сообщение", "Headers": [{"Заголовок1": "ЗначениеЗаголовка1"}]}]"
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Загружает список сообщений из JSON в внутренний пул ВК на отправку
Функция Отправитель_ЗагрузитьСообщенияИзJSONВСписокНаОтправку(Знач стрJSON) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	

	сткРезультат.булУспешно = глProducer.SetJSONMessageList(стрJSON);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глProducer.ErrorDescription;
	КонецЕсли;

	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель получить количество сообщений в списке на отправку
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 			- Истина, если успешно
//		чКоличество 		- количество сообщений в списке на отправку
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Возвращает количество сообщенйи в списке на отправку (внутри ВК)
Функция Отправитель_ПолучитьКоличествоСообщенийВСпискеНаОтправку() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("чКоличество", -1);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	

	сткРезультат.чКоличество = глProducer.GetMessagePoolLength();
	сткРезультат.булУспешно = сткРезультат.чКоличество >= 0;
	
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глProducer.ErrorDescription;
	КонецЕсли;

	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель очистить список на отправку
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Очищает внутренний пул на отправку ВК
Функция Отправитель_ОчиститьСписокНаОтправку() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глProducer.ClearMessagePool();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глProducer.ErrorDescription;
	КонецЕсли;

	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель все сообщения доставлены
// 
// Возвращаемое значение:
// - Структура - (булУспешно, булВсеСообщенияДоставлены, стрОписнаиеОшибки)
//      булУспешно 					- Истина, если успешно
//		булВсеСообщенияДоставлены	- Истина, если все сообщения из внутреннего пула сообщений ВК были успешно доставлены (после вызова "Отправитель_Отправить")
//		стрОписаниеОшибки 			- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Получает признак успешности обработки пула сообщений (булВсеСообщенияДоставлены = Истина, ели все сообщения из пула успешно доставлены, после вызова функции "Отправитель_Отправить")
Функция Отправитель_ВсеСообщенияДоставлены() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Истина);
	сткРезультат.Вставить("булВсеСообщенияДоставлены", Ложь);

	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булВсеСообщенияДоставлены = глProducer.IsDelivered();
	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель получить JSONОтчет о доставке
// (БЫСТРЫЙ СПОСОБ)
// Возвращаемое значение:
// - Структура - (булУспешно, стрОтчетОДоставке, стрОписнаиеОшибки)
//      булУспешно 					- Истина, если успешно
//		стрОтчетОДоставке			- JSON отчет о доставке
//		стрОписаниеОшибки 			- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Возвращает массив структур в формате JSON - отчет о доставке (колонки: key, status, topic, partition, timestamp, offset, error)
Функция Отправитель_ПолучитьJSONОтчетОДоставке() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОтчетОДоставке", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.стрОтчетОДоставке = глProducer.GetJSONDeliveryReport();	
	Если сткРезультат.стрОтчетОДоставке = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глProducer.ErrorDescription;
		Возврат сткРезультат;	
	КонецЕсли;

	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФункции

// Функция - Отправитель отправить
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки)
//      булУспешно 					- Истина, если успешно
//		стрОписаниеОшибки 			- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Выполняет отправку из внутреннего пула сообщений ВК, очищает этот пул
Функция Отправитель_Отправить() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глProducer.Produce();	
	Если сткРезультат.булУспешно <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = глProducer.ErrorDescription;
		Возврат сткРезультат;	
	КонецЕсли;

	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФункции

Функция Отправитель_ДекодироватьКлючСообщенияКакBase64Строку(булДекодировать = Ложь) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глProducer.DecodeBase64Key = булДекодировать;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

Функция Отправитель_ДекодироватьТелоСообщенияКакBase64Строку(булДекодировать = Ложь) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глProducer.DecodeBase64Value = булДекодировать;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

Функция Отправитель_ДекодироватьЗначенияЗаголовковКакBase64Строку(булДекодировать = Ложь) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = Отправитель_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	глProducer.DecodeBase64HeadersValue = булДекодировать;
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции

// Функция - Отправитель закрыть
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Закрывает соединения, завершает управялющие потоки внутри ВК, закрывает объект отправителя
Функция Отправитель_Закрыть() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	глProducer = Неопределено;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФункции


#КонецОбласти  


#Область КлиентАдминистрирования   

// Функция - Создать клиент администрирования
// 
// Возвращаемое значение:
//   - Структура - (булУспешно, стрОписнаиеОшибки)
//
//Создает объект клиента администрирования в глобальной переменной модуля
Функция СоздатьКлиентАдминистрирования(булФлагДляСовместимости = Истина) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	Если глAdminClient <> Неопределено Тогда
		сткРезультат.булУспешно = Истина;
		Возврат сткРезультат;	
	КонецЕсли;
	
	Если Не  гл_булВнешняяКомпонентаПодключена Тогда
		 сткРезультатПодключения = ВыполнитьПодключениеВнешнейКомпоненты(булФлагДляСовместимости);
		 Если Не сткРезультатПодключения.булУспешно Тогда
			 сткРезультат.стрОписаниеОшибки = сткРезультатПодключения.стрОписаниеОшибки;
			Возврат сткРезультат;	 
		 КонецЕсли;
	 КонецЕсли;
	 
	Попытка
		 глAdminClient = Неопределено;
		 глAdminClient = Новый("AddIn." + гл_стрИмяКомпоненты + ".KafkaAdminClient");
	Исключение
		сткРезультат.стрОписаниеОшибки = ОписаниеОшибки();
		Возврат сткРезультат;
	КонецПопытки;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;
КонецФункции
 
// Функция - Клиент администрирования проверить доступность объекта 
// 
// Возвращаемое значение:
//   - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Если объект существует
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Проверяет, существует ли объект клиента администрирования и возможна ли работа с ним
Функция КлиентАдминистрирования_ПроверитьДоступностьОбъекта() Экспорт

	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	Если НЕ гл_булВнешняяКомпонентаПодключена Тогда
		сткРезультат.стрОписаниеОшибки = "Не подключена внешняя компонента";
		Возврат сткРезультат;
	КонецЕсли;
	
	Если глAdminClient = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = "Не создан клиент администрирования";
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФункции
 
// Функция - Клиент администрирования установить параметр глобальный
//
// Параметры:
//  стрКлюч		 - 	Строка - Имя параметра
//  стрЗначение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если параметр установлен
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр глабольного контекста для клиента администрирования (глAdminClient)
//	Пример: стрКлюч = socket.timeout.ms стрЗначение = 30000  
//!!Для применения установленных параметров - необходимка повторная инициализация клиента администрирования с помощью функции "КлиентАдминистрирования_Инициализировать"
Функция КлиентАдминистрирования_УстановитьПараметрГлобальный(Знач стрКлюч, Знач стрЗначение) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глAdminClient.SetGlobalConf(стрКлюч, стрЗначение);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции

// Функция - Клиент администрирования сбросить параметры
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Сбрасывает установенные параметры глобального контекста (установленные функцией КлиентАдминистрирования_УстановитьПараметрГлобальный)
//!!Для применения - необходимка повторная инициализация клиента администрирования с помощью функции "КлиентАдминистрирования_Инициализировать"
Функция КлиентАдминистрирования_СброситьПараметры() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глAdminClient.ConfReset();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции
	
// Функция - Клиент администрирования инициализировать
//
// Параметры:
//  стрСписокБрокеров		 - Строка 	 - Адреса брокеров в формате "10.0.5.187:9092,10.0.5.85:9092,10.0.5.86:9092"
//
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Применяет параметры, установленные функциями "КлиентАдминистрирования_УстановитьПараметрГлобальный", "КлиентАдминистрирования_СброситьПараметры" 
//Открывает соединения до брокера, создает управляющие потоки внутри ВК
Функция КлиентАдминистрирования_Инициализировать(Знач стрСписокБрокеров) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
		
	сткРезультат.булУспешно = глAdminClient.Инициализация(стрСписокБрокеров);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
	КонецЕсли;

	
	Возврат сткРезультат;	
КонецФункции    

// Функция - Клиент администрирования добавить назначение
//
// Параметры:
//  стрТопик	 - Строка	 -  Имя топика, который необходимо добавить в список назначений
//  чслРаздел	 - Число	 -  Номер раздела топика
//  чслСмещение	 - Число,Неопределено	 - Смещение раздела
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Добавляет назначение в специальный список, внутри ВК
Функция КлиентАдминистрирования_ДобавитьНазначение(Знач стрТопик, Знач чслРаздел, Знач чслСмещение = Неопределено) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глAdminClient.AddRecordToTopicPartitionList(стрТопик, чслРаздел, чслСмещение);
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
	КонецЕсли;
	
	Возврат сткРезультат;	
КонецФункции   

// Функция - Клиент администрирования очистить список назначений
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Очищает список назначений внутри ВК, сформированный посредставм вызова функции "КлиентАдминистрирования_ДобавитьНазначение"
Функция КлиентАдминистрирования_ОчиститьСписокНазначений() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = глAdminClient.ClearTopicPartitionList();
	Если сткРезультат.булУспешно <> Истина Тогда 
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
	КонецЕсли;

	
	Возврат сткРезультат;	
КонецФункции

Функция КлиентАдминистрирования_УдалитьСообщенияДоСмещения(Знач чслТаймаут = 30000) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрРезультат", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Удаляем сообщения	
	сткРезультат.стрРезультат = глAdminClient.DeleteRecordsBefore(чслТаймаут);
	Если  сткРезультат.стрРезультат = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	

	//Очищаем список назначений	
	Результат = глAdminClient.ClearTopicPartitionList();
	Если Результат <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции    

Функция КлиентАдминистрирования_ПолучитьСмещенияГруппыПолучателей(Знач стрГруппа, Знач чслТаймаут = 30000) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрРезультат", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Удаляем сообщения	
	сткРезультат.стрРезультат = глAdminClient.GetGroupOffsets(стрГруппа, чслТаймаут);
	Если сткРезультат.стрРезультат = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции  

Функция КлиентАдминистрирования_ПолучитьСписокГруппПолучателей(Знач чслТаймаут = 30000) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрРезультат", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Удаляем сообщения	
	сткРезультат.стрРезультат = глAdminClient.GetGroupList(чслТаймаут);            
	сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
	Если сткРезультат.стрРезультат = Неопределено Тогда
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции           

Функция КлиентАдминистрирования_ПолучитьМетаданные(Знач чслТаймаут = 30000, Знач стрТопик = Неопределено) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрРезультат", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Удаляем сообщения	
	сткРезультат.стрРезультат = глAdminClient.GetMetadata(чслТаймаут, стрТопик);            
	Если сткРезультат.стрРезультат = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции 

Функция КлиентАдминистрирования_ИзменитьСмещенияГруппы(Знач стрГруппа, Знач чслТаймаут = 30000) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрРезультат", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Изменяем смещения группы	
	сткРезультат.стрРезультат = глAdminClient.AlterGroupOffsets(стрГруппа, чслТаймаут);
	Если  сткРезультат.стрРезультат = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	

	//Очищаем список назначений	
	Результат = глAdminClient.ClearTopicPartitionList();
	Если Результат <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции  

Функция КлиентАдминистрирования_УдалитьСмещенияГруппы(Знач стрГруппа, Знач чслТаймаут = 30000) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрРезультат", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Удаляем смещения группы	
	сткРезультат.стрРезультат = глAdminClient.DeleteGroupOffsets(стрГруппа, чслТаймаут);
	Если  сткРезультат.стрРезультат = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	

	//Очищаем список назначений	
	Результат = глAdminClient.ClearTopicPartitionList();
	Если Результат <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции   

Функция КлиентАдминистрирования_ПолучитьСмещенияРазделов(Знач чслТаймаут = 30000) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрРезультат", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	сткРезультатПроверки = КлиентАдминистрирования_ПроверитьДоступностьОбъекта();
	Если Не сткРезультатПроверки.булУспешно Тогда
		сткРезультат.стрОписаниеОшибки = сткРезультатПроверки.стрОписаниеОшибки;
		Возврат сткРезультат;
	КонецЕсли;	
	
	//Получаем смещения разделов	
	сткРезультат.стрРезультат = глAdminClient.QueryWatermarkOffsets(чслТаймаут);
	Если  сткРезультат.стрРезультат = Неопределено Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;	

	//Очищаем список назначений	
	Результат = глAdminClient.ClearTopicPartitionList();
	Если Результат <> Истина Тогда
		сткРезультат.стрОписаниеОшибки = глAdminClient.ErrorDescription;
		Возврат сткРезультат;
	КонецЕсли;
	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФУнкции 

// Функция - Клиент администрирования закрыть
// 
// Возвращаемое значение:
// - Структура - (булУспешно, стрОписнаиеОшибки
//      булУспешно 			- Истина, если успешно
//		стрОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Закрывает соединения, завершает управялющие потоки внутри ВК, закрывает объект клиента администрирования
Функция КлиентАдминистрирования_Закрыть() Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("стрОписаниеОшибки", "");	
	
	глAdminClient = Неопределено;	
	сткРезультат.булУспешно = Истина;
	Возврат сткРезультат;	
КонецФункции

#КонецОбласти  


#Область ВспомогательныеПроцедурыИФункции

Функция СтруктуруВJSON(п_сткДанные)    Экспорт
	
	_сткРезультат = Новый Структура();
	
	_сткРезультат.Вставить("булУспешно",        Ложь);
	_сткРезультат.Вставить("стрДанные", 	    "");
	_сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	_ЗаписьJSON                = Новый ЗаписьJSON();
	_НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON();
	
	_ЗаписьJSON.УстановитьСтроку();
	
	_НастройкиСериализацииJSON.ВариантЗаписиДаты      = ВариантЗаписиДатыJSON.ЛокальнаяДата;
	_НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	Попытка
		ЗаписатьJSON(_ЗаписьJSON, п_сткДанные, _НастройкиСериализацииJSON);
		
		_сткРезультат.стрДанные  = _ЗаписьJSON.Закрыть();
		_сткРезультат.булУспешно = Истина;
	Исключение
		_сткРезультат.стрОписаниеОшибки = ОписаниеОшибки();	
	КонецПопытки;
	
	Возврат _сткРезультат;   	
КонецФункции

Функция JSONВСтруктуру(п_стрJSON)   Экспорт
		
	_сткРезультат = Новый Структура();
	
	_сткРезультат.Вставить("булУспешно",        Ложь);
	_сткРезультат.Вставить("масДанные",         Неопределено);
	_сткРезультат.Вставить("стрОписаниеОшибки", "");
	
	_Чтение = Новый ЧтениеJSON;
	_Чтение.УстановитьСтроку(п_стрJSON);
	
	Попытка
		_Данные = ПрочитатьJSON(_Чтение, Ложь);
		
		Если ТипЗнч(_Данные) = Тип("Структура") Тогда
			_сткРезультат.масДанные = Новый Массив();	
			_сткРезультат.масДанные.Добавить(_Данные);
		ИначеЕсли ТипЗнч(_Данные) = Тип("Массив") Тогда
			_сткРезультат.масДанные = _Данные;				
		Иначе
			ВызватьИсключение "Не верный тип данных!";
		КонецЕсли;
		
		_сткРезультат.булУспешно = Истина;
	Исключение
		_сткРезультат.стрОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;	
	
	_Чтение.Закрыть();
	
	Возврат _сткРезультат;	
КонецФункции

Функция СтрокаВДвоичныеДанные(СтрокаДляПреобразованияВДвоичныеДанные) Экспорт
	
	сткРезультат = Новый Структура();
	сткРезультат.Вставить("булУспешно", Ложь);
	сткРезультат.Вставить("ДвоичныеДанные", Неопределено);
	сткРезультат.Вставить("стрОписаниеОшибки", "");

	Поток  = Новый ПотокВПамяти();
	Запись = Новый ЗаписьДанных(Поток,КодировкаТекста.UTF8);
	
	Попытка
		Запись.ЗаписатьСимволы(СтрокаДляПреобразованияВДвоичныеДанные);
		Запись.Закрыть(); 
	Исключение
		сткРезультат.стрОписаниеОшибки = ОписаниеОшибки(); 
		Возврат сткРезультат;
	КонецПопытки;
	
	сткРезультат.ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные(); 
	сткРезультат.булУспешно = Истина;
	
	Возврат сткРезультат; 	
КонецФункции

#КонецОбласти


//ИнициализацияМодуля();
